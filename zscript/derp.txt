// ------------------------------------------------------------
// D.E.R.P. Robot
// ------------------------------------------------------------
class ZEDDERP:HDUPK{
	double lineattackheight;
	default{
		+ismonster +noblockmonst +nodropoff +shootable
		+friendly +nofear +dontgib
		painchance 250;painthreshold 12;
		damagefactor "Thermal",0.7;damagefactor "SmallArms3",0.8;
		radius 5;height 8;deathheight 8;maxdropoffheight 4;maxstepheight 4;
		bloodcolor "22 22 22";scale 0.6;
		health 100;mass 20;
		maxtargetrange 256;
	}
	override void postbeginplay(){
		super.postbeginplay();
		lineattackheight=height*0.5+6; //missile spawn height is 2, hence 6 not 8
	}
	bool DerpTargetCheck(bool face=false){
		if(!target)return false;
		if(face){
			//play sound
			A_FaceTarget(2,2);
		}
		actor derpuf;int garbage;
		addz(-lineattackheight);
		[derpuf,garbage]=lineattack(
			angle,256,pitch,0,"none",
			"NewBulletMovePuff",flags:LAF_NORANDOMPUFFZ|LAF_NOINTERACT
		);
		addz(lineattackheight);
		if(derpuf && derpuf.tracer==target){
			return true;
		}else{
			return false;
		}
	}
	void DerpAlert(string msg="Derpy derp!"){
		if(!master)return;
		int time=max(con_midtime,msg.length()*3);
		master.A_Print(string.format("%s  %s","\cd[DERP]",msg,time));
	}
	void DerpShot(){
		A_PlaySound("weapons/pistol",0,1,0,4);
		if(!random(0,11)){
			if(bfriendly)A_AlertMonsters(0,AMF_TARGETEMITTER);
			else A_AlertMonsters();
		}
		actor b=spawn("HDBullet9",pos+(0,0,2));
		b.pitch=pitch;b.angle=angle;b.target=self;b.vel+=vel;
	}
}




