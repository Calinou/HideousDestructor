//-------------------------------------------------
// Blood Substitute
//-------------------------------------------------
class SecondBlood:HDWeapon{
	default{
		//$Category "Items/Hideous Destructor/Supplies"
		//$Title "Synhetic Blood"
		//$Sprite "PBLDA0"

		scale 0.37;
		-hdweapon.droptranslation
		+inventory.invbar
		inventory.pickupmessage "Picked up a synthblood pack.";
		inventory.icon "PBLDA0";
		hdweapon.nicename "Synthetic Blood";
		hdweapon.refid HDLD_BLODPAK;
		species "HealingItem";
	}
	override double weaponbulk(){
		return ENC_STIMPACK;
	}
	states{
	spawn:
		PBLD A -1;
		stop;
	reload:
		TNT1 A 14{
			A_DropInventory("HDArmourWorn");
			A_ClearRefire();
		}goto readyend;
	ready:
		TNT1 A 1 A_WeaponReady();
		goto readyend;
	fire:
	altfire:
		TNT1 A 10 A_PlaySound("bloodpack/open");
		TNT1 AAA 10 A_PlaySound("bloodpack/shake",CHAN_WEAPON);
		TNT1 A 20;
		TNT1 A 0 A_Refire();
		goto ready;
	hold:
		TNT1 A 1{
			hdplayerpawn patient=null;
			int bt=player.cmd.buttons;
			if(bt&BT_ATTACK){
				patient=hdplayerpawn(self);
			}else if(bt&BT_ALTATTACK){
				//get the patient
				flinetracedata blt;
				LineTrace(
					angle,
					32,
					pitch,
					0,
					offsetz:height-10.,
					data:blt
				);
				actor tracbak=invoker.tracer;
				invoker.tracer=blt.hitactor;
				if(
					!tracbak
					||tracbak!=blt.hitactor
					||!hdplayerpawn(tracbak)
				){
					A_WeaponMessage("Can't reach patient.");
					invoker.weaponstatus[SBS_INJECTCOUNTER]=0;
					return;
				}
				patient=hdplayerpawn(tracbak);
			}
			if(patient)invoker.weaponstatus[SBS_INJECTCOUNTER]++;else{
				invoker.weaponstatus[SBS_INJECTCOUNTER]=0;
				return;
			}
			if(
				patient.countinv("WornRadsuit")
				||patient.countinv("HDArmourWorn")
			){
				if(
					bt&BT_ALTATTACK
					&&getcvar("hd_autostrip")
				){
					setweaponstate("reload");
				}else{
					A_WeaponMessage("Remove "..((bt&BT_ALTATTACK)?"their":"your").." armour first.");
					invoker.weaponstatus[SBS_INJECTCOUNTER]=0;
				}
				return;
			}
			if(
				patient.countinv("BloodBagWorn")
			){
				A_WeaponMessage(((bt&BT_ALTATTACK)?"Patient has":"You have").." a blood injector already!");
				invoker.weaponstatus[SBS_INJECTCOUNTER]=0;
				return;
			}
			if(invoker.weaponstatus[SBS_INJECTCOUNTER]>70){
				patient.A_GiveInventory("BloodBagWorn");
				A_PlaySound("bloodbag/inject",CHAN_WEAPON);
				setweaponstate("nope");
				invoker.amount=0;
			}
		}
		TNT1 A 0 A_Refire();
		goto ready;
	}
	enum SecondBloodNums{
		SBS_INJECTCOUNTER=1,
	}
}
class BloodBagWorn:Inventory{
	int bloodleft;
	default{
		-solid -noblockmap
		inventory.maxamount 1;
		height 3;radius 3;
	}
	override void postbeginplay(){
		super.postbeginplay();
		bloodleft=64;
	}
	override void touch(actor toucher){}
	override inventory createtossable(int amount){
		let onr=hdplayerpawn(owner);
		if(onr)onr.woundcount++;
		return super.createtossable(amount);
	}
	override void DoEffect(){
		let hp=HDPlayerPawn(owner);
		if(!hp){destroy();return;}
		if(!hp.beatcount){
			bloodleft--;
			hp.bledout=max(0,hp.bledout-4);
			hp.fatigue=min(hp.fatigue+1,HDCONST_SPRINTFATIGUE);
		}
		//fall off
		//nah, let's make the player work for it
		//if(bloodleft<1)owner.dropinventory(self);
	}
	states{
	spawn:
		PBLD B 3{
			if(bloodleft>0){
				if(floorz==pos.z&&vel!=(0,0,0))A_SpawnItemEx("HDBloodTrailFloor");
				if(level.time%3)bloodleft--;
				angle+=frandom(3,6);
				A_SpawnParticle("red",
					SPF_RELATIVE,70,frandom(0.6,1.6),0,
					0,2,frandom(2.5,3),
					frandom(-0.4,0.4),frandom(-0.4,0.4),frandom(1.1,1.2),
					frandom(-0.1,0.1),frandom(-0.1,0.1),-1.
				);
			}
		}
		wait;
	}
}
