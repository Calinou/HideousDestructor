// ------------------------------------------------------------
// Sight picture crosshairs
// ------------------------------------------------------------
extend class HDStatusBar{
	virtual void DrawGrenadeLadder(int airburst,vector2 bob){
		drawimage(
			"XH27",(0,1.8)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
			scale:(1.6,1.6)
		);
		if(airburst)drawnum(airburst,
			12+bob.x,42+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
		);
	}
	virtual void DrawHDXHair(hdplayerpawn hpl){
		int nscp=cvar.getcvar("hd_noscope",cplayer).getint();
		if(
			!(cplayer.cmd.buttons&(BT_USE|BT_ZOOM))
			&&(
				nscp>1
				||cvar.getcvar("hd_hudusedelay",cplayer).getint()<-1
			)
		)return;

		let wp=hdweapon(cplayer.readyweapon);
		bool sightbob=cvar.getcvar("hd_sightbob",cplayer).getbool();
		vector2 bob=hpl.hudbob;
		double fov=cplayer.fov;

		//have no crosshair at all
		if(
			!wp
			||hpl.barehanded
			||hpl.nocrosshair>0
			||(!sightbob&&hpl.countinv("IsMoving"))
			||abs(bob.x)>50
			||fov<13
		)return;


		//multiple weapons use this
		string whichdot="redpxl";
		int whichdotthough=cvar.getcvar("hd_crosshair",hpl.player).getint();
		switch(whichdotthough){
			case 1:case 2:case 3:case 4:case 5:
			whichdot=string.format("riflsit%i",whichdotthough);
			default:break;
		}


		//all weapon sights go here
		double scl=fov/(90.*clamp(cvar.getcvar("hd_xhscale",cplayer).getfloat(),0.1,3.0));
		SetSize(0,400*scl,250*scl);
		BeginHUD(forcescaled:true);

		actor hpc=hpl.scopecamera;
		int cpbt=cplayer.cmd.buttons;

		bob*=scl;
		bool scopeview=!!hpc&&(
			!nscp
			||cpbt&BT_ZOOM
			||!gimmehud
		);
		if(hpl.findinventory("PortableLiteAmp")&&PortableLiteAmp(hpl.findinventory("PortableLiteAmp")).worn)scopeview=false;

		if((wp is "HDPistol")){
			int cx,cy,cw,ch;
			[cx,cy,cw,ch]=screen.GetClipRect();
			SetClipRect(
				-8+bob.x,-4+bob.y,16,10,
				DI_SCREEN_CENTER
			);
			vector2 bobb=bob*1.6;
			bobb.y=clamp(bobb.y,-8,8);
			drawimage(
				"frntsite",(0,0)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
				alpha:0.9,scale:(0.6,0.6)
			);
			screen.SetClipRect(cx,cy,cw,ch);
			drawimage(
				"backsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_TOP,
				scale:(0.6,0.6)
			);
		}
		else if((wp is "HDSMG"||wp is "Hunter")){
			int cx,cy,cw,ch;
			[cx,cy,cw,ch]=screen.GetClipRect();
			SetClipRect(
				-16+bob.x,-4+bob.y,32,16,
				DI_SCREEN_CENTER
			);
			vector2 bobb=bob*3;
			bobb.y=clamp(bobb.y,-8,8);
			drawimage(
				"frntsite",(0,0)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
				alpha:0.9
			);
			screen.SetClipRect(cx,cy,cw,ch);
			drawimage(
				"backsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_TOP
			);
		}
		else if((wp is "Slayer")){
			int cx,cy,cw,ch;
			[cx,cy,cw,ch]=screen.GetClipRect();
			SetClipRect(
				-16+bob.x,-4+bob.y,32,12,
				DI_SCREEN_CENTER
			);
			vector2 bobb=bob*3;
			bobb.y=clamp(bobb.y,-8,8);
			drawimage(
				"frntsite",(0,0)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
				alpha:0.9,scale:(0.7,1)
			);
			screen.SetClipRect(cx,cy,cw,ch);
			drawimage(
				"dbbaksit",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_TOP
			);
		}
		else if(
			wp is "ZM66AssaultRifle"
		){
			int scaledyoffset=47;
			if(wp.weaponstatus[0]&ZM66F_GLMODE)drawgrenadeladder(wp.airburst,bob);
			else{
				double dotoff=max(abs(bob.x),abs(bob.y));
				if(dotoff<20){
					drawimage(
						whichdot,(0,0)+bob*1.6,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						alpha:0.8-dotoff*0.04
					);
				}
				drawimage(
					"riflsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER
				);
				if(scopeview){
					double degree=(wp.weaponstatus[ZM66S_ZOOM])*0.1;
					texman.setcameratotexture(hpc,"HDXHCAM3",degree);
					drawimage(
						"HDXHCAM3",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.31,0.31)
					);
					int scaledwidth=57;
					int cx,cy,cw,ch;
					[cx,cy,cw,ch]=screen.GetClipRect();
					SetClipRect(
						-28+bob.x,19+bob.y,scaledwidth,scaledwidth,
						DI_SCREEN_CENTER
					);
					drawimage(
						"scophole",(0,scaledyoffset)+bob*3,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.78,0.78)
					);
					screen.SetClipRect(cx,cy,cw,ch);
					drawimage(
						"zm66scop",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.82,0.82)
					);
					drawnum(degree*10,
						3+bob.x,74+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
					);
					drawimage(
						"BLETA0",(0,78)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						alpha:0.6,scale:(1.3,1.3)
					);
				}
			}
		}
		else if((wp is "Vulcanette")){
			double dotoff=max(abs(bob.x),abs(bob.y));
			if(dotoff<20){
				drawimage(
					whichdot,(0,0)+bob*1.6,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					alpha:0.8-dotoff*0.04
				);
			}
			drawimage(
				"riflsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER
			);
			int scaledyoffset=47;
			if(scopeview){
				double degree=(wp.weaponstatus[VULCS_ZOOM])*0.1;
				texman.setcameratotexture(hpc,"HDXHCAM3",degree);
				drawimage(
					"HDXHCAM3",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(0.31,0.31)
				);
				int scaledwidth=57;
				int cx,cy,cw,ch;
				[cx,cy,cw,ch]=screen.GetClipRect();
				SetClipRect(
					-28+bob.x,19+bob.y,scaledwidth,scaledwidth,
					DI_SCREEN_CENTER
				);
				drawimage(
					"scophole",(0,scaledyoffset)+bob*3,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(0.78,0.78)
				);
				screen.SetClipRect(cx,cy,cw,ch);
				drawimage(
					"zm66scop",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(0.8,0.8)
				);
				drawnum(degree*10,
					3+bob.x,73+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
				);
				drawimage(
					"BLETA0",(0,77)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					alpha:0.6,scale:(1.3,1.3)
				);
			}
		}
		else if((wp is "Blooper"))drawgrenadeladder(wp.airburst,bob);
		else if((wp is "HDRL")){
			if(wp.weaponstatus[0]&RLF_GRENADEMODE)drawgrenadeladder(wp.airburst,bob);
			else if(wp.weaponstatus[0]&RLF_ZOOM){
				int scaledyoffset=4;
				texman.setcameratotexture(hpc,"HDXHCAM6",1.6);
				drawimage(
					"HDXHCAM6",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(0.35,0.35)
				);
				drawimage(
					"rlstadia",bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(0.77,0.77)
				);
				int airburst=wp.airburst;
				if(airburst)drawnum(airburst,
					17+bob.x,15+bob.y,DI_SCREEN_CENTER,Font.CR_DARKRED,0.5
				);
			}
			else{
				double dotoff=max(abs(bob.x),abs(bob.y));
				if(dotoff<10){
					drawimage(
						"riflsit3",(0,0)+bob*1.6,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						alpha:0.8-dotoff*0.04,scale:(0.8,0.8)
					);
				}
				drawimage(
					"xh25",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1.6,1.6)
				);
				int airburst=wp.airburst;
				if(airburst)drawnum(airburst,
					10+bob.x,9+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
				);
			}
		}
		else if(wp is "ThunderBuster"){
			int cx,cy,cw,ch;
			[cx,cy,cw,ch]=screen.GetClipRect();
			SetClipRect(
				-16+bob.x,-4+bob.y,32,16,
				DI_SCREEN_CENTER
			);
			vector2 bobb=bob*2;
			bobb.y=clamp(bobb.y,-8,8);
			drawimage(
				"frntsite",(0,0)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
				alpha:0.9,scale:(1.6,2)
			);
			screen.SetClipRect(cx,cy,cw,ch);
			drawimage(
				"backsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_TOP,
				scale:(2,1)
			);

			if(scopeview){
				bool alt=wp.weaponstatus[0]&TBF_ALT;
				int scaledyoffset=36;
				texman.setcameratotexture(hpc,"HDXHCAM1",3);
				drawimage(
					"HDXHCAM1",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					alpha:alt?(hpl.flip?0.7:0.8):1.,scale:(1,1)
				);
				drawnum(wp.weaponstatus[TBS_MAXRANGEDISPLAY],
					24+bob.x,12+bob.y,DI_SCREEN_CENTER,Font.CR_RED,0.4
				);
				if(alt)drawnum(2000/HDCONST_ONEMETRE,
					23+bob.x,11+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK,1.
				);
				drawimage(
					"tbwindow",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1,1)
				);
				bobb*=3.7;
				double dotoff=max(abs(bobb.x),abs(bobb.y));
				if(dotoff<40)drawimage(
					"redpxl",(0,scaledyoffset)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
					alpha:(alt?0.4:0.9)*(1.-dotoff*0.04),scale:alt?(hpl.flip?(3,3):(1,1)):(2,2)
				);
			}
		}
		else if(wp is "LiberatorRifle"){
			if(wp.weaponstatus[0]&LIBF_GRENADEMODE)drawgrenadeladder(wp.airburst,bob);
			else{
				double dotoff=max(abs(bob.x),abs(bob.y));
				if(dotoff<20){
					drawimage(
						whichdot,(0,0)+bob*1.6,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						alpha:0.8-dotoff*0.04
					);
				}
				drawimage(
					"riflsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER
				);
				if(scopeview){
					int scaledyoffset=60;
					int scaledwidth=72;
					double degree=wp.weaponstatus[LIBS_ZOOM]*0.1;
					double deg=1/degree;
					int cx,cy,cw,ch;
					[cx,cy,cw,ch]=screen.GetClipRect();
					SetClipRect(
						-36+bob.x,24+bob.y,scaledwidth,scaledwidth,
						DI_SCREEN_CENTER
					);
					string reticle=
						wp.weaponstatus[0]&LIBF_ALTRETICLE?"reticle2":"reticle1";
					texman.setcameratotexture(hpc,"HDXHCAM3",degree);
					drawimage(
						"HDXHCAM3",(0,scaledyoffset)+bob,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.4,0.4)
					);
					if(wp.weaponstatus[0]&LIBF_FRONTRETICLE){
						drawimage(
							reticle,(0,scaledyoffset)+bob*5,
							DI_SCREEN_CENTER|DI_ITEM_CENTER,
							scale:(2.,2.)*deg
						);
					}else{
						drawimage(
							reticle,(0,scaledyoffset)+bob,
							DI_SCREEN_CENTER|DI_ITEM_CENTER,
							scale:(0.52,0.52)
						);
					}
					drawimage(
						"scophole",(0,scaledyoffset)+bob*5,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.95,0.95)
					);
					screen.SetClipRect(cx,cy,cw,ch);
					drawimage(
						"libscope",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER
					);
					drawnum(degree*10,
						4+bob.x,95+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
					);
					drawnum(wp.weaponstatus[LIBS_DROPADJUST]*5,
						6+bob.x,17+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
					);
					drawimage(
						"BLETA0",(0.8,99)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						alpha:0.6,scale:(1.3,1.3)
					);
				}
			}
		}
		else if((wp is "Brontornis")){
			int scaledyoffset=-4;
			if(wp.weaponstatus[0]&BRONF_ZOOM){
				texman.setcameratotexture(hpc,"HDXHCAM5",2.6);
				drawimage(
					"HDXHCAM5",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(0.42,0.42)
				);
				scaledyoffset=-6;
				drawimage(
					"bstadia",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(1.2,1.2)
				);
			}else{
				double dotoff=max(abs(bob.x),abs(bob.y));
				if(dotoff<10){
					drawimage(
						"riflsit4",(0,0)+bob*1.6,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						alpha:0.8-dotoff*0.04,scale:(1.6,1.6)
					);
				}
				drawimage(
					"xh25",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1.6,1.6)
				);
			}
		}
		else if(wp is "BossRifle"){

			int cx,cy,cw,ch;
			[cx,cy,cw,ch]=screen.GetClipRect();
			SetClipRect(
				-16+bob.x,-4+bob.y,32,16,
				DI_SCREEN_CENTER
			);
			vector2 bobb=bob*2;
			bobb.y=clamp(bobb.y,-8,8);
			drawimage(
				"frntsite",(0,0)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
				alpha:0.9,scale:(1.6,2)
			);
			screen.SetClipRect(cx,cy,cw,ch);
			drawimage(
				"backsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_TOP,
				scale:(2,1)
			);

			if(scopeview){
				int scaledyoffset=60;
				int scaledwidth=89;
				vector2 sclhalf=(0.5,0.5);
				double degree=0.1*wp.weaponstatus[BOSSS_ZOOM];
				double deg=1/degree;
				int cx,cy,cw,ch;
				[cx,cy,cw,ch]=screen.GetClipRect();
				SetClipRect(-44+bob.x,16+bob.y,scaledwidth,scaledwidth,
					DI_SCREEN_CENTER);
				texman.setcameratotexture(hpc,"HDXHCAM3",degree);
				drawimage(
					"HDXHCAM3",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:sclhalf
				);
				if(wp.weaponstatus[0]&BOSSF_FRONTRETICLE){
					drawimage(
						"reticle1",(0,scaledyoffset)+bob*deg*5,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(1.6,1.6)*deg
					);
				}else{
					drawimage(
						"reticle1",(0,scaledyoffset)+bob,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:sclhalf
					);
				}
				drawimage(
					"scophole",(0,scaledyoffset)+bob*5,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1.5,1.5)
				);
				screen.SetClipRect(cx,cy,cw,ch);
				drawimage(
					"libscope",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1.24,1.24)
				);
				drawnum(degree*10,
					4+bob.x,105+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
				);
				drawnum(wp.weaponstatus[BOSSS_DROPADJUST]*5,
					6+bob.x,9+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
				);
				drawimage(
					"BLETA0",(0.8,109)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					alpha:0.6,scale:(1.3,1.3)
				);
			}
			// the scope display is in 10ths of an arcminute.
			// every 2 BossMOA (item) should be one arcminute.
			// every 60 BossMOA (scope) should be one dot.
			// one dot = 6 arcminutes.
		}
		else wp.DrawSightPicture(self,wp,hpl);
	}
}
