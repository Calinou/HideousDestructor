//-------------------------------------------------
// Blur Sphere
//-------------------------------------------------
class BlurTaint:InventoryFlag{default{+inventory.undroppable}}
class HDBlurSphere:HDPickup{
	//true +invisible can never be used.
	//it will cause the monsters to be caught in a consant 1-tic see loop.
	//no one seems to consider this to be a bug.
	//shadow will at least cause attacks to happen less often.
	default{
		//$Category "Items/Hideous Destructor/Magic"
		//$Title "Blur Sphere"
		//$Sprite "PINSA0"

		inventory.maxamount 9;
		inventory.interhubamount 1;
		inventory.pickupmessage "So precious in your sight.";
		inventory.pickupsound "misc/casing";
		inventory.icon "PINSA0";
		scale 0.3;
		accuracy 1;
	}
	int intensity;int xp;int level;bool worn;
	int randticker[4];double randtickerfloat;
	override void tick(){
		super.tick();
		double frnd=frandom[blur](0.93,1.04);
		scale=(0.3,0.3)*frnd;
		alpha=0.9*frnd;
		randticker[0]=random(0,3);
		randticker[1]=random(8,25);
		randticker[2]=random(0,40+level);
		randticker[3]=random(0,BLUR_LEVELUP);
		randtickerfloat=frandom(0.,1.);
	}
	override void ownerdied(){
		buntossable=false;
		owner.A_DropInventory("HDBlursphere",amount);
	}
	states{
	spawn:
	spawn2:
		PINS ABCDCB random(1,6);
		loop;
	use:
		TNT1 A 0{
			A_SetBlend("01 00 00",0.9,48);
			if(!invoker.worn){
				invoker.worn=true;
				A_GiveInventory("BlurTaint");
				A_PlaySound("imp/sight2",CHAN_BODY,frandom(0.3,0.5),false,8);
				invoker.level+=invoker.xp/BLUR_LEVELUP;
				invoker.xp%=BLUR_LEVELUP;
				invoker.stamina=clamp(invoker.level+random(-2,2),0,10);
				if(invoker.level>7)invoker.buntossable=true;  

				int spac=countinv("SpiritualArmour");
				if(spac){
					hdplayerpawn(self).cheatgivestatusailments("fire",spac*3);
					A_TakeInventory("SpiritualArmour");
				}
			}else{
				invoker.worn=false;
				A_PlaySound("imp/sight1",CHAN_BODY,frandom(0.3,0.5),false,8);
			}
		}fail;
	}
	enum blurstats{
		BLUR_LEVELUP=3500,
	}
	override void DoEffect(){
		if(
			!owner||
			owner.health<1
		){
			return;
		}

		//they eat their own
		if(amount>1){  
			amount=1;
			xp+=100;
		}


		if(!worn){
			intensity=max(0,intensity-1);
			if(randticker[0])xp++;
		}else{
			if(intensity<99)intensity=max(intensity+1,-135);
			xp++;
		}
		bool invi=true;

		if(intensity<randticker[1]){
			owner.a_setrenderstyle(1.,STYLE_Normal);
			invi=false;
		}else{
			owner.a_setrenderstyle(0.9,STYLE_Fuzzy);
		}

		//apply result
		owner.bshadow=invi;

		if(!owner.countinv("blurtaint"))return;

		//medusa gaze
		if(invi&&!!randticker[0]){
			actor aaa;int bbb;
			[aaa,bbb]=owner.LineAttack(owner.angle,4096,owner.pitch,0,"none",
				"CheckPuff",flags:LAF_NORANDOMPUFFZ|LAF_OVERRIDEZ|LAF_NOINTERACT,
				offsetz:owner.height-6
			);
			if(aaa&&aaa.tracer&&aaa.tracer.bismonster){
				aaa.tracer.A_ClearTarget();
				blockthingsiterator itt=blockthingsiterator.create(aaa,512);
				while(itt.Next()){
					if(itt.thing.target==owner)itt.thing.A_ClearTarget();
					if(
						itt.thing.bismonster
						&&itt.thing!=aaa.tracer
						&&itt.thing.health>0  
						&&!aaa.tracer.target
					)aaa.tracer.target=itt.thing;
				}
				aaa.tracer.A_ClearSoundTarget();
			}
			owner.A_ClearSoundTarget();
		}

		let hdp=hdplayerpawn(owner);
		if(hdp){
			if(hdp.regenblues>0){  
				hdp.regenblues=max(0,hdp.regenblues-15);
				hdp.cheatgivestatusailments("fire",1);
				if(!worn&&!randticker[2]){
					hdp.A_TakeInventory("BlurTaint");
				}
			}
			if(hdp.countinv("SpiritualArmour")){
				hdp.cheatgivestatusailments("fire",countinv("SpiritualArmour")*10);
				hdp.A_TakeInventory("SpiritualArmour");
			}
		}

		//precious.
		if(randticker[3]<level){
			if(!(xp%3)){
				sound snd[7];
				snd[0]="imp/sight";
				snd[1]="grunt/sight";
				snd[2]="grunt/active";
				snd[3]="demon/active";
				snd[4]="world/riflefar";
				snd[5]="world/rocketfar";
				snd[6]="misc/gibbed";
				owner.A_PlaySound(snd[clamp(randtickerfloat*snd.size(),0,snd.size()-1)],
					CHAN_VOICE,randtickerfloat*0.3+0.3,false,8
				);
			}
			if(!(xp%5)){
				string msg[15];
				msg[0]=string.format("Out of sync with: %i",randticker[0]+1);
				msg[1]="Error: no such actor \"HDPlayer\" exists. Execution may abort!";
				msg[2]="\cd[DERP] \cjEngaging hostile.";
				msg[3]="Memory allocation error: recovered segfault at address 00x6f24ff.";
				msg[4]="rendering error";
				msg[5]="Noise.";
				msg[6]="hello";
				msg[7]="I hate you.";
				msg[8]="This is worthless.";
				msg[9]="it hurts";
				msg[10]="error";
				msg[11]="Precious.";
				msg[12]="Precious.";
				msg[13]="Precious.";
				msg[14]="Precious.";
				owner.A_Log(msg[clamp(randtickerfloat*msg.size(),0,msg.size()-1)],true);
			}
			if(!(xp%7)){
				hdplayerpawn(owner).aggravateddamage++;
				if(!randticker[0])owner.A_Log("Precious.",true);
			}
		}
	}
	override void DetachFromOwner(){
		owner.bshadow=false;
		owner.a_setrenderstyle(1.,STYLE_Normal);
		if(worn){
			worn=false;
			owner.damagemobj(self,owner,random(1,level),"balefire");
		}
		intensity=0;
		owner.A_PlaySound("imp/sight1",CHAN_BODY,frandom(0.3,0.5),false,8);
		super.detachfromowner();
	}
}





//a mortal man doomed to die
class WraithLight:PointLight{
	default{
		+dynamiclight.subtractive
	}
	override void postbeginplay(){
		args[0]=66;
		args[1]=17;
		args[2]=13;
		args[3]=0;
		args[4]=0;
	}
	override void tick(){
		super.tick();
		if(!target){
			args[3]+=random(-5,2);
			if(args[3]<1)destroy();
		}else if(target.bmissile)args[3]=random(32,40);
		else args[3]=random(48,64);
	}
}
//In geometry, a spherical shell is a generalization of an annulus to three dimensions.
class ShellShade:ZombieStormtrooper{
	default{
		//$Category "Monsters/Hideous Destructor"
		//$Title "Shellshade"
		//$Sprite "POSSA1"

		+shadow -solid
		renderstyle "Fuzzy";
		health 900;
		stencilcolor "04 00 06";
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_SpawnItemEx("WraithLight",flags:SXF_SETTARGET);
	}
	override void tick(){
		super.tick();
		bshootable=randompick(0,1,1,1);
		scale=bshootable?(1.,1.):(0.98,0.98);
		binvisible=bshootable?false:true;
	}
	states{
	death:
	xdeath:
		POSS G 5{
			A_NoBlocking();
			A_DropItem("ZM66Regular");
			bnointeraction=true;
			for(int i=0;i<10;i++){A_SpawnItemEx("HDSmoke",
				frandom(-12,12),frandom(-12,12),frandom(4,36),
				flags:SXF_NOCHECKPOSITION
			);}
			DistantQuaker.Quake(self,
				6,100,16384,10,256,512,128
			);
			vel=(0,0,0);
		}
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA
			random(1,3) A_PlaySound("marine/death",random(1,6),frandom(0.3,1.),false,0.1);
		TNT1 A 35;
		TNT1 A 0 A_DropItem("HDBlurSphere");
		stop;
	}
}

