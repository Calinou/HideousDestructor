// ------------------------------------------------------------
// Rifleman and derivatives
// ------------------------------------------------------------
class HDMarine:HDActor{// replaces ScriptedMarine{
	default{
		monster;
		+friendly
		+quicktoretaliate
		height 52;
		radius 12;
		health 130;
		speed 16;
		maxdropoffheight 48;
		maxstepheight 48;
		maxtargetrange 65536;
		minmissilechance 24;
		mass 150;
		seesound "marine/sight";
		painchance 240;
		obituary "%o was shot up by a marine.";
		hitobituary "%o violated a marine's personal space.";

		accuracy 0; //set to hdmw_*+1
		stamina 0; //+1 for setting
	}
	double spread;
	double turnamount;
	int gunloaded;
	int gunmax;
	int gunspent;
	int pistolloaded;
	bool glloaded;
	int timesdied;
	bool hasdropped;
	int wep;
	override void die(actor source,actor inflictor,int dmgflags=0){
		timesdied++;
		super.die(source,inflictor,dmgflags);
	}
	override void beginplay(){
		super.beginplay();
		hasdropped=false;
		spread=0;
		timesdied=0;

		//weapon
		pistolloaded=15;
		glloaded=true;
		if(!accuracy)wep=clamp(random(0,3)-random(0,3),0,3);
		else wep=accuracy-1;

		if(wep==HDMW_ZM66)gunmax=50;
		else if(wep==HDMW_HUNTER)gunmax=8;
		else if(wep==HDMW_SMG)gunmax=30;
		else if(wep==HDMW_ROCKET)gunmax=6;
		gunloaded=gunmax;


		//appearance
		string trnsl="";
		if(bfriendly){
			bpushable=true;
		}
		if(self is "Tango")trnsl="Tango";else{
			if(wep==HDMW_ZM66)trnsl="Rifleman";
			else if(wep==HDMW_HUNTER)trnsl="Enforcer";
			else if(wep==HDMW_SMG)trnsl="Infiltrator";
			else if(wep==HDMW_ROCKET)trnsl="Rocketeer";
		}

		int melanin=stamina+1;
		if(!accuracy)melanin=random(0,2);
		if(!melanin)trnsl=string.format("White%s",trnsl);
		else if(melanin==1)trnsl=string.format("Brown%s",trnsl);
		else if(melanin==2)trnsl=string.format("Black%s",trnsl);

		A_SetTranslation(trnsl);

		if(random(0,1)){
			painsound="marinef/pain";
			deathsound="marinef/death";
		}else{
			painsound="marine/pain";
			deathsound="marine/death";
		}
	}
	override void postbeginplay(){
		super.postbeginplay();

		hdmobster.spawnmobster(self);
	}
	void noblockwepdrop(){
		if(hasdropped){
			class<actor> dropammo="";
			if(wep==HDMW_SMG)dropammo="HDSMGMag";
			else if(wep==HDMW_ZM66)dropammo="HDPistolMag";
			else if(wep==HDMW_ROCKET)dropammo="HDRocketAmmo";
			else if(wep==HDMW_HUNTER)dropammo="ShellPickup";
			if(!random(0,timesdied))A_DropItem(dropammo);
			if(!random(0,12+timesdied))A_DropItem("HDPistolMag");
			if(
				!random(0,timesdied)&&wep==HDMW_SMG
			)A_DropItem("HDRocketAmmo");
		}else{
			hasdropped=true;
			hdweapon dropped;
			if(wep==HDMW_SMG){
				dropped=hdweapon(spawn("HDSMG",pos));
				if(gunloaded){
					dropped.weaponstatus[SMGS_MAG]=gunloaded-1;
					dropped.weaponstatus[SMGS_CHAMBER]=2;
				}else{
					dropped.weaponstatus[SMGS_MAG]=0;
					dropped.weaponstatus[SMGS_CHAMBER]=0;
				}
			}else if(wep==HDMW_ZM66){
				dropped=hdweapon(spawn("ZM66AssaultRifle",pos));
				if(gunloaded){
					dropped.weaponstatus[ZM66S_MAG]=gunloaded-1;
					dropped.weaponstatus[0]=ZM66F_CHAMBER;
				}else{
					dropped.weaponstatus[ZM66S_MAG]=0;
					dropped.weaponstatus[0]=0;
				}
				if(glloaded)dropped.weaponstatus[0]|=ZM66F_GRENADELOADED;
			}else if(wep==HDMW_ROCKET){
				dropped=hdweapon(spawn("HDRL",pos));
				if(gunloaded){
					dropped.weaponstatus[RLS_MAG]=gunloaded-1;
					dropped.weaponstatus[RLS_CHAMBER]=1;
				}else{
					dropped.weaponstatus[RLS_MAG]=0;
					dropped.weaponstatus[RLS_CHAMBER]=0;
				}
			}else if(wep==HDMW_HUNTER){
				dropped=hdweapon(spawn("Hunter",pos));
				if(gunloaded){
					dropped.weaponstatus[HUNTS_TUBE]=gunloaded-1;
					dropped.weaponstatus[HUNTS_CHAMBER]=2;
				}else{
					dropped.weaponstatus[HUNTS_TUBE]=0;
					dropped.weaponstatus[HUNTS_CHAMBER]=0;
				}
				dropped.weaponstatus[HUNTS_SIDESADDLE]=random(0,12);
				dropped.weaponstatus[0]=HUNTF_AUTO;
			}
			dropped.addz(32);
			dropped.vel=vel+(frandom(-1,1),frandom(-1,1),2);

			//drop the pistol
			dropped=hdweapon(spawn("HDPistol",pos));
			dropped.addz(32);
			dropped.vel=vel+(frandom(-1,1),frandom(-1,1),2);
			if(pistolloaded){
				dropped.weaponstatus[PISS_MAG]=pistolloaded-1;
				dropped.weaponstatus[PISS_CHAMBER]=2;
			}else{
				dropped.weaponstatus[PISS_MAG]=0;
				dropped.weaponstatus[PISS_CHAMBER]=0;
			}

			//drop the blooper
			if(wep!=HDMW_SMG&&wep!=HDMW_HUNTER)return;
			dropped=hdweapon(spawn("Blooper",pos));
			dropped.addz(32);
			dropped.vel=vel+(frandom(-1,1),frandom(-1,1),2);
			if(glloaded)dropped.weaponstatus[0]|=BLOPF_LOADED;
		}
	}

	//returns true if area around target is clear of friendlies
	bool A_CheckBlast(actor tgt=null,double checkradius=256){
		if(!tgt)tgt=target;
		if(!tgt)return true;
		blockthingsiterator itt=blockthingsiterator.create(tgt,checkradius);
		while(itt.next()){
			actor it=itt.thing;
			if(
				isfriend(it)||isteammate(it)
			)return false;
		}
		return true;
	}

	// #### E 1 A_LeadTarget1();
	// #### E 3{
	//	A_LeadTarget2(shotspeed:getdefaultbytype(missilename).speed);
	//	A_DropAdjust(missilename);
	// }
	// #### F 1 bright light("SHOT") A_MarineShot(missilename);
	// maybe generalize this later?
	vector2 leadoldaim;vector2 leadaim;
	vector2 A_LeadTarget1(){
		if(!target){
			leadoldaim=(angle,pitch);
			return leadoldaim;
		}
		vector2 aimbak=(angle,pitch);
		A_FaceTarget(0,0);
		leadoldaim=(angle,pitch);
		angle=aimbak.x;pitch=aimbak.y;
		return leadoldaim;
	}
	vector2 A_LeadTarget2(
		double dist=-1,
		double shotspeed=20,
		vector2 oldaim=(-1,-1),
		double adjusttics=1
	){
		if(!target||!shotspeed)return(0,0);

		//get current angle for final calculation
		vector2 aimbak=(angle,pitch);

		//distance defaults to distance from target
		if(dist<0)dist=distance3d(target);

		//figure out how many tics to adjust
		double ticstotarget=dist/shotspeed+adjusttics;
		if(ticstotarget<1.)return(0,0);

		//retrieve result from A_LeadTarget1
		if(oldaim==(-1,-1))oldaim=leadoldaim;

		//check the aim to change and revert immediately
		//I could use angleto but the pitch calculations would be awkward
		A_FaceTarget(0,0);
		vector2 aimadjust=(
			deltaangle(oldaim.x,angle),
			deltaangle(oldaim.y,pitch)
		);

		//something fishy is going on
		if(abs(aimadjust.x)>45)return (0,0);

		//multiply by tics
		aimadjust*=ticstotarget;

		//apply and return
		angle=aimbak.x+aimadjust.x;pitch=aimbak.y+aimadjust.y;
		return aimadjust;
	}
	void A_DropAdjust(class<actor> missiletype,double dist=0,bool userocket=false){
		double dist=max(1,(target?distance3d(target):0));
		double spd=getdefaultbytype(missiletype).speed;
		if(getdefaultbytype(missiletype).gravity&&dist>spd){
			if(userocket&&missiletype is "GyroGrenade")spd*=6.4; //used only for the gyrorockets
			int ticstotake=dist/spd;
			int dropamt=0;
			for(int i=1;i<=ticstotake;i++){
				dropamt+=i;
			}
			pitch-=min(atan(dropamt/dist),10);
		}

		//because we don't shoot from height 32 but 42
		if(dist>0)pitch+=atan(10/dist);
	}
	actor A_MarineShot(class<actor> missiletype,bool userocket=false){
		actor mmm=spawn(missiletype,pos);
		mmm.pitch=pitch+frandom(0,spread)-frandom(0,spread);
		mmm.angle=angle+frandom(0,spread)-frandom(0,spread);
		mmm.addz(height-6);
		mmm.target=self;

		//one very special case
		if(userocket&&mmm is "GyroGrenade")gyrogrenade(mmm).isrocket=true;
		else userocket=false;

		if(!(mmm is "HDBullet")&&!(mmm is "SlowProjectile"))mmm.A_ChangeVelocity(
			mmm.speed*cos(mmm.pitch),0,mmm.speed*sin(mmm.pitch),CVF_RELATIVE
		);
		return mmm;
	}
	enum HDMarineStats{
		HDMW_ZM66=0,
		HDMW_HUNTER=1,
		HDMW_SMG=2,
		HDMW_ROCKET=3,
	}
	states{
	spawn:
		PLAY A 0;
		#### AA 4{hdmobai.wander(self);}
		#### A 0 A_Look();
		#### BB 4{hdmobai.wander(self);}
		#### A 0 A_Look();
		#### CC 4{hdmobai.wander(self);}
		#### A 0 A_Look();
		#### DD 4{hdmobai.wander(self);}
		#### A 0 HealThing(1);
	spawn2:
		#### A 0 A_Jump(60,"spawn");
		#### A 0{angle+=random(-30,30);}
		#### EEE 3 A_Look();
		#### A 0{angle+=random(-30,30);}
		#### EEE 3 A_Look();
		#### A 0 A_Jump(60,"spawn");
		loop;
	see:
		#### AABBCCDD 2{hdmobai.chase(self);}
		#### A 0{
			if(health<40)damagemobj(self,self,1,"bleedout",DMG_NO_PAIN);
			else HealThing(1);
			A_SetSolid();
		}
		#### A 0{
			if(
				(
					//must reload
					!gunloaded
					&&(!pistolloaded||!random(0,3))
				)||(
					//may reload
					!random(0,7)
					&&(
						gunloaded<1
						||pistolloaded<1
						||(wep!=HDMW_ROCKET&&!glloaded)
					)
					&&(!target||!checksight(target))
				)
			)setstatelabel("reload");
		}
		#### E 0 A_JumpIfTargetInLOS("see");
	spwander:
		#### E 0 A_ClearTarget();
		#### AA 3{hdmobai.wander(self);}
		#### A 0 A_Chase(flags:CHF_DONTMOVE);
		#### BB 3{hdmobai.wander(self);}
		#### A 0 A_Chase(flags:CHF_DONTMOVE);
		#### CC 3{hdmobai.wander(self);}
		#### A 0 A_Chase(flags:CHF_DONTMOVE);
		#### DD 3{hdmobai.wander(self);}
		#### A 0 HealThing(1);
		#### E 0 A_Jump(128,"spwander");
	spwander2:
		#### A 0 A_Look();
		#### A 0 A_Jump(4,"spawn");
		#### A 0{angle+=random(-30,30);}
		#### EEE 2 A_Chase(flags:CHF_DONTMOVE);
		#### A 0{angle+=random(-30,30);}
		#### EEE 2 A_Chase(flags:CHF_DONTMOVE);
		#### A 0 A_Jump(60,"spwander");
		#### E 0 A_JumpIfTargetInLOS("see");
		goto spwander;

	missile:
		#### A 0{
			if(
				(
					//must reload
					!gunloaded
					&&!pistolloaded
				)
			)setstatelabel("reload");
		}
		#### A 0 A_JumpIfTargetInLOS(3,120);
		#### CD 2 A_FaceTarget(40);
	missile2:
		#### A 0{
			double dist=distance3d(target);
			if(dist<500)turnamount=50;
			else if(dist<1200)turnamount=30;
			else turnamount=10;
		}
	turntoaim:
		#### E 2 A_FaceTarget(turnamount,turnamount);
		#### A 0 A_JumpIfTargetInLOS(1);
		goto see;
		#### A 0 A_JumpIfTargetInLOS(1,10);
		loop;
		#### A 0 A_FaceTarget(turnamount,turnamount);
		#### E 1 A_SetTics(random(0,80/turnamount));
		#### E 0{
			spread=turnamount*0.02+min(timesdied,15);
			A_SetTics(10/spread);
		}goto shoot;

	shoot:
		#### E 4{	
			if(!target)return;
			if(checksight(target)&&target.health<1){
				target=null;
				setstatelabel("noshot");
				return;
			}
			A_FaceTarget(0,0); //can't lead without this
			double dist=distance3d(target);
			if(
				!hdmobai.tryshoot(self,
					range:min(1024,dist*0.5),
					pradius:min(target.radius*0.6,4),
					pheight:min(target.height*0.6,4)
				)
			){
				return;
			}

			//grenade
			if(
				dist<5000
				&&dist>600
				&&(
					(wep==HDMW_ROCKET&&gunloaded>0)
					||(glloaded&&!random(0,10))
				)
			){
				setstatelabel("shootgl");
				return;
			}

			//pistol
			if(
				gunloaded<1
				||(wep==HDMW_ROCKET&&dist<600)
			){
				if(pistolloaded<1||!random(0,3))setstatelabel("ohforfuckssake");
				else setstatelabel("shootpistol");
				return;
			}

			//all other guns
			if(wep==HDMW_SMG)setstatelabel("shootsmg");
			else if(wep==HDMW_HUNTER)setstatelabel("shootsg");
			else if(wep==HDMW_ZM66)setstatelabel("shootzm66");
			else if(wep==HDMW_ROCKET)setstatelabel("shootrl");
		}goto see;
	shootzm66:
		#### E 1;
		#### E 1 A_LeadTarget1();
		#### E 1{
			class<actor> mn="HDBullet426";
			A_LeadTarget2(shotspeed:getdefaultbytype(mn).speed,adjusttics:1);
			A_DropAdjust(mn);
			gunspent=min(gunloaded,randompick(1,1,1,1,1,3));
		}
	firezm66:
		#### FFF 1 bright light("SHOT"){
			if(gunloaded<1||gunspent<1)setstatelabel("firezm66end");
			gunloaded--;gunspent--;
			A_PlaySound("weapons/rifle",CHAN_WEAPON);
			A_MarineShot("HDBullet426");
		}
	firezm66end:
		#### E 2;
		#### E 0 A_JumpIf(gunloaded>0&&random(0,2),"firezm66");
		#### E 0 A_MonsterRefire(20,"see");
		goto missile;
	shootsmg:
		#### E 1 A_LeadTarget1();
		#### E 1{
			class<actor> mn="HDBullet9";
			A_LeadTarget2(shotspeed:getdefaultbytype(mn).speed,adjusttics:1);
			A_DropAdjust(mn);
		}
	firesmg:
		#### F 1 bright light("SHOT"){
			gunloaded--;
			A_PlaySound("weapons/pistol",CHAN_WEAPON,0.7);
			actor bbb=A_MarineShot("HDBullet9");
			bbb.A_ChangeVelocity(cos(bbb.pitch)*20,0,-sin(bbb.pitch)*20,CVF_RELATIVE);
		}
		#### E 2 A_SpawnItemEx("HDSpent9mm",
			cos(pitch)*10,0,height-8-sin(pitch)*10,
			vel.x,vel.y,vel.z,
			0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
		);
		#### E 0 A_JumpIf(gunloaded>0&&random(0,2),"firesmg");
		#### E 0 A_MonsterRefire(20,"see");
		goto missile;
	shootsg:
		#### E 1;
		#### E 1 A_LeadTarget1();
		#### E 1{
			class<actor> mn="HDBullet00b";
			A_LeadTarget2(shotspeed:getdefaultbytype(mn).speed,adjusttics:1);
			A_DropAdjust(mn);
		}
		#### F 1 bright light("SHOT"){
			gunloaded--;
			A_PlaySound("weapons/hunter",CHAN_WEAPON);
			A_MarineShot("HDBullet00b");
		}
		#### E 2{
			if(random(0,4)){
				gunspent=0;
				A_SpawnItemEx("HDSpentShell",
					cos(pitch)*8,0,height-7-sin(pitch)*8,
					vel.x+cos(pitch)*cos(angle-random(86,90))*6,
					vel.y+cos(pitch)*sin(angle-random(86,90))*6,
					vel.z+sin(pitch)*random(5,7),0,
					SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}else gunspent=1;
		}
		#### E 1{
			if(gunspent){
				gunspent=0;
				A_PlaySound("weapons/shotgr",5);
				A_SetTics(random(4,6));
				A_SpawnItemEx("HDSpentShell",
					cos(pitch)*8,0,height-7-sin(pitch)*8,
					vel.x+cos(pitch)*cos(angle-random(86,90))*6,
					vel.y+cos(pitch)*sin(angle-random(86,90))*6,
					vel.z+sin(pitch)*random(5,7),0,
					SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}
		}
		#### E 0 A_MonsterRefire(20,"see");
		goto missile;
	shootrl:
		#### E 2;
		#### E 1{
			if(A_CheckBlast(target))A_LeadTarget1();
			else setstatelabel("noshot");
		}
		#### E 1{
			class<actor> mn="GyroGrenade";
			A_LeadTarget2(shotspeed:getdefaultbytype(mn).speed*6.4,adjusttics:1);
			A_DropAdjust(mn,userocket:true);
		}
		#### F 2 bright light("SHOT"){
			if(wep==HDMW_ROCKET)gunloaded--;else glloaded=false;
			A_PlaySound("weapons/rockignite",CHAN_WEAPON);
			A_PlaySound("weapons/bronto",5);
			A_MarineShot("GyroGrenade",userocket:true);
		}
		#### E 5{
			A_Recoil(-4);
			A_PlaySound("weapons/rocklaunch",CHAN_AUTO,0.6);
		}
		#### E 0 A_PlaySound("weapons/shotgr",5);
		goto see;
	shootgl:
		#### E 1{
			if(A_CheckBlast(target))A_LeadTarget1();
			else setstatelabel("noshot");
		}
		#### E 2{
			class<actor> mn="GyroGrenade";
			A_LeadTarget2(shotspeed:getdefaultbytype(mn).speed,adjusttics:2);
			A_DropAdjust(mn);
		}
		#### F 1 bright{
			if(wep==HDMW_ROCKET)gunloaded--;else glloaded=false;
			A_PlaySound("weapons/grenadeshot",CHAN_WEAPON);
			A_MarineShot("GyroGrenade");
		}
		#### E 4;
		#### E 0 A_MonsterRefire(20,"see");
		goto missile;

	shootpistol:
		#### E 1 A_LeadTarget1();
		#### E 1{
			class<actor> mn="HDBullet9";
			A_LeadTarget2(shotspeed:getdefaultbytype(mn).speed,adjusttics:random(1,4));
			A_DropAdjust(mn);
		}
		#### F 1 bright light("SHOT"){
			pistolloaded--;
			A_PlaySound("weapons/pistol",CHAN_WEAPON);
			A_MarineShot("HDBullet9");
		}
		#### E random(1,4) A_SpawnItemEx("HDSpent9mm",
			cos(pitch)*12,0,height-7-sin(pitch)*12,
			vel.x,vel.y,vel.z,
			0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
		);
		#### E 0 A_MonsterRefire(20,"see");
		goto missile;
	noshot:
		#### E 6;
		goto see;

	ohforfuckssake:
		#### E 4 A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);

	reload:
		#### A 0{
			if(
				pistolloaded<0
				||wep==HDMW_SMG
				||wep==HDMW_ZM66
			)setstatelabel("reloadmag");
			else if(gunloaded<gunmax){
				if(wep==HDMW_HUNTER)setstatelabel("reloadsg");
				if(wep==HDMW_ROCKET)setstatelabel("reloadrl");
			}else if(!glloaded&&wep!=HDMW_ROCKET)setstatelabel("reloadgl");
		}goto see;
	reloadsg:
		#### A 0 A_PlaySound("weapons/huntopen",CHAN_WEAPON);
		#### AB 3{hdmobai.chase(self,"melee",null,true);}
	reloadsgloop:
		#### A 0 A_PlaySound("weapons/pocket",5);
		#### CDAB 3{hdmobai.chase(self,"melee",null,true);}
		#### BBC 3{
			if(!random(0,1))hdmobai.chase(self,"melee",null,true);
			if(gunloaded<gunmax){
				gunloaded++;
				A_PlaySound("weapons/sshotl",CHAN_WEAPON);
			}
		}
		#### A 0 A_JumpIf(gunloaded<gunmax,"reloadsgloop");
		goto see;
	reloadrl:
		#### A 0 A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		#### AB 3{hdmobai.chase(self,"melee",null,true);}
	reloadrlloop:
		#### A 0 A_PlaySound("weapons/pocket",5);
		#### CDAB 3{hdmobai.chase(self,"melee",null,true);}
		#### C 4{
			if(!random(0,3))hdmobai.chase(self,"melee",null,true);
			if(gunloaded<gunmax){
				gunloaded++;
				A_PlaySound("weapons/rockreload",CHAN_WEAPON);
			}
		}
		#### A 0 A_JumpIf(gunloaded<gunmax,"reloadsgloop");
		goto see;
	reloadmag:
		#### AB 3{hdmobai.chase(self,"melee",null,true);}
		#### C 3{
			hdmobai.chase(self,"melee",null,true);
			A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
			A_PlaySound("weapons/rifleload",5);
			class<actor> oldthing="";
			if(pistolloaded<1&&gunloaded<1)oldthing="HDPistolEmptyMag";
			else if(wep==HDMW_SMG)oldthing="HDSMGEmptyMag";
			else if(wep==HDMW_ZM66)oldthing="HD4mmMagEmpty";
			if(oldthing){
				actor ooo=spawn(oldthing,pos+(0,0,40));
				ooo.vel+=vel;
			}
		}
		#### DABC 3{hdmobai.chase(self,"melee",null,true);}
		#### D 2 A_PlaySound("weapons/rifleload",5);
		#### A 3{
			A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
			hdmobai.chase(self,"melee",null);
			if(pistolloaded<1&&gunloaded<1)pistolloaded=15;
			else gunloaded=gunmax;
		}
		goto see;
	reloadgl:
		#### A 0 A_PlaySound("weapons/grenopen",CHAN_WEAPON);
		#### ABCD 3{hdmobai.chase(self,"melee",null,true);}
		#### AB 2 A_PlaySound("weapons/rockreload",5);
		#### C 3{
			A_PlaySound("weapons/grenopen",CHAN_WEAPON);
			hdmobai.chase(self,"melee",null);
			glloaded=1;
		}
		#### D 4;
		goto see;

	melee:
		#### C 7 A_FaceTarget();
		#### D 2;
		#### E 6 A_CustommeleeAttack(
			random(10,100),"weapons/smack","","none",randompick(0,0,0,1)
		);
		#### ABCD 2{
			if(target&&!target.bcorpse&&distance3d(target)-target.radius<meleerange){
				setstatelabel("melee");
				return;
			}
			if(gunloaded>0){
				setstatelabel("missile");
				return;
			}
			A_FaceTarget(0,0);
			A_Recoil(-3);
		}goto see;
	pain:
		#### G 3;
		#### G 3 A_Pain();
		#### G 0 A_Jump(100,"see");
		#### AB 2 A_FaceTarget(50,50);
		#### CD 3 A_FastChase();
		#### G 0 A_CPosRefire();
		goto missile;

	death.bleedout:
		#### H 5{
			hdmobai.corpseflags(self);
			bpushable=false;
			A_SpawnItemEx("tempshield",0,0,0,vel.x,vel.y,vel.z,0,SXF_SETMASTER);
		}
		#### I 5;
		goto deathpostscream;
	death:
		#### H 5{
			hdmobai.corpseflags(self);
			bpushable=false;
			A_SpawnItemEx("tempshield2",0,0,0,vel.x,vel.y,vel.z,0,SXF_SETMASTER);
		}
		#### I 5 A_Scream();
	deathpostscream:
		#### J 5 noblockwepdrop();
		#### K 5;
		goto dead;

	dead:
		#### K 3 canraise A_JumpIf(abs(vel.z)<2.,1);
		loop;
		#### LMN 5 canraise A_JumpIf(abs(vel.z)>=2.,"dead");
		wait;
	raise:
		#### A 0{
			hdmobai.corpseflags(self);
			A_SetSolid();
			if(countinv("IsGibbed"))setstatelabel("raisegibbed");
			A_TakeInventory("IsGibbed");
		}
		#### MMK 7 A_SpawnItemEx("MegaBloodSplatter",0,0,4,
			vel.x,vel.y,vel.z,0,
			SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
		);
		#### JHE 4;
		#### H 0{
			if(!random(0,15+timesdied))return;
			else if(!random(0,10-timesdied))A_Die("raisebotch");
			else{
				speed=max(1,speed-random(0,1));
				health=max(50,health-random(0,3*timesdied));
				seesound="grunt/sight";
				painsound="grunt/pain";
				deathsound="grunt/death";
				A_PlaySound(seesound,CHAN_VOICE);
			}
		}goto see;

	xdeath:
		#### O 5{
			hdmobai.corpseflags(self);
			bpushable=false;
			A_GiveInventory("IsGibbed");
			A_UnsetShootable(); 
		}
		#### P 5{
			A_SpawnItemEx("MegaBloodSplatter",0,0,34,flags:SXF_NOCHECKPOSITION);
			A_XScream();
		}
		#### Q 5{
			A_SpawnItemEx("MegaBloodSplatter",0,0,34,flags:SXF_NOCHECKPOSITION);
			noblockwepdrop();
		}
		#### Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,flags:SXF_NOCHECKPOSITION);
		#### RSTUV 5;
		#### W -1 canraise;
		stop;
	death.raisebotch:
		#### O 0{hdmobai.corpseflags(self);}
	xdeathbrewtlelulz:
		#### O 5{
			A_NoBlocking();
			A_UnsetShootable();
			A_SpawnItemEx("MegaBloodSplatter",0,0,34,flags:SXF_NOCHECKPOSITION);
			A_GiveInventory("IsGibbed");
		}
		#### P 5 A_XScream();
		#### QR 5 A_SpawnItemEx("MegaBloodSplatter",0,0,34,flags:SXF_NOCHECKPOSITION);
		#### STUV 5;
		#### W -1 canraise;
		stop;
	raisegibbed:
		#### W 0 A_JumpIf((random(1,12)-timesdied)<5,"RaiseZombie");
		#### WW 8 A_SpawnItemEx("MegaBloodSplatter",
			0,0,4,vel.x,vel.y,vel.z,
			0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
		);
		#### VUT 7;
		#### SRQ 5;
		#### POH 4;
		#### I 1 A_Die("raisedrop");
	death.raisedrop:
		#### H 5{
			timesdied+=4;
			hdmobai.corpseflags(self);
			bpushable=false;
			A_SpawnItemEx("tempshield2",0,0,0,vel.x,vel.y,vel.z,0,SXF_SETMASTER);
		}
		#### IJK 5;
		goto dead;
	raisezombie:
		#### U 4{
			A_UnsetShootable();
			A_SpawnItemEx("MegaBloodSplatter",0,0,4,
				vel.x,vel.y,vel.z+3,0,
				SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
			);
		}
		#### U 8;
		#### T 4;
		#### T 2 A_PlaySound("weapons/bigcrack",6);
		POSS S 2 A_PlaySound("misc/wallchunks",7);
		POSS AAAAA 0 A_SpawnItemEx("HugeWallChunk",0,0,40,random(4,6),0,random(-2,7),random(1,360));
		#### SRQ 6;
		#### PONMH 4;
		#### IJKL 4;
		#### M 0{spawn("DeadZombieStormtrooper",pos);}
		stop;
	}
}

/*

class Rifleman:HDMarine{default{accuracy HDMW_ZM66+1;}}
class BlackRifleman:HDMarine{default{accuracy HDMW_ZM66+1;stamina 3;}}
class BrownRifleman:HDMarine{default{accuracy HDMW_ZM66+1;stamina 2;}}
class WhiteRifleman:HDMarine{default{accuracy HDMW_ZM66+1;stamina 1;}}
class RifleFistman:Rifleman replaces MarineFist{}
class RifleChaingunman:Rifleman replaces MarineChaingun{}

class Enforcer:HDMarine{default{accuracy HDMW_HUNTER+1;}}
class BlackEnforcer:HDMarine{default{accuracy HDMW_HUNTER+1;stamina 3;}}
class BrownEnforcer:HDMarine{default{accuracy HDMW_HUNTER+1;stamina 2;}}
class WhiteEnforcer:HDMarine{default{accuracy HDMW_HUNTER+1;stamina 1;}}
class EnforcerShot:Enforcer replaces MarineShotgun {}
class EnforcerSuperShot:Enforcer replaces MarineSSG {}
class EnforcerNoShot:Enforcer replaces MarineBerserk {}

class Infiltrator:HDMarine{default{accuracy HDMW_SMG+1;}}
class BlackInfiltrator:HDMarine{default{accuracy HDMW_SMG+1;stamina 3;}}
class BrownInfiltrator:HDMarine{default{accuracy HDMW_SMG+1;stamina 2;}}
class WhiteInfiltrator:HDMarine{default{accuracy HDMW_SMG+1;stamina 1;}}
class InfiltratorPistol:Infiltrator replaces MarinePistol{}
class InfiltratorChainsaw:Infiltrator replaces MarineChainsaw{}

class Rocketeer:HDMarine{default{accuracy HDMW_ROCKET+1;}}
class BlackRocketeer:HDMarine{default{accuracy HDMW_ROCKET+1;stamina 3;}}
class BrownRocketeer:HDMarine{default{accuracy HDMW_ROCKET+1;stamina 2;}}
class WhiteRocketeer:HDMarine{default{accuracy HDMW_ROCKET+1;stamina 1;}}
class RRocketeer:Rocketeer replaces MarineRocket{}
class BFuglyteer:Rocketeer replaces MarineBFG{}
class Plasmateer:Rocketeer replaces MarinePlasma{}
class Railgunteer:Rocketeer replaces MarineRailgun{}


class Tango:HDMarine{default{-friendly}}
class BlackTango:Tango{default{stamina 3;}}
class BrownTango:Tango{default{stamina 2;}}
class WhiteTango:Tango{default{stamina 1;}}

class BlackRifleTango:Tango{default{accuracy HDMW_ZM66+1;stamina 3;}}
class BrownRifleTango:Tango{default{accuracy HDMW_ZM66+1;stamina 2;}}
class WhiteRifleTango:Tango{default{accuracy HDMW_ZM66+1;stamina 1;}}

class BlackShotTango:Tango{default{accuracy HDMW_HUNTER+1;stamina 3;}}
class BrownShotTango:Tango{default{accuracy HDMW_HUNTER+1;stamina 2;}}
class WhiteShotTango:Tango{default{accuracy HDMW_HUNTER+1;stamina 1;}}

class BlackSMGTango:Tango{default{accuracy HDMW_SMG+1;stamina 3;}}
class BrownSMGTango:Tango{default{accuracy HDMW_SMG+1;stamina 2;}}
class WhiteSMGTango:Tango{default{accuracy HDMW_SMG+1;stamina 1;}}

class BlackRocketTango:Tango{default{accuracy HDMW_ROCKET+1;stamina 3;}}
class BrownRocketTango:Tango{default{accuracy HDMW_ROCKET+1;stamina 2;}}
class WhiteRocketTango:Tango{default{accuracy HDMW_ROCKET+1;stamina 1;}}



// ------------------------------------------------------------
// Marine corpse
// ------------------------------------------------------------
class DeadRifleman:HDMarine replaces DeadMarine{
	default{
		translation "ice"; //this should never appear
		-solid
		+shootable
		+ismonster
		+telestomp
		+alwaystelefrag
		mass 200;
		radius 12;
		height 56;
		health 1;
		gibhealth 100;
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_Die("spawndead");
	}
	states{
	spawndead:
		---- A 0{
			A_NoBlocking();
			hdmobai.corpseflags(self);
		}goto dead;
	}
}
class ReallyDeadRifleman:DeadRifleman replaces GibbedMarine{
	states{
	spawndead:
		---- A 0{
			A_NoBlocking();
			hdmobai.corpseflags(self);
		}goto xdead;
	}
}
class DeadRiflemanCrouched:DeadRifleman{
	states{
	spawndead:
		PLYC A;
		goto super::spawndead;
	}
}
class ReallyDeadRiflemanCrouched:ReallyDeadRifleman replaces GibbedMarineExtra{
	states{
	spawndead:
		PLYC A;
		goto super::spawndead;
	}
}



// ------------------------------------------------------------
// Summoned marines - no friendly fire
// ------------------------------------------------------------
class GhostMarine:HDMarine{
	void A_GhostShot(){
		actor p=spawn("GhostBullet",pos+(0,0,height-6));
		p.target=self;p.angle=angle;p.pitch=pitch;
		p.vel+=self.vel;
	}
	default{
		+noblooddecals
		+shootable +noblockmonst +ghost +shadow -solid -pushable
		+nopain +nofear +seeinvisible +nodamage +noclip +nonshootable
		+frightening
		damagefactor "GhostSquadAttack",0;
		maxdropoffheight 40;
		maxstepheight 40;
		health 200000000;
		gibhealth 500;
		renderstyle "add";
		bloodtype "idledummy";
		height 50;
		radius 12;
		speed 8;
		dropitem "SquadSummoner",8;
	}
	states{
	spawn:
		#### E 10 A_Look();
		loop
	see:
		#### AABBCCDD 2 A_Chase();
		#### A 0{
			A_ClearTarget();
			HealThing(1);
			timesdied++;
			if(timesdied>=360)A_Die("fade");
		}loop;
	death.fade:
		#### A 0 A_NoBlocking();
	fade:
		#### ABCD 2{
			A_Wander();
			A_FadeOut(0.1);
		}loop;
	missile:
		#### E 1{
			if(bfriendly)A_AlertMonsters(0,AMF_TARGETEMITTER);
			if(!deathmatch)timesdied=0;
			A_SetTics(random(0,3));
		}
	missile2:
		#### E 7 A_FaceTarget(0,0);
	grenade:
		#### E 12{
			NoiseAlert(0,0);
			double dist=distance3d(target);
			if(
				dist<900
				||dist>2000
				||random(0,2)
			){
				setstatelabel("rifle");
				return;
			}
			A_PlaySound("weapons/grenadeshot",CHAN_WEAPON);
			A_SpawnProjectile("SquadGyroGrenade",44,0,0,0,14);
		}
		#### E 0 A_Jump(256,"see");
	rifle:
		#### E 4 A_JumpIfCloser(600,"riflecqb");
	riflesnipe:
		#### F 1 bright light("SHOT"){
			A_PlaySound("weapons/bigrifle",CHAN_WEAPON);
			pitch+=frandom(-1,1);
			A_GhostShot();
		}
		#### E 2 A_Jump(24,"postsnipe")

		#### F 1 bright light("SHOT"){
			A_PlayWeaponSound("weapons/bigrifle");
			pitch+=frandom(-1,1);
			A_GhostShot();
		}
		#### E 2 A_Jump(168,"postsnipe");

		#### F 1 bright light("SHOT"){
			A_PlayWeaponSound("weapons/bigrifle");
			pitch+=frandom(-1,1);
			A_GhostShot();
		}
		#### E 2 A_Jump(96,"postsnipe");
		#### A 0 A_Jump(256,"postsnipe");
	postsnipe:
		#### E 6;
		#### E 0 A_Jump(256,"see");
	riflecqb:
		#### F 1 bright light("SHOT"){
			A_PlayWeaponSound("weapons/bigrifle");
			pitch+=frandom(-1,1);
			A_GhostShot();
		}
		#### E 2;

		#### F 1 bright light("SHOT"){
			A_PlayWeaponSound("weapons/bigrifle");
			pitch+=frandom(-1,1);
			A_GhostShot();
		}
		#### E 2 A_Jump(32,"postcqb");

		#### F 1 bright light("SHOT"){
			A_PlayWeaponSound("weapons/bigrifle");
			pitch+=frandom(-1,1);
			A_GhostShot();
		}
		#### E 2 A_Jump(96,"postcqb");

		#### F 1 bright light("SHOT"){
			A_PlayWeaponSound("weapons/bigrifle");
			pitch+=frandom(-1,1);
			A_GhostShot();
		}
		#### E 2;
		#### A 0 A_Jump(256,"postcqb");
	postcqb:
		#### E 0 A_Jump(96,3);
		#### E 9 A_CPosRefire();
		#### E 0 A_Jump(256,"missile2");
		#### E 6;
		#### E 0 A_Jump(256,"see");
	melee:
		#### E 0{
			if(!deathmatch)timesdied=0;
		}
		#### C 8 A_FaceTarget(0,0);
		#### D 4;
		#### E 1 A_CustomMeleeAttack((1),"weapons/smack","","GhostSquadAttack");
		#### E 3{if(!random(0,11))target.health++;}

		#### E 3 A_JumpIfCloser(64,3);
		#### E 4 A_FaceTarget(0,0);
		#### E 0 A_Jump(256,"missile2");

		#### A 4;
		#### E 0 A_Jump(256,"see");

	pain:
		#### G 4;
		#### G 4 A_Pain();
		#### G 0 A_Jump(100,"see");
		#### AB 2 A_FaceTarget(0,0);
		#### CD 3 A_FastChase();
		#### G 0 A_CPosRefire();
		#### E 0 A_Jump(256,"missile");
	death:
	xdeath:
		#### H 6;
		#### I 6 A_Scream();
		#### J 6 A_NoBlocking();
		#### KKKLLLMMM 2 A_FadeOut(0.1);
		#### N 2 A_FadeOut(0.1);
		wait;
	raise:
		stop;
	}
}
class BlackGhostMarine:GhostMarineBase{default{stamina 3;}}
class BrownGhostMarine:GhostMarineBase{default{stamina 2;}}
class WhiteGhostMarine:GhostMarineBase{default{stamina 1;}}

class SquadGyroGrenade:GyroGrenade{
	default{
		 +forcepain +nodamage
		renderstyle "add";
		damagetype "GhostSquadAttack";
	}
	override void ExplodeSlowMissile(line blockingline=null,actor blockingobject=null){
		bmissile=false;
		setstatelabel("death");
	}
	states{
	death:
		TNT1 A 0{gravity=0;}
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx(
			"HugeWallChunk",0,0,0,
			random(-7,7),random(-7,7),random(4,18),
			random(0,360),160
		);
		TNT1 A 1 A_Explode(1,512,0,1,512,
			0,0,none,
			"GhostSquadAttack"
		)
		TNT1 AA 0 A_SpawnItemEx("HDBBWTH",
			random(-1,1),random(-1,1),2,
			flags:SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS
		);
		TNT1 A 2 A_SpawnItemEx("HDBBWTH",zvel:2,
			flags:SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS
		);
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",
			random(-6,6),random(-6,6),1,
			random(-1,4),random(-1,1),0,
			random(-15,15),SXF_NOCHECKPOSITION
		);
		TNT1 A 21{
			A_AlertMonsters();
			spawn("DistantRocket");
			A_Quake(2,21,0,200,"none");
		}stop;
	}
}

class SquadSummoner:HDPickup{
	inventory.maxamount 3
	inventory.interhubamount 3
	inventory.icon PLHELMA0
	inventory.pickupsound "misc/p_pkup"
	inventory.pickupmessage "Picked up a summoning talisman."
	+inventory.fancypickupsound
	+forcexybillboard
	states{
	spawn2:
		PRIF A -1
	use:
		#### A 0{
			A_PlaySound("misc/p_pkup",CHAN_AUTO);
			A_AlertMonsters;
			A_SpawnItemEx("GhostMarine",0,0,0,8,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
			A_SpawnItemEx("GhostMarine",0,0,0,0,5,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
			A_SpawnItemEx("GhostMarine",0,0,0,0,-5,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER);
			A_SpawnItemEx("HDSmoke",0,0,0,8,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("HDSmoke",0,0,0,0,5,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("HDSmoke",0,0,0,0,-5,0,0,SXF_NOCHECKPOSITION);
		}
		#### A 0 A_Jump(256,1,2,3,4)
		#### A 0 A_PrintBold("\cj'They shall stand again and hear there\n\cja horn in the hills ringing.\n\n\cjWhose shall the horn be?'",8)
		stop
		#### A 0 A_PrintBold("\cj'For this war will last through years uncounted\n\n\cjand you shall be summoned once again ere the end.'",8)
		stop
		#### A 0 A_PrintBold("\cj'Faint cries I heard,and dim horns blowing,\n\n\cjand a murmur as of countless far voices:\n\n\n\cjit was like the echo of some forgotten battle\n\n\cjin the Dark Years long ago.'",8)
		stop
		#### A 0 A_PrintBold("\cj'Pale swords were drawn; but I know not\n\n\cjwhether their blades would still bite,\n\n\n\cjfor the Dead needed no longer\n\n\cjany weapon but fear.'",8)
	}
}




class BotBot:HDMarine{
	obituary "%o died."
	+noblockmonst
	+nofear
	+donthurtspecies
	species "Player"
	states{
	death:
		#### A 0{
			A_GiveInventory("BotKill",1,AAPTR_MASTER);
			A_DamageMaster(1000000,"bleedout",DMSS_KILL,"None","None",AAPTR_TARGET,AAPTR_TARGET);
		}goto super::death
	death.bleedout:
		#### A 0{
			A_GiveInventory("BotKill",1,AAPTR_MASTER);
			A_DamageMaster(1000000,"bleedout",DMSS_KILL,"None","None",AAPTR_TARGET,AAPTR_TARGET);
		}goto super::death.bleedout
	xdeath:
		#### A 0{
			A_GiveInventory("BotKill",1,AAPTR_MASTER);
			A_DamageMaster(1000000,"bleedout",DMSS_KILL,"None","None",AAPTR_TARGET,AAPTR_TARGET);
		}goto super::xdeath
	see:
		---- A 0 A_TakeInventory("NotSeen")
		goto super::see
	missile:
		---- A 0 A_TakeInventory("NotSeen")
		goto super::missile
	melee:
		---- A 0 A_TakeInventory("NotSeen")
		goto super::melee
	pain:
		---- A 0 A_TakeInventory("NotSeen")
		goto super::pain
	spawn2:
		---- A 0 A_JumpIf(deathmatch,2)
		---- A 0 A_CheckSight(3)
		---- A 0 A_TakeInventory("NotSeen")
		---- A 0 A_Jump(256,3)
		---- A 0 A_GiveInventory("NotSeen",1)
		---- A 0 A_JumpIfInventory("NotSeen",12,"warpandwait")
		#### A 0 A_Look();
		#### A 0 A_Jump(60,"spawn")
		#### A 0{angle+=random(-30,30))
		#### EEE 3 A_Look();
		#### A 0{angle+=random(-30,30))
		#### EEE 3 A_Look();
		#### A 0 HealThing(2)
		#### A 0 A_Jump(60,"spawn")
		loop
	spwander:
		#### E 0 A_ClearTarget
		---- A 0 A_JumpIf(deathmatch,2)
		---- A 0 A_CheckSight(3)
		---- A 0 A_TakeInventory("NotSeen")
		---- A 0 A_Jump(256,3)
		---- A 0 A_GiveInventory("NotSeen",1)
		---- A 0 A_JumpIfInventory("NotSeen",7,"warpandwait")
		#### AA 3{hdmobai.wander(self);}
		#### A 0 A_Chase(flags:CHF_DONTMOVE);
		#### BB 3{hdmobai.wander(self);}
		#### A 0 A_Chase(flags:CHF_DONTMOVE);
		#### CC 3{hdmobai.wander(self);}
		#### A 0 A_Chase(flags:CHF_DONTMOVE);
		#### DD 3{hdmobai.wander(self);}
		#### A 0 HealThing(3)
		#### E 0 A_Jump(128,"spwander")
		#### E 0 A_Jump(256,"spwander2")
	warpandwait:
		#### A 0 A_ChangeFlag("shootable",0)
		#### A 0 A_ChangeFlag("solid",0)
		#### A 0 A_ChangeFlag("canusewalls",0)
		#### A 0 A_ChangeFlag("noblockmap",1)
		#### A 0 A_ChangeFlag("cannotpush",1)
		#### A 0 A_ChangeFlag("noteleport",1)
		#### A 0 A_ChangeFlag("friendly",0)
		#### A 0 A_ChangeFlag("nofear",0)
		#### A 0 A_ChangeFlag("frightened",1)
		#### A 0 A_Warp(AAPTR_PLAYER1,0,0,0,0,WARPF_TOFLOOR|WARPF_NOCHECKPOSITION)
	warpedwait:
		#### AAAAAA 0{hdmobai.chase(self);}("","")
		#### A 0 A_Jump(4,"warpandwait")
		#### A 0 A_Jump(4,2)
		#### A 1 A_CheckSight(1)
		loop
		#### A 0 A_ChangeFlag("shootable",1)
		#### A 0 A_ChangeFlag("solid",1)
		#### A 0 A_ChangeFlag("friendly",1)
		#### A 0 A_ChangeFlag("nofear",1)
		#### A 0 A_ChangeFlag("frightened",0)
		#### A 0 A_ChangeFlag("canusewalls",1)
		#### A 0 A_ChangeFlag("noblockmap",0)
		#### A 0 A_ChangeFlag("cannotpush",0)
		#### A 0 A_ChangeFlag("noteleport",0)
		#### A 0 A_TakeInventory("NotSeen",100)
		#### A 0 A_Stop
		goto spawn
	}
}
class BotBotF:BotBot{
	PainSound "marinef/pain"
	DeathSound "marinef/death"
}



*/