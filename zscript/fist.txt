// ------------------------------------------------------------
//   Fist
// ------------------------------------------------------------
class FistTurner:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(radius<50){
				A_SetAngle(angle+random(-4,4)*30,SPF_INTERPOLATE);
				A_SetPitch(randompick(-90,90),SPF_INTERPOLATE);
				if(mass<50)vel.z+=0.1;
			}
		}fail;
	}
}
class HDNullPuff:HDFistPuff{
	default{
		+puffonactors
		stamina 0;
	}
	states{
	spawn:
		TNT1 A 0 nodelay;
		stop;
	}
}
class HDZerkGibber:FastProjectile{
	default{
		+hittarget
		projectile;speed 1;height 2;radius 2;
	}
	states{
	spawn:
		TNT1 A 0 nodelay{vel*=64;}
		TNT1 A 1;
		stop;
	xdeath:
		TNT1 A 1 A_GiveInventory("ZerkGib",1,AAPTR_TARGET);
		stop;
	}
}
class Ring:HDWeapon replaces Fist{
	int targettimer;
	int targethealth;
	int targetspawnhealth;
	bool flicked;
	default{
		+WEAPON.WIMPY_WEAPON +WEAPON.MELEEWEAPON +WEAPON.NOALERT +WEAPON.NO_AUTO_SWITCH
		+forcepain
		obituary "%o made %k take the kid gloves off.";
		attacksound "*fist";
		weapon.selectionorder 100;
		weapon.kickback 120;
		weapon.bobstyle "Alpha";
		weapon.bobspeed 2.6;
		weapon.bobrangex 0.1;
		weapon.bobrangey 0.5;
		weapon.slotnumber 1;
	}
	override inventory CreateTossable(int amount){
		owner.A_GiveInventory("DropMisc");
		owner.A_GiveInventory("Ring");
		owner.A_SelectWeapon("Ring");
		return null;//super.createtossable(amount);
	}
	override void DoEffect(){
		super.DoEffect();
		if(targettimer<70)targettimer++;else{
			tracer=null;
			targettimer=0;
			targethealth=0;
		}
	}
	action void A_CheckGender(statelabel st){
		if(player){
			int gnd=player.getgender();
			if(!gnd)gnd=getspriteindex("PUNGA0");
			else if(gnd==1)gnd=getspriteindex("PUNFA0");
			else gnd=getspriteindex("TNT1A0");
			player.findPSprite(PSP_WEAPON).sprite=gnd;
		}
	}
	action void HDPunch(int dmg){
		actor a;int b;
		[a,b]=LineAttack(angle,48,pitch,0,"none",
			"HDFistPuncher",flags:LAF_NORANDOMPUFFZ|LAF_OVERRIDEZ,
			offsetz:height-12
		);
		if(!a)return;

		//actual puff effect if the shot connects
		LineAttack(angle,48,pitch,0,"none",
			countinv("PowerStrength")?"BulletPuffBig":"BulletPuffSmall",
			flags:LAF_NORANDOMPUFFZ|LAF_OVERRIDEZ,
			offsetz:height-12
		);

		if(!a.tracer)return;
		actor punchee=a.tracer;

		//charge!
		if(invoker.flicked)dmg*=1.5;
		else dmg+=HDMath.TowardsEachOther(self,punchee)*2;

		//come in swinging
		let onr=hdplayerpawn(invoker.owner);
		if(onr){
			int iy=onr.iyaw;
			int ip=onr.ipitch;
			if(abs(ip)>abs(iy))iy=-ip;
			if(iy>0)iy*=100;
			else if(iy<0)iy*=200;
			dmg+=min(abs(iy),dmg*2);

			//need to be well grounded
			if(onr.zat<pos.z)dmg*=0.5;
		}

		//shit happens
		dmg*=frandom(0.6,1.6);


		//other effects
		if(
			onr&&(
				punchee.mass<200
				||(
					punchee.radius*2<punchee.height
					&& a.pos.z>punchee.pos.z+punchee.height*0.6
				)
			)
		){
			if(abs(onr.iyaw)>0.5){
				punchee.A_SetAngle(punchee.angle-onr.iyaw*100,SPF_INTERPOLATE);
			}
			if(abs(onr.ipitch)>0.5){
				punchee.A_SetPitch(punchee.angle+onr.ipitch*100,SPF_INTERPOLATE);
			}
		}
		//headshot lol
		if(
			!punchee.bnopain
			&& punchee.health>0
			&& !(punchee is "KillerBarrel")
			&& punchee.findstate("pain")
			&& a.pos.z>punchee.pos.z+punchee.height*0.75
		){
			if(hd_debug)A_Log("HEAD SHOT");
			punchee.setstatelabel("pain");
			dmg*=frandom(1.1,1.8);
		}

		if(hd_debug){
			string pch="";
			if(punchee.player)pch=punchee.player.getusername();
				else pch=punchee.getclassname();
			A_Log(string.format("Punched %s for %i damage!",pch,dmg));
		}
		if(dmg*2>punchee.health)punchee.A_PlaySound("misc/bulletflesh",CHAN_BODY);  
		punchee.damagemobj(a,self,dmg,"SmallArms0");

		if(!punchee)invoker.targethealth=0;else{
			invoker.targethealth=punchee.health;
			invoker.targetspawnhealth=punchee.spawnhealth();
			invoker.targettimer=0;
		}
	}
	states{
	female:
		PUNF A 0;goto nope;
	male:
		PUNG A 0;goto nope;
	ready:
		TNT1 A 1{
			A_WeaponReady(WRF_ALLOWRELOAD);
			invoker.flicked=false;
		}goto readyend;
	reload:
		TNT1 A 0 A_CheckGender("flick");
	flick:
		#### A 1 offset(0,50){
			if(floorz==pos.z)A_Recoil(-3);
		}
		#### A 1 offset(0,36);
		#### A 0 A_JumpIfInventory("PowerStrength",1,"ZerkFlick");
		#### AAAAAAA 0 A_CustomPunch((1),1,CPF_PULLIN,"HDNullPuff",36);
		goto flickend;
	zerkflick:
		#### AAAAAAA 0 A_CustomPunch((random(1,3)),1,CPF_PULLIN,"HDNullPuff",36);
	flickend:
		#### AA 1 offset(0,38){invoker.flicked=true;}
		#### A 1 offset(0,42);
		#### A 1 offset(0,50);
		goto fire;
	fire:
	hold:
	althold:
		TNT1 A 0 A_CheckGender("startfire");
	startfire:
		#### A 0 A_JumpIfInventory("PowerStrength",1,"zerkpunch");
		goto punch;
	punch:
		#### B 1 offset(0,32);
		#### D 0 HDPunch(12);
		#### D 6;
		#### CB 3;
		TNT1 A 3;
		TNT1 A 1 A_ReFire();
		goto ready;
	zerkpunch:
		#### D 0 A_Recoil(-1);
		#### D 0 HDPunch(invoker.flicked?140:100);
		#### D 3;
		#### CB 1;
		TNT1 A 5;
		TNT1 A 2 A_ReFire();
		goto ready;
	altfire:
		#### A 1 offset(0,36);
		#### A 1 offset(0,50);
		TNT1 A 2 A_CheckFloor("lunge");
		goto kick;
	lunge:
		TNT1 A 0{
			hdplayerpawn hdp=hdplayerpawn(self);
			if(hdp){
				if(hdp.fatigue>=30){setweaponstate("hold");return;}
				else hdp.fatigue+=3;
			}
			A_Recoil(-4);
		}
		TNT1 AA 1{
			if(countinv("PowerStrength"))A_Recoil(-random(12,24));
		}
		TNT1 A 1 A_Recoil(-4);
	kick:
		TNT1 A 13{
			if(hdplayerpawn(self))hdplayerpawn(self).fatigue+=2;
			A_SpawnItemEx("KickProjectile",0,0,4,vel.x,vel.y,vel.z,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
			if(countinv("PowerStrength"))A_SetTics(8);
		}
		PUNF A 0 A_Refire();
		goto ready;
	spawn:
		TNT1 A 1;
		stop;
	}
}


class HDFistPuncher:IdleDummy{
	default{
		+bloodlessimpact +nodecal +hittracer +puffonactors
		stamina 1;
	}
}
class HDFistPuff:BulletPuffMedium{
	default{
		+bloodlessimpact +hittracer +puffonactors
		stamina 6;scale 0.5;
	}
	states{
	xdeath:
		TNT1 A 6{
			if(hd_debug)sprite=(GetSpriteIndex("BAL2A0"));
			A_PlaySound("misc/punch");
			if(tracer)tracer.A_GiveInventory("FistTurner");
		}stop;
	}
}
class HDZerkPuff:HDFistPuff{
	default{
		-bloodlessimpact -nodecal
		decal "BulletChipSmall";
	}
}



