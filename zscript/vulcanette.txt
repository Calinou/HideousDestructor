// ------------------------------------------------------------
// Vulcanette
// ------------------------------------------------------------
enum vulcstatus{
	VULCF_FAST=1,
	VULCF_SPINNINGFAST=2,
	VULCF_JUSTUNLOAD=4,
	VULCF_LOADCELL=8,

	VULCS_BATTERY=1,
	VULCS_ZOOM=2,
	VULCS_HEAT=3,
	VULCS_CHANNEL=4,
};
class Vulcanette:HDWeapon{
	default{
		//$Category "Weapons/Hideous Destructor"
		//$Title "Vulcanette"
		//$Sprite "VULCA0"

		scale 0.8;
		inventory.pickupmessage "You got the Vulcanette! Now it's coward killing time!";
		weapon.selectionorder 50;
		weapon.slotnumber 4;
		weapon.ammotype1 "ZM66RifleMags";
		weapon.ammouse 0;
		weapon.kickback 24;
		weapon.bobrangex 0.3;
		weapon.bobrangey 1;
		weapon.bobspeed 1.2;
		weapon.bobstyle "normal";
		obituary "%o met the budda-budda-budda on the street,and %k killed %h.";
	}
	override void tick(){
		super.tick();
		drainheat(3,18);
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(owner&&owner.player){
			if(cvar.getcvar("hd_vulcsuper",owner.player).getint())weaponstatus[0]|=VULCF_FAST;
			weaponstatus[VULCS_ZOOM]=clamp(
				cvar.getcvar("hd_vulczoom",owner.player).getint(),0,27
			);
		}
	}
	states{
	select0:
		VULG A 0{
			A_Overlay(2,"droop");
		}goto select0bfg;
	deselect0:
		VULG A 0;
		goto deselect0bfg;

	droop:
		TNT1 A 1{
			if(pitch<5&&(!gunbraced())){
				if(countinv("IsMoving")>2 && countinv("PowerStrength")<1){    
					A_MuzzleClimb(frandom(-0.1,0.1),
						frandom(0.1,clamp(1-pitch,0.1,0.3)));
				}else{
					A_MuzzleClimb(frandom(-0.06,0.06),
						frandom(0.1,clamp(1-pitch,0.06,0.12)));
				}
			}
		}loop;


	zoom:
	zoomadjust:
		VULG A 1 A_WeaponReady(WRF_NOFIRE);
		VULG A 0{
			if(!gunbraced())A_SetInventory("WeaponBusy",1);
			if(PressingFire()||PressingAltfire())
				setweaponstate("zoomclick");
			else if(PressingZoom())
				setweaponstate("zoomadjust");
			else setweaponstate("nope");
		}
	zoomclick:
		VULG A 0{
			int iws=invoker.weaponstatus[VULCS_ZOOM]+1;
			if(PressingAltfire())iws-=2;
			invoker.weaponstatus[VULCS_ZOOM]=clamp(iws,0,27);
		}goto zoomadjust;
	zoomclicked:
		VULG A 1{
			A_WeaponReady(WRF_NOFIRE);
			if(!PressingFire()&&!PressingAltfire())
			setweaponstate("zoomadjust");
		}goto zoomclicked;

	altfire:
	firemode:
		VULG A 1{
			invoker.weaponstatus[0]^=VULCF_FAST;
			A_WeaponReady(WRF_NOFIRE);
		}goto nope;

	ready:
		VULG A 1{
			A_SetCrosshair(21);
			A_WeaponReady(WRF_ALL);
		}
		goto readyend;

	fire:
		VULG A 1{
			A_WeaponReady(WRF_NONE);
			if(invoker.weaponstatus[VULCS_BATTERY]>0 && !random(0,700))invoker.weaponstatus[VULCS_BATTERY]--;    
		}goto shoot;
	hold:
		VULG A 0{
			if(invoker.weaponstatus[VULCS_BATTERY]<1)setweaponstate("nope");
		}
	shoot:
		VULG A 2{
			A_WeaponReady(WRF_NONE);
			if(
				invoker.weaponstatus[VULCS_BATTERY]>0    
				&&!random(0,invoker.weaponstatus[0]&VULCF_SPINNINGFAST?200:210)
			)invoker.weaponstatus[VULCS_BATTERY]--;
			invoker.weaponstatus[0]&=~VULCF_SPINNINGFAST;

			//check speed and then shoot
			if(
				invoker.weaponstatus[0]&VULCF_FAST &&
				invoker.weaponstatus[VULCS_BATTERY]>=4 &&    
				invoker.vulcchamber[0]
			){
				A_SetTics(1);
				invoker.weaponstatus[0]|=VULCF_SPINNINGFAST;
			}else if(invoker.weaponstatus[VULCS_BATTERY]<2){
				A_SetTics(random(3,4));
			}else if(invoker.weaponstatus[VULCS_BATTERY]<3){
				A_SetTics(random(2,3));
			}
			VulcShoot();
			VulcNextRound();
		}
		VULG B 1{
			A_WeaponReady(WRF_NONE);
			//check speed and then shoot
			if(
				invoker.weaponstatus[0]&VULCF_SPINNINGFAST&&
				invoker.vulcchamber[0]
			){
				A_SetTics(1);
				VulcShoot(true);
				VulcNextRound();
			}else if(invoker.weaponstatus[VULCS_BATTERY]<2){
				A_SetTics(random(3,4));
			}else if(invoker.weaponstatus[VULCS_BATTERY]<3){
				A_SetTics(random(2,3));
			}
		}
		VULG B 1{
			A_WeaponReady(WRF_NONE);
			if(invoker.weaponstatus[VULCS_BATTERY]<1)setweaponstate("spindown");
			else A_Refire("holdswap");
		}goto spindown;
	holdswap:
		VULG A 0{
			if(invoker.vulcmag[0]<1){
				VulcNextMag();
				A_PlaySound("weapons/grenopen",CHAN_BODY);
			}
		}goto hold;
	spindown:
		VULG B 0{
			A_ClearRefire();
			if(!(invoker.weaponstatus[0]&VULCF_SPINNINGFAST))setweaponstate("nope");
			invoker.weaponstatus[0]&=~VULCF_SPINNINGFAST;
		}
		VULG AB 1{
			A_WeaponReady(WRF_NONE);
			A_MuzzleClimb(frandom(0.4,0.6),-frandom(0.4,0.6));
		}
		VULG ABAABB 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
		goto ready;


	flash2:
		VULF B 0;
		goto flashfollow;
	flash:
		VULF A 0;
		goto flashfollow;
	flashfollow:
		---- A 0{
			A_MuzzleClimb(0,0,-frandom(0.1,0.3),-frandom(0.4,0.8));
			A_ZoomFactor(0.99,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			HDFlashAlpha(invoker.weaponstatus[VULCS_HEAT]*48);
		}
		---- A 1 bright A_Light2();
		TNT1 A 0{
			A_ZoomFactor(1.0,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
		}goto lightdone;


	reload:
		VULG A 0{
			if(
				//abort if all mag slots taken or no spare ammo
				(
					!countinv("ZM66RifleMags")
					&&!countinv("FourMilAmmo")
				)||(
					invoker.vulcmag[0]>0
					&&invoker.vulcmag[1]>0
					&&invoker.vulcmag[2]>0
					&&invoker.vulcmag[3]>0
					&&invoker.vulcmag[4]>0    
				)
			)setweaponstate("nope");else{
				invoker.weaponstatus[0]&=~(VULCF_JUSTUNLOAD|VULCF_LOADCELL);
				setweaponstate("lowertoopen");
			}
		}
	altreload:
	cellreload:
		VULG A 0{
			if(
				//abort if full battery loaded or no spares
				(
					invoker.weaponstatus[VULCS_BATTERY]>=20    
				)||(
					!countinv("HDCellPacks")
					&&!countinv("HDCellAmmo")
				)
			)setweaponstate("nope");else{
				invoker.weaponstatus[0]&=~VULCF_JUSTUNLOAD;
				invoker.weaponstatus[0]|=VULCF_LOADCELL;
				setweaponstate("lowertoopen");
			}
		}
	unload:
		VULG A 0{
			invoker.weaponstatus[0]&=~VULCF_LOADCELL;
			invoker.weaponstatus[0]|=VULCF_JUSTUNLOAD;
			setweaponstate("lowertoopen");
		}
	//what key to use for cellunload???
	cellunload:
		VULG A 0{
			//abort if no cell to unload
			if(invoker.weaponstatus[VULCS_BATTERY]<0)
			setweaponstate("nope");else{
				invoker.weaponstatus[0]|=VULCF_JUSTUNLOAD;
				invoker.weaponstatus[0]|=VULCF_LOADCELL;
				setweaponstate("lowertoopen");
			}
		}

	//lower the weapon, open it, decide what to do
	lowertoopen:
		VULG A 2 offset(0,36);
		VULG A 2 offset(4,38){
			A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
			A_MuzzleClimb(-frandom(1.2,1.8),-frandom(1.8,2.4));
		}
		VULG A 6 offset(9,41)A_PlaySound("weapons/pocket",CHAN_AUTO);
		VULG A 8 offset(12,43)A_PlaySound("weapons/huntreload",CHAN_WEAPON);
		VULG A 5 offset(10,41)A_PlaySound("weapons/grenopen",CHAN_AUTO);
		VULG A 0{
			if(invoker.weaponstatus[0]&VULCF_LOADCELL)setweaponstate("uncell");
			else if(invoker.weaponstatus[0]&VULCF_JUSTUNLOAD)setweaponstate("unmag");
		}goto loadmag;

	uncell:
		VULG A 10 offset(11,42){
			if(invoker.weaponstatus[VULCS_BATTERY]<0)setweaponstate("cellout");
			else if(
				(
					!PressingUnload()
					&&!PressingAltReload()
					&&!PressingReload()
				)||(
					invoker.weaponstatus[VULCS_BATTERY]<20
					&& countinv("HDCellAmmo")+invoker.weaponstatus[VULCS_BATTERY]>AmmoCap("HDCellAmmo")
				)||(
					invoker.weaponstatus[VULCS_BATTERY]>=20    
					&& A_JumpIfInventory("HDCellPacks",0,"null")
				)||(
					!invoker.weaponstatus[VULCS_BATTERY]
					&& A_JumpIfInventory("HDCellPackEmpty",0,"null")
				)
			){
				A_SetTics(4);
				actor a;int b;
				[b,a]=A_SpawnItemEx("HDCellPickup",
					cos(pitch)*10,0,height-10-10*sin(pitch),
					vel.x,vel.y,vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
				a.stamina=invoker.weaponstatus[VULCS_BATTERY];
				invoker.weaponstatus[VULCS_BATTERY]=-1;
				setweaponstate("cellout");
			}else setweaponstate("pocketcell");
		}

	pocketcell:
		VULG A 10 offset(11,42){
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			if(invoker.weaponstatus[VULCS_BATTERY]>=20)A_GiveInventory("HDCellPacks",1);    
			else if(!invoker.weaponstatus[VULCS_BATTERY])A_GiveInventory("HDCellPackEmpty",1);
			else A_GiveInventory("HDCellAmmo",invoker.weaponstatus[VULCS_BATTERY]);
			invoker.weaponstatus[VULCS_BATTERY]=-1;
		}goto cellout;

	cellout:
		VULG A 0 offset(10,40) A_JumpIf(invoker.weaponstatus[0]&VULCF_JUSTUNLOAD,"reloadend");
	loadcell:
		VULG A 0{
			if(countinv("HDCellPacks")){
				invoker.weaponstatus[VULCS_BATTERY]=20;
				A_TakeInventory("HDCellPacks",1,TIF_NOTAKEINFINITE);
			}else{
				int lod=min(random(1,19),countinv("HDCellAmmo"));
				invoker.weaponstatus[VULCS_BATTERY]=lod;
				A_TakeInventory("HDCellAmmo",lod,TIF_NOTAKEINFINITE);
			}
		}goto reloadend;

	reloadend:
		VULG A 3 offset(9,41);
		VULG A 2 offset(6,38);
		VULG A 3 offset(2,34);
		goto ready;


	unchamber:
		VULG B 4 A_PlaySound("derp/crawl",CHAN_AUTO,0.3);
		VULG A 4;
		VULG A 0 A_JumpIf(PressingUnload(),"unchamber");
		goto nope;
	unmag:
		//if no mags, remove battery
		//if not even battery, remove rounds from chambers
		VULG A 0{
			if(
				invoker.vulcmag[0]<0&&
				invoker.vulcmag[1]<0&&
				invoker.vulcmag[2]<0&&
				invoker.vulcmag[3]<0&&
				invoker.vulcmag[4]<0
			){
				if(invoker.weaponstatus[VULCS_BATTERY]>=0)setweaponstate("cellunload");    
				else setweaponstate("unchamber");
			}
		}
		//first, check if there's a mag2-5.
		//if there's no mag2 but stuff after that, shunt everything over until there is.
		//if there's nothing but mag1, unload mag1.
		VULG A 6 offset(10,40){
			if(
				!invoker.weaponstatus[0]&VULCF_JUSTUNLOAD
			)setweaponstate("loadmag");
			A_PlaySound("weapons/rifleload");
			A_MuzzleClimb(-frandom(1.2,1.8),-frandom(1.8,2.4));
		}
	//remove mag #2 first, #1 only if out of options
	unmag0:
		VULG A 0{
			if(invoker.vulcmag[1]>=0)setweaponstate("unmag2");    
			else if(
				invoker.vulcmag[2]>=0||
				invoker.vulcmag[3]>=0||
				invoker.vulcmag[4]>=0  
			)setweaponstate("unmagshunt");
			else if(
				invoker.vulcmag[0]>=0    
			)setweaponstate("unmag1");
		}goto reloadend;
	unmagshunt:
		VULG A 0{
			for(int i=1;i<4;i++){
				invoker.vulcmag[i]=invoker.vulcmag[i+1];
			}
			invoker.vulcmag[4]=-1;
			A_PlaySound("weapons/grenopen",CHAN_BODY);
		}
		VULG AB 2 A_MuzzleClimb(-frandom(0.4,0.6),frandom(0.4,0.6));
		goto ready;

	unmag2:
		VULC A 0{
			int mg1=invoker.vulcmag[1];
			invoker.vulcmag[1]=-1;
			if(
				!mg1
				||(
					!PressingUnload()
					&&!PressingReload()
				)||(
					mg1>50  
					&&A_JumpIfInventory("ZM66RifleMags",0,"null")
				)||(
					countinv("FourMilAmmo")+mg1>AmmoCap("FourMilAmmo")    
				)
			){
				actor a;int b;
				[b,a]=A_SpawnItemEx("HD4mmMag",
					cos(pitch)*8,0,height-10-8*sin(pitch),
					vel.x,vel.y,vel.z,0,
					SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
				a.stamina=mg1;
				setweaponstate("mag2out");
			}else{
				if(mg1>50)A_GiveInventory("ZM66RifleMags",1);    
				else A_GiveInventory("FourMilAmmo",mg1);
				setweaponstate("pocketmag");
			}
		}goto mag2out;
	unmag1:
		VULC A 0{
			int mg0=invoker.vulcmag[0];
			invoker.vulcmag[0]=-1;
			if(
				!mg0
				||(
					!PressingUnload()
					&&!PressingReload()
				)||(
					mg0>50    
					&&A_JumpIfInventory("ZM66RifleMags",0,"null")
				)||(
					countinv("FourMilAmmo")+mg0>AmmoCap("FourMilAmmo")  
				)
			){
				actor a;int b;
				[b,a]=A_SpawnItemEx("HD4mmMag",
					cos(pitch)*8,0,height-10-8*sin(pitch),
					vel.x,vel.y,vel.z,0,
					SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
				a.stamina=mg0;
				setweaponstate("mag2out");
			}else{
				if(mg0>50)A_GiveInventory("ZM66RifleMags",1);    
				else A_GiveInventory("FourMilAmmo",mg0);
				setweaponstate("pocketmag");
			}
		}goto reloadend;
	pocketmag:
		VULG A 0 A_PlaySound("weapons/pocket");
		VULG AA 6 A_MuzzleClimb(frandom(0.4,0.6),-frandom(0.4,0.6));
		VULG A 0 A_JumpIf(invoker.vulcmag[0]<1,"mag1out");
		goto mag2out;
	mag2out:
		VULG A 1{
			for(int i=1;i<4;i++){
				invoker.vulcmag[i]=invoker.vulcmag[i+1];
			}
			invoker.vulcmag[4]=-1;
			A_PlaySound("weapons/grenopen",CHAN_BODY);
		}
		VULG AB 2 A_MuzzleClimb(-frandom(0.4,0.6),frandom(0.4,0.6));
		VULG A 6{
			if(
				invoker.vulcmag[1]<0&&
				invoker.vulcmag[2]<0&&
				invoker.vulcmag[3]<0&&
				invoker.vulcmag[4]<0
			)setweaponstate("reloadend");
		}goto unmag0;

	loadmag:
		//pick the first empty slot and fill that
		VULG A 0 A_PlaySound("weapons/pocket",CHAN_WEAPON);
		VULG AA 6 A_MuzzleClimb(-frandom(0.4,0.6),frandom(-0.4,0.4));
		VULG A 6 offset(10,41){
			int lod;
			int magslot=-1;
			if(countinv("ZM66RifleMags")){
				lod=51;
				A_TakeInventory("ZM66RifleMags",1,TIF_NOTAKEINFINITE);
			}else{
				lod=min(random(1,50),countinv("FourMilAmmo"));
				A_TakeInventory("FourMilAmmo",lod,TIF_NOTAKEINFINITE);
			}
			if(invoker.vulcmag[0]<0)magslot=0;
			else if(invoker.vulcmag[1]<0)magslot=1;
			else if(invoker.vulcmag[2]<0)magslot=2;
			else if(invoker.vulcmag[3]<0)magslot=3;
			else if(invoker.vulcmag[4]<0)magslot=4;
			else{
				//mag1 is loaded but empty
				actor a;int b;
				[b,a]=A_SpawnItemEx("HD4mmMag",
					cos(pitch)*8,0,height-10-8*sin(pitch),
					vel.x,vel.y,vel.z,0,
					SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
				a.stamina=0;
				A_SetTics(9);
				magslot=0;
				invoker.vulcmag[0]=-1;
			}

			if(lod<51)invoker.vulcmag[magslot]=100+lod;
			else invoker.vulcmag[magslot]=51;

			A_MuzzleClimb(-frandom(0.4,0.8),-frandom(0.5,0.7));
		}
		VULG A 8 offset(9,38){
			A_PlaySound("weapons/rifleclick");
			A_MuzzleClimb(
				-frandom(0.2,0.8),-frandom(0.2,0.3)
				-frandom(0.2,0.8),-frandom(0.2,0.3)
			);
		}
		VULG A 0{
			if(
				(
					PressingReload()
					||PressingUnload()
					||PressingFire()
				)||(
					!countinv("ZM66RifleMags")
					&&!countinv("FourMilAmmo")
				)||(
					invoker.vulcmag[0]>=0
					&&invoker.vulcmag[1]>=0
					&&invoker.vulcmag[2]>=0
					&&invoker.vulcmag[3]>=0
					&&invoker.vulcmag[4]>=0    
					
				)
			)setweaponstate("reloadend");
		}goto loadmag;

	spawn:
		VULC A -1;
	}

	int vulcmag[5]; //-1 is no mag loaded; 100+ is jailbroken mag
	int vulcchamber[5]; //2 is broken round

	override void InitializeWepStats(bool idfa=false){
		weaponstatus[VULCS_BATTERY]=20;
		for(int i=0;i<5;i++)vulcmag[i]=51;
		if(idfa)for(int i=0;i<5;i++)vulcchamber[i]=1;
	}

	//shooting and cycling actions
	//move this somewhere sensible
	action void VulcShoot(bool flash2=false){
		if(invoker.vulcchamber[0]!=1)return; //0 is empty, 2 is broken
		if(flash2)A_GunFlash("flash2");else A_GunFlash("flash");

		if(invoker.weaponstatus[0]&VULCF_FAST){
			if(invoker.weaponstatus[VULCS_CHANNEL]>7)invoker.weaponstatus[VULCS_CHANNEL]=4;  
			else if(invoker.weaponstatus[VULCS_CHANNEL]==1)invoker.weaponstatus[VULCS_CHANNEL]=5;
			else invoker.weaponstatus[VULCS_CHANNEL]++;
		}else invoker.weaponstatus[VULCS_CHANNEL]=CHAN_WEAPON;
		A_PlaySound("weapons/vulcanette",invoker.weaponstatus[VULCS_CHANNEL]);

		int cm=countinv("IsMoving");if(
			invoker.weaponstatus[0]&VULCF_FAST
			&&!countinv("PowerStrength")
		)cm*=2;
		double offx=frandom(-0.1,0.1)*cm;
		double offy=frandom(-0.1,0.1)*cm;

		actor b=spawn("HDBullet426",pos+(0,0,height-8));
		b.target=self;b.vel+=vel;b.angle=angle+offx;b.pitch=pitch+offy;

		int heat=min(50,invoker.weaponstatus[VULCS_HEAT]);
		if(heat>20)b.vel+=(frandom(-heat,heat),frandom(-heat,heat),frandom(-heat,heat))*0.1;    
		invoker.weaponstatus[VULCS_HEAT]+=2;

		if(random(0,8192)<min(10,heat))invoker.weaponstatus[VULCS_BATTERY]++;

		invoker.vulcchamber[0]=0;
	}
	action void VulcNextMag(){
		if(invoker.vulcmag[0]>=0){    
			double cp=cos(pitch);double ca=cos(angle);
			double sp=sin(pitch);double sa=sin(angle);
			actor mmm=spawn("HD4mmMag",pos+(
				cp*ca*12,
				cp*sa*12,
				height-12-12*sp
			));
			mmm.vel=vel+(
				cp*cos(angle+random(55,65)),
				cp*sin(angle+random(55,65)),
				sp
			);
			mmm.stamina=invoker.vulcmag[0];
		}
		for(int i=0;i<4;i++){
			invoker.vulcmag[i]=invoker.vulcmag[i+1];
		}
		invoker.vulcmag[4]=-1;
	}
	action void VulcNextRound(){
		if(invoker.vulcchamber[0]){
			//spit out a misfired, wasted or broken round
			if(invoker.vulcchamber[0]>1){    
				for(int i=0;i<5;i++){
					A_SpawnItemEx("TinyWallChunk",3,0,height-18,
						random(4,7),random(-2,2),random(-2,1),
						-30,SXF_NOCHECKPOSITION
					);
				}
			}else{
				A_SpawnItemEx("ZM66DroppedRound",3,0,height-18,
					random(4,7),random(-2,2),random(-2,1),
					-30,SXF_NOCHECKPOSITION
				);
			}
			A_MuzzleClimb(frandom(0.6,2.4),frandom(1.2,2.4));
			invoker.vulcchamber[0]=0;
		}
		//cycle all chambers
		for(int i=0;i<4;i++){
			invoker.vulcchamber[i]=invoker.vulcchamber[i+1];
		}
		invoker.vulcchamber[4]=0;
		if(invoker.vulcmag[0]==51)invoker.vulcmag[0]=50; //open the seal
		if(invoker.vulcmag[0]==100)invoker.vulcmag[0]=0; //jailbroken but empty

		//figure out what's in the mag and load it to the final chamber
		int inmag=max(0,invoker.vulcmag[0]);
		if(inmag>100)inmag-=100;
		if(inmag>0){    
			A_PlaySound("weapons/rifleclick2",CHAN_BODY);
			invoker.vulcchamber[4]++;
			if(random(1,1000)<=
				1+(invoker.vulcmag[0]>100)?(invoker.weaponstatus[0]&VULCF_FAST?13:9):0    
			)invoker.vulcchamber[4]++;
			invoker.vulcmag[0]--;
		}else invoker.vulcchamber[4]=0;
	}
}


