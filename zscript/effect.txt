// ------------------------------------------------------------
//   Misc. effects
// ------------------------------------------------------------

//debris actor: simplified physics, just bounce until dead and lie still, +noblockmap
//basically we just need to account for conveyors and platforms

class HDDebris:HDActor{
	bool stopped;
	default{
		+noblockmap -solid -shootable +dontgib +forcexybillboard +noblockmonst
		height 2;radius 2;
		bouncesound "misc/casing2";bouncefactor 0.8;maxstepheight 2;
		+rollsprite;+rollcenter;
	}
	override void postbeginplay(){
		super.postbeginplay();
		stopped=false;
	}
	override void Tick(){
		if(!stopped){
			clearinterpolation();
			if(globalfreeze||level.Frozen) return;
			vel.z-=getgravity();
			int spd=abs(vel.x);if(abs(vel.y)>spd)spd=abs(vel.y);
			vector2 frac=self.vel.xy*radius/spd;
			for(int i=0;i<spd;i+=radius){
				if(!trymove(self.pos.xy+frac,true)){
					A_PlaySound(bouncesound);
					vel.xy*=-1;
					vel.xy+=(random(-1,1),random(-1,1));
				}
			}
			setz(pos.z+vel.z);
			if(pos.z<floorz)setz(floorz);
			if(pos.z+height>ceilingz)setz(ceilingz-height);
			if(
				pos.z==getzat()
				||pos.z+height==getzat(flags:GZF_CEILING)
			){
				if(abs(vel.z)>2){
					A_PlaySound(bouncesound);
					vel.xy+=(random(-1,1),random(-1,1));
					vel.xy*=bouncefactor;
					vel.z*=-bouncefactor;
				}else{
					vel.xy*=bouncefactor;
					vel.z*=-bouncefactor;
				}
			}
			if(vel.length()<1){
				stopped=true;
				setz(floorz);
				bmovewithsector=true;
				setstatelabel("death");
				if(bdestroyed) return; //if nexttic() is called below, it would crash
			}
			nexttic();
		}else Actor.Tick();
	}
}


//puffs for smoke, bulletpuffs, flames, etc.
class HDPuff:HDActor{
	double decel;double fade;double grow;int fadeafter;double minalpha;
	default{
		+puffgetsowner;+hittracer;
		+noblockmap;-solid;+rollsprite;+rollcenter;+forcexybillboard;height 0;radius 0;
		renderstyle "translucent";gravity 0.1;
	}
	override void postbeginplay(){
		HDActor.postbeginplay(); //for all future puffs use this, not super
		roll=random(0,3)*90;
		decel=0.9;
		fadeafter=10;
		fade=0.09;
		grow=0.14;
		minalpha=0.1;
		vel.z+=2;
	}
	override void Tick(){
		clearinterpolation();
		if(globalfreeze||level.Frozen) return;
		scale.x+=grow;scale.y=scale.x;
		if(stamina>0) stamina--;
		else{
			alpha*=fade;
			vel.z-=getgravity();
		}
		vel*=decel;
		if(!trymove(pos.xy+vel.xy,true)) vel.xy=(0,0);
		setz(pos.z+vel.z);
		if(vel.z!=0){
			if(pos.z+8>ceilingz && vel.z>0) vel.z=0;
			else if(pos.z<floorz && vel.z<0) vel.z=0;
		}
		if(pos.z>ceilingz) setz(ceilingz-8);
		else if(pos.z<floorz) setz(floorz);
		if(alpha<=minalpha){
			destroy();
			return;
		}
		nexttic();
	}
}
class HDBulletPuff:HDPuff{
	default{
		stamina 5;missiletype "WallChunk";alpha 0.8;
	}
	override void postbeginplay(){
		HDActor.postbeginplay();
		roll=random(0,3)*90;vel.z+=2;fadeafter=0;
		decel=0.9;fade=0.9;grow=scale.x*0.14;
		minalpha=0.1;
		A_PlaySound("misc/bullethit",0,0.02*stamina*stamina);
		A_ChangeVelocity(-0.4,0,frandom(0.1,0.4),CVF_RELATIVE);
		trymove(pos.xy+vel.xy,false);
		for(int i=0;i<stamina;i++){
			actor ch=spawn(missilename,self.pos,ALLOW_REPLACE);
			ch.vel=self.vel+(random(-stamina,stamina),random(-stamina,stamina),random(-2,12));
		}
	}
	states{
	spawn:
		PUFF CD 8;wait;
	}
}
class BulletPuffBig:HDBulletPuff{
	default{
		stamina 5;scale 0.6;
	}
}
class BulletPuffMedium:HDBulletPuff{
	default{
		stamina 4;scale 0.5;
	}
}
class BulletPuffSmall:HDBulletPuff{
	default{
		stamina 3;scale 0.4;missiletype "TinyWallChunk";
	}
}
class FragPuff:HDBulletPuff{
	default{
		stamina 1;scale 0.5;
	}
}
class PenePuff:HDBulletPuff{
	default{
		stamina 4;scale 0.6;
	}
	states{
	spawn:
		PUFF ABCD 2;wait;
	}
}
class HDSmoke:HDPuff{
	default{
		scale 1;gravity 0.05;alpha 0.7;
	}
	override void postbeginplay(){
		HDActor.postbeginplay();
		scale.x*=randompick(-1,1);
		roll=random(0,3)*90;vel.z+=random(1,3);fadeafter=10;
		decel=0.96;fade=0.95;grow=scale.x*0.02;
		minalpha=0.005;
	}
	states{
	spawn:
		SMOK A 4;
		SMOK A 0 A_Jump(256,1,2,3);
		SMOK BCD -1;wait;
	}
}
class HDGunSmoke:HDSmoke{
	default{
		scale 0.3;renderstyle "add";alpha 0.4;
	}
	override void postbeginplay(){
		HDActor.postbeginplay();
		scale.x*=randompick(-1,1);
		roll=random(0,7)*45;fadeafter=0;
		decel=0.97;fade=0.8;grow=scale.x*0.06;
		a_changevelocity(cos(pitch)*4,0,-sin(pitch)*4,CVF_RELATIVE);
		vel+=(frandom(-0.1,0.1),frandom(-0.1,0.1),frandom(0.4,0.9));
		minalpha=0.01;
	}
}
class HDGunSmoker:IdleDummy{
	states{
	spawn:
	death:
		TNT1 A 1 nodelay{
			for(int i=0;i<15;i+=5){
				A_SpawnItemEx("HDGunSmoke",
					cos(pitch)*i,0,-sin(pitch)*i,
					cos(pitch)*4,0,-sin(pitch)*4,
					0,SXF_NOCHECKPOSITION
				);
			}
		}
		stop;
	}
}


//transfer sprite frame fader
//deathheight = amount to fade every 4 tics
class HDCopyTrail:IdleDummy{
	default{
		+noclip +rollsprite +rollcenter
		deathheight 0.6;
	}
	states{
	spawn:"####" A -1;stop;
	}
	override void Tick(){
		clearinterpolation();
		if(globalfreeze||level.Frozen) return;
		scale.x+=frandom(-0.01,0.01);scale.y=scale.x;
		accuracy++;
		if(accuracy>=4){
			accuracy=0;
			alpha*=deathheight;
			vel*=deathheight;
			if(alpha<0.04){destroy();return;}
		}
		setxyz(pos+vel);
		//don't even bother with nexttic, it's just one frame!
	}
}
extend class HDActor{
	void A_Trail(double spread=0.6){
		vector3 v;
		v=(random(-10,10),random(-10,10),random(-10,10));
		if(v==(0,0,0)) v.z=1;
		v=HDMath.ScaleVector(v,1);
		A_SpawnItemEx("HDCopyTrail",
			0,0,0,vel.x+v.x,vel.y+v.y,vel.z+v.z,0,
			SXF_TRANSFERALPHA|SXF_TRANSFERRENDERSTYLE|SXF_TRANSFERSCALE|
			SXF_TRANSFERPITCH|SXF_TRANSFERSPRITEFRAME|SXF_TRANSFERROLL|
			SXF_ABSOLUTEVELOCITY|SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION|
			SXF_TRANSFERSTENCILCOL|SXF_TRANSFERPOINTERS
		);
	}
}

//distant noise generator designed to imitate speed of sound
//generates a noisemaker for each player with its own delay based on distance
//special usages: deathsound=sound to make; mass=length of the sound
class DistantDummy:IdleDummy{
	default{
		deathsound "world/riflefar";mass 20;
	}
	double dist;
	states{
	spawn:
		TNT1 A 0 nodelay{
			if(target)A_AlertMonsters();
			for(int i=0;i<MAXPLAYERS;i++){
				if((playeringame[i])&&(players[i].mo)){
					dist=(players[i].mo.pos-pos).length();
					if(dist>HDCONST_MINDISTANTSOUND){ //don't bother if too close
						actor id=spawn("DistantNoisemaker",pos);
						id.target=players[i].mo;
						id.deathsound=self.deathsound;
						id.stamina=dist/HDCONST_SPEEDOFSOUND;
						id.mass=self.mass;
					}
				}
			}
		}stop;
	}
}
class DistantNoisemaker:IdleDummy{
	default{
		stamina 10;mass 20;
		deathsound "world/riflefar";
	}
	states{
	spawn:
		TNT1 A 0;
		TNT1 A 1{
			A_SetTics(stamina);
		}
		TNT1 A 0 A_Jump(256,"spawn2");
	spawn2:
		TNT1 A 0 A_PlaySound(deathsound,CHAN_AUTO,1,0,24);
		TNT1 A 1{
			if(self.mass<=0){destroy();return;}
			self.mass--;
			setxyz(target.pos);
		}wait;
	}
}
class DistantRifle:DistantDummy{
	default{deathsound "world/riflefar";mass 18;}
}
class DistantShotgun:DistantDummy{
	default{deathsound "world/shotgunfar";mass 34;}
}
class DistantRocket:DistantDummy{
	default{deathsound "world/rocketfar";mass 21;}
}
class DistantPlasma:DistantDummy{
	default{deathsound "world/plasmafar";mass 14;}
}
class DistantBFG:DistantDummy{
	default{deathsound "world/bfgfar";mass 44;}
}
class DoubleDistantRifle:DistantRifle{
	override void postbeginplay(){
		super.postbeginplay();
		spawn("DistantRifle",pos,ALLOW_REPLACE);
	}
}
class DoubleDistantShotgun:DistantShotgun{
	override void postbeginplay(){
		super.postbeginplay();
		spawn("DistantShotgun",pos,ALLOW_REPLACE);
	}
}


//Quake effect affecting each player differently depending on distance
//A_DistantQuake(self,8,40,4096,10,256,512,256);
extend class HDActor{
	virtual void A_DistantQuake(
		int intensity=3,
		int duration=35,
		int quakeradius=1024,
		int frequency=10,
		int speed=HDCONST_SPEEDOFSOUND,
		int minwaveradius=HDCONST_MINDISTANTSOUND,
		int dropoffrate=HDCONST_MINDISTANTSOUND
	){
		double dist;

		let s=Actor(self);
		if(s){
			if(s.ceilingpic==skyflatnum
			|| s.ceilingz-s.floorz>HDCONST_MINDISTANTSOUND){
				intensity=clamp(intensity-1,1,9);
				duration*=0.9;
			}
		}

		for(int i=0;i<MAXPLAYERS;i++){
			if(playeringame[i] && players[i].mo){
				dist=(players[i].mo.pos-self.pos).length();
				if(dist<=quakeradius){
					actor itt=self.spawn("DistantQuaker",players[i].mo.pos);
					let it=DistantQuaker(itt);
					if(it){
						if(dist<=dropoffrate) it.intensity=intensity;
							else it.intensity=clamp(intensity-floor(dist/dropoffrate),1,9);
						if(dist>minwaveradius) it.wave=true; else it.wave=false;
						if(it.intensity<3) it.deathsound="null";
							else it.deathsound="world/quake";
						it.stamina=floor(dist/speed);
						it.mass=duration;
						it.frequency=frequency;
						it.target=players[i].mo;
					}
				}
			}
		}
	}
}
class DistantQuaker:IdleDummy{
	int intensity;
	double frequency;
	bool wave;
	states{
	spawn:
		TNT1 A 1 nodelay A_SetTics(stamina);
		TNT1 A 0{
			if(wave){
				A_PlaySound("weapons/subfwoosh",CHAN_AUTO,0.1*intensity);
				if(target.pos.z<target.floorz+8)
					A_QuakeEx(0,0,intensity,mass,0,16,deathsound,
					QF_SCALEDOWN|QF_WAVE,0,0,frequency,0,mass*0.62);
			}else{
				A_QuakeEx(intensity*2,intensity*2,intensity*2,mass,0,16,deathsound,
				QF_SCALEDOWN,highpoint:mass*0.62);
			}
		}
		TNT1 A 1{
			if(mass>0){
				mass--;
				setxyz(target.pos);
			}else{
				destroy();
				return;
			}
		}wait;
	}
}


//SO MUCH BLOOD
class BloodSplatSilent:HDPuff{
	default{
		alpha 0.8;gravity 0.7;
	}
	override void postbeginplay(){
		HDActor.postbeginplay();
		roll=random(0,3)*90;vel.z+=2;fadeafter=4;
		decel=0.9;fade=0.8;grow=0.06;
		minalpha=0.1;
		A_ChangeVelocity(-0.4,0,frandom(0.1,0.4),CVF_RELATIVE);
	}
	states{
	spawn:
		BLUD ABC 4;wait;
	}
}
class BloodSplat:BloodSplatSilent replaces Blood{
	override void postbeginplay(){
		super.postbeginplay();
		A_PlaySound("misc/bulletflesh",0,0.02);
	}
}
class BloodSplattest:BloodSplat replaces BloodSplatter{}
class NotQuiteBloodSplat:BloodSplat{
	override void postbeginplay(){
		super.postbeginplay();
		A_PlaySound("misc/bulletflesh",0,0.02);
		actor p=spawn("PenePuff",pos,ALLOW_REPLACE);
		p.target=target;p.master=master;p.vel=vel*0.3;
		scale*=frandom(0.2,0.5);
	}
}
class BloodTrail:HDPuff{
	default{
		alpha 0.7;scale 0.2;gravity 0.02;
	}
	states{
	spawn:
		BLOD A -1;
	}
}
class MegaBloodSplatter:HDPuff{
	override void postbeginplay(){
		actor.postbeginplay();
		if(!A_CheckSight("null")){
			for(int i=0;i<20;i++){
				actor b=spawn("BloodSplatSilent",self.pos,ALLOW_REPLACE);
				b.vel=self.vel+(random(-4,4),random(-4,4),random(-1,7));
				b.translation=self.translation;
			}
		}
	}
}
class HDBloodTrailFloor:Actor{
	default{
		+flatsprite +noblockmap
		height 0;radius 0;alpha 0.6;
	}
	override void postbeginplay(){
		super.postbeginplay();
		angle=random(0,3)*90;
		scale*=frandom(0.6,1.2);
	}
	override void tick(){
		super.tick();
	}
	states{
	spawn:
		BLUD A 0 nodelay A_Jump(256,1,2,3,4);
		BLUD ABCD 0 A_Jump(256,"spawn2");
	spawn2:
		---- A 100 A_FadeOut(0.1);
		wait;
	}
}


//Ominous shards of green energy
class FragShard:IdleDummy{
	default{
		renderstyle "add";+forcexybillboard;scale 0.3;alpha 0;
	}
	override void tick(){
		ClearInterpolation();
		if(globalfreeze||level.Frozen) return;
		trymove(self.pos.xy+vel.xy,true);
		if(alpha<1)alpha+=0.05;
		addz(vel.z);
		nexttic();
	}
	states{
	spawn:
		BFE2 D 20 bright{
			if(stamina>0) A_SetTics(stamina);
		}stop;
	}
}
extend class HDActor{
	//A_ShardSuck(self.pos+(0,0,32),20);
	virtual void A_ShardSuck(vector3 aop,int range=4){
		actor a=self.spawn("FragShard",aop);
		a.setxyz(aop+(random(-range,range)*6,random(-range,range)*6,random(-range,range)*6));
		a.vel=(aop-a.pos)*0.05;
		a.stamina=20;
	}
}

//Teleport fog
class TeleFog:IdleDummy replaces TeleportFog{
	default{
		renderstyle "add";alpha 0.6;
	}
	override void postbeginplay(){
		actor.postbeginplay();
		scale.x=randompick(-1,1)*scale.x;
		A_PlaySound("misc/teleport");
		spawn("TeleportSmoke",pos,ALLOW_REPLACE);
		if(!A_CheckSight("null")) setstatelabel("nope");
	}
	states{
	spawn:
		TFOG AA 2 bright light("BFS1") A_FadeIn(0.2);
		TFOG BBCCCDDEEFGHII 2 bright light("BFS1"){
			A_ShardSuck(self.pos+(0,0,random(1,4)*12+24));
		}
		TFOG JJJJ random(2,3) bright light("BFS1"){
			alpha-=0.2;
			A_ShardSuck(self.pos+(0,0,random(1,4)*12+24));
		}
		TNT1 AAAAA random(4,12){
			A_ShardSuck(self.pos+(0,0,36));
		}stop;
	nope:
		TNT1 A 20 light("BFS1");
		stop;
	}
}





