// ------------------------------------------------------------
// Encumbrance
// ------------------------------------------------------------

//a battery should be about the size of your old flip phone but significantly heavier.
const BLOX_BATTERY=12;
const BLOX_BATTERY_LOADED=BLOX_BATTERY*0.4;

//a ZM66 mag should be roughly the size of a battery.
const BLOX_426MAG=10;
const BLOX_426MAG_LOADED=BLOX_426MAG*0.4;
//50 single rounds should inconvenience you far more than a single mag.
const BLOX_426=BLOX_426MAG*0.03;
const BLOX_426_LOADED=BLOX_426*0.3;

const BLOX_776MAG=27;
const BLOX_776MAG_LOADED=BLOX_776MAG*0.5;
//cased rounds are easier to keep in your pocket and fit where a mag will not.
const BLOX_776=BLOX_776MAG*0.035;
const BLOX_776_LOADED=BLOX_776*0.3;
//clips take up more space than 10 assorted rounds.
const BLOX_776CLIP=BLOX_776*12;

const BLOX_9MAG=9;
//it's almost entirely inside the handle!
const BLOX_9MAG_LOADED=BLOX_9MAG*0.2;
const BLOX_9MAG30=23;
//the opposite of that...
const BLOX_9MAG30_LOADED=BLOX_9MAG30*0.6;
const BLOX_9=BLOX_9MAG*0.07;
const BLOX_9_LOADED=BLOX_9*0.3;

//other things
const BLOX_SHELL=1;
const BLOX_SHELLLOADED=0.6;
const BLOX_ROCKET=BLOX_426MAG;
const BLOX_ROCKETLOADED=BLOX_ROCKET*0.5;
const BLOX_HEATROCKET=BLOX_ROCKET*1.2;
const BLOX_HEATROCKETLOADED=BLOX_ROCKETLOADED;
const BLOX_BRONTOSHELL=BLOX_426MAG*0.4;
const BLOX_BRONTOSHELLLOADED=BLOX_BRONTOSHELL*0.4;
const BLOX_FRAG=BLOX_ROCKET*1.2;


extend class HDPlayerPawn{
	double CheckEncumbrance(){
		if(!deathmatch && !hd_carrylimit && skill<5){
			A_TakeInventory("Backpack");
			return 0;
		}

		//scriptified weapons
		blox=0;
		thinkeriterator it=thinkeriterator.create("HDWeapon");
		HDweapon hdw;
		while(
			hdw=HDWeapon(it.Next())
		){
			if(hdw.owner==self){
				int blx=0;
				if(hdw is "Lumberjack"){
					blx+=100;
					if(hdw.weaponstatus[1]>=0)blx+=BLOX_BATTERY_LOADED;
				}else if(hdw is "HDPistol"){
					blx+=30;//+countinv("HDPistolLoaded")*BLOX_9MAG_LOADED;
				}else if(hdw is "HDSMG"){
					if(countinv("HDSMGNoMag"))blx+=120-BLOX_9MAG30_LOADED;
					else blx+=120+countinv("HDSMGLoaded")*BLOX_9_LOADED;
				}else if(hdw is "Hunter"){
					blx+=125+(
							countinv("ShotgunTube")
							+countinv("ShotgunQuickAmmo")
						)*BLOX_SHELLLOADED;
				}else if(hdw is "Slayer"){
					blx+=100+countinv("ShotgunQuickAmmo2")*BLOX_SHELLLOADED;
				}else if(hdw is "ZM66AssaultRifle"){
					if(!(hdw.weaponstatus[0]&ZM66F_NOLAUNCHER)){
						blx+=125;
						if(
							hdw.weaponstatus[0]&ZM66F_GRENADELOADED
						)blx+=BLOX_ROCKETLOADED;
					}else blx+=100;
					blx+=hdw.weaponstatus[ZM66S_MAG]*BLOX_426_LOADED;
				}else if(hdw is "Vulcanette"){
					blx+=225;
					if(hdw.weaponstatus[1]>=0)blx+=BLOX_BATTERY_LOADED;
					let vvv=vulcanette(hdw);
					for(int i=0;i<5;i++){
						if(vvv.vulcmag[i]>=0)blx+=BLOX_426MAG_LOADED;
					}
				}else if(hdw is "Blooper"){
					blx+=75+countinv("BloopChamber")*BLOX_ROCKETLOADED;
				}else if(hdw is "RocquetteLauncher"){
					blx+=175
						+countinv("RocketMag")*BLOX_ROCKETLOADED
						+countinv("HEATLoaded")*BLOX_HEATROCKETLOADED;
				}else if(hdw is "LiberatorRifle"){
					if(!(hdw.weaponstatus[0]&LIBF_NOLAUNCHER)){
						blx+=150;
						if(
							hdw.weaponstatus[0]&LIBF_GRENADELOADED
						)blx+=BLOX_ROCKETLOADED;
					}else blx+=125;
					blx+=hdw.weaponstatus[LIBS_MAG]*BLOX_776_LOADED;
				}else if(hdw is "ThunderBuster"){
					blx+=175;
					if(hdw.weaponstatus[1]>=0)blx+=BLOX_BATTERY_LOADED;
				}else if(hdw is "Brontornis"){
					blx+=75+countinv("BrontornisMag")*BLOX_BRONTOSHELLLOADED;
				}else if(hdw is "BFG9k"){
					blx+=220;
					if(hdw.weaponstatus[2]>=0)blx+=BLOX_BATTERY_LOADED;
				}else if(hdw is "BossRifle"){
					blx+=175+countinv("BossMag")*BLOX_776_LOADED;
				}
				if(
					hdw==lastweapon||
					hdw==player.readyweapon
				)blox+=blx*0.6;
				else blox+=blx;
			}
		}

		//because herps are special
		let herp=HERPUsable(findinventory("HERPUsable"));
		if(herp)blox+=125+herp.ammo<0?0:BLOX_426MAG*0.4;
		else blox+=countinv("HERPDEAD")*125;

		//let's make the derp special too
		if(countinv("DERPUsable")||countinv("DERPDEAD"))
			blox+=45+countinv("DERPAmmo")*BLOX_9MAG_LOADED;


		//and now for stuff that should just be listed
		blox+=

		//second pistol
//		(countinv("HDSecondPistol")?(30+countinv("HDSecondPistolLoaded")*BLOX_9MAG_LOADED):0)
		(countinv("HDSecondPistol")?30:0)

		//armour
		+countinv("PortableRadsuit")*50
		+countinv("GreenArmourStored")*60
		+countinv("BlueArmourStored")*80
		+countinv("BlackArmourStored")*100

		//usables
		+countinv("HDIEDKit")*3
		+(countinv("PortableStimpack")+countinv("PortableBerserkPack"))*5
		+countinv("SquadSummoner")*7
		+countinv("BluePotion")*10
		+countinv("PortableLiteAmp")*20
		+countinv("PortableMedikit")*45

		//loose/magged ambiguous ammo
		+countinv("HDPistolAmmo")*BLOX_9
		+countinv("SevenMilAmmo")*BLOX_776
		+countinv("FourMilAmmo")*BLOX_426
		+countinv("HDCellAmmo")*BLOX_BATTERY*0.3

		//single loose ammo
		+countinv("HDShellAmmo")*BLOX_SHELL
		+countinv("BrontornisRound")*BLOX_BRONTOSHELL
		+countinv("HDFragGrenadeAmmo")*BLOX_FRAG
		+(countinv("HDRocketAmmo")+countinv("DudRocquetteAmmo"))*BLOX_ROCKET
		+countinv("HEATAmmo")*BLOX_HEATROCKET

		//full mags
		+countinv("HDPistolMag")*BLOX_9MAG
		+countinv("BossClip")*BLOX_776CLIP
		+countinv("ZM66RifleMags")*BLOX_426MAG
		+(countinv("HDCellPacks")+countinv("HDCellPackEmpty"))*BLOX_BATTERY
		+countinv("HDSMGMag")*BLOX_9MAG30
		+countinv("LiberatorMag")*BLOX_776MAG
		;

		A_SetInventory("Backpack",countinv("HDBackpack"));
		carrymax=
			360+
			countinv("PowerStrength")*100+
			countinv("RegenBlues")+
			countinv("RegenStims")*2+
			+countinv("HDBackpack")*100
		;
		return blox/carrymax;
	}
}

