// ------------------------------------------------------------
// Encumbrance
// ------------------------------------------------------------

//a battery should be about the size of your old flip phone but significantly heavier.
const BLOX_BATTERY=12;
const BLOX_BATTERY_LOADED=BLOX_BATTERY*0.4;

//a ZM66 mag should be roughly the size of a battery.
const BLOX_426MAG=10;
const BLOX_426MAG_LOADED=BLOX_426MAG*0.4;
//50 single rounds should inconvenience you far more than a single mag.
const BLOX_426=BLOX_426MAG*0.03;
const BLOX_426_LOADED=BLOX_426*0.3;

const BLOX_776MAG=22;
const BLOX_776MAG_LOADED=BLOX_776MAG*0.5;
//cased rounds are easier to keep in your pocket and fit where a mag will not.
const BLOX_776=BLOX_776MAG*0.035;
const BLOX_776_LOADED=BLOX_776*0.3;
//clips take up more space than 10 assorted rounds.
const BLOX_776_CLIP=BLOX_776*12;

const BLOX_9MAG=9;
//it's almost entirely inside the handle!
const BLOX_9MAG_LOADED=BLOX_9MAG*0.2;
const BLOX_9MAG30=23;
//the opposite of that...
const BLOX_9MAG30_LOADED=BLOX_9MAG30*0.6;
const BLOX_9=BLOX_9MAG*0.07;
const BLOX_9_LOADED=BLOX_9*0.3;

//other things
const BLOX_SHELL=2;
const BLOX_SHELLLOADED=1;
const BLOX_ROCKET=BLOX_426MAG;
const BLOX_ROCKETLOADED=BLOX_ROCKET*0.5;
const BLOX_HEATROCKET=BLOX_ROCKET*1.2;
const BLOX_HEATROCKETLOADED=BLOX_ROCKETLOADED;
const BLOX_BRONTOSHELL=BLOX_426MAG*0.4;
const BLOX_BRONTOSHELLLOADED=BLOX_BRONTOSHELL*0.4;


extend class HDPlayerPawn{
	double CheckEncumbrance(){
		if(!hd_carrylimit){
			A_TakeInventory("Backpack");
			return 0;
		}

		//scriptified weapons
		blox=0;
		thinkeriterator it=thinkeriterator.create("HDWeapon");
		HDweapon hdw;
		while(
			hdw=HDWeapon(it.Next())
		){
			int blx;
			if(hdw.owner==self){
				if(hdw is "Lumberjack"){
					blox+=100;
					if(hdw.weaponstatus[1]>=0)blox+=BLOX_BATTERY_LOADED;
				}else if(hdw is "HDPistol"){
					blox+=30+countinv("HDPistolLoaded")*BLOX_9MAG_LOADED;
				}else if(hdw is "HDSMG"){
					if(countinv("HDSMGNoMag"))blox+=120-BLOX_9MAG30_LOADED;
					else blox+=120+countinv("HDSMGLoaded")*BLOX_9_LOADED;
				}else if(hdw is "Hunter"){
					blox+=125
						+countinv("ShotgunTube")
						+countinv("ShotgunChamber")
						+countinv("ShotgunQuickAmmo");
				}else if(hdw is "Slayer"){
					blox+=100
						+countinv("ShellChamber1")
						+countinv("ShellChamber2")
						+countinv("ShotgunQuickAmmo2");
				}else if(hdw is "ZM66GrenadeRifle"){
					if(hdw is "ZM66Rifle")blox+=100;
					else blox+=125+countinv("ZM66GLoaded")*BLOX_ROCKETLOADED;
					blox+=countinv("ZM66Loaded")*BLOX_426_LOADED;
				}else if(hdw is "Vulcanette"){
					blox+=225;
					if(hdw.weaponstatus[1]>=0)blox+=BLOX_BATTERY_LOADED;
					let vvv=vulcanette(hdw);
					for(int i=0;i<5;i++){
						if(vvv.vulcmag[i]>=0)blox+=BLOX_426MAG_LOADED;
					}
				}else if(hdw is "Blooper"){
					blox+=75+countinv("BloopChamber")*BLOX_ROCKETLOADED;
				}else if(hdw is "RocquetteLauncher"){
					blox+=175
						+countinv("RocketMag")*BLOX_ROCKETLOADED
						+countinv("HEATLoaded")*BLOX_HEATROCKETLOADED;
				}else if(hdw is "LiberatorRifle"){
					if(hdw is "LiberatorGrenadeRifle")
						blox+=150
							+countinv("LiberatorGrenadeChamber")
								*BLOX_ROCKETLOADED
							+countinv("LiberatorLoaded")
								*BLOX_776_LOADED;
						else blox+=125+countinv("LiberatorLoaded")*BLOX_776_LOADED;
				}else if(hdw is "ThunderBuster"){
					blox+=175;
					if(hdw.weaponstatus[1]>=0)blox+=BLOX_BATTERY_LOADED;
				}else if(hdw is "Brontornis"){
					blox+=75+countinv("BrontornisMag")*BLOX_BRONTOSHELLLOADED;
				}else if(hdw is "BFG9k"){
					blox+=220;
					if(hdw.weaponstatus[2]>=0)blox+=BLOX_BATTERY_LOADED;
				}else if(hdw is "BossRifle"){
					blox+=175+countinv("BossMag")*BLOX_776_LOADED;
				}
			}
			if(hdw.owner==self.player.readyweapon)blox+=blx*0.6;
			else blox+=blx;
		}

		//because herps are special
		let herp=HERPUsable(findinventory("HERPUsable"));
		if(herp)blox+=125+herp.ammo<0?0:BLOX_426MAG*0.4;
		else blox+=countinv("HERPDEAD")*125;

		//let's make the derp special too
		if(countinv("DERPUsable")||countinv("DERPDEAD"))
			blox+=35+countinv("DERPAmmo")*BLOX_9MAG_LOADED;


		//and now for stuff that should just be listed
		blox+=

		//second pistol
		(countinv("HDSecondPistol")?(30+countinv("HDSecondPistolLoaded")*BLOX_9MAG_LOADED):0)

		//armour
		+countinv("GreenArmourStored")*35
		+countinv("BlueArmourStored")*55
		+countinv("BlackArmourStored")*65

		//usables
		+countinv("HDIEDKit")*3
		+countinv("SquadSummoner")*5
		+countinv("PortableStimpack")*5
		+countinv("PortableBerserkPack")*8
		+countinv("PortableMedikit")*10
		+countinv("PortableLiteAmp")*12
		+countinv("PortableRadsuit")*25
		+countinv("BluePotion")*8

		//loose/magged ambiguous ammo
		+countinv("HDPistolAmmo")*1
		+countinv("SevenMilAmmo")*3*0.5
		+countinv("FourMilAmmo")*0.3
		+countinv("HDCellAmmo")*0.5

		//single loose ammo
		+countinv("HDShellAmmo")*3
		+countinv("BrontornisRound")*6
		+countinv("HDFragGrenadeAmmo")*13
		+countinv("HDRocketAmmo")*13
		+countinv("DudRocquetteAmmo")*13
		+countinv("HEATAmmo")*15

		//full mags
		+countinv("HDPistolMag")*10
		+countinv("BossClip")*12
		+countinv("ZM66RifleMags")*17
		+countinv("HDCellPacks")*18
		+countinv("HDCellPackEmpty")*18
		+countinv("HDSMGMag")*18
		+countinv("LiberatorMag")*21
		;

		A_SetInventory("Backpack",countinv("HDBackpack"));
		carrymax=
			550+
			countinv("PowerStrength")*200+
			countinv("RegenBlues")*2+
			countinv("RegenStims")*3+
			+countinv("HDBackpack")*100
		;
		return blox*1./carrymax;
	}
}

