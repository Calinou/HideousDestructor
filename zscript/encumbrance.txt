// ------------------------------------------------------------
// Encumbrance
// ------------------------------------------------------------

//a battery should be about the size of your old flip phone but significantly heavier.
const ENC_BATTERY=12;
const ENC_BATTERY_LOADED=ENC_BATTERY*0.4;

//a ZM66 mag should be roughly the size of a battery.
const ENC_426MAG=10;
const ENC_426MAG_LOADED=ENC_426MAG*0.4;
//50 single rounds should inconvenience you far more than a single mag.
const ENC_426=ENC_426MAG*0.03;
const ENC_426_LOADED=ENC_426*0.3;

const ENC_776MAG=27;
const ENC_776MAG_LOADED=ENC_776MAG*0.5;
//cased rounds are easier to keep in your pocket and fit where a mag will not.
const ENC_776=ENC_776MAG*0.035;
const ENC_776B=ENC_776*0.6;
const ENC_776_LOADED=ENC_776*0.3;
//clips take up more space than 10 assorted rounds.
const ENC_776CLIP=ENC_776*12;

const ENC_9MAG=9;
//it's almost entirely inside the handle!
const ENC_9MAG_LOADED=ENC_9MAG*0.2;
const ENC_9MAG30=23;
//the opposite of that...
const ENC_9MAG30_LOADED=ENC_9MAG30*0.6;
const ENC_9=ENC_9MAG*0.07;
const ENC_9_LOADED=ENC_9*0.3;

//other things
const ENC_SHELL=1;
const ENC_SHELLLOADED=0.3;
const ENC_ROCKET=ENC_426MAG;
const ENC_ROCKETLOADED=ENC_ROCKET*0.5;
const ENC_HEATROCKET=ENC_ROCKET*1.2;
const ENC_HEATROCKETLOADED=ENC_ROCKETLOADED;
const ENC_BRONTOSHELL=ENC_426MAG*0.4;
const ENC_BRONTOSHELLLOADED=ENC_BRONTOSHELL*0.4;
const ENC_FRAG=ENC_ROCKET*1.2;

//more things
const ENC_BLUEARMOUR=200;
const ENC_GREENARMOUR=80;
const ENC_RADSUIT=50;
const ENC_IEDKIT=3;
const ENC_STIMPACK=5;
const ENC_SQUADSUMMONER=7;
const ENC_BLUEPOTION=10;
const ENC_LITEAMP=20;
const ENC_MEDIKIT=45;
const ENC_LADDER=70;
const ENC_DERP=45;
const ENC_HERP=125;


extend class HDPlayerPawn{
	double enc;
	double CheckEncumbrance(){
		enc=0;

		//backpack
		let hdbp=HDBP(findinventory("HDBP"));
		if(hdbp)enc+=max(100,(hdbp.bulk)*0.62);

		if(
			deathmatch||hd_carrylimit||skill>=5
		){

			//scriptified weapons
			for(inventory hdww=inv;hdww!=null;hdww=hdww.inv){
				let hdw=hdweapon(hdww);
				if(hdw){
					int blx=0;
					if(hdw is "Lumberjack"){
						blx+=100;
						if(hdw.weaponstatus[CSAWS_BATTERY]>=0)blx+=ENC_BATTERY_LOADED;
					}else if(hdw is "HDPistol"){
						blx+=30+hdw.weaponstatus[PISS_MAG]*ENC_9MAG_LOADED;
					}else if(hdw is "HDSMG"){
						int mg=hdw.weaponstatus[SMGS_MAG];
						if(mg<0)blx+=120-ENC_9MAG30_LOADED;
						else blx+=120+mg*ENC_9_LOADED;
					}else if(hdw is "Hunter"){
						int bluh=hdw.weaponstatus[HUNTS_TUBE];
						if(bluh>4)bluh+=(bluh-4)*2;
						bluh+=bluh+hdw.weaponstatus[HUNTS_SIDESADDLE];
						blx+=125+bluh*ENC_SHELLLOADED;
					}else if(hdw is "Slayer"){
						blx+=100+hdw.weaponstatus[SLAYS_SIDESADDLE]*ENC_SHELLLOADED;
					}else if(hdw is "ZM66AssaultRifle"){
						if(!(hdw.weaponstatus[0]&ZM66F_NOLAUNCHER)){
							blx+=125;
							if(
								hdw.weaponstatus[0]&ZM66F_GRENADELOADED
							)blx+=ENC_ROCKETLOADED;
						}else blx+=100;
						blx+=hdw.weaponstatus[ZM66S_MAG]*ENC_426_LOADED;
					}else if(hdw is "Vulcanette"){
						blx+=225;
						if(hdw.weaponstatus[VULCS_BATTERY]>=0)blx+=ENC_BATTERY_LOADED;
						let vvv=vulcanette(hdw);
						for(int i=0;i<5;i++){
							if(vvv.vulcmag[i]>=0)
								blx+=ENC_426MAG_LOADED*min(10,vvv.vulcmag[i]);
						}
					}else if(hdw is "Blooper"){
						blx+=75;
						if(hdw.weaponstatus[0]&BLOPF_LOADED)blx+=ENC_ROCKETLOADED;
					}else if(hdw is "HDRL"){
						blx+=175+hdw.weaponstatus[RLS_MAG]*ENC_ROCKETLOADED;
						int chmb=hdw.weaponstatus[RLS_CHAMBER];
						if(chmb>1)blx+=ENC_HEATROCKETLOADED;
						else if(chmb==1)blx+=ENC_ROCKETLOADED;
					}else if(hdw is "LiberatorRifle"){
						if(!(hdw.weaponstatus[0]&LIBF_NOLAUNCHER)){
							blx+=150;
							if(
								hdw.weaponstatus[0]&LIBF_GRENADELOADED
							)blx+=ENC_ROCKETLOADED;
						}else blx+=125;
						blx+=hdw.weaponstatus[LIBS_MAG]*ENC_776_LOADED;
					}else if(hdw is "ThunderBuster"){
						blx+=175;
						if(hdw.weaponstatus[1]>=0)blx+=ENC_BATTERY_LOADED;
					}else if(hdw is "Brontornis"){
						blx+=75+(
							hdw.weaponstatus[BRONS_CHAMBER]>1?
							ENC_BRONTOSHELLLOADED:0
						);
					}else if(hdw is "BFG9k"){
						if(hdw.weaponstatus[0]&BFGF_STRAPPED)blx+=200;
						else blx+=240;
						if(hdw.weaponstatus[BFGS_BATTERY]>=0)blx+=ENC_BATTERY_LOADED;
					}else if(hdw is "BossRifle"){
						blx+=144+hdw.weaponstatus[BOSSS_MAG]*ENC_776_LOADED;
					}
					if(
						hdw==lastweapon||
						hdw==player.readyweapon
					)enc+=blx*0.6;
					else enc+=blx;
				}
			}

			//because herps are special
			let herp=HERPUsable(findinventory("HERPUsable"));
			if(herp)enc+=ENC_HERP+herp.ammo<0?0:ENC_426MAG*0.4;
			enc+=countinv("HERPDEAD")*ENC_HERP;

			//mag manager
			let hdmm=MagManager(findinventory("MagManager"));
			if(hdmm)enc+=
				(hdmm.weaponstatus[HDMMS_PIST]+hdmm.weaponstatus[HDMMS_SMG])*ENC_9+
				(hdmm.weaponstatus[HDMMS_LIB]+hdmm.weaponstatus[HDMMS_BOSS])*ENC_776;


			//and now for stuff that should just be listed
			enc+=
	
			//second pistol
			(
				countinv("HDSecondPistol")?30
				+HDSecondPistol(
					findinventory("HDSecondPistol")
				).weaponstatus[PISS_MAG]*ENC_9MAG_LOADED:0)

			//armour
			+countinv("PortableRadsuit")*ENC_RADSUIT
			+(countinv("HDArmour")?(
				findinventory("HDArmour").meleethreshold==3?200:80
			):0)

			//usables
			+countinv("HDIEDKit")*ENC_IEDKIT
			+(countinv("PortableStimpack")+countinv("PortableBerserkPack"))*ENC_STIMPACK
			+countinv("SquadSummoner")*ENC_SQUADSUMMONER
			+countinv("BluePotion")*ENC_BLUEPOTION
			+countinv("PortableLiteAmp")*ENC_LITEAMP
			+countinv("PortableMedikit")*ENC_MEDIKIT
			+countinv("PortableLadder")*ENC_LADDER
			+countinv("DERPUsable")*ENC_DERP
			+countinv("DERPDEAD")*ENC_DERP
	
			//loose/magged ambiguous ammo
			+countinv("HDPistolAmmo")*ENC_9
			+countinv("SevenMilAmmo")*ENC_776
			+countinv("SevenMilBrass")*ENC_776B
			+countinv("FourMilAmmo")*ENC_426
			+countinv("HDCellAmmo")*ENC_BATTERY*0.3
	
			//single loose ammo
			+countinv("HDShellAmmo")*ENC_SHELL
			+countinv("BrontornisRound")*ENC_BRONTOSHELL
			+countinv("HDFragGrenadeAmmo")*ENC_FRAG
			+(countinv("HDRocketAmmo")+countinv("DudRocketAmmo"))*ENC_ROCKET
			+countinv("HEATAmmo")*ENC_HEATROCKET
	
			//full mags
			+countinv("HDPistolMag")*ENC_9MAG
			+countinv("BossClip")*ENC_776CLIP
			+countinv("ZM66RifleMags")*ENC_426MAG
			+(countinv("HDCellPacks")+countinv("HDCellPackEmpty"))*ENC_BATTERY
			+countinv("HDSMGMag")*ENC_9MAG30
			+countinv("LiberatorMag")*ENC_776MAG
			;
	
		}

		double carrymax=
			360+
			countinv("PowerStrength")*100+
			min(regenblues,150)+
			stimcount*2
		;
		return enc/carrymax;
	}
}

