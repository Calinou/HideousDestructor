// ------------------------------------------------------------
// Encumbrance
// ------------------------------------------------------------

//a battery should be about the size of your old flip phone but significantly heavier.
const ENC_BATTERY=12;
const ENC_BATTERY_LOADED=ENC_BATTERY*0.4;

//a ZM66 mag should be roughly the size of a battery.
//50 single rounds should inconvenience you far LESS than a single mag.
//https://www.youtube.com/watch?v=mjuEJjzon-g
const ENC_426MAG=10;
const ENC_426MAG_EMPTY=ENC_426MAG*0.4;
const ENC_426_LOADED=(ENC_426MAG-ENC_426MAG_EMPTY)/50.;
const ENC_426=ENC_426_LOADED*4;
const ENC_426MAG_LOADED=ENC_426MAG_EMPTY*0.4;

const ENC_776MAG=30;
const ENC_776MAG_EMPTY=ENC_776MAG*0.4;
const ENC_776_LOADED=(ENC_776MAG-ENC_776MAG_EMPTY)/30.;
const ENC_776=ENC_776_LOADED*3.5;
const ENC_776MAG_LOADED=ENC_776MAG_EMPTY*0.5;
const ENC_776B=ENC_776*0.3;
const ENC_776CLIP_EMPTY=ENC_776B;
const ENC_776CLIP=ENC_776CLIP_EMPTY+ENC_776*10;

const ENC_9MAG=6;
const ENC_9MAG_EMPTY=ENC_9MAG*0.3;
const ENC_9_LOADED=(ENC_9MAG-ENC_9MAG_EMPTY)/15.;
const ENC_9=ENC_9_LOADED*3;
const ENC_9MAG_LOADED=ENC_9MAG_EMPTY*0.1; //it's almost entirely inside the handle!
const ENC_9MAG30_EMPTY=ENC_9MAG_EMPTY*2.4;
const ENC_9MAG30=ENC_9MAG30_EMPTY+ENC_9_LOADED*30;
const ENC_9MAG30_LOADED=ENC_9MAG30*0.9; //it's almost entirely outside!


//other things
const ENC_SHELL=1;
const ENC_SHELLLOADED=0.3;
const ENC_ROCKET=7;
const ENC_ROCKETLOADED=ENC_ROCKET*0.5;
const ENC_HEATROCKET=ENC_ROCKET*1.2;
const ENC_HEATROCKETLOADED=ENC_ROCKETLOADED;
const ENC_BRONTOSHELL=ENC_426MAG*0.4;
const ENC_BRONTOSHELLLOADED=ENC_BRONTOSHELL*0.4;
const ENC_FRAG=ENC_ROCKET*1.6;

//more things
const ENC_BLUEARMOUR=220;
const ENC_GREENARMOUR=70;
const ENC_RADSUIT=50;
const ENC_IEDKIT=3;
const ENC_SQUADSUMMONER=7;
const ENC_BLUEPOTION=12;
const ENC_LITEAMP=20;
const ENC_MEDIKIT=45;
const ENC_STIMPACK=7;
const ENC_LADDER=70;
const ENC_DERP=55;
const ENC_HERP=125;
const ENC_DOORBUSTER=ENC_HEATROCKET;


extend class HDPlayerPawn{
	double enc;
	double CheckEncumbrance(){
		if(!player)return 0;
		enc=0;

		//booleans for reference
		bool forcefull=(
			deathmatch
			||!hd_onemanarmy
			||skill>=5
		);
		bool weaponly=(!forcefull&&hd_onemanarmy>1);

		int useammotypes=forcefull?(1+2+4+8+16+32+64+128+256):0;
		if(countinv("DERPUsable"))useammotypes|=USE_9;
		if(countinv("HERPUsable"))useammotypes|=USE_426;
		if(countinv("HERPUsable")||countinv("PortableLiteAmp"))useammotypes|=USE_BATTERY;
		if(countinv("HDIEDKit"))useammotypes|=USE_ROCKET;

		//weapons and items
		double stacker=0;
		for(inventory hdww=inv;hdww!=null;hdww=hdww.inv){
			let hdw=hdweapon(hdww);
			if(hdw){
				bool thisweapon=(
					hdw==lastweapon||
					hdw==player.readyweapon
				);
				if(hdbp(hdw)){
					enc+=thisweapon?hdw.weaponbulk():(hdw.weaponbulk()*0.62);
					continue;
				}else if(
					thisweapon
					||forcefull
				){
					double gunbulk;
					int ammotouse;
					[gunbulk,ammotouse]=hdw.weaponbulk();
					enc+=gunbulk;
					if(gunbulk){
						enc+=stacker;
						if(!stacker)stacker=20;
						stacker*=1.1;
					}
					useammotypes|=ammotouse;
					//if(gunbulk)A_Log(string.format("%s  %.2f",hdw.getclassname(),gunbulk));
				}
				continue;
			}
			//items
			if(!weaponly){
				let hdp=hdpickup(hdww);
				if(hdp)enc+=hdp.getbulk();
			}
		}



		//if sv_infiniteammo is on, give just enough to reload a gun once
		if(sv_infiniteammo){
			//single loose ammo does not inherit from HDPickup
			if(useammotypes&USE_426){
				int togive=findinventory("Vulcanette")?5:
					findinventory("HERPUsable")?3:1;
				A_SetInventory("HD4mMag",togive);
				HDMagAmmo(findinventory("HD4mMag")).MaxCheat();
			}
			if(useammotypes&USE_776){
				if(countinv("LiberatorRifle")){
					A_SetInventory("HD7mMag",1);
					HDMagAmmo(findinventory("HD7mMag")).MaxCheat();
				}
				if(countinv("BossRifle")){
					A_SetInventory("HD7mClip",1);
					HDMagAmmo(findinventory("HD7mClip")).MaxCheat();
				}
			}
			if(useammotypes&USE_9){
				if(countinv("HDSMG")){
					A_SetInventory("HD9mMag30",1);
					HDMagAmmo(findinventory("HD9mMag30")).MaxCheat();
				}
				if(countinv("BossRifle")){
					A_SetInventory("HD9mMag15",3); //2 pistols and a derp
					HDMagAmmo(findinventory("HD9mMag15")).MaxCheat();
				}
			}
			if(useammotypes&USE_BATTERY){
				A_SetInventory("HDBattery",1);
				HDMagAmmo(findinventory("HDBattery")).MaxCheat();
			}
			A_SetInventory("HDShellAmmo",useammotypes&USE_12?20:0);
			A_SetInventory("HDRocketAmmo",useammotypes&USE_ROCKET?6:0);
			A_SetInventory("BrontornisRound",useammotypes&USE_BRONTO?1:0);
			A_SetInventory("HEATAmmo",useammotypes&USE_HEAT?1:0);
			A_SetInventory("HDFragGrenadeAmmo",1);
		}

		else if(!weaponly){

			//and now for stuff that should just be listed
			enc+=

			//single loose ammo does not inherit from HDPickup
			+(useammotypes&USE_12?(
				countinv("HDShellAmmo")*ENC_SHELL
			):0)
			+(useammotypes&USE_426?(
				countinv("FourMilAmmo")*ENC_426
			):0)
			+(useammotypes&USE_776?(
				countinv("SevenMilAmmo")*ENC_776
			):0)
			+(useammotypes&USE_9?(
				countinv("HDPistolAmmo")*ENC_9
			):0)
			+(useammotypes&USE_ROCKET?(
				(countinv("HDRocketAmmo")+countinv("DudRocketAmmo"))*ENC_ROCKET
			):0)
			+(useammotypes&USE_BATTERY?(
				countinv("HDBattery")*ENC_BATTERY
			):0)
			+(useammotypes&USE_BRONTO?(
				countinv("BrontornisRound")*ENC_BRONTOSHELL
			):0)
			+(useammotypes&USE_HEAT?(
				countinv("HEATAmmo")*ENC_HEATROCKET
			):0)
			+countinv("HDFragGrenadeAmmo")*ENC_FRAG
			;
		}


		double carrymax=
			400+
			countinv("PowerStrength")*100+
			min(regenblues,150)+
			stimcount*2
		;
		return enc/carrymax;
	}
}
enum useammoflags{
	USE_BATTERY=1,
	USE_9=2,
	USE_SECONDPISTOL=4,
	USE_12=8,
	USE_426=16,
	USE_ROCKET=32,
	USE_HEAT=64,
	USE_BRONTO=128,
	USE_776=256
}

