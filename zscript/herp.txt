// ------------------------------------------------------------
// H.E.R.P. Robot
// ------------------------------------------------------------
enum HERPConst{
	HERP_TID=851816,
}
class HERPLeg:Actor{
	default{
		+flatsprite +nointeraction +nogravity
	}
	vector3 relpos;
	double oldfloorz;
	override void Tick(){
		if(!master){destroy();return;}
		if(
			master.pos.z<floorz+12
			&&oldfloorz==floorz
		){
			master.setz(floorz+12);
			master.vel.z=max(0,master.vel.z);
		}
		binvisible=oldfloorz!=floorz;
		setorigin(master.pos+relpos,true);
		oldfloorz=floorz;
		if(pos.z<=floorz)master.vel.xy*=getfriction();
	}
	states{
	spawn:
		HLEG A -1;
		stop;
	}
}
class HERPBot:HDUPK{
	default{
		//$Category "Monsters/Hideous Destructor"
		//$Title "H.E.R.P. Robot"
		//$Sprite "HERPA1"

		+ismonster +noblockmonst +friendly +standstill +nofear
		+shootable +ghost +noblood +dontgib
		+missilemore //on/off
		height 12;radius 10;mass 400;health 200;
		damagefactor "Thermal",0.7;damagefactor "SmallArms3",0.8;
		obituary "%o went HERP.";
		hdupk.pickupmessage "Picked up a Heavy Engagement Rotary Platform robot.";
		hdupk.pickupsound "misc/w_pkup";
		scale 0.8;
	}

	//it is now canon: the mag and seal checkers are built inextricably into the AI.
	//if you tried to use a jailbroken mag, the whole robot just segfaults.
	int ammo[3]; //the mag being used: -1-51, -1 no mag, 0 empty, 51 sealed, >100  dirty
	int battery; //the battery, -1-20
	double startangle;
	bool scanright;
	int botid;

	override bool cancollidewith(actor other,bool passive){return other.bmissile;}
	override void ongrab(actor other){
		if(ishostile(other)){
			bmissilemore=false;
			setstatelabel("off");
		}
	}
	override void postbeginplay(){
		super.postbeginplay();
		startangle=angle;
		scanright=false;
		if(!master){
			ammo[0]=51;
			ammo[1]=51;
			ammo[2]=51;
			battery=20;
		}
		bool gbg;actor lll;
		[gbg,lll]=A_SpawnItemEx(
			"HERPLeg",xofs:-7,zofs:-12,
			angle:0,
			flags:SXF_NOCHECKPOSITION|SXF_SETMASTER
		);
		HERPLeg(lll).relpos=lll.pos-pos;
		lll.pitch=-60;
		[gbg,lll]=A_SpawnItemEx(
			"HERPLeg",xofs:-7,zofs:-12,
			angle:-120,
			flags:SXF_NOCHECKPOSITION|SXF_SETMASTER
		);
		HERPLeg(lll).relpos=lll.pos-pos;
		lll.pitch=-60;
		[gbg,lll]=A_SpawnItemEx(
			"HERPLeg",xofs:-7,zofs:-12,
			angle:120,
			flags:SXF_NOCHECKPOSITION|SXF_SETMASTER
		);
		HERPLeg(lll).relpos=lll.pos-pos;
		lll.pitch=-60;
	}
	void message(string msg){
		if(!master)return;
		master.A_Log(string.format("\cd[HERP]\cj  %s",msg),true);
	}
	void scanturn(){
		if(battery<1){
			message("Operational fault. Please check your manual for proper maintenance. (ERR-4fd92-00B) Power low.");
			setstatelabel("nopower");
			return;
		}
		if(health<1){
			A_Die();
			setstatelabel("death");
			return;
		}
		if(!bmissilemore){
			setstatelabel("off");
			return;
		}
		if(!random(0,8192))battery--;
		A_ClearTarget();

		//shoot 5 lines for at least some z-axis awareness
		actor a;int b;int c=-2;
		while(
			c<=1
		){
			c++;
			//shoot a line out
			flinetracedata hlt;
			linetrace(
				angle,4096,c,
				flags:TRF_NOSKY,
				offsetz:9.5,
				data:hlt
			);

			if(!c&&hlt.hittype!=Trace_HitNone)a_spawnparticle(
				"red",SPF_FULLBRIGHT,lifetime:2,size:2,0,
				hlt.hitlocation.x-pos.x,
				hlt.hitlocation.y-pos.y,
				hlt.hitlocation.z-pos.z
			);

			//if the line hits a valid target, go into shooting state
			actor hitactor=hlt.hitactor;
			if(
				hitactor
				&&isHostile(hitactor)
				&&hitactor.bshootable
				&&!hitactor.bnotarget
				&&!hitactor.bnevertarget
				&&(hitactor.bismonster||hitactor.player)
				&&(!hitactor.player||!(hitactor.player.cheats&CF_NOTARGET))
				&&hitactor.health>random(-2,20)
			){
				target=hitactor;
				setstatelabel("ready");
				message("IFF system alert: enemy pattern recognized.");
				if(hd_debug)A_Log(string.format("HERP targeted %s",hitactor.getclassname()));
				return;
			}
		}

		//if nothing, keep moving (add angle depending on scanright)
		angle+=scanright?-3:3;

		//if anglechange is too far, start moving the other way
		double chg=deltaangle(angle,startangle);
		if(abs(chg)>35){
			if(chg<0)scanright=true;
			else scanright=false;
			setstatelabel("postbeep");
		}
	}

	states{
	spawn:
		HERP A 0;
	spawn2:
		HERP A 0 A_JumpIfHealthLower(1,"dead");
		HERP A 10 A_ClearTarget();
	idle:
		HERP A 2 scanturn();
		wait;
	postbeep:
		HERP A 6 A_PlaySound("herp/beep");
		goto idle;
	ready:
		HERP A 7 A_PlaySound("weapons/vulcanup",CHAN_BODY);
		HERP AAA 1 A_PlaySound("herp/beep",CHAN_VOICE);
	aim:
		HERP A 2 A_FaceTarget(2.,2.,0,0,FAF_TOP,-4);
	shoot:
		HERP B 1 bright light("SHOT"){
			int currammo=ammo[0];
			if(
				currammo<1
				||(currammo>100&&!random(0,7))
			){
				message("Operational fault. Please check your manual for proper maintenance. (ERR-42392-41A) Cartridge empty. Shutting down...");
				if(currammo>100&&!random(0,3))ammo[0]--;
				setstatelabel("off");
				return;
			}

			//deplete 1 round
			if(currammo>100){
				//"100" is an empty mag so set it to empty
				if(currammo==101)ammo[0]=0;
				else ammo[0]--;
			}else if(currammo>50){
				//51-99 = sealed mag, break seal and deplete one = 49
				ammo[0]=49;
			}else ammo[0]--;

			A_PlaySound("weapons/rifle",CHAN_WEAPON);
			actor p=Spawn("HDBullet426",pos+(0,0,6),ALLOW_REPLACE);
			if(p){
				p.target=self;p.angle=angle;p.pitch=pitch;
				p.vel+=(
					frandom(-1,1),frandom(-1,1),frandom(-1,1)
				);
				p.vel+=vel;
			}
		}
		HERP A 2{
			if(bfriendly)A_AlertMonsters(0,AMF_TARGETEMITTER);
			else A_AlertMonsters();
		}
		HERP A 0{
			if(ammo[0]<1){
				setstatelabel("swapmag");
			}else if(target && target.health>random(-30,30)){
				flinetracedata herpline;
				linetrace(
					angle,4096,pitch,
					offsetz:12,
					data:herpline
				);
				if(herpline.hitactor!=target){
					if(checksight(target))setstatelabel("aim");
					else target=null;
				}else setstatelabel("shoot");
			}
		}goto idle;
	swapmag:
		HERP A 3{
			int nextmag=ammo[1];
			if(
				nextmag<1
				||nextmag==100
				||(nextmag>100&&!random(0,3))
			){
				message("Operational fault. Please check your manual for proper maintenance. (ERR-42392-41A) Cartridge empty. Shutting down...");
				A_PlaySound("weapons/vulcandown");
				setstatelabel("off");
			}else{
				//create a thrown empty, then...
				ammo[0]=ammo[1];
				ammo[1]=ammo[2];
				ammo[2]=-1;
			}
		}goto idle;
	nopower:
		HERP A -1;
	off:
		HERP A 10{
			if(health>0){
				double turn=clamp(deltaangle(angle,startangle),-24,24);
				if(turn){
					A_PlaySound("derp/crawl",CHAN_BODY,0.6);
					angle+=turn;
					A_SetTics(5);
				}
			}
		}
		HERP A 0{
			if(
				!bmissilemore
				||absangle(angle,startangle)>12
				||(
					ammo[0]%100<1
					&&ammo[1]%100<1
					&&ammo[2]%100<1
				)
			)setstatelabel("off");
		}goto idle;
	give:
		---- A 0{
			if(
				(health<1 && !target.countinv("HERPDEAD"))||
				(health>0 && !target.countinv("HERPUsable"))
			){
				if(health<1){
					A_GiveToTarget("HERPDEAD");
					if(random(0,3))HDMagAmmo.GiveMag(target,"HD4mMag",ammo[0]%100);
					if(random(0,3))HDMagAmmo.GiveMag(target,"HD4mMag",ammo[1]%100);
					if(random(0,3))HDMagAmmo.GiveMag(target,"HD4mMag",ammo[2]%100);
					HDMagAmmo.GiveMag(target,"HDBattery",battery);
				}else{
					let hu=HERPUsable(target.GiveInventoryType("HERPUsable"));
					if(hu){
						hu.weaponstatus[1]=ammo[0];
						hu.weaponstatus[2]=ammo[1];
						hu.weaponstatus[3]=ammo[2];
						hu.weaponstatus[4]=battery;
					}
				}
				destroy();return;
			}else if(battery>=0){
				A_PlaySound("weapons/rifleclick");
				HDMagAmmo.GiveMag(target,"HDBattery",battery);
				battery=-1;
			}
		}goto spawn;
	death:
		HERP A 0{
			ammo[0]=random(0,ammo[0]+randompick(0,0,0,100));
			ammo[1]=random(0,ammo[1]+randompick(0,0,0,100));
			ammo[2]=random(0,ammo[2]+randompick(0,0,0,100));
			battery=random(0,battery);
			A_NoBlocking();
			A_PlaySound("world/shotgunfar",CHAN_BODY,0.4);
		}
		HERP A 1 A_PlaySound("weapons/bigcrack",5);
		HERP A 1 A_PlaySound("weapons/bigcrack",6);
		HERP A 1 A_PlaySound("weapons/bigcrack",7);
		HERP AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("HugeWallChunk",random(-6,6),random(-6,6),random(0,6), vel.x+random(-6,6),vel.y+random(-6,6),vel.z+random(1,8),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
		HERP A 0{
			A_PlaySound("weapons/vulcandown",CHAN_WEAPON);
			string yay="";
			switch(random(0,8)){
			case 0:
				yay="Operational fault. Please check your manual for proper maintenance. (ERR-4fd92-00B) Power low.";break;
			case 1:
				yay="Operational fault. Please check your manual for proper maintenance. (ERR-74x29-58A) Unsupported ammunition type.\n\n\cjPlease note: Reloading a 4.26 UAC Standard magazine or its components without the supervision of a Volt UAC Standard Certified Cartridge Professional(tm) is a breach of the Volt End User License Agreement.";break;
			case 2:
				yay="Operational fault. Please check your manual for proper maintenance. (ERR-8w8i7-8VX) No interface detected.";break;
			case 3:
				yay="Illegal operation. Please check your manual for proper maintenance. (ERR-u0H85-6NN) System will restart.";break;
			case 4:
				yay="Illegal operation. Identify Friend/Foe system has been tampered with. Please contact your commanding officer immediately. (ERR-0023j-000) System will halt.";break;
			case 5:
				yay="Formatting C:\\ (DBG-444j2-0A0)";break;
			case 6:
				yay="Testing mode initialized.  (DBG-86nm8-BN5) Cache cleared.";break;
			case 7:
				yay="*** Fatal Error *** Address not mapped to object (signal 11) Address: 0x8";break;
			case 8:
				yay="*** Fatal Error *** Segmentation fault (signal 11) Address: (nil)";break;
			}
			if(!random(0,3))yay="\cg"..yay;
			message(yay);
		}
		HERP AAA 1 A_SpawnItemEx("HDSmoke",random(-2,2),random(-2,2),random(-2,2), vel.x+random(-2,2),vel.y+random(-2,2),vel.z+random(1,4),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
		HERP AAA 3 A_SpawnItemEx("HDSmoke",random(-2,2),random(-2,2),random(-2,2), vel.x+random(-2,2),vel.y+random(-2,2),vel.z+random(1,4),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
		HERP AAA 9 A_SpawnItemEx("HDSmoke",random(-2,2),random(-2,2),random(-2,2), vel.x+random(-2,2),vel.y+random(-2,2),vel.z+random(1,4),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
		TNT1 A 1 A_SpawnItemEx("HERPDEAD",0,0,0,vel.x,vel.y,vel.z,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION);
		stop;
	}
}
class EnemyHERP:HERPBot{
	default{
		//$Category "Monsters/Hideous Destructor"
		//$Title "H.E.R.P. Robot (Hostile)"
		//$Sprite "HERPA1"

		-friendly
		translation "112:120=152:159","121:127=9:12";
	}
}
class HERPUsable:HDWeapon{
	default{
		//$Category "Items/Hideous Destructor"
		//$Title "H.E.R.P. Robot (Pickup)"
		//$Sprite "HERPA1"

		+weapon.wimpy_weapon
		+inventory.invbar
		inventory.amount 1;
		inventory.maxamount 1;
		inventory.icon "HERPEX";
		inventory.pickupsound "misc/w_pkup";
		inventory.pickupmessage "Picked up a Heavy Engagement Rotary Platform robot.";
		hdweapon.barrelsize 30,3,3;
		hdweapon.nicename "H.E.R.P. Robot";
		hdweapon.refid HDLD_HERPBOT;
		hdweapon.fitsinbackpack true;
	}
	override double gunmass(){
		double amt=9+weaponstatus[HERP_BATTERY]<0?0:1;
		if(weaponstatus[1]>=0)amt+=3.6;
		if(weaponstatus[2]>=0)amt+=3.6;
		if(weaponstatus[3]>=0)amt+=3.6;
		return amt;
	}
	override double weaponbulk(){
		double enc=ENC_HERP;
		for(int i=1;i<4;i++){
			if(weaponstatus[i]>=0)enc+=max(ENC_426MAG*0.2,weaponstatus[i]*ENC_426*0.8);
		}
		return enc;
	}
	override int getsbarnum(int flags){return weaponstatus[HERP_BOTID];}
	override void InitializeWepStats(bool idfa){
		weaponstatus[HERP_BATTERY]=20;
		weaponstatus[1]=51;
		weaponstatus[2]=51;
		weaponstatus[3]=51;
		weaponstatus[4]=20;
	}
	states{
	select:
		TNT1 A 0 A_WeaponMessage("\n\n\n\n\n\n\n\n\n\n\n\n\n\n\n\nHold Use while hitting Alt.Unload\nto unload battery.\n\nHold Use while hitting Unload\nto remove partially-spent mags.\n\nHold Altfire to change BotID.\n\nPress Fire to deploy.",3500);
		goto super::select;
	ready:
		TNT1 A 1 A_HERPWeaponReady();
		goto readyend;
	user3:
		TNT1 A 0 A_MagManager("HD4mMag");
		TNT1 A 1 A_WeaponReady(WRF_NOFIRE);
		goto nope;

	unload:
		TNT1 A 0 A_JumpIf(invoker.weaponstatus[1]<0
			&&invoker.weaponstatus[2]<0
			&&invoker.weaponstatus[3]<0,"altunload");
		TNT1 A 0{invoker.weaponstatus[0]|=HERPF_UNLOADONLY;}
		//fallthrough to unloadmag
	unloadmag:
		TNT1 A 20;
		TNT1 A 5 A_UnloadMag();
		TNT1 A 0 A_JumpIf(invoker.weaponstatus[0]&HERPF_UNLOADONLY,"reloadend");
		goto reloadend;
	reload:
		TNT1 A 0 A_JumpIf(HD4mMag.NothingLoaded(self,"HD4mMag"),"nope");
		TNT1 A 14 A_PlaySound("weapons/pocket",CHAN_WEAPON);
		TNT1 A 5 A_LoadMag();
		goto reloadend;

	altreload:
		TNT1 A 0 A_JumpIf(pressinguse()||pressingzoom(),"altunload");
		TNT1 A 0{
			if(HDBattery.NothingLoaded(self,"HDBattery"))setweaponstate("nope");
			else invoker.weaponstatus[0]&=~HERPF_UNLOADONLY;
		}goto unloadbattery;
	altunload:
		TNT1 A 0{invoker.weaponstatus[0]|=HERPF_UNLOADONLY;}
		//fallthrough to unloadbattery
	unloadbattery:
		TNT1 A 20;
		TNT1 A 5 A_UnloadBattery();
		TNT1 A 0 A_JumpIf(invoker.weaponstatus[0]&HERPF_UNLOADONLY,"reloadend");
	reloadbattery:
		TNT1 A 14 A_PlaySound("weapons/pocket",CHAN_WEAPON);
		TNT1 A 5 A_LoadBattery();
	reloadend:
		TNT1 A 6;
		goto nope;
	spawn:
		HERP A -1;
		stop;
	}
	action void Message(string msg){
		A_Log("\cd[HERP]\cj  "..msg,true);
	}
	action void A_LoadMag(){
		let magg=HD4mMag(findinventory("HD4mMag"));
		if(!magg)return;
		for(int i=1;i<4;i++){
			if(invoker.weaponstatus[i]<0){
				int toload=magg.takemag(true);
				invoker.weaponstatus[i]=toload;
				break;
			}
		}
	}
	action void A_UnloadMag(){
		bool unsafe=(player.cmd.buttons&BT_USE)||(player.cmd.buttons&BT_ZOOM);
		for(int i=3;i>0;i--){
			int thismag=invoker.weaponstatus[i];
			if(thismag<0)continue;
			if(unsafe||!thismag||thismag>50){
				invoker.weaponstatus[i]=-1;
				if(pressingunload()||pressingreload()){
					HD4mMag.GiveMag(self,"HD4mMag",thismag);
					A_PlaySound("weapons/pocket",CHAN_WEAPON);
					A_SetTics(20);
				}else HD4mMag.SpawnMag(self,"HD4mMag",thismag);
				break;
			}
		}
	}
	action void A_LoadBattery(){
		if(invoker.weaponstatus[4]>=0)return;
		let batt=HDBattery(findinventory("HDBattery"));
		if(!batt)return;
		int toload=batt.takemag(true);
		invoker.weaponstatus[4]=toload;
		A_PlaySound("weapons/vulcopen1",CHAN_WEAPON);
	}
	action void A_UnloadBattery(){
		int batt=invoker.weaponstatus[4];
		if(batt<0)return;
		if(pressingunload()||pressingreload()){
			HDBattery.GiveMag(self,"HDBattery",batt);
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			A_SetTics(20);
		}else HDBattery.SpawnMag(self,"HDBattery",batt);
		invoker.weaponstatus[4]=-1;
	}
	action void A_HERPWeaponReady(){
		if(pressingfire()){
			if(invoker.amount>0)A_DeployHERP();
		}else if(invoker.amount<1)invoker.goawayanddie();
		else if(pressingaltfire()){
			int inputamt=clamp((player.cmd.pitch>>8),-4,4);
			inputamt+=(justpressed(BT_ATTACK)?1:justpressed(BT_ALTATTACK)?-1:0);
			hijackmouse();
			invoker.weaponstatus[HERP_BOTID]=clamp(
				invoker.weaponstatus[HERP_BOTID]-inputamt,0,63
			);
		}else if(justpressed(BT_FIREMODE)){
			invoker.weaponstatus[0]^=HERPF_STARTOFF;
			A_PlaySound("weapons/fmswitch",CHAN_WEAPON);
		}else A_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD|WRF_ALLOWUSER1|WRF_ALLOWUSER3|WRF_ALLOWUSER4);
	}
	action bool A_CheckFail(){
		if(invoker.weaponstatus[4]<1){
			message("No power. Please load 1 cell pack before deploying.");
			return true;
		}
		if(
			(
				invoker.weaponstatus[1]>100
				&&!random(0,7)
			)||(
				invoker.weaponstatus[1]%100<0
				&&invoker.weaponstatus[2]%100<0
				&&invoker.weaponstatus[3]%100<0
			)
		){
			message("Please load at least 1 fresh 4.26mm UAC Standard magazine before deploying.");
			return true;
		}
		return false;
	}
	action void A_DeployHERP(){
		if(A_CheckFail()){
			setweaponstate("nope");
			return;
		}

		actor hhh;int iii;
		[iii,hhh]=A_SpawnItemEx("HERPBot",5,0,height-16,
			2.5*cos(pitch),0,-2.5*sin(pitch),
			0,SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION
			|SXF_TRANSFERPOINTERS|SXF_SETMASTER
		);
		hhh.A_PlaySound("misc/w_pkup",5);
		hhh.changetid(HERP_TID);
		hhh.vel+=vel;hhh.angle=angle;
		let hhhh=HERPBot(hhh);
		hhhh.startangle=angle;
		hhhh.ammo[0]=invoker.weaponstatus[1];
		hhhh.ammo[1]=invoker.weaponstatus[2];
		hhhh.ammo[2]=invoker.weaponstatus[3];
		hhhh.battery=invoker.weaponstatus[4];
		hhhh.botid=invoker.weaponstatus[HERP_BOTID];
		hhhh.bmissilemore=invoker.weaponstatus[0]&HERPF_STARTOFF?false:true;
		A_Log(string.format("\cd[HERP] \cjDeployed with tag ID \cy%i",invoker.weaponstatus[HERP_BOTID]),true);
		invoker.amount--;
	}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(hdw.amount<1)return;
		vector2 bob=hpl.hudbob*0.2;
		sb.drawimage("HERPA7A3",(10,14)+bob,
			sb.DI_SCREEN_CENTER|sb.DI_ITEM_CENTER|sb.DI_TRANSLATABLE,
			scale:(2,2)
		);
		for(int i=1;i<4;i++){
			int bbb=hdw.weaponstatus[i];
			if(bbb>=51)sb.drawimage("ZMAGA0",(-20,i*10)+bob,
				sb.DI_SCREEN_CENTER|sb.DI_ITEM_CENTER,
				scale:(2,2)
			);else if(bbb>=0)sb.drawbar(
				"ZMAGNORM","ZMAGGREY",
				bbb,50,
				(-20,i*10),-1,
				sb.SHADER_VERT,sb.DI_SCREEN_CENTER|sb.DI_ITEM_CENTER
			);
		}
		int batt=hdw.weaponstatus[4];
		if(batt>=0){
			string batsprite;
			if(batt>13)batsprite="CELLA0";
			else if(batt>6)batsprite="CELLB0";
			else batsprite="CELLC0";
			sb.drawimage(batsprite,(0,30)+bob,
				sb.DI_SCREEN_CENTER|sb.DI_ITEM_CENTER
			);
		}

		//bottom status bar
		for(int i=2;i<4;i++){
			if(hdw.weaponstatus[i]>=0)sb.drawwepdot(-8-i*4,-13,(3,2));
		}
		int magone=hdw.weaponstatus[1];
		sb.drawwepnum(magone<100?magone:magone-100,50,posy:-10);
		sb.drawwepcounter(hdw.weaponstatus[0]&HERPF_STARTOFF,
			-28,-16,"STBURAUT","blank"
		);

		if(!batt)sb.drawstring(
			sb.mamountfont,"00000",(-16,-8),
			sb.DI_TEXT_ALIGN_RIGHT|sb.DI_TRANSLATABLE|sb.DI_SCREEN_CENTER_BOTTOM,
			Font.CR_DARKGRAY
		);else if(batt>0)sb.drawwepnum(batt,20);
	}
}
enum HERPNum{
	HERP_MAG1=1,
	HERP_MAG2=2,
	HERP_MAG3=3,
	HERP_BATTERY=4,
	HERP_BOTID=5,

	HERPF_STARTOFF=1,
	HERPF_UNLOADONLY=2,
}
class HERPDEAD:HDPickup{
	default{
		//$Category "Items/Hideous Destructor/"
		//$Title "H.E.R.P. Robot (Broken)"
		//$Sprite "HERPA0"

		inventory.amount 1;
		inventory.maxamount 1;
		inventory.icon "HERPEX";
		inventory.pickupsound "misc/w_pkup";
		inventory.pickupmessage "Picked up a Heavy Engagement Rotary Platform robot. It is damaged.";
		scale 0.8;
		hdpickup.bulk ENC_HERP;
		hdpickup.nicename "H.E.R.P. (broken)";
	}
	override double getbulk(){
		return bulk;
	}
	override void Consolidate(){
		if(
			owner
			&&!owner.A_JumpIfInventory("HERPUsable",0,"null")
		){
			HDF.Give(owner,"HERPUsable");
			goawayanddie();
		}
	}
	states{
	spawn:
		HERP A -1;
	use:
		TNT1 A 0 A_Log(string.format("\cd[HERP]\cj  :("),true);
		fail;
	}
}



extend class HDHandlers{
	void SetHERP(hdplayerpawn ppp,int botcmd,int botcmdid,int achange){
		let herpinv=HERPUsable(ppp.findinventory("HERPUsable"));
		int botid=herpinv?herpinv.weaponstatus[HERP_BOTID]:1;

		//set HERP tag number with -#
		if(botcmd<0){
			if(!herpinv)return;
			herpinv.weaponstatus[HERP_BOTID]=-botcmd;
			ppp.A_Log(string.format("\cd[HERP] \cjNext HERP tag set to \cy%i",-botcmd),true);
			return;
		}
		//get some info
		else if(botcmd==411){
			if(!herpinv)return;
			string ammo[3];
			for(int i=1;i<4;i++){
				int inherp=herpinv.weaponstatus[i];
				if(inherp>=100)ammo[i]=string.format("\cg%i",random(0,99));
				else if(inherp>50)ammo[i]=string.format("\cy50");
				else if(inherp<0)ammo[i]=string.format("\cu0");
				else if(inherp<1)ammo[i]=string.format("\cg-");
				else ammo[i]=string.format("\cw%i",min(50,inherp));
//				ammo[i]=string.format("%i",inherp);	//debug
			}
			ppp.A_Log(string.format("\cd[HERP] \cjSTATUS UPDATE:\n\cjCartridges loaded: \cy%s %s %s",ammo[0],ammo[1],ammo[2]),true);
			return;
		}
		//unload
		else if(
			botcmd==412
			||botcmd==413
		){
			if(!herpinv)return;
			bool takeall=(botcmd==413);
			bool tookdirty=false;
			bool tookany=false;
			for(int i=1;i<4;i++){
				int gettt=herpinv.weaponstatus[i];
				if(
					gettt<0
					||(!takeall&&gettt>100)
				)continue;
				tookany=true;
				herpinv.weaponstatus[i]=-1;
				if(gettt>100){
					tookdirty=true;
					gettt=clamp(gettt,0,50);
				}else{
					gettt=clamp(gettt,0,51);
				}
				HDMagAmmo.GiveMag(ppp,"HD4mMag",gettt);
			}
			ppp.A_PlaySound("misc/w_pkup",5);
			if(tookany)ppp.A_Log("\cd[HERP] \cjUnloaded.",true);
			if(tookdirty)ppp.A_Log(HDCONST_426MAGMSG,true);
			return;
		}

		//give actual commands
		bool anybots=false;
		int affected=0;
		bool badcommand=true;
		actoriterator it=actoriterator.create(HERP_TID,"HERPBot");
		actor bot=null;
		while(bot=it.Next()){
			anybots=true;
			let herp=HERPBot(bot);
			if(
				herp
				&&herp.master==ppp
				&&herp.health>0
				&&(
					!botcmdid||
					botcmdid==herp.botid
				)
			){
				if(botcmd==1){
					badcommand=false;
					if(
						herp.battery<1
						||(
							herp.ammo[0]<1
							&&herp.ammo[1]<1
							&&herp.ammo[2]<1
						)
					){
						ppp.A_Log(string.format("\cd[HERP] \crERROR:\cj HERP at [\cj%i\cu,\cj%i\cu] out of ammo or cells, \cxNOT\cj activated.",herp.pos.x,herp.pos.y),true);
					}else{
						affected++;
						herp.bmissilemore=true;
					}
				}
				else if(botcmd==2){
					affected++;
					badcommand=false;
					herp.bmissilemore=false;
				}
				else if(botcmd==3){
					if(!achange){
						ppp.A_Log(string.format("\cd[HERP] \crERROR:\cj No angle change indicated."),true);
					}else{
						badcommand=false;
						affected++;
						int anet=(herp.startangle+achange)%360;
						if(anet<0)anet+=360;
						herp.startangle=anet;
						herp.setstatelabel("off");

						ppp.A_Log(string.format("\cd[HERP] \cj HERP at [\cj%i\cu,\cj%i\cu]\cj base angle now facing %s",herp.pos.x,herp.pos.y,hdmath.cardinaldirection(anet)),true);
					}
				}
				else if(botcmd==123){
					badcommand=false;
					ppp.A_Log(string.format("\cd[HERP] \cu [\cj%i\cu,\cj%i\cu]\cj facing %s \cy%i\cj %s",
						herp.pos.x,herp.pos.y,
						hdmath.cardinaldirection(herp.startangle),
						herp.botid,
						herp.bmissilemore?"\cxACTIVE":"\cyinactive"
					),true);
				}
				else{
					badcommand=true;
					break;
				}
			}
		}
		if(
			!badcommand
			&&botcmd!=123
		){
			string verb="hacked";
			if(botcmd==1)verb="\cxactivated";
			else if(botcmd==2)verb="\cydeactivated";
			else if(botcmd==3)verb="\curedirected";
			ppp.A_Log(string.format(
				"\cd[HERP] \cj%i HERP%s %s%s\cj.",affected,affected==1?"":"s",
				botcmdid?string.format("with tag \ca%i\cj ",botcmdid):"",
				verb
			),true);
		}else if(badcommand)ppp.A_Log(string.format("\cd[HERP] \cj%sCommand format:\cu herp <option> <tag number> <direction>\n\cjOptions\n 1 = ON\n 2 = OFF\n 3 = DIRECTION (counterclockwise in degrees)\n 123 = QUERY\n -n = set tag number\n 411 = examine\n 412 = unload\n 413 = unload (unsafe)\n\cj  tag number on next deployment: \cy%i",anybots?"":"No HERPs currently deployed.\n",botid),true);
	}
}


