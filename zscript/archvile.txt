// ------------------------------------------------------------
// Archvile
// ------------------------------------------------------------
class BloodyHellFire:HDActor{
	default{
		+ismonster
		+lookallaround
		-shootable +dontharmclass +missilemore
		+alwayspuff +puffonactors +fixmapthingpos +puffgetsowner
		+nofear
		health 60;
		damagefactor "Thermal",0;
		bloodcolor "44 3c 22";
		damagetype "Thermal";
		renderstyle "add";;
		alpha 0.8;
		radius 16;
		height 44;
		damage 24;
		speed 3;
		maxstepheight 1024;
		maxdropoffheight 1024;
		seesound "misc/firecrkl";
		deathsound "weapons/plasmas";
		obituary "%o was roasted by an archvile.";
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_GiveInventory("ImmunityToFire");
		A_RearrangePointers(AAPTR_NULL,AAPTR_TARGET);
		A_TransferPointer(AAPTR_MASTER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET);
		if(
			pos.z>floorz+40
			||A_JumpIfInTargetInventory("ImmunityToFire",1,"putto")
		){
			setstatelabel("putto");
			return;
		}
		A_GiveInventory("Timer",random(120,240));
	}
	states{
	spawn:
		TNT1 A 0 A_Jump(256,"spawn2");
		TNT1 AAAAAAA random(4,6) A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),vel.x+random(-1,1),vel.y+random(-1,1),vel.z+random(1,3),0,SXF_NOCHECKPOSITION);
		TNT1 AAAAAAA random(1,4) A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),vel.x+random(-1,1),vel.y+random(-1,1),vel.z+random(1,3),0,SXF_NOCHECKPOSITION);
		TNT1 A 0 A_Playsound("vile/firestrt",6);
		TNT1 A 0 A_SetShootable();
		TNT1 AAAAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),vel.x+random(-1,1),vel.y+random(-1,1),vel.z+random(1,3),0,SXF_NOCHECKPOSITION);
	goto see;
	spawn2:
		FIRE AB 2 bright light("HELL") A_Look();
		loop;
	see:
		TNT1 A 0 A_GiveInventory("PowerFrightener");
		TNT1 A 0 A_Playsound("misc/firecrkl");
		TNT1 A 0 A_JumpIfInventory("Timer",1,1);
		goto detonate;
		TNT1 A 0 A_TakeInventory("Timer",1);
		TNT1 A 0 A_Setscale(randompick(-1,1)*frandom(0.8,1.2),frandom(0.8,1.2));
		FIRE D 0 A_SpawnItemEx("BloodyHellFireTail",0,0,0,-1,0,1,0,SXF_NOCHECKPOSITION);
		FIRE D 0 A_VileChase();
		FIRE CC random(1,2) bright light("HELL") A_Chase();
		FIRE D 0 A_SpawnProjectile("BloodyHellFireTail",0,0,0,2,0);
		FIRE DD random(1,2) bright light("HELL") A_Wander();
		loop;
	death:
		FIRE CD 2 bright light("HELL");
		TNT1 A 0 A_NoBlocking();
		TNT1 E 2 bright A_Scream();
		FIRE FFGGHH 2 bright light("HELL") A_fadeOut(0.1);
		TNT1 A 6{bnointeraction=true;}
		TNT1 A 0 A_QuakeEx(2,2,5,20,0,256,"",QF_SCALEDOWN|QF_WAVE,10,10,10);
	death2:
		TNT1 A 0 A_radiusThrust(1024,128,RTF_NOIMPACTdamage|RTF_NOTMISSILE,64);
		TNT1 A 0 A_JumpIfCloser(64,2);
		TNT1 A 0 A_Jump(256,3);
		TNT1 A 0 A_ChangeVelocity(0,0,12,CVF_RELATIVE,AAPTR_TARGET);
		TNT1 A 1 A_GiveToTarget("HDFireStarter",1);
		TNT1 AAA 0 A_SpawnItemEx("HDExplosion",random(-1,1),random(-1,1),24,0,0,random(4,9),0,SXF_NOCHECKPOSITION);
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("HugeWallChunk",random(-1,1),random(-1,1),random(1,2),random(4,12),0,random(8,16),random(1,360),SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION);
		TNT1 AAAA 1 A_SpawnItemEx("HDSmokeChunk",random(-1,1),random(-1,1),random(1,2),random(4,8),0,random(8,12),random(1,360),SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,64);
		TNT1 A 20 A_AlertMonsters();
		stop;
	missile:
		TNT1 A 0 A_SpawnProjectile("HeckFire",0,0,0);
		FIRE D 0 A_SpawnProjectile("BloodyHellFireTail",0,0,0,2,0);
		FIRE D random(1,2) bright light("HELL") A_Wander();
		goto see;
	putto:
		TNT1 A 0 A_TransferPointer(AAPTR_MASTER,AAPTR_DEFAULT,AAPTR_MASTER,AAPTR_MASTER);
		TNT1 A 0 A_Playsound("vile/firestrt");
		TNT1 AAAAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),vel.x+random(-1,1),vel.y+random(-1,1),vel.z+random(1,3),0,SXF_NOCHECKPOSITION);
		TNT1 AA 0 A_SpawnItemEx("Putto",0,0,0,vel.x,vel.y,vel.z,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		stop;
	melee:
	detonate:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),vel.x+random(-1,1),vel.y+random(-1,1),vel.z+random(1,3),0,SXF_NOCHECKPOSITION);
		TNT1 A 0 A_Die();
	heal:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),vel.x+random(-1,1),vel.y+random(-1,1),vel.z+random(1,3),0,SXF_NOCHECKPOSITION);
		TNT1 A 0 A_Die("heal");
	death.heal:
		TNT1 A 0 A_FaceTarget();
		FIRE CD 2 bright light("HELL");
		TNT1 A 0 A_NoBlocking();
		TNT1 E 2 bright A_Scream();
		FIRE FFGGHH 2 bright light("HELL") A_fadeOut(0.1);
		stop;
	}
}
class HeavenlyCarnageFlame:BloodyHellFire{
	default{
		+friendly
		species "Archangel";
	}
}
class BloodyHellFireTail:HDActor{
	default{
		+nointeraction
		height 0;radius 0;
		renderstyle "add";
		damagetype "Thermal";
		alpha 0.6;
		speed 2;
	}
	states{
	spawn:
		TNT1 A 0 A_Setscale(randompick(-1,1)*frandom(0.7,1.3),frandom(0.7,1.3));
		FIRE CDCDC random(1,2) bright;
		FIRE BABA random(1,2) bright A_fadeOut(0.2);
		stop;
	}
}


class HeckFire:HDActor{
	default{
		projectile;
		+ripper
		-nogravity
		+slidesonwalls
		+noexplodefloor
		+seekermissile
		+nodamagethrust
		+bloodlessimpact
		-noblockmap
		gravity 0.6;
		damagetype "Thermal";
		renderstyle "add";;
		alpha 0.7;
		speed 6;
		radius 8;
		height 16;
		damagefactor(1);
	}
	states{
	spawn:
		FIRE ABAB random(1,2) bright;
		TNT1 A 0 A_Gravity();
		FIRE ABAB random(1,2) bright A_seekerMissile(2,4);
	see:
		FIRE A 0 A_Setscale(randompick(-1,1)*frandom(0.8,1.2),frandom(0.8,1.2));
		FIRE A random(1,2) bright A_Playsound("misc/firecrkl");
		FIRE B random(1,2) bright A_NoGravity();
		FIRE A 0 A_Setscale(randompick(-1,1)*frandom(0.8,1.2),frandom(0.8,1.2));
		FIRE ABAB random(1,2) bright A_SeekerMissile(4,8);
		FIRE A 0 A_Setscale(randompick(-1,1)*frandom(0.8,1.2),frandom(0.8,1.2));
		FIRE A random(1,2) bright A_Jump(24,2);
		FIRE B random(1,2) bright A_Gravity();
		loop;
	death:
		FIRE CDCDEFEFGH 2 bright;
		stop;
	}
}

class ArchFiend:Archvile replaces ArchVile{
	default{
		+shadow
		+nofear
		-notarget
		-quicktoretaliate
		+seeinvisible
		radius 16;
		height 56;
		scale 0.8;
		renderstyle "add";
		bloodcolor "ff ff 44";
		damagetype "Thermal";
		speed 14;
		painchance 72;
		health 1000000;
	}
	void A_ChangeVileFlags(bool attacking){
		if(attacking){
			bshootable=true;
			bnofear=true;
			bfrightened=false;
		}else{
			bshootable=false;
			bnofear=false;
			bfrightened=true;
			bnoclip=true;
		}
	}
	vector2 oldpos;
	override void postbeginplay(){
		super.postbeginplay();
		A_GiveInventory("ImmunityToFire");
		A_Setscale(scale.x*frandom(0.8,1.3));
		A_SetSize(radius*scale.x,height*scale.y);
		mass*=scale.x;
		if(scale.x>1.0){
			A_SetHealth(health*scale.x);
		}
		oldpos=pos.xy;
	}
	states{
	see:
		VILE A 0 A_JumpIf(bfriendly,"seefriendly");
		VILE A 0 A_AlertMonsters(0,AMF_TARGETNONPLAYER);
		VILE H 0 A_GiveInventory("PowerFrightener");
		VILE A 0{oldpos=pos.xy;}
		VILE AABBCCDDEEFF 3 A_VileChase();
		VILE A 0{
			if(oldpos==pos.xy)bnoclip=true;
			else if(checkmove(pos.xy)) bnoclip=false;
		}
		loop;
	seefriendly:
		VILE H 0 A_GiveInventory("PowerFrightener");
		VILE H 0 A_ClearTarget();
		VILE H 0 A_AlertMonsters(0,AMF_TARGETEMITTER);
		VILE AABBCCDDEEFF 3 A_VileChase();
		loop;
	missile:
		VILE H 0 A_GiveInventory("PowerFrightener");
		TNT1 A 0 A_Jump(8,"Heal");
		VILE G 10 bright A_FaceTarget();
		VILE H 2 bright light("CANF") A_ChangeVileFlags(true);
		VILE HHH 3 bright light("CANF") A_SpawnProjectile("HeckFire",16,0,frandom(-4,4));
		TNT1 A 0 A_FaceTarget();
		VILE IJ 4 bright light("CANF");
		VILE K 4 bright light("HECK");
		VILE KLL 2 bright light("HECK") A_SpawnProjectile("HeckFire",16,0,8);
		TNT1 A 0 A_FaceTarget();
		VILE L 8 bright light("HELL");
		VILE MN 2 bright light("HELL");
		TNT1 A 0 A_FaceTarget();
		VILE NOO 2 bright A_SpawnProjectile("HeckFire",16,0,-8);
		TNT1 A 0 A_FaceTarget();
		TNT1 A 0 A_JumpIfTargetInLOS("MissileLong");
		TNT1 A 0 bright A_SpawnItemEx("Putto",4,0,30,2,0,2,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
	missilelong:
		TNT1 A 0 A_JumpIf(bfriendly,"MissileLongfriendly");
		TNT1 A 0 A_CustomBulletAttack(0,0,1,1,"BloodyHellFire");
		goto missileend;
	missilelongfriendly:
		TNT1 A 0 A_CustomBulletAttack(0,0,1,1,"HeavenlyCarnageFlame");
		goto missileend;
	missileend:
		VILE PO 4 bright light("HELL");
		VILE N 8 bright light("HELL");
		VILE A 0{bnopain=false;}
		goto see;
	heal:
		TNT1 A 0 A_radiusgive("VileCount",64,RGF_CORPSES|RGF_MONSTERS,random(1,3));
		TNT1 A 0 A_JumpIf(bfriendly,"healfriendly");
		VILE A 0 A_ChangeVileFlags(true);
		VILE "[\]" 8 bright light("HEAL");
		VILE "[\]" 3 bright light("HEAL") A_radiusGive("HDRaiser",256,RGF_CORPSES|RGF_MONSTERS,random(-1,1));
		VILE "[\]" 16 bright light("HEAL");
		goto see;
	healfriendly:
		TNT1 A 0 A_radiusGive("VileCount",64,RGF_CORPSES|RGF_MONSTERS,7);
		VILE A 0 A_ChangeVileFlags(true);
		VILE "[\]" 8 bright light("HEAL");
		VILE "[\]" 16 bright light("HEAL");
		TNT1 A 0{bnopain=false;}
		TNT1 A 0 A_TakeInventory("Clip",1);
		goto see;
	pain.thermal:
		TNT1 A 0 A_GiveInventory("HDFireEnder");
		goto see;
	pain:
		VILE H 0{bnopain=true;}
		TNT1 A 0 A_JumpIf(bfriendly,"painfriendly");
		TNT1 A 0 A_JumpIfInventory("Clip",5,"dying");
		TNT1 A 0 Spawn("DistantRocket",pos);
		VILE Q 0 A_SpawnItemEx("SpawnFire",0,0,28,flags:SXF_NOCHECKPOSITION);
		VILE Q 0 A_Explode(10,96,XF_NOTMISSILE,0,64);
		VILE Q 0 bright A_Quake(3,36,0,360);
		VILE Q 0 A_ChangeVileFlags(false);
		VILE Q 2 light("HELL") A_Pain();
		VILE Q 20 light("HELL") A_GiveInventory("Clip",1);
		VILE H 0{bnopain=false;}
		goto pained;
	pained:
		VILE A 1 A_Chase(null,null);
		VILE AA 0 A_Chase(null,null);
		VILE B 1 A_Chase(null,null);
		VILE AA 0 A_Chase(null,null);
		VILE C 1 A_Chase(null,null);
		VILE AA 0 A_Chase(null,null);
		VILE D 1 A_Chase(null,null);
		VILE AA 0 A_Chase(null,null);
		VILE E 1 A_Chase(null,null,CHF_RESURRECT);
		VILE AA 0 A_Chase(null,null,CHF_RESURRECT);
		VILE F 1 A_Chase(null,null,CHF_RESURRECT);
		VILE AA 0 A_Chase(null,null,CHF_RESURRECT);
		VILE F 0 A_SetTranslucent(alpha-0.1,1);
		VILE F 0 A_JumpIf(alpha<0.1,1);
		loop;
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,8192,0,0,0,WARPF_NOCHECKPOSITION);
		TNT1 A 0 A_SetTics(random(500,3500));
		VILE F 0{
			alpha=1.;
			bfrightened=false;
			bshootable=1;
		}
		goto see;
	painfriendly:
		TNT1 A 0 A_JumpIfInventory("Clip",7,"dying");
		TNT1 A 0 Spawn("DistantRocket",pos);
		VILE Q 0 A_SpawnItemEx("SpawnFire",0,0,28,flags:SXF_NOCHECKPOSITION);
		VILE Q 0 A_Explode(46,196);
		VILE Q 0 bright A_Quake(3,36,0,360);
		VILE Q 0{bshootable=false;}
		VILE Q 2 light("HELL") A_Pain();
		VILE Q 20 light("HELL") A_GiveInventory("Clip",1);
		VILE H 0{
			bshootable=true;
			bnopain=false;
		}
		goto see;
	dying:
		VILE Q 0 bright{
			bnodamage=false;
			A_SetTranslucent(0.6,1);
			A_Die();
		}
		goto death;
	death:
		TNT1 A 0 A_SpawnItemEx("tempshieldyellow",flags:SXF_SETMASTER);
		VILE Q 42 bright A_Pain();
		VILE Q 0 A_FaceTarget();
		VILE Q 0 A_Quake(2,40,0,768,0);
		VILE G 6 bright light("HELL") A_SetTranslucent(0.8,1);
		VILE G 6 A_SetTranslucent(0.4,1);
		VILE G 6 bright light("HELL") A_SetTranslucent(0.8,1);
		VILE G 6 A_SetTranslucent(0.4,1);
		VILE G 6 bright light("HELL") A_Pain();
		VILE Q 0 A_Quake(4,40,0,768,0);
		VILE G 4 bright light("HELL") A_SetTranslucent(0.8,1);
		VILE G 4 A_SetTranslucent(0.4,1);
		VILE G 4 bright light("HELL") A_SetTranslucent(0.8,1);
		VILE G 4 A_SetTranslucent(0.4,1);
		VILE G 4 bright light("HELL") A_Pain();
		VILE G 2 bright light("HELL") A_SetTranslucent(1,1);
		VILE G 2 A_SetTranslucent(0.5,1);
		VILE G 2 bright light("HELL") A_SetTranslucent(1,1);
		VILE G 2 A_SetTranslucent(0.5,1);
		VILE G 0 A_SetTranslucent(1,1);
		VILE Q 0 A_Quake(6,8,0,768,0);
		VILE GGG 2 bright light("HELL") A_Pain();
	xdeath:
		VILE Q 0 A_spawnItem("spawnFire",0.1,28,0,0);
		TNT1 AA 0 Spawn("DistantRocket",pos);
		VILE Q 0 A_PlaySound("weapons/rocklx",CHAN_WEAPON);
		VILE Q 0 A_Explode(72,196);
		VILE Q 6 bright light("HELL") A_Scream();
		VILE Q 14 bright light("HELL") A_Quake(8,14,0,768,0);
		VILE Q 0 A_spawnItem("spawnFire",0.1,30,0,0);
		VILE Q 0 A_spawnItem("spawnFire",0.6,28,0,0);
		VILE QQQQQQQQ 0 A_SpawnProjectile("HeckFire",random(12,32),0,random(0,360),2,random(-2,12));
		VILE Q 0 A_Quake(3,26,0,1024,0);
		VILE QQQQQ 8 A_SpawnItemEx("VileShard",random(-8,8),random(-8,8),random(26,50),random(-1,1),0,0,random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,32);
	fade:
		VILE QQ 2 A_SpawnItemEx("VileShard",random(-8,8),random(-8,8),random(26,50),random(-1,1),0,0,random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,16);
		VILE Q 8 A_fadeOut(0.05);
		VILE Q 0 A_JumpIf(alpha < 0.10,1);
		loop;
		VILE Q 8 A_fadeOut(0.04);
		TNT1 A 8 A_SpawnItemEx("VileShard",random(-8,8),random(-8,8),random(26,50),random(-1,1),0,0,random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,16);
		TNT1 A 0 A_NoBlocking();
		TNT1 A 0 A_JumpIf(bfriendly,"fadefriendly");
		TNT1 A 0 A_DropItem("Megasphere");
	fadefriendly:
		TNT1 AAAAA 12 A_SpawnItemEx("VileShard",random(-8,8),random(-8,8),random(26,50),random(-1,1),0,0,random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,16);
		TNT1 A 12 A_SpawnItemEx("VileGhost",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		stop;
	}
}
class ArchAngel:ArchFiend{
	default{
		+friendly
		+noclip
		+noblockmonst
		maxstepheight 4096;
		maxdropoffheight 4096;
		painchance 56;
		scale 1.0;
		speed 18;
		species "Archangel";
	}
	states{
	see:
		TNT1 A 0{bnopain=false;}
		goto super::see;
	missile:
		TNT1 A 0{bnopain=true;}
		goto super::missile;
	heal:
		TNT1 A 0{bnopain=true;}
		goto super::heal;
	}
}
class HDRaiser:ActionItem{
	states{
	pickup:
		TNT1 A 0 A_JumpIf(bcorpse,"raise");
		stop;
	raise:
		TNT1 A 0 A_Jump(180,1);
		stop;
		TNT1 A 0 A_SpawnItemEx("HDRaiserDummy",0,0,0,vel.x,vel.y,vel.z,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION);
		stop;
	}
}
class HDRaiserfriendly:HDRaiser{
	states{
	raise:
		TNT1 A 0 A_Jump(200,1);
		stop;
		TNT1 A 0 A_SpawnItemEx("HDRaiserDummyfriendly",0,0,0,vel.x,vel.y,vel.z,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION);
		stop;
	}
}
class HDRaiserDummy:HDActor{
	default{
		-solid -shootable +noblockmap
		height 2; radius 2;
	}
	states{
	spawn:
		TNT1 AAAAAAAAAAAAAAA 1 A_Chase(null,null,CHF_RESURRECT|CHF_DONTMOVE);
		stop;
	heal:
		TNT1 A 1;
		stop;
	}
}
class HDRaiserDummyfriendly:HDRaiserDummy{
	default{+friendly}
}

class BloodyHellspawner:ActionItem{
	states{
	pickup:
		TNT1 A 0 A_SpawnProjectile("BloodyHellspawner2",4);
		TNT1 A 0 A_SpawnProjectile("Rocket",32);
		stop;
	}
}
class BloodyHellspawner2:HDActor{
	default{
		projectile;
		height 1;
		radius 1;
	}
	states{
	spawn:
		TNT1 A 0 A_SpawnItemEx("BloodyHellFire",24,flags:SXF_NOCHECKPOSITION);
		BAL1 A -1;
		stop;
	}
}

class VileGhost:HDActor{
	default{
		+ismonster
		-countkill
		+noclip
		+nogravity
		+lookallaround
		+nosector
		+nofear
		radius 1;
		height 1;
		speed 20;
	}
	states{
	spawn:
		TNT1 A 10 A_Look();
		wait;
	see:
		TNT1 A 20;
		TNT1 A 0 A_Jump(1,"birth");
		loop;
	birth:
		TNT1 A 20 A_Warp(AAPTR_TARGET,0,0,32,0,WARPF_NOCHECKPOSITION);
		TNT1 A 80 A_Quake(1,40,0,512,"vile/active");
		TNT1 AAAAAA 0 A_SpawnItemEx("VileGhostShard",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,72);
		TNT1 A 40;
		TNT1 A 0 A_JumpIf(bfriendly,"Birthfriendly");
		TNT1 A 40 A_radiusGive("HDRaiser",1024,RGF_CORPSES|RGF_MONSTERS,1);
		stop;
	birthfriendly:
		TNT1 A 0 A_radiusGive("HDRaiserfriendly",1024,RGF_CORPSES|RGF_MONSTERS,1);
		stop;
	}
}
class VileGhostShard:VileGhost{
	default{
		height 56;
		radius 20;
	}
	states{
	spawn:
		TNT1 A 2 A_VileChase();
		loop;
	heal:
		TNT1 A 0 A_JumpIf(bfriendly,"Healfriendly");
		TNT1 A 1 A_RadiusGive("HDRaiser",256,RGF_CORPSES|RGF_MONSTERS,1);
		stop;
	healfriendly:
		TNT1 A 1 A_RadiusGive("HDRaiserfriendly",256,RGF_CORPSES|RGF_MONSTERS,1);
		stop;
	}
}
class VileShard:HDActor{
	default{
		+ismonster
		+float
		+nogravity
		+noclip
		+lookallaround
		translation 1;
		scale 0.6;
		radius 0;
		height 0;
		renderstyle "add";;
		speed 5;
	}
	states{
	spawn:
		APLS A 0;
		APLS A 0 A_SetGravity(0.1);
		APLS AB 1 bright A_Look();
		loop;
	see:
		APLS A 0 A_Jump(64,2);
		APLS A 0 {vel.z+=frandom(-4,8);}
		APLS A 2 bright A_JumpIf(alpha<0.1,"null");
		APLS B 2 A_Wander();
		APLS A 0 A_SpawnProjectile("VileShardTail",0,random(-24,24),random(-24,24),2,random(-14,14));
		APLS A 2 bright;
		APLS A 0 A_Wander();
		APLS B 2 bright A_fadeOut(0.05);
		APLS A 0 A_Wander();
		loop;
	heal:
		TNT1 A 1;
		stop;
	}
}
class VileShardTail:HDActor{
	default{
		projectile;
		+noclip
		+nogravity
		speed 2;
		scale 0.4;
		translation 1;
		renderstyle "add";
	}
	states{
	spawn:
		APLS AB 2 bright nodelay A_fadeOut(0.01);
		APLS A 0 ThrustThingZ(0,random(-4,8),0,0);
		loop;
	}
}

