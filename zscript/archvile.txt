// ------------------------------------------------------------
// Archvile
// ------------------------------------------------------------
class BloodyHellFire:HDActor{
	default{
		+ismonster
		+lookallaround
		-shootable +dontharmclass +missilemore +noblooddecals
		+alwayspuff +puffonactors +fixmapthingpos +puffgetsowner
		+nofear
		health 60;
		damagefactor "thermal",0;
		bloodcolor "44 3c 22";
		damagetype "Thermal";
		renderstyle "add";
		alpha 0.8;
		radius 16;
		height 44;
		damage 24;
		speed 3;
		maxstepheight 1024;
		maxdropoffheight 1024;
		seesound "misc/firecrkl";
		deathsound "weapons/plasmas";
		obituary "%o was roasted by an archvile.";
	}
	int firetimer;
	override void postbeginplay(){
		super.postbeginplay();
		A_GiveInventory("ImmunityToFire");
		master=target;
		target=master.target;
		bfriendly=master.bfriendly;
		if(
			pos.z>floorz+40
			||target.countinv("ImmunityToFire")
		){
			setstatelabel("putto");
			return;
		}
		firetimer=random(120,240);
	}
	states{
	spawn:
		TNT1 A 0 A_Jump(256,"spawn2");
		TNT1 AAAAAAA random(4,6) A_SpawnItemEx("HDSmoke",frandom(-1,1),frandom(-1,1),frandom(1,6),frandom(-1,1),frandom(-1,1),frandom(1,3),0,SXF_ABSOLUTE);
		TNT1 AAAAAAA random(1,4) A_SpawnItemEx("HDSmoke",frandom(-1,1),frandom(-1,1),frandom(1,6),frandom(-1,1),frandom(-1,1),frandom(1,3),0,SXF_ABSOLUTE);
		TNT1 A 0 A_Playsound("vile/firestrt",6);
		TNT1 A 0 A_SetShootable();
		TNT1 AAAAA 0 A_SpawnItemEx("HDSmoke",frandom(-1,1),frandom(-1,1),frandom(1,6),frandom(-1,1),frandom(-1,1),frandom(1,3),0,SXF_ABSOLUTE);
		goto see;
	spawn2:
		FIRE AB 2 bright light("HELL") A_Look();
		loop;
	see:
		TNT1 A 0{
			A_SetScale(randompick(-1,1)*frandom(0.8,1.2),frandom(0.8,1.2));
			A_SetInventory("PowerFrightener",1);
			A_Playsound("misc/firecrkl");
			if(firetimer<1)setstatelabel("detonate");
			else firetimer--;
		}
		FIRE D 0 A_SpawnItemEx("BloodyHellFireTail",0,0,0,-1,0,1,0,SXF_NOCHECKPOSITION);
		FIRE CC random(1,2) bright light("HELL") A_VileChase();
		FIRE D 0 A_SpawnItemEx("BloodyHellFireTail",0,0,0,-1,0,1,0,SXF_NOCHECKPOSITION);
		FIRE DD random(1,2) bright light("HELL") A_Wander();
		loop;
	death:
		FIRE CD 2 bright light("HELL");
		TNT1 A 0 A_NoBlocking();
		TNT1 E 2 bright A_Scream();
		FIRE FFGGHH 2 bright light("HELL") A_FadeOut(0.1);
		TNT1 A 6{bnointeraction=true;}
	death2:
		TNT1 A 0{
			A_QuakeEx(2,2,5,20,0,256,"",QF_SCALEDOWN|QF_WAVE,10,10,10);
			A_HDBlast(pushradius:128,pushamount:64);
			if(target&&target.distance3d(self)<(target.radius+radius)*1.44){
				target.vel.z+=12;
				A_Immolate(target,master,60);
			}
		}
		TNT1 AAA 0 A_SpawnItemEx("HDExplosion",
			frandom(-1,1),frandom(-1,1),24,
			flags:SXF_ABSOLUTE|SXF_TRANSFERPOINTERS
		);
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("HugeWallChunk",
			frandom(-1,1),frandom(-1,1),frandom(1,2),
			frandom(4,12),0,frandom(8,16),frandom(1,360),
			SXF_ABSOLUTEPOSITION|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION
		);
		TNT1 AAAA 1 A_SpawnItemEx("HDSmokeChunk",
			frandom(-1,1),frandom(-1,1),frandom(1,2),
			frandom(4,8),0,frandom(8,12),frandom(1,360),
			SXF_ABSOLUTEPOSITION|SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,64
		);
		TNT1 A 20 A_AlertMonsters();
		stop;
	missile:
		TNT1 A 0 A_SpawnProjectile("HeckFire",0,0,0);
		FIRE D 0 A_SpawnProjectile("BloodyHellFireTail",0,0,0,2,0);
		FIRE D random(1,2) bright light("HELL") A_Wander();
		goto see;
	putto:
		TNT1 A 0 A_Playsound("vile/firestrt");
		TNT1 AAAAA 0 A_SpawnItemEx("HDSmoke",frandom(-1,1),frandom(-1,1),frandom(1,6),frandom(-1,1),frandom(-1,1),frandom(1,3),0,SXF_ABSOLUTE);
		TNT1 AA 0 A_SpawnItemEx("Putto",flags:SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
		stop;
	melee:
	detonate:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",frandom(-1,1),frandom(-1,1),frandom(1,6),frandom(-1,1),frandom(-1,1),frandom(1,3),0,SXF_ABSOLUTE);
		TNT1 A 0 A_Die();
	heal:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",frandom(-1,1),frandom(-1,1),frandom(1,6),frandom(-1,1),frandom(-1,1),frandom(1,3),0,SXF_ABSOLUTE);
		TNT1 A 0 A_Die("heal");
	death.heal:
		TNT1 A 0 A_NoBlocking();
		FIRE CD 2 bright light("HELL");
		FIRE E 2 bright A_Scream();
		FIRE FFGGHH 2 bright light("HELL") A_FadeOut(0.1);
		stop;
	}
}
class BloodyHellFireTail:HDActor{
	default{
		+nointeraction
		height 0;radius 0;
		renderstyle "add";
		alpha 0.6;
	}
	states{
	spawn:
		FIRE CDCDC random(1,2) bright;
		FIRE BABA random(1,2) bright A_fadeOut(0.2);
		stop;
	}
}


class HeckFire:HDActor{
	default{
		projectile;
		+ripper
		-nogravity
		+slidesonwalls
		+noexplodefloor
		+seekermissile
		+nodamagethrust
		+bloodlessimpact
		+bright
		-noblockmap
		gravity 0.6;
		damagetype "Thermal";
		renderstyle "add";
		alpha 0.7;
		speed 6;
		radius 8;
		height 16;
		damagefactor(1);
	}
	states{
	spawn:
		FIRE ABAB random(1,2);
		TNT1 A 0 A_Gravity();
		FIRE ABAB random(1,2) A_SeekerMissile(2,4);
	see:
		FIRE A 0 A_Setscale(randompick(-1,1)*frandom(0.8,1.2),frandom(0.8,1.2));
		FIRE A random(1,2) A_Playsound("misc/firecrkl");
		FIRE B random(1,2) A_NoGravity();
		FIRE A 0 A_Setscale(randompick(-1,1)*frandom(0.8,1.2),frandom(0.8,1.2));
		FIRE ABAB random(1,2) A_SeekerMissile(4,8);
		FIRE A 0 A_Setscale(randompick(-1,1)*frandom(0.8,1.2),frandom(0.8,1.2));
		FIRE A random(1,2) A_Jump(24,2);
		FIRE B random(1,2) A_Gravity();
		loop;
	death:
		FIRE CDCDEFEFGH 2;
		stop;
	}
}

class ArchFiend:Archvile replaces ArchVile{
	default{
		+shadow
		+nofear
		-notarget
		-quicktoretaliate
		+seeinvisible
		radius 16;
		height 56;
		scale 0.8;
		renderstyle "add";
		bloodcolor "ff ff 44";
		damagetype "Thermal";
		speed 14;
		painchance 0;
		health 1000000;
	}
	int hitsleft;
	void A_VilePain(){
		Spawn("DistantRocket",pos);
		A_SpawnItemEx("SpawnFire",0,0,28,flags:SXF_NOCHECKPOSITION);
		A_Explode(46,196);
		A_Quake(3,36,0,360);
		A_AlertMonsters();
		if(!bfriendly)A_ChangeVileFlags(false);
		A_Pain();
		for(int i=0;i<3;i++)A_SpawnItemEx("BFGVileShard",
			0,0,42,flags:SXF_SETMASTER|SXF_TRANSFERPOINTERS
		);
	}
	void A_ChangeVileFlags(bool attacking){
		if(attacking){
			bshootable=true;
			bnofear=true;
			bfrightened=false;
		}else{
			bshootable=false;
			bnofear=false;
			bfrightened=true;
			bnoclip=true;
		}
	}
	static void A_MassHeal(actor caller){
		actor aaa;
		blockthingsiterator it=blockthingsiterator.create(caller,256);
		while(it.next()){
			aaa=it.thing;
			if(
				aaa.bcorpse
				&&aaa.findstate("raise")
				&&aaa.checksight(caller)
			){
				int dist=max(
					abs(aaa.pos.x-caller.pos.x),
					abs(aaa.pos.y-caller.pos.y),
					abs(aaa.pos.z-caller.pos.z)
				);
				actor hhh=spawn("HDRaiser",aaa.pos);
				hhh.bfriendly=caller.bfriendly;
				hhh.master=aaa;
				hhh.tracer=caller;
				hhh.stamina=(dist>>3)+random(10,30);
			}
		}
	}
	vector2 oldpos;
	override void postbeginplay(){
		super.postbeginplay();
		hdmobster.spawnmobster(self);
		HDMobAI.Resize(self,0.8,1.3);
		A_GiveInventory("ImmunityToFire");
		oldpos=pos.xy;
		hitsleft=bfriendly?7:6;
	}
	override int damagemobj(
		actor inflictor,actor source,int damage,
		name mod,int flags,double angle
	){
		if(
			mod!="thermal"
			&&mod!="balefire"
			&&damage>(bfriendly?333:random(0,666))
		){
			if(hitsleft>0){
				bshootable=false;
				hitsleft--;
				setstatelabel("pain");
				return 0;
			}
			return super.damagemobj(
				inflictor,source,health,
				bfriendly?"SmallArms0":"Falling",DMG_FORCED
			);
		}
		return 0;
	}
	states{
	see:
		VILE A 0 A_JumpIf(bfriendly,"seefriendly");
		VILE A 0{
			oldpos=pos.xy;
			A_GiveInventory("PowerFrightener");
			A_AlertMonsters(0,AMF_TARGETNONPLAYER);
		}
		VILE AABBCCDDEEFF 3 A_VileChase();
		VILE A 0{
			if(oldpos==pos.xy)bnoclip=true;
		}
		loop;
	seefriendly:
		VILE H 0 A_GiveInventory("PowerFrightener");
		VILE H 0 A_ClearTarget();
		VILE H 0 A_AlertMonsters(0,AMF_TARGETEMITTER);
		VILE AABBCCDDEEFF 3 A_VileChase();
		loop;
	missile:
		VILE G 10 bright{
			bnopain=false;
			A_GiveInventory("PowerFrightener");
			A_FaceTarget();
		}
		VILE H 2 bright light("CANF") A_ChangeVileFlags(true);
		VILE HHH 3 bright light("CANF") A_SpawnProjectile("HeckFire",16,0,frandom(-4,4));
		TNT1 A 0 A_FaceTarget();
		VILE IJ 4 bright light("CANF");
		VILE K 4 bright light("HECK");
		VILE KLL 2 bright light("HECK") A_SpawnProjectile("HeckFire",16,0,8);
		TNT1 A 0 A_FaceTarget();
		VILE L 8 bright light("HELL");
		VILE MN 2 bright light("HELL");
		TNT1 A 0 A_FaceTarget();
		VILE NOO 2 bright A_SpawnProjectile("HeckFire",16,0,-8);
		TNT1 A 0 A_FaceTarget();
		TNT1 A 0 A_JumpIfTargetInLOS("missilelong");
		TNT1 A 0 bright A_SpawnItemEx("Putto",4,0,30,2,0,2,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
	missilelong:
		VILE P 0 A_CustomBulletAttack(0,0,1,1,"BloodyHellFire");
		VILE PO 4 bright light("HELL");
		VILE N 8 bright light("HELL");
		goto see;
	heal:
		VILE A 0 A_ChangeVileFlags(true);
		VILE \\ 8 bright light("HEAL");
		VILE # 8 bright light("HEAL"){
			if(bfriendly){
				hitsleft=7;
				flinetracedata hlt;
				linetrace(
					angle,radius*3,0,
					flags:TRF_THRUBLOCK|TRF_THRUHITSCAN|TRF_ALLACTORS,
					offsetz:8,
					data:hlt
				);
				if(hlt.hitactor){
					if(hlt.hitactor.countinv("VileCount")){
						hlt.hitactor.A_GiveInventory("VileCount",7);
					}
					hlt.hitactor.bfriendly=bfriendly;
				}
			}else{
				A_MassHeal(self);
				A_SetTics(16);
			}
		}
		goto see;
	pain:
		VILE Q 20 light("HELL")A_VilePain();
		VILE H 0{
			bshootable=true;
			bnopain=false;
			if(bfriendly)setstatelabel("see");
		}
	pained:
		VILE A 1 A_Chase(null,null);
		VILE AA 0 A_Chase(null,null);
		VILE B 1 A_Chase(null,null);
		VILE AA 0 A_Chase(null,null);
		VILE C 1 A_Chase(null,null);
		VILE AA 0 A_Chase(null,null);
		VILE D 1 A_Chase(null,null);
		VILE AA 0 A_Chase(null,null);
		VILE E 1 A_Chase(null,null,CHF_RESURRECT);
		VILE AA 0 A_Chase(null,null,CHF_RESURRECT);
		VILE F 1 A_Chase(null,null,CHF_RESURRECT);
		VILE AA 0 A_Chase(null,null,CHF_RESURRECT);
		VILE F 0 A_SetTranslucent(alpha-0.1,1);
		VILE F 0 A_JumpIf(alpha<0.1,1);
		loop;
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,8192,0,0,0,WARPF_NOCHECKPOSITION);
		TNT1 A 0 A_SetTics(random(500,3500));
		VILE F 0{
			alpha=1.;
			bfrightened=false;
			bshootable=1;
		}
		goto see;
	death:
		TNT1 A 0 A_SpawnItemEx("tempshieldyellow",flags:SXF_SETMASTER);
		VILE Q 42 bright A_VilePain();
		VILE Q 0 A_FaceTarget();
		VILE Q 0 A_Quake(2,40,0,768,0);
		VILE G 6 bright light("HELL") A_SetTranslucent(0.8,1);
		VILE G 6 A_SetTranslucent(0.4,1);
		VILE G 6 bright light("HELL") A_SetTranslucent(0.8,1);
		VILE G 6 A_SetTranslucent(0.4,1);
		VILE G 6 bright light("HELL") A_Pain();
		VILE Q 0 A_Quake(4,40,0,768,0);
		VILE G 4 bright light("HELL") A_SetTranslucent(0.8,1);
		VILE G 4 A_SetTranslucent(0.4,1);
		VILE G 4 bright light("HELL") A_SetTranslucent(0.8,1);
		VILE G 4 A_SetTranslucent(0.4,1);
		VILE G 4 bright light("HELL") A_Pain();
		VILE G 2 bright light("HELL") A_SetTranslucent(1,1);
		VILE G 2 A_SetTranslucent(0.5,1);
		VILE G 2 bright light("HELL") A_SetTranslucent(1,1);
		VILE G 2 A_SetTranslucent(0.5,1);
		VILE G 0 A_SetTranslucent(1,1);
		VILE Q 0 A_Quake(6,8,0,768,0);
		VILE GGG 2 bright light("HELL")A_Pain();
	xdeath:
		VILE Q 0 A_spawnItem("spawnFire",0.1,28,0,0);
		TNT1 AA 0 Spawn("DistantRocket",pos);
		VILE Q 0 A_PlaySound("weapons/rocklx",CHAN_WEAPON);
		VILE Q 0 A_Explode(72,196);
		VILE Q 6 bright light("HELL") A_Scream();
		VILE Q 14 bright light("HELL") A_Quake(8,14,0,768,0);
		VILE Q 0 A_SpawnItemEx("SpawnFire",0,0,30,flags:SXF_ABSOLUTE);
		VILE Q 0 A_SpawnItemEx("SpawnFire",0,0,28,flags:SXF_ABSOLUTE);
		VILE QQQQQQQQ 0 A_SpawnProjectile("HeckFire",frandom(12,32),0,frandom(0,360),2,frandom(-2,12));
		VILE Q 0 A_Quake(3,26,0,1024,0);
		VILE QQQQQ 8 A_SpawnItemEx("VileShard",frandom(-8,8),frandom(-8,8),frandom(26,50),frandom(-1,1),0,0,frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
	fade:
		VILE QQ 2 A_SpawnItemEx("VileShard",frandom(-8,8),frandom(-8,8),frandom(26,50),frandom(-1,1),0,0,frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,16);
		VILE Q 8 A_fadeOut(0.05);
		VILE Q 0 A_JumpIf(alpha < 0.10,1);
		loop;
		VILE Q 8 A_fadeOut(0.04);
		TNT1 A 8 A_SpawnItemEx("VileShard",frandom(-8,8),frandom(-8,8),frandom(26,50),frandom(-1,1),0,0,frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,16);
		TNT1 A 0 {
			A_NoBlocking();
			if(!bfriendly)A_DropItem("Megasphere");
		}
		TNT1 AAAAA 12 A_SpawnItemEx("VileShard",frandom(-8,8),frandom(-8,8),frandom(26,50),frandom(-1,1),0,0,frandom(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,16);
		TNT1 A -1{
			A_SpawnItemEx("VileGhost",flags:SXF_ABSOLUTE|SXF_TRANSFERPOINTERS|SXF_SETMASTER);
			bnointeraction=true;
		}stop;
	}
}
class ArchAngel:ArchFiend{
	default{
		+friendly
		+noclip
		+noblockmonst
		maxstepheight 4096;
		maxdropoffheight 4096;
		painchance 56;
		scale 1.0;
		speed 18;
		species "Archangel";
	}
}
class HDRaiser:Actor{
	default{
		+nointeraction
	}
	void A_RaiseThis(){
		if(!master||master.health>0){destroy();return;}
		A_RaiseMaster(RF_TRANSFERFRIENDLINESS|RF_NOCHECKPOSITION);
		master.master=tracer;
	}
	states{
	spawn:
		TNT1 A 10 nodelay A_SetTics(stamina);
		TNT1 A 0 A_RaiseThis();
		stop;
	}
}

class VileGhost:HDActor{
	default{
		+ismonster
		-countkill
		+noclip
		+nogravity
		+lookallaround
		+nosector
		+nofear
		radius 1;
		height 1;
		speed 20;
	}
	states{
	spawn:
		TNT1 A 10 A_Look();
		wait;
	see:
		TNT1 A 20;
		TNT1 A 0 A_Jump(1,"birth");
		loop;
	birth:
		TNT1 A 20 A_Warp(AAPTR_TARGET,0,0,32,0,WARPF_NOCHECKPOSITION);
		TNT1 A 80 A_Quake(1,40,0,512,"vile/active");
		TNT1 AAAAAA 0 A_SpawnItemEx("VileGhostShard",flags:SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION);
		TNT1 A 40;
		TNT1 A 1{ArchFiend.A_MassHeal(master);}
		stop;
	}
}
class VileGhostShard:VileGhost{
	default{
		height 56;
		radius 20;
	}
	states{
	spawn:
		TNT1 A 2 A_VileChase();
		loop;
	heal:
		TNT1 A 1{ArchFiend.A_MassHeal(master);}
		stop;
	}
}
class VileShard:HDActor{
	default{
		+ismonster
		+float
		+nogravity
		+noclip
		+lookallaround
		translation 1;
		scale 0.6;
		radius 0;
		height 0;
		renderstyle "add";
		speed 5;
	}
	states{
	spawn:
		APLS A 0;
		APLS A 0 A_SetGravity(0.1);
		APLS AB 1 bright A_Look();
		loop;
	see:
		APLS A 0 A_Jump(64,2);
		APLS A 0 {vel.z+=frandom(-4,8);}
		APLS A 2 bright A_JumpIf(alpha<0.1,"null");
		APLS B 2 A_Wander();
		APLS A 0 A_SpawnProjectile("VileShardTail",0,random(-24,24),random(-24,24),2,random(-14,14));
		APLS A 2 bright;
		APLS A 0 A_Wander();
		APLS B 2 bright A_fadeOut(0.05);
		APLS A 0 A_Wander();
		loop;
	heal:
		TNT1 A 1;
		stop;
	}
}
class VileShardTail:HDActor{
	default{
		projectile;
		+noclip
		+nogravity
		speed 2;
		scale 0.4;
		translation 1;
		renderstyle "add";
	}
	states{
	spawn:
		APLS AB 2 bright nodelay A_fadeOut(0.01);
		APLS A 0 ThrustThingZ(0,random(-4,8),0,0);
		loop;
	}
}

