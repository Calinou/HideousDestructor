//-------------------------------------------------
// Ladder
//-------------------------------------------------

//show where the ladder is hanging
//no it doesn't swing, math is hard :(
class hdladdersection:idledummy{
	default{
		+wallsprite
	}
	override void postbeginplay(){
		super.postbeginplay();
		//master is top, target is bottom
		if(!master||!target){
			destroy();return;
		}
	}
	override void tick(){
		if(!master||!target){
			destroy();return;
		}
		setz(max(target.floorz,master.pos.z-LADDER_SECTIONLENGTH*health));
		nexttic();
	}
	states{
	spawn:
		LADD B -1;
	}
}
class hdladdertop:hdactor{
	default{
		+missile //testing
		+flatsprite
		height 1;radius 10;
	}
	states{
	spawn:
		LADD A -1;
	}
	override void tick(){
		setz(floorz+4);
		nexttic();
	}
	override void postbeginplay(){
		super.postbeginplay();
		pitch=18;
		bmissile=false;master=target;
		setz(floorz);
		fcheckposition tm;
		vector2 mvlast=pos.xy;
		vector2 mv=angletovector(angle,2);
		for(int i=0;i<20;i++){

			mvlast+=mv;
			checkmove(mvlast,PCM_DROPOFF|PCM_NOACTORS,tm);

			//found a place for the ladder to hang down
			int htdiff=clamp(floorz-tm.floorz,0,LADDER_MAX);
			if(
				htdiff
			){
				A_PlaySound("misc/ladder");

				//spawn the ladder end
				target=spawn("hdladderbottom",tm.pos);
				target.target=self;
				target.master=master;
				target.angle=angle;
				target.pitch=-27;
				target.translation=master.translation;

				vector2 mv2=mv*0.02;
				vector3 newpos=tm.pos;

				//spawn the ladder sections
				double sectionlength=min(htdiff,LADDER_MAX)/LADDER_SECTIONS;
				for(int i=1;i<=LADDER_SECTIONS;i++){
					newpos.xy+=mv2;
					actor sss=spawn("hdladdersection",newpos);
					sss.master=self;sss.target=target;sss.angle=angle+frandom(-1.,1.);
					sss.translation=master.translation;
					sss.health=i;
					target.setorigin(newpos+(0,0,-sectionlength*i),true);
				}

				//reposition the thing
				setorigin((tm.pos.xy-mv*radius,floorz),true);

				//only complete if within throwable range, else abort
				if(master&&pos.z-master.pos.z<108){
					master.A_Log("You hang up a ladder.",true);
					master.A_TakeInventory("PortableLadder",1);
					return;
				}
			}
		}

		//if there's no lower floor to drop the ladder, abort.
		if(master){
			master.A_Log("Can't hang a ladder here.",true);
		}else{
			actor hdl=spawn("PortableLadder",pos);
			hdl.A_PlaySound("misc/ladder");
		}
		destroy();
	}
}
const LADDER_MAX=800.;
const LADDER_SECTIONLENGTH=12.;
const LADDER_SECTIONS=LADDER_MAX/LADDER_SECTIONLENGTH;


class hdladderbottom:hdactor{
	default{
		+nogravity +flatsprite
		height 56;radius 10;
	}
	actor currentuser;
	double currentuserz;
	override bool used(actor user){
		double upz=user.pos.z;
		if(
			upz>target.pos.z+24  
			||upz+user.height*1.3<pos.z
		)return false;
		if(currentuser){
			disengageladder();
			return false;
		}
		currentuser=user;
		currentuser.vel.z+=1;
		currentuserz=user.pos.z;
		currentuser.A_Log("You climb the ladder.",true);
		return true;
	}
	void disengageladder(bool message=true){
		if(!currentuser)return;
		if(playerpawn(currentuser))playerpawn(currentuser).viewbob=1.;
		if(message)currentuser.A_Log("Ladder disengaged.",true);
		currentuser=null;
	}
	override void ondestroy(){
		if(playerpawn(currentuser))playerpawn(currentuser).viewbob=1.;
		super.ondestroy();
	}
	override void tick(){
		if(!target){destroy();return;}
		setz(
			clamp(pos.z,
				max(target.pos.z-LADDER_MAX,floorz),
				target.pos.z+LADDER_MAX
			)
		);

		if(currentuser){
			if(currentuser.health<1){disengageladder(false);return;}


			//check if facing the ladder
			bool facing=abs(
				deltaangle(
					angleto(currentuser,true),
					currentuser.angle
				)
			)>100;  

			//checks when above ladder
			if(
				currentuser.pos.z>target.pos.z-16  
			){
				//throw in some use of controls still
				if(currentuser.player){
					int bt=currentuser.player.cmd.buttons;
					if(
						bt&BT_JUMP
						||bt&BT_SPEED
						||(!facing&&bt&BT_USE)
					){
						if(
							bt&BT_JUMP
							&&currentuser.height<
							getdefaultbytype(currentuser.getclass()).height
						){
							currentuser.A_Log("Ladder taken up.",true);
							actor hdl=spawn("PortableLadder",target.pos);
							hdl.A_PlaySound("misc/ladder");
							hdl.translation=translation;
							target.destroy();
							if(self)destroy();
						}else disengageladder();
						return;
					}
					if(currentuser.floorz<currentuser.pos.z){
						double fm=currentuser.player.cmd.forwardmove*0.000125;
						double sm=currentuser.player.cmd.sidemove*0.000125;
						if(fm||sm)currentuser.trymove(
							currentuser.pos.xy
							+angletovector(currentuser.angle,fm)
							+angletovector(currentuser.angle-90,sm),
							true
						);
					}
				}
				if(target.distance2d(currentuser)>40){  
					vector2 tp=pos.xy;
					currentuser.setorigin((
						clamp(currentuser.pos.x,
							tp.x-40,
							tp.x+40
						),
						clamp(currentuser.pos.y,
							tp.y-40,
							tp.y+40
						),
						min(currentuser.pos.z,target.pos.z+24)
					),true);
				}
				return;
			}
			currentuser.vel.xy*=0.7;
			currentuser.vel.z=0;

			//climbing interface
			if(currentuser.player){
				double spm=currentuser.speed;
				double fm=currentuser.player.cmd.forwardmove;
				if(fm>0)fm=spm;else if(fm<0)fm=-spm;else fm=0;
				double sm=currentuser.player.cmd.sidemove;
				if(sm>0)sm=spm;else if(sm<0)sm=-spm;else sm=0;

				int bt=currentuser.player.cmd.buttons;

				//barehanded and descending are faster
				if(facing){
					if(!sm&&fm<0)fm*=1.5;
					weapon wp=currentuser.player.readyweapon;
					if(wp is "Ring"||wp is "NullWeapon"){
						sm*=2;fm*=2;
					}
				}else fm*=-1;

				if(currentuser.countinv("PowerStrength"))fm*=1.8;
				if(hdplayerpawn(currentuser)&&hdplayerpawn(currentuser).stunned)
					fm*=0.2;

				//apply climbing
				currentuserz+=fm;
				if(sm)currentuser.trymove(
					currentuser.pos.xy+angletovector(currentuser.angle-90,sm),
					true
				);
				if(fm||sm)playerpawn(currentuser).viewbob=1;
					else playerpawn(currentuser).viewbob=0.;

				//jump also disengages
				//crouch+jump to remove the rope
				if(bt){
					if(bt&BT_JUMP){
						vector3 vl=(
							(currentuser.pos.xy-pos.xy).unit()*3,
							4
						);
						if(currentuser.countinv("PowerStrength"))vl*=2.2;
						currentuser.vel+=vl;

						if(
							currentuser.height<
							getdefaultbytype(currentuser.getclass()).height
						){
							currentuser.A_Log("Ladder taken down.",true);

							actor hdl=spawn("PortableLadder",target.pos);
							hdl.A_PlaySound("misc/ladder");
							hdl.vel.xy=vl.xy;
							hdl.translation=translation;

							target.destroy();
							if(self)destroy();
							return;
						}else disengageladder();
					}else if(!facing&&bt&BT_USE)disengageladder();
				}
			}
			if(!currentuser)return;

			currentuserz=clamp(currentuserz,
				max(currentuser.floorz,pos.z-currentuser.height*1.3),
				currentuser.ceilingz-currentuser.height
			);
			currentuser.setorigin((
				clamp(currentuser.pos.x,
					pos.x-16,
					pos.x+16
				),
				clamp(currentuser.pos.y,
					pos.y-16,
					pos.y+16
				),
				currentuserz
			),true);
		}
		nexttic();
	}
	states{
	spawn:
		LADD C -1;wait;
	}
}
class PortableLadder:HDPickup{
	default{
		inventory.icon "LADDD0";
		inventory.pickupmessage "Picked up a ladder.";
		inventory.maxamount 3;
		height 24;radius 12;
	}
	states{
	spawn:
		LADD D -1;
	use:
		TNT1 A 0{
			actor aaa;int bbb;
			[bbb,aaa]=A_SpawnItemEx(
				"HDLadderTop",16*cos(pitch),0,48-16*sin(pitch),
				flags:SXF_NOCHECKPOSITION|SXF_SETTARGET
			);if(!aaa)return;

			//only face player if above player's height - otherwise why not just mantle?
			if(aaa.floorz>pos.z+height){  
				aaa.angle+=180;
			}
		}fail;
	}
}



