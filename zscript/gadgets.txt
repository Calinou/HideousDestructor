//-------------------------------------------------
// Environment/Radiation Suit
//-------------------------------------------------
class WornRadsuit:InventoryFlag{
	override void attachtoowner(actor owner){	
		if(!owner.countinv("PortableRadsuit"))owner.A_GiveInventory("PortableRadsuit");
		super.attachtoowner(owner);
	}
	override void DetachFromOwner(){
		owner.A_TakeInventory("PortableRadsuit",1);
		owner.A_PlaySound("weapons/pocket",CHAN_AUTO);
		let onr=HDPlayerPawn(self);
		if(onr)onr.stunned+=60;
		super.DetachFromOwner();
	}
	override void DoEffect(){
		if(stamina>0)stamina--;
	}
}
class PortableRadsuit:HDPickup replaces RadSuit{
	default{
		//$Category "Gear/Hideous Destructor/Supplies"
		//$Title "Environment Suit"
		//$Sprite "SUITA0"

		inventory.maxamount 2;
		inventory.interhubamount 2;
		inventory.pickupmessage "Environmental shielding suit.";
		inventory.pickupsound "weapons/pocket";
		inventory.icon "SUITB0";
		accuracy 1;
		hdpickup.bulk ENC_RADSUIT;
	}
	override void DetachFromOwner(){
		owner.A_TakeInventory("WornRadsuit");
		target=owner;
		super.DetachFromOwner();
	}
	override void actualpickup(actor user){
		HDF.TransferFire(self,user);
		super.actualpickup(user);
	}
	states{
	spawn2:
		SUIT A 1;
		SUIT A -1{
			if(!target)return;
			HDF.TransferFire(target,self);
		}
	use:
		TNT1 A 0{
			A_SetBlend("00 00 00",1,6,"00 00 00");
			A_PlaySound("weapons/pocket");
			A_ChangeVelocity(0,0,2);
			let onr=HDPlayerPawn(self);
			if(onr)onr.stunned+=60;
			if(!countinv("WornRadsuit")){
				int fff=HDF.TransferFire(self,self);
				if(fff){
					if(random(1,fff)>30){
						A_PlaySound("misc/fwoosh",CHAN_AUTO);
						A_TakeInventory("PortableRadsuit",1);
						return;
					}else{
						HDF.TransferFire(self,null);
						if(onr){
							onr.fatigue+=fff;
							onr.stunned+=fff;
						}
					}
				}
				A_GiveInventory("WornRadsuit");
			}else{
				actor a;int b;
				inventory wrs=findinventory("wornradsuit");
				[b,a]=A_SpawnItemEx("PortableRadsuit",0,0,height/2,2,0,4);
				if(a &&  wrs){
					//transfer sticky fire
					if(wrs.stamina){
						let aa=HDActor(a);
						if(aa)aa.A_Immolate(a,self,wrs.stamina);
					}
					//transfer heat
					let hhh=heat(findinventory("heat"));
					if(hhh){
						double realamount=hhh.realamount;
						double intosuit=clamp(realamount*0.9,0,min(200,realamount));
						let hhh2=heat(a.GiveInventoryType("heat"));
						if(hhh2){
							hhh2.realamount+=intosuit;
							hhh.realamount=max(0,hhh.realamount-intosuit);
						}
					}
				}
				A_TakeInventory("WornRadsuit");
			}
		}fail;
	}
}


//-------------------------------------------------
// Light Amplification Visor
//-------------------------------------------------
class PortableLiteAmp:HDPickup replaces Infrared{
	default{
		//$Category "Gear/Hideous Destructor/Supplies"
		//$Title "Light Amp"
		//$Sprite "PVISB0"

		inventory.maxamount 1;
		inventory.interhubamount 1;
		inventory.pickupmessage "Light amplification visor.";
		inventory.icon "PPVIA0";
		scale 0.5;
		accuracy 1;
		hdpickup.bulk ENC_LITEAMP;
	}
	int spent;bool worn;
	const LITEBAT=20000;
	pointlight nozerolight;
	override double getbulk(){return bulk;}
	override void DetachFromOwner(){
		worn=false;
		if(owner&&owner.player){
//			if(owner.player.fixedcolormap==5)owner.player.fixedcolormap=-1;
//			owner.player.fixedlightlevel=-1;
			Shader.SetEnabled(owner.player,"NiteVis",false);
		}
		super.DetachFromOwner();
	}
	override void DoEffect(){
		super.DoEffect();
		if(owner && owner.player){
			if(
				worn
				&&!owner.countinv("PowerInvisibility")
//				&&(owner.player.fixedcolormap<0||owner.player.fixedcolormap==5)
			){
				//actual goggle effect
				owner.player.fov=min(owner.player.fov,90);
//				owner.player.fixedcolormap=5;
//				owner.player.fixedlightlevel=1;
				Shader.SetEnabled(owner.player,"NiteVis",true);

				//check if totally drained
				if(HDMagAmmo.NothingLoaded(owner,"HDBattery")){
					owner.A_SetBlend("01 00 00",0.8,16);
					worn=false;
					return;
				}

				let bbb=HDBattery(owner.findinventory("HDBattery"));

				//get the lowest non-empty
				int bbbindex=bbb.mags.size()-1;
				int bbblowest=20;
				for(int i=bbbindex;i>=0;i--){
					if(
						bbb.mags[i]>0
						&&bbb.mags[i]<bbblowest
					){
						bbbindex=i;
						bbblowest=bbb.mags[i];
					}
				}
				int bbbi=bbb.mags[bbbindex];

				//flicker
				if(!random[rand1](0,bbbi*bbbi)){
//						owner.player.fixedcolormap=-1;
//						owner.player.fixedlightlevel=-1;
					Shader.SetEnabled(owner.player,"NiteVis",false);
				}

				//drain
				spent++;
				if(spent>litebat){
					bbb.mags[bbbindex]=max(0,bbbi-1);
					spent=0;
				}
			}else{
//				if(owner.player.fixedcolormap==5)owner.player.fixedcolormap=-1;
//				owner.player.fixedlightlevel=-1;
				Shader.SetEnabled(owner.player,"NiteVis",false);
			}
		}
	}
	states{
	spawn2:
		PVIS B -1;
	use:
		TNT1 A 0{
			A_SetBlend("01 00 00",0.8,16);
			if(HDMagAmmo.NothingLoaded(self,"HDBattery")){
				A_Log("No power for lite-amp. Need at least 1 battery on you.",true);
				invoker.worn=false;
			}
			if(invoker.worn)invoker.worn=false;else{
				invoker.worn=true;
				if(!invoker.nozerolight)invoker.nozerolight=pointlight(spawn("visorlight",pos));
				invoker.nozerolight.target=self;
			}
		}fail;
	}
}
class VisorLight:PointLight{
	override void postbeginplay(){
		args[0]=1;
		args[1]=0;
		args[2]=0;
		args[3]=256;
		args[4]=0;
	}
	override void tick(){
		super.tick();
		if(
			target
			&&target.findinventory("PortableLiteAmp")
			&&portableliteamp(target.findinventory("PortableLiteAmp")).worn
		)args[3]=256;else args[3]=0;
	}
}
