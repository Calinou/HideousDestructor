//-------------------------------------------------
// Environment/Radiation Suit
//-------------------------------------------------
class WornRadsuit:InventoryFlag{
	override void attachtoowner(actor owner){	
		if(!owner.countinv("PortableRadsuit"))owner.A_GiveInventory("PortableRadsuit");
		super.attachtoowner(owner);
	}
	override void DetachFromOwner(){
		owner.A_TakeInventory("PortableRadsuit",1);
		owner.A_PlaySound("weapons/pocket",CHAN_AUTO);
		let onr=HDPlayerPawn(self);
		if(onr)onr.stunned+=60;
		super.DetachFromOwner();
	}
	override void DoEffect(){
		if(stamina>0)stamina--;
	}
}
class PortableRadsuit:HDPickup replaces RadSuit{
	default{
		inventory.maxamount 2;
		inventory.interhubamount 2;
		inventory.pickupmessage "Environmental shielding suit.";
		inventory.pickupsound "weapons/pocket";
		inventory.icon "SUITB0";
		accuracy 1;
	}
	override void DetachFromOwner(){
		owner.A_TakeInventory("WornRadsuit");
		target=owner;
		super.DetachFromOwner();
	}
	override void actualpickup(actor user){
		HDF.TransferFire(self,user);
		super.actualpickup(user);
	}
	states{
	spawn2:
		SUIT A 1;
		SUIT A -1{
			if(!target)return;
			HDF.TransferFire(target,self);
		}
	use:
		TNT1 A 0{
			A_SetBlend("00 00 00",1,6,"00 00 00");
			A_PlaySound("weapons/pocket");
			A_ChangeVelocity(0,0,2);
			let onr=HDPlayerPawn(self);
			if(onr)onr.stunned+=60;
			if(!countinv("WornRadsuit")){
				int fff=HDF.TransferFire(self,self);
				if(fff){
					if(random(1,fff)>30){
						A_PlaySound("misc/fwoosh",CHAN_AUTO);
						A_TakeInventory("PortableRadsuit",1);
						return;
					}else{
						HDF.TransferFire(self,null);
						if(onr){
							onr.fatigue+=fff;
							onr.stunned+=fff;
						}
					}
				}
				A_GiveInventory("WornRadsuit");
			}else{
				actor a;int b;
				inventory wrs=findinventory("wornradsuit");
				[b,a]=A_SpawnItemEx("PortableRadsuit",0,0,height/2,2,0,4);
				if(a && wrs.stamina){
					let aa=HDActor(a);
					if(aa)aa.A_Immolate(a,self,wrs.stamina);
				}
				A_TakeInventory("WornRadsuit");
			}
		}fail;
	}
}




//-------------------------------------------------
// Light Amplification Visor
//-------------------------------------------------
class PortableLiteAmp:HDPickup replaces Infrared{
	default{
		inventory.maxamount 1;
		inventory.interhubamount 1;
		inventory.pickupmessage "Light amplification visor.";
		inventory.icon "PPVIA0";
		scale 0.5;
		accuracy 1;
	}
	int spent;bool worn;
	const LITEBAT=84000;
	override void DetachFromOwner(){
		if(owner && owner.player && owner.player.fixedcolormap==5)owner.player.fixedcolormap=-1;
		worn=false;
		owner.player.fixedlightlevel=-1;
		super.DetachFromOwner();
	}
	override void DoEffect(){
		super.DoEffect();
		if(owner && owner.player){
			if(
				worn
				&&!owner.countinv("PowerInvisibility")
				&&(owner.player.fixedcolormap<0||owner.player.fixedcolormap==5)
			){
				owner.player.fov=min(owner.player.fov,90);
				owner.player.fixedcolormap=5;
				owner.player.fixedlightlevel=1;
				if(!random[rand1](0,LITEBAT-spent)){
					owner.player.fixedcolormap=-1;
					owner.player.fixedlightlevel=-1;
				}
				spent++;
				if(spent>LITEBAT)worn=false;
			}else{
				if(owner.player.fixedcolormap==5)owner.player.fixedcolormap=-1;
				owner.player.fixedlightlevel=-1;
			}
		}
	}
	states{
	spawn2:
		PVIS B -1;
	use:
		TNT1 A 0{
			A_SetBlend("01 00 00",0.8,16);
			if(invoker.spent>=LITEBAT){
				if(countinv("HDCellPacks")){
					A_TakeInventory("HDCellPacks",1,TIF_NOTAKEINFINITE);
					invoker.spent=0;
				}else if(countinv("HDCellAmmo")){
					int rnd=min(random(1,19),countinv("HDCellAmmo"));
					A_TakeInventory("HDCellAmmo",rnd,TIF_NOTAKEINFINITE);
					invoker.spent-=rnd*LITEBAT/20;
				}else A_Log("No power for lite-amp.",true);
			}
			if(invoker.spent<LITEBAT){
				if(invoker.worn)invoker.worn=false;else invoker.worn=true;
			}
		}fail;
	}
}









//-------------------------------------------------
// Rope
//-------------------------------------------------

//show where the rope is hanging
//no it doesn't swing, math is hard :(
class hdropesection:idledummy{
	default{
		+wallsprite
	}
	double proportion;
	override void postbeginplay(){
		super.postbeginplay();
		//master is top, target is bottom
		if(!master||!target){
			destroy();return;
		}
		proportion=target.pos.z-master.pos.z;
		if(!proportion){
			destroy();return;
		}
		proportion=abs(master.pos.z-pos.z)/proportion;
	}
	override void tick(){
		if(!master||!target){
			destroy();return;
		}
		double mpz=master.pos.z;
		setz(mpz+(mpz-target.pos.z)*proportion);
		nexttic();
	}
	states{
	spawn:
		ROCK A -1;
	}
}
class hdropetop:hdactor{
	default{
		+missile //testing
		+flatsprite
		height 1;radius 2;
	}
	override void postbeginplay(){
		super.postbeginplay();
		bmissile=false;
		setz(floorz);
		fcheckposition tm;
		vector2 mvlast=pos.xy;
		vector2 mv=angletovector(angle,2);
		for(int i=0;i<10;i++){
			mvlast+=mv;
			checkmove(mvlast,PCM_DROPOFF|PCM_NOACTORS,tm);

			//found a place for the rope to hang down
			int htdiff=clamp(floorz-tm.floorz,0,HDCONST_MAXROPE);
//			A_Log(string.format("%i %i",floorz,tm.floorz));
			if(htdiff){
				//spawn the rope end
				target=spawn("hdropebottom",tm.pos);
				target.target=self;
				target.master=master;

				//spawn the rope sections
				int sectionlength=min(htdiff,HDCONST_MAXROPE)/20;
				for(int i=0;i<HDCONST_MAXROPE/20;i++){
					tm.pos.z-=sectionlength;
					actor sss=spawn("hdropesection",tm.pos);
					sss.master=self;sss.target=target;sss.angle=angle;
				}

				//now that we're at the bottom, move the rope end
				target.setz(tm.pos.z);

				return;
			}
		}

		//if there's no lower floor to drop the rope, abort.
		if(master)master.A_Log("No point hanging a rope here.",true);
		destroy();
	}
	states{
	spawn:
		BAL2 A -1;
	}
}
const HDCONST_MAXROPE=200;
class hdropebottom:hdactor{
	default{
		+nogravity
		height 0.1;radius 0.1;
	}
	states{
	spawn:
		BAL1 A 1{
			setz(clamp(pos.z,target.pos.z-HDCONST_MAXROPE,target.pos.z+HDCONST_MAXROPE));
		}wait;
	}
}