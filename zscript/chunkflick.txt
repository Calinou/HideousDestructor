//-------------------------------------------------
// Flick a piece of tiny debris at something to set off a shootable switch.
//-------------------------------------------------
class FlickChunk:HDBullet{
	default{
		+rollsprite +rollcenter
		speed 256;
		woundhealth 0;
		accuracy 0;
		mass 100;
		deathsound "misc/casing3";
	}
	override actor Puff(int sp){
		bmissile=0;
		setstatelabel("death");
		return null;
	}
	override void GunSmoke(){}
	states{
	spawn:
		DUST ABCD -1 nodelay{
			frame=random(0,3);
			roll=frandom(0,360);
			scale=(randompick(-1,1),randompick(-1,1))*frandom(0.05,0.12);
		}
	death:
		---- A 10 A_Scream();
		stop;
	}
}
class WallChunkAmmo:Ammo{default{+inventory.untossable inventory.maxamount 5;}}
class BoogerFlicker:HDWeapon{
	action void A_Flick(){
		if(
			!countinv("WallChunkAmmo")&&(
				countinv("WornRadsuit")
				||(
					findinventory("PortableLiteAmp")
					&&PortableLiteAmp(findinventory("PortableLiteAmp")).worn
				)
			)
		){
			A_Log("You're wearing something over your face. Take it off first before you do this.",true);
			return;
		}

		bool isrobot=player.getgender()==2;

		actor p=spawn("FlickChunk",pos+(0,0,height-6));
		p.target=self;p.angle=angle;p.pitch=pitch;
		p.speed=256;
		p.vel+=(
			frandom(-1.,1.),frandom(-1.,1.),frandom(-1.,1.)
		);
		p.vel+=self.vel;

		if(!countinv("WallChunkAmmo")){
			p.deathsound="misc/bloodchunks";
			p.A_SetTranslation(isrobot?"allred":"booger");
		}else A_TakeInventory("WallChunkAmmo",1);
		A_PlaySound("weapons/rifleclick",CHAN_WEAPON,0.3);
	}
	action void A_Pick(){
		if(countinv("WallChunkAmmo")){
			A_Log("You find some random punched-wall debris in your pockets and flick it at the offending thing.",true);
			return;
		}
		A_SetTics(30);
		A_PlaySound("weapons/pocket",CHAN_WEAPON);
		if(player.getgender()==2)
		A_Log("For want of random wall-punched debris or a natural mucus system, you scrape some tainted lubricant off of some of your unseen parts and wad it into a filthy, flickable lump.");
		else A_Log("For want of random wall-punched debris in your pockets, you pick your nose and flick it at the offending thing.",true);
	}
	states{
	ready:
		TNT1 A 10;
		TNT1 A 0 A_Pick();
		TNT1 A 10 A_Flick();
		TNT1 A 0 A_SelectWeapon("Ring");
		TNT1 A 1 A_WeaponReady(WRF_NOFIRE);
		goto nope;
	}
}
