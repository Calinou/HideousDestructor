// ------------------------------------------------------------
//   ZM66 assault rifle
// ------------------------------------------------------------

class HDFourMilTrail:IdleDummy{
	states{
	spawn:
		TNT1 A 10 nodelay{
			if(random(1,3)==1){A_AlertMonsters();}
			A_PlaySound("weapons/bulletcrack",0,0.12);
		}stop;
	}
}

class HDFourMil:HDBullet{
	default{
		obituary "%o was shot up by %k.";
		decal "BulletScratch";
		speed 1100;mass 45;accuracy 5;woundhealth 3;
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_LongArmWobble();
		spawn("DistantRifle",pos);
		A_Gunsmoke();
	}
	states{
	spawn:
	spawn1:
		TNT1 A 0;
		goto spawn2;
	death:
		TNT1 A 0{
			A_AlertMonsters();
			actor pf=spawn("BulletPuffSmall",self.pos);
			pf.target=target;
		}goto death1;
	xdeath:
		TNT1 A 0{
			if (random(1,10)==1){A_GiveInventory("SawGib",1,AAPTR_TRACER);}
		}goto xdeath1;
	}
}
class HDFourMilFast:HDFourMil{
	default{
		speed 1200;accuracy 6;
	}
}
class HDFourMilSlow:HDFourMil{
	default{
		speed 1000;accuracy 4;
	}
}

class ZM66RifleEmptyMag:HDDebris{
	default{
		translation "160:167=104:111";
		bouncesound "misc/casing2";bouncefactor 0.6;xscale 0.4;yscale 0.3;radius 2;height 2;
	}
	states{
	spawn:
		CLIP B 2{
			A_SetRoll(roll+30,SPF_INTERPOLATE);
		}loop;
	death:
		CLIP B -1{
			roll=randompick(0,0,0,0,2,2,2,2,1,3)*90;
		}stop;
	}
}


