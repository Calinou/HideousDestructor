// ------------------------------------------------------------
// Chainsaw
// ------------------------------------------------------------
const LUMBERJACKDRAIN=1023;
class Lumberjack:HDWeapon replaces Chainsaw{
	default{
		//$Category "Weapons/Hideous Destructor"
		//$Title "Lumberjack"
		//$Sprite "CSAWA0"
		weapon.selectionorder 90;
		weapon.slotnumber 1;
		weapon.bobstyle "Alpha";
		weapon.bobrangex 0.3;
		weapon.bobrangey 1.4;
		weapon.bobspeed 2.1;
		weapon.kickback 2;
		scale 0.4;
		inventory.pickupmessage "You got the chainsaw! Go find some meat!";
		obituary "%o got cut by %k.";
		hdweapon.barrelsize 26,1,2;
		hdweapon.refid HDLD_CHAINSW;
		hdweapon.nicename "Chainsaw";
	}
	override string getobituary(actor victim,actor inflictor,name mod,bool playerattack){
		bool clam=true;
		for(int i=0;i<MAXPLAYERS;i++){
			if(playeringame[i]&&(players[i].getgender()!=1)){
				clam=false;
				break;
			}
		}
		if(
			clam
		)return "%o was felled by the power of %k's terribly eager beaver.";
		return obituary;
	}
	override double gunmass(){
		return 5+weaponstatus[CSAWS_BATTERY]<0?0:1;
	}
	override double,int weaponbulk(){
		return 100+(weaponstatus[CSAWS_BATTERY]>=0?ENC_BATTERY_LOADED:0),USE_BATTERY;
	}
	states{
	ready:
		BEVG C 1{
			A_TakeInventory("WasChoppingFlesh");
			if(invoker.weaponstatus[CSAWS_INERTIA]>0)setweaponstate("ready2");
			else A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWUSER3|WRF_ALLOWUSER4);
		}goto readyend;
	ready2:
		BEVG CD 3{
			if(invoker.weaponstatus[CSAWS_INERTIA]>0)invoker.weaponstatus[CSAWS_INERTIA]--;
			if((invoker.weaponstatus[CSAWS_INERTIA]>4)&&(invoker.weaponstatus[CSAWS_BATTERY]>4)){
				A_SetTics(2);
				A_PlaySound("weapons/sawfull",CHAN_WEAPON);
			}else if((invoker.weaponstatus[CSAWS_INERTIA]>1)&&(invoker.weaponstatus[CSAWS_BATTERY]>2)){
				A_PlaySound("weapons/sawidle",CHAN_WEAPON);
			}else{
				A_SetTics(random(2,4));
				A_PlaySound("weapons/sawidle",CHAN_WEAPON);
			}
			A_WeaponReady(WRF_NOSECONDARY);
		}goto readyend;
	select0:
		BEVG A 0{invoker.weaponstatus[CSAWS_INERTIA]=0;}
		goto select0big;
	deselect0:
		BEVG A 0;
		goto deselect0big;
	hold:
		BEVG A 0 A_JumpIf(invoker.weaponstatus[CSAWS_BATTERY]>0,"saw");
		goto nope;
	fire:
		BEVG C 2;
		BEVG C 4 A_JumpIf(invoker.weaponstatus[CSAWS_BATTERY]>0,"saw");
		goto beatdown;
	saw:
		BEVG AB 1{
			A_WeaponReady(WRF_NONE);
			int drainprob=LUMBERJACKDRAIN;
			if(invoker.weaponstatus[CSAWS_INERTIA]<12)invoker.weaponstatus[CSAWS_INERTIA]++;
			if((invoker.weaponstatus[CSAWS_BATTERY]>random(5,8))&&(invoker.weaponstatus[CSAWS_INERTIA]>11)){
				A_MuzzleClimb(
					randompick(-1,1)*frandom(-0.2,-0.3),
					randompick(-1,1)*frandom(0.2,0.4)
				);
				A_Saw("weapons/sawfull","weapons/sawhit",5,"HDSawPuff",SF_NOPULLIN);
				A_AlertMonsters();
			}else if((invoker.weaponstatus[CSAWS_BATTERY]>random(2,4))&&(invoker.weaponstatus[CSAWS_INERTIA]>6)){
				A_MuzzleClimb(
					randompick(-1,1)*frandom(-0.1,-0.3),
					randompick(-1,1)*frandom(0.1,0.4)
				);
				A_Saw("weapons/sawfull","weapons/sawhit",3,"HDSawPuff",SF_NOPULLIN);
				A_SetTics(2);
				A_AlertMonsters();
			}else if((invoker.weaponstatus[CSAWS_BATTERY]>random(1,4))&&(invoker.weaponstatus[CSAWS_INERTIA]>1)){
				drainprob*=1.5;
				A_MuzzleClimb(
					randompick(-1,1)*frandom(-0.05,-0.6),
					randompick(-1,1)*frandom(0.05,0.2)
				);
				A_Saw("weapons/sawfull","weapons/sawhit",1,"HDSawPuff",SF_NOPULLIN);
				A_SetTics(random(2,4));
				A_AlertMonsters();
			}else{
				drainprob*=4;
				A_MuzzleClimb(frandom(-0.2,0.2),frandom(-0.2,0.2));
				A_PlaySound("weapons/sawidle",CHAN_WEAPON);
				A_FireProjectile("HDSawPuffShitty");
				A_SetTics(random(3,6));
			}
			if(!random(0,drainprob)&&invoker.weaponstatus[CSAWS_BATTERY]>0)invoker.weaponstatus[CSAWS_BATTERY]--;
		}
		BEVG B 1{
			A_WeaponReady(WRF_NONE);
			A_Refire();
		}goto ready;

	altfire:
	beatdown:
		BEVG C 0 A_JumpIfInventory("PowerStrength",1,"beatdownstrong");
		BEVG C 1 offset(0,33);
		BEVG C 1 offset(0,32);
		BEVG DC 1 A_SetPitch(pitch-1,SPF_INTERPOLATE);
		BEVG DC 2 A_SetPitch(pitch-1,SPF_INTERPOLATE);
		BEVG CC 2 A_SetPitch(pitch-2,SPF_INTERPOLATE);
		BEVG C 0 A_Refire("beatdown2");
		goto whack;
	beatdownstrong:
		BEVG D 1 A_SetPitch(pitch-4,SPF_INTERPOLATE);
		BEVG C 1 A_SetPitch(pitch-6,SPF_INTERPOLATE);
		BEVG C 1 A_SetPitch(pitch-8,SPF_INTERPOLATE);
		BEVG C 0 A_Refire("beatdown2");
		goto whack;
	althold:
	beatdown2:
		BEVG C 1{
			A_SetAngle(angle+frandom(-0.1,0.08),SPF_INTERPOLATE);
			A_SetPitch(pitch+frandom(-0.05,0.1),SPF_INTERPOLATE);
		}
		BEVG C 0 A_Refire("beatdown2");
	whack:
		BEVG D 1 A_JumpIf(pitch>25,"nowhack");
		BEVG AB 1 offset(0,36) A_SetPitch(pitch+10,SPF_INTERPOLATE);
		BEVG AAAAA 0 A_CustomPunch(random(1,5),0,0,"HDFistPuncher",46);
		BEVG A 1 offset(0,35){
			if(countinv("PowerStrength")>0){A_CustomPunch(random(10,20),0,0,"HDFistPuncher",46);}  
			A_SetPitch(pitch+10,SPF_INTERPOLATE);
		}
		BEVG B 2 offset(0,34);
		BEVG C 2 offset(0,33);
	nowhack:
		BEVG C 2 offset(0,32);
		BEVG CCCCC 1 A_WeaponReady(WRF_NOFIRE);
		BEVG C 0 A_JumpIfInventory("PowerStrength",1,6);
		BEVG CCCCC 1 A_WeaponReady(WRF_NOFIRE);
		BEVG C 0 A_Refire("beatdown2");
		goto ready;

	reload:
		BEVG C 0{
			if(
				invoker.weaponstatus[CSAWS_BATTERY]>=20
				||!countinv("HDBattery")
			){return resolvestate("nope");}
			invoker.weaponstatus[0]&=~CSAWF_JUSTUNLOAD;
			return resolvestate("unmag");
		}

	user4:
	unload:
		BEVG C 0{
			if(invoker.weaponstatus[CSAWS_BATTERY]<0){
				return resolvestate("nope");
			}invoker.weaponstatus[0]|=CSAWF_JUSTUNLOAD;return resolvestate(null);
		}
	unmag:
		BEVG A 1 offset(0,33);
		BEVG A 1 offset(0,35);
		BEVG A 1 offset(0,37);
		BEVG A 1 offset(0,39);
		BEVG A 2 offset(0,44);
		BEVG A 2 offset(0,52);
		BEVG A 3 offset(2,62);
		BEVG A 4 offset(4,74);
		BEVG A 7 offset(6,78)A_PlaySound("weapons/rifleload",CHAN_WEAPON);
		BEVG A 0{
			A_PlaySound("weapons/grenopen",CHAN_WEAPON);
			if(
				!PressingUnload()&&!PressingReload()
			){
				setweaponstate("dropmag");
			}else setweaponstate("pocketmag");
		}
	dropmag:
		BEVG A 0{
			if(invoker.weaponstatus[CSAWS_BATTERY]>=0){
				HDMagAmmo.SpawnMag(self,"HDBattery",invoker.weaponstatus[CSAWS_BATTERY]);
			}
			invoker.weaponstatus[CSAWS_BATTERY]=-1;
		}goto magout;
	pocketmag:
		BEVG A 6 offset(7,80){
			if(invoker.weaponstatus[CSAWS_BATTERY]>=0){
				HDMagAmmo.GiveMag(self,"HDBattery",invoker.weaponstatus[CSAWS_BATTERY]);
				A_PlaySound("weapons/pocket");
				A_MuzzleClimb(
					randompick(-1,1)*frandom(-0.3,-1.2),
					randompick(-1,1)*frandom(0.3,1.8)
				);
			}
			invoker.weaponstatus[CSAWS_BATTERY]=-1;
		}
		BEVG A 7 offset(6,81) A_PlaySound("weapons/pocket");
		goto magout;

	magout:
		BEVG A 0 A_JumpIf(invoker.weaponstatus[0]&CSAWF_JUSTUNLOAD,"reloadend");
	loadmag:
		BEVG A 4 offset(7,79) A_MuzzleClimb(
			randompick(-1,1)*frandom(-0.3,-1.2),
			randompick(-1,1)*frandom(0.3,0.8)
		);
		BEVG A 2 offset(6,78) A_PlaySound("weapons/pocket");
		BEVG AA 5 offset(5,76) A_MuzzleClimb(
			randompick(-1,1)*frandom(-0.3,-1.2),
			randompick(-1,1)*frandom(0.3,0.8)
		);
		BEVG A 0{
			let mmm=HDMagAmmo(findinventory("HDBattery"));
			if(mmm)invoker.weaponstatus[CSAWS_BATTERY]=mmm.TakeMag(true);
		}
	reloadend:
		BEVG A 6 offset(5,72);
		BEVG A 5 offset(4,74)A_PlaySound("weapons/huntopen",CHAN_WEAPON);
		BEVG A 4 offset(2,62);
		BEVG A 3 offset(0,52);
		BEVG A 4 offset(0,44);
		BEVG A 1 offset(0,37);
		BEVG A 1 offset(0,35);
		BEVG C 1 offset(0,33);
		goto ready;

	user3:
		BEVG A 0 A_MagManager("HDBattery");
		goto ready;

	spawn:
		CSAW A -1;
	}
	override void initializewepstats(bool idfa){
		weaponstatus[CSAWS_BATTERY]=20;
	}
}
enum lumberstatus{
	CSAWF_JUSTUNLOAD=1,

	CSAWS_FLAGS=0,
	CSAWS_BATTERY=1,
	CSAWS_INERTIA=2,
};



class HDSawPuffShitty:FastProjectile{
	default{
		damage(0);
		speed 42;height 1;radius 2;
	}
	states{
	spawn:
		TNT1 A 1;
		stop;
	death:
		TNT1 A 10 A_PlaySound("weapons/rifleclick",0,0.4);
		stop;
	xdeath:
		TNT1 A 10 A_PlaySound("weapons/pocket",0,0.4);
		stop;
	}
}


class WasChoppingFlesh:InventoryFlag{}
class HDSawPuff:IdleDummy{
	default{
		+puffonactors +bloodsplatter +puffgetsowner +hittracer +nointeraction
		damagetype "Chainsaw";decal "BulletChipSmall";
	}
	lumberjack lmb;
	states{
	spawn:
		TNT1 A 1 nodelay{
			A_GiveInventory("WasChoppingFlesh",1,AAPTR_TARGET);
			if(tracer){
				if(tracer.bcorpse)hdf.give(tracer,"SawGib",random(5,10));
				if(tracer.bnoblood)spawn("BulletPuffMedium",pos);
				else hdwound.inflict(tracer,random(1,7));
			}
		}stop;
	crash:
		TNT1 A 1{
			if(target){
				if(!random(0,LUMBERJACKDRAIN)){
					lmb=lumberjack(target.findinventory("lumberjack"));
					if(lmb && lmb.weaponstatus[CSAWS_BATTERY]>0)lmb.weaponstatus[CSAWS_BATTERY]--;
				}
				if(target && target.countinv("WasChoppingFlesh")){
					let tgt=HDPlayerPawn(target);
					if(tgt){
						tgt.muzzleclimb1.x+=random(-30,10);
						tgt.muzzleclimb1.y+=random(-10,6);
					}
					target.A_Recoil(random(-1,2));
					target.damagemobj(self,target,1,"chainsaw");
					target.A_TakeInventory("WasChoppingFlesh");
				}
			}
			spawn("BulletPuffMedium",pos);
			if(lmb && lmb.weaponstatus[CSAWS_BATTERY]>0 && !random(0,LUMBERJACKDRAIN*3))lmb.weaponstatus[CSAWS_BATTERY]--;
		}stop;
	}
}


