//-------------------------------------------------
// Not-Quite-Improvised Explosive Device
//-------------------------------------------------
enum HDIEDConst{
	HDIED_TID=8495,
}
class HDIEDKit:HDPickup{
	default{
		inventory.amount 1;
		inventory.maxamount 24;
		inventory.interhubamount 24;
		inventory.icon "IEDI";
		inventory.pickupmessage "Picked up an IED kit.";
		height 4;radius 4;scale 0.5;
	}
	override void attachtoowner(actor user){
		super.attachtoowner(user);
		user.A_GiveInventory("IEDController");
	}
	states{
	spawn2:
		IEDK A -1;
	use:
		TNT1 A 0{
			class<inventory> which="";
			if(countinv("DudRocketAmmo"))which="DudRocketAmmo";
			else if(countinv("HDRocketAmmo"))which="HDRocketAmmo";
			else{
				A_Log("You need at least one live or dud rocket grenade.",true);
				return;
			}

			A_TakeInventory(which,1,TIF_NOTAKEINFINITE);
			A_SpawnItemEx("HDIED",0,0,height-12,
				vel.x,vel.y,vel.z,0,
				SXF_SETMASTER|SXF_NOCHECKPOSITION|
				SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPITCH
			);
		}fail;
	}
}
class HDEnemyIED:HDIED{
	default{
		//$Category "Misc/Hideous Destructor/Traps"
		//$Title "Enemy IED"
		//$Sprite "IEDSC0"
		-friendly
	}
}
class HDIED:DudRocket{
	default{
		//$Category "Misc/Hideous Destructor/Traps"
		//$Title "Friendly IED"
		//$Sprite "IEDSA0"

		//mm: actively scanning; mem: live
		-missilemore -missileevenmore

		-missile +friendly +lookallaround +nosplashalert
		-pushable +shootable +noblood +nodamage
		height 7;radius 4;
		painchance 256;maxtargetrange 96;
		obituary "%o was blown up by an anonymous %k.";
	}
	states{
	spawn:
		IEDS C 35 nodelay{
			A_ChangeVelocity(4*cos(pitch),0,4*sin(-pitch),CVF_RELATIVE);
			if(master){
				master.A_TakeInventory("HDIEDKit",1,TIF_NOTAKEINFINITE);
				ChangeTid(HDIED_TID);
			}
		}
		IEDS C 0 A_PlaySound("misc/i_pkup");
		IEDS CBCBC 4;
		IEDS ABABABABABABAB 2;
		IEDS ABABAB 1;
		IEDS A 0{
			bmissileevenmore=true;
			if(!master||master.countinv("IEDController")==1)bmissilemore=true;
		}
		goto idle;
	idle:
		IEDS A 10{
			if(master&&master.countinv("IEDController")==2)bmissilemore=false;
				else bmissilemore=true;
			if(bmissilemore){
				A_LookEx(LOF_NOSOUNDCHECK,0,96.0,0,360,"melee");
			}
			A_ClearTarget();
		}loop;
	melee:
		IEDS A 0;
		goto detonate;
	pain:
		IEDS A 0 A_Jump(8,"destroy");
		goto idle;
	pain.smallarms1:
	pain.smallarms2:
	pain.smallarms3:
		IEDS A 0 A_Jump(200,"destroy");
		goto idle;
	detonate:
		IEDS A 1{
			bshootable=false;
			bfriendly=false;
			bismonster=false;
			target=master;
		}goto explode;
	destroy:
		IEDS A 0{
			if(!random(0,7))setstatelabel("detonate");
		}goto dismantle;
	grab:
	dismantle:
		IEDS A 0{
			A_SpawnItemEx("HDIEDKit",0,0,height-12,
				vel.x,vel.y,vel.z+4,0,
				SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
			);
			A_SpawnItemEx("DudRocket",0,0,height-12,
				vel.x,vel.y,vel.z+2,0,
				SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
			);
		}stop;
	}
}
class IEDController:InventoryFlag{
	default{
		inventory.maxamount 2;
	}
	override void DoEffect(){
		if(!owner.player){destroy();return;}

		string cmd=cvar.getcvar("ied",owner.player).getstring();
		if(cmd=="")return;
		cmd.tolower();

		//2=safety on
		if(cmd.charat(0)=="s"||cmd.charat(0)=="p")amount=2;else amount=1;

		actoriterator it=actoriterator.create(HDIED_TID,"HDIED");
		actor ied;
		while(ied=it.Next()){
			if(ied.master==owner){
				if(cmd=="detonate"){
					if(ied.bmissileevenmore)ied.setstatelabel("detonate");
				}
				else if(cmd.charat(0)=="a"){
					ied.bmissilemore=true;
				}
				else{
					ied.bmissilemore=false;
				}
			}
		}

		//reset
		cvar.getcvar("ied",owner.player).resettodefault();
	}
}


//spawn actor
class HDIEDKits:IdleDummy{
	states{
	spawn:
		TNT1 A 0 nodelay{
			A_SpawnItemEx("HDIEDKit",-2,0,0);
			A_SpawnItemEx("HDIEDKit",-2,-2,0);
			A_SpawnItemEx("HDIEDKit",-2,-4,0);
			A_SpawnItemEx("HDIEDKit",0,-2,0);
			A_SpawnItemEx("HDIEDKit",0,-4,0);
			A_SpawnItemEx("HDIEDKit",0,0,0);
		}stop;
	}
}




