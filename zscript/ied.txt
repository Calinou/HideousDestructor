//-------------------------------------------------
// Not-Quite-Improvised Explosive Device
//-------------------------------------------------
enum HDIEDConst{
	HDIED_TID=84954,

	HDIED_DETONATE=99,
	HDIED_AWAKEN=42,
	HDIED_PASSIVE=2,
}
class HDIEDKit:HDPickup{
	default{
		inventory.amount 1;
		inventory.maxamount 24;
		inventory.interhubamount 24;
		inventory.icon "IEDI";
		inventory.pickupmessage "Picked up an IED kit.";
		height 4;radius 4;scale 0.5;
	}
	override void attachtoowner(actor user){
		super.attachtoowner(user);
		user.A_SetInventory("IEDController",1);
		user.A_GiveInventory("IEDDet");
		user.A_GiveInventory("IEDSetPassive");
	}
	states{
	spawn2:
		IEDK A -1;
	use:
		TNT1 A 0{
			class<inventory> which="";
			if(countinv("DudRocketAmmo"))which="DudRocketAmmo";
			else if(countinv("HDRocketAmmo"))which="HDRocketAmmo";
			else{
				A_Log("You need at least one live or dud rocket grenade.",true);
				return;
			}

			A_TakeInventory(which,1,TIF_NOTAKEINFINITE);
			A_SpawnItemEx("HDIED",0,0,height-12,
				vel.x,vel.y,vel.z,0,
				SXF_SETMASTER|SXF_NOCHECKPOSITION|
				SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPITCH
			);
		}fail;
	}
}
class HDIED:DudRocket{
	default{
		//mm: actively scanning; mem: live
		-missilemore -missileevenmore

		-missile +friendly +lookallaround +nosplashalert
		-pushable +shootable +noblood +nodamage
		height 7;radius 4;
		painchance 256;maxtargetrange 96;
		obituary "%o was blown up by an anonymous %k.";
	}
	states{
	spawn:
		IEDS C 35 nodelay{
			A_ChangeVelocity(4*cos(pitch),0,4*sin(-pitch),CVF_RELATIVE);
			if(master){
				master.A_TakeInventory("HDIEDKit",1,TIF_NOTAKEINFINITE);
				ChangeTid(HDIED_TID);
			}
		}
		IEDS C 0 A_PlaySound("misc/i_pkup");
		IEDS CBCBC 7;
		IEDS ABABABABABABAB 5;
		IEDS ABABAB 1;
		IEDS A 0{
			bmissileevenmore=true;
			if(!master||master.countinv("IEDController")==1)bmissilemore=true;
		}
		goto idle;
	idle:
		IEDS A 10{
			if(bmissilemore){
				A_LookEx(LOF_NOSOUNDCHECK,0,96.0,0,360,"melee");
			}
			A_ClearTarget();
		}loop;
/*
	see:
		IEDS B 2 A_JumpIfCloser(96,"melee");
		IEDS C 2 A_ClearTarget();
		goto idle;
*/
	melee:
		IEDS A 1;
		goto detonate;
	pain:
		IEDS A 0 A_Jump(8,"destroy");
		goto idle;
	pain.smallarms1:
	pain.smallarms2:
	pain.smallarms3:
		IEDS A 0 A_Jump(200,"destroy");
		goto idle;
	detonate:
		IEDS A 0{
			bshootable=false;
			bfriendly=false;
			bismonster=false;
			target=master;
			master=null;
		}goto explode;
	destroy:
		IEDS A 0{
			if(!random(0,7))setstatelabel("detonate");
		}goto dismantle;
	grab:
	dismantle:
		IEDS A 0{
			A_SpawnItemEx("HDIEDKit",0,0,height-12,
				vel.x,vel.y,vel.z+4,0,
				SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
			);
			A_SpawnItemEx("DudRocket",0,0,height-12,
				vel.x,vel.y,vel.z+2,0,
				SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
			);
		}stop;
	}
}
class IEDDet:ActionItem{
	default{+inventory.undroppable}
	states{
	use:
		TNT1 A 0{
			A_Print("All online IEDs \cgDETONATED");
			A_SetInventory("IEDController",HDIED_DETONATE);
		}fail;
	}
}
class IEDSetPassive:ActionItem{
	default{+inventory.undroppable}
	states{
	use:
		TNT1 A 0{
			if(countinv("IEDController")!=2){
				A_Print("All online IEDs \cjPASSIVE");
				A_SetInventory("IEDController",HDIED_PASSIVE);
			}else{
				A_Print("All online IEDs \cdACTIVE");
				A_SetInventory("IEDController",HDIED_AWAKEN);
			}
		}fail;
	}
}
class IEDController:InventoryFlag{
	default{
		inventory.maxamount 999;
	}
	override void DoEffect(){
		if(amount==1)return;

		actoriterator it=actoriterator.create(HDIED_TID,"HDIED");
		actor ied;
		while(ied=it.Next()){
			if(amount==HDIED_DETONATE){
				if(ied.bmissileevenmore)ied.setstatelabel("explode");
			}
			else if(amount==HDIED_PASSIVE){
				ied.bmissilemore=false;
			}
			else if(amount==HDIED_AWAKEN){
				ied.bmissilemore=true;
			}
		}

		//reset as necessary
		if(amount!=2)amount=1;
	}
}


//spawn actor
class HDIEDKits:IdleDummy{
	states{
	spawn:
		TNT1 A 0 nodelay{
			A_SpawnItemEx("HDIEDKit",-2,0,0);
			A_SpawnItemEx("HDIEDKit",-2,-2,0);
			A_SpawnItemEx("HDIEDKit",-2,-4,0);
			A_SpawnItemEx("HDIEDKit",0,-2,0);
			A_SpawnItemEx("HDIEDKit",0,-4,0);
			A_SpawnItemEx("HDIEDKit",0,0,0);
		}stop;
	}
}




