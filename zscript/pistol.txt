// ------------------------------------------------------------
//   Pistol
// ------------------------------------------------------------

class HDSecondPistol:Ammo{
	default{
		+inventory.ignoreskill +inventory.untossable
		inventory.maxamount 1;ammo.backpackmaxamount 1;ammo.backpackamount 0;
	}
	states{
		spawn:TNT1 A 0 nodelay;
		stop;
	}
}
extend class HDPlayerPawn{
	void PistolConsolidator(){
		if(countinv("HDPistol")) return;
		if(!countinv("HDSecondPistol")) return;
		A_SetInventory("HDPistolChamber",countinv("HDSecondPistolChamber"));
		A_SetInventory("HDPistolNoMag",countinv("HDSecondPistolNoMag"));
		A_SetInventory("HDPistolLoaded",countinv("HDSecondPistolLoaded"));
		A_SetInventory("HDPistolFullAuto",countinv("HDSecondPistolFullAuto"));
		A_GiveInventory("HDPistol");
		A_TakeInventory("HDSecondPistol");
		A_TakeInventory("HDSecondPistolChamber");
		A_TakeInventory("HDSecondPistolNoMag");
		A_TakeInventory("HDSecondPistolLoaded");
		A_TakeInventory("HDSecondPistolFullAuto");
	}
}

class HDSpent9mm:HDDebris{
	override void postbeginplay(){
		super.postbeginplay();
		//should we just put this in the weapon itself? but right now they're all the same...
		A_ChangeVelocity(frandom(-1,1),frandom(-8,-9),frandom(-1,1),CVF_RELATIVE);
	}
	default{
		bouncesound "misc/casing3";scale 0.6;
	}
	states{
	spawn:
		PBRS A 2{
			A_SetRoll(roll+45,SPF_INTERPOLATE);
		}loop;
	death:
		PBRS "#" -1;
	}
}
class HDLoose9mm:HDSpent9mm{
	override void postbeginplay(){
		HDDebris.postbeginplay();
	}
	default{
		bouncefactor 0.5;
	}
	states{
	death:
		TNT1 A 1{
			actor a=spawn("HDPistolAmmo",self.pos,ALLOW_REPLACE);
			a.roll=self.roll;a.vel=self.vel;
		}stop;
	}
}
class HDPistolEmptyMag:HDDebris{
	default{
		bouncesound "misc/casing2";scale 0.6;bouncefactor 0.6;scale 1.0;
	}
	states{
	spawn:
		CLP2 B 2{
			A_SetRoll(roll+45,SPF_INTERPOLATE);
		}loop;
	death:
		CLP2 B -1{
			roll=randompick(0,0,0,0,2,2,2,2,1,3)*90;
		}stop;
	}
}
class HDZPistol:HDWeapon{
	states{
	spawn:
		TNT1 A 1 nodelay;
		TNT1 A 0{
			actor a=spawn("HDPistolPickup",pos);
			a.vel=vel;
			actor o=invoker.target;
			if(o && !invoker.target.countinv("HDPistol")){
				a.A_SetInventory("HDPistolLoaded",o.countinv("HDPistolLoaded"));
				a.A_SetInventory("HDPistolChamber",o.countinv("HDPistolChamber"));
				a.A_SetInventory("HDPistolFullAuto",o.countinv("HDPistolFullAuto"));
				a.A_SetInventory("HDPistolNoMag",o.countinv("HDPistolNoMag"));
				a.A_SetInventory("Clip",1);
				if(o.countinv("HDSecondPistol")){
					let oo=HDPlayerPawn(o);
					if(oo)oo.PistolConsolidator();
				}
			}
		}
		stop;
	}
}


