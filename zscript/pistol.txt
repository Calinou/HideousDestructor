// ------------------------------------------------------------
// Pistol
// ------------------------------------------------------------
class HDPistol:HDWeapon{
	default{
		+hdweapon.fitsinbackpack
		scale 0.63;
		weapon.selectionorder 50;
		weapon.slotnumber 2;
		weapon.kickback 30;
		weapon.bobrangex 0.1;
		weapon.bobrangey 0.6;
		weapon.bobspeed 2.5;
		weapon.bobstyle "normal";
		obituary "%o got capped by %k's pea shooter.";
		inventory.pickupmessage "You got the pistol!";
		hdweapon.nicename "Pistol";
		hdweapon.refid HDLD_PISTOL;
		inventory.maxamount 3;
	}
	override bool AddSpareWeapon(actor newowner){return AddSpareWeaponRegular(newowner);}
	override hdweapon GetSpareWeapon(actor newowner){return GetSpareWeaponRegular(newowner);}
	override double weaponbulk(){
		int mgg=weaponstatus[PISS_MAG];
		return 30+(mgg<0?0:(ENC_9MAG_LOADED+mgg*ENC_9_LOADED));
	}
	override void DrawHUDStuff(HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl){
		if(sb.gimmehud){
			int nextmagloaded=sb.GetNextLoadMag(hdmagammo(hpl.findinventory("HD9mMag15")));
			if(nextmagloaded>=15){
				sb.drawimage("CLP2A0",(-46,-3),sb.DI_SCREEN_CENTER_BOTTOM,scale:(3,3));
			}else if(nextmagloaded<1){
				sb.drawimage("CLP2B0",(-46,-3),sb.DI_SCREEN_CENTER_BOTTOM,alpha:nextmagloaded?0.6:1.,scale:(3,3));
			}else sb.drawbar(
				"CLP2NORM","CLP2GREY",
				nextmagloaded,15,
				(-46,-3),-1,
				sb.SHADER_VERT,sb.DI_SCREEN_CENTER_BOTTOM
			);
			sb.drawnum(hpl.countinv("HD9mMag15"),-43,-8,sb.DI_SCREEN_CENTER_BOTTOM,font.CR_BLACK);
		}
		sb.drawwepcounter(hdw.weaponstatus[PISS_AUTO]+1,
			-22,-10,"blank","RBRSA3A7","STFULAUT"
		);
		sb.drawwepnum(hdw.weaponstatus[PISS_MAG],15);
		if(hdw.weaponstatus[PISS_CHAMBER]==2)sb.drawwepdot(-16,-10,(3,1));
	}
	override void DrawSightPicture(
		HDStatusBar sb,HDWeapon hdw,HDPlayerPawn hpl,
		bool sightbob,vector2 bob,double fov,bool scopeview,actor hpc,string whichdot
	){
		int cx,cy,cw,ch;
		[cx,cy,cw,ch]=screen.GetClipRect();
		sb.SetClipRect(
			-8+bob.x,-4+bob.y,16,10,
			sb.DI_SCREEN_CENTER
		);
		vector2 bobb=bob*1.6;
		bobb.y=clamp(bobb.y,-8,8);
		sb.drawimage(
			"frntsite",(0,0)+bobb,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP,
			alpha:0.9,scale:(0.6,0.6)
		);
		sb.SetClipRect(cx,cy,cw,ch);
		sb.drawimage(
			"backsite",(0,0)+bob,sb.DI_SCREEN_CENTER|sb.DI_ITEM_TOP,
			scale:(0.6,0.6)
		);
	}
	override void DropOneAmmo(int amt){
		if(owner){
			amt=clamp(amt,1,10);
			if(owner.countinv("HDPistolAmmo"))owner.A_DropInventory("HDPistolAmmo",amt*15);
			else owner.A_DropInventory("HD9mMag15",amt);
		}
	}
	override void startconfigure(){
		switch(amount){
		case 3:
			weaponstatus[PISS_AUTO]=-1;
			break;
		case 2:
			weaponstatus[PISS_AUTO]=0;
			break;
		default:
			if(
				!random(0,127)
			)weaponstatus[PISS_AUTO]=0;
			break;
		}
		amount=1;maxamount=1;

		if(
			weaponstatus[PISS_AUTO]>=0
			&&owner
			&&owner.player
			&&cvar.getcvar("hd_pistauto",owner.player).getbool()
		)weaponstatus[PISS_AUTO]=1;
	}
	states{
	select0:
		PISG A 0 A_JumpIf(invoker.weaponstatus[PISS_CHAMBER]>0,2);
		PISG C 0;
		---- A 1 A_Raise();
		---- A 1 A_Raise(30);
		---- A 1 A_Raise(30);
		---- A 1 A_Raise(24);
		---- A 1 A_Raise(18);
		wait;
	deselect0:
		PISG A 0 A_JumpIf(invoker.weaponstatus[PISS_CHAMBER]>0,2);
		PISG C 0;
		---- AAA 1 A_Lower();
		---- A 1 A_Lower(18);
		---- A 1 A_Lower(24);
		---- A 1 A_Lower(30);
		wait;
	ready:
		PISG A 0 A_JumpIf(invoker.weaponstatus[PISS_CHAMBER]>0,2);
		PISG C 0;
		PISG # 1{
			A_SetCrosshair(21);
			A_WeaponReady(WRF_ALL);
		}goto readyend;
	user3:
		---- A 0 A_MagManager("HD9mMag15");
		goto ready;
	user2:
	firemode:
		---- A 0{
			int pa=invoker.weaponstatus[PISS_AUTO];
			if(pa>=0){
				invoker.weaponstatus[PISS_AUTO]=pa==0?1:0;
			}
		}goto nope;
	altfire:
		---- A 0{
			invoker.weaponstatus[0]&=~PISF_JUSTUNLOAD;
			if(
				invoker.weaponstatus[PISS_CHAMBER]!=2
				&&invoker.weaponstatus[PISS_MAG]>0
			)setweaponstate("chamber_manual");
		}goto nope;
	chamber_manual:
		---- A 0{
			if(
				!(invoker.weaponstatus[0]&PISF_JUSTUNLOAD)
				&&(
					invoker.weaponstatus[PISS_CHAMBER]==2
					||invoker.weaponstatus[PISS_MAG]<1
				)
			)setweaponstate("nope");
		}
		PISG B 3 offset(0,34);
		PISG C 4 offset(0,37){
			A_MuzzleClimb(frandom(0.4,0.5),-frandom(0.6,0.8));
			A_PlaySound("weapons/pismagclick",CHAN_WEAPON);
			int psch=invoker.weaponstatus[PISS_CHAMBER];
			invoker.weaponstatus[PISS_CHAMBER]=0;
			if(psch==2){
				A_SpawnItemEx("HDPistolAmmo",cos(pitch*12),0,height-8-sin(pitch)*12,1,2,3,0);
			}else if(psch==1){
				A_SpawnItemEx("HDSpent9mm",
					cos(pitch)*12,0,height-7-sin(pitch)*12,
					vel.x,vel.y,vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}
			if(invoker.weaponstatus[PISS_MAG]>0){
				invoker.weaponstatus[PISS_CHAMBER]=2;
				invoker.weaponstatus[PISS_MAG]--;
			}
		}
		PISG B 3 offset(0,35);
		PISG B 0 A_ReFire();
		goto ready;
	althold:
	hold:
		goto nope;
	fire:
		---- A 0{
			invoker.weaponstatus[0]&=~PISF_JUSTUNLOAD;
			if(invoker.weaponstatus[PISS_CHAMBER]==2)setweaponstate("shoot");
			else if(invoker.weaponstatus[PISS_CHAMBER]>0)setweaponstate("chamber_manual");
		}goto nope;
	shoot:
		PISG B 1{
			if(invoker.weaponstatus[PISS_CHAMBER]==2)A_GunFlash();
		}
		PISG C 1{
			if(hdplayerpawn(self)){
				hdplayerpawn(self).gunbraced=false;
				A_GiveInventory("IsMoving",1);
			}
			A_MuzzleClimb(
				-frandom(0.8,1.),-frandom(1.2,1.6),
				frandom(0.4,0.5),frandom(0.6,0.8)
			);
		}
		PISG C 0{
			A_SpawnItemEx("HDSpent9mm",
				cos(pitch)*12,0,height-7-sin(pitch)*12,
				vel.x,vel.y,vel.z,
				0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
			);
			invoker.weaponstatus[PISS_CHAMBER]=0;
			if(invoker.weaponstatus[PISS_MAG]<1){
				A_PlaySound("weapons/pistoldry",7,0.9);
				setweaponstate("nope");
			}
		}
		PISG B 1{
			A_WeaponReady(WRF_NOFIRE);
			invoker.weaponstatus[PISS_CHAMBER]=2;
			invoker.weaponstatus[PISS_MAG]--;
			if(invoker.weaponstatus[PISS_AUTO]==1){
				let pnr=HDPlayerPawn(self);
				if(
					pnr&&countinv("IsMoving")
					&&pnr.fatigue<12
				)pnr.fatigue++;
				A_GiveInventory("IsMoving",5);
				A_Refire("fire");
			}else A_Refire();
		}goto ready;
	flash:
		PISF A 1 bright{
			HDFlashAlpha(64);
			A_Light1();
			actor p=spawn("HDBullet9",pos+(0,0,height-6),ALLOW_REPLACE);
			p.target=self;p.angle=angle;p.pitch=pitch;
			if(p){
				p.vel+=self.vel+(frandom(-2.,2.),frandom(-2.,2.),frandom(-2.,2.));
				p.speed+=10*frandom(-2.,2.);
			}
			invoker.weaponstatus[PISS_CHAMBER]=1;
			A_ZoomFactor(0.995,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			A_MuzzleClimb(-frandom(0.4,1.2),-frandom(0.4,1.6));
		}
		PISF A 0{
			A_PlaySound("weapons/pistol",CHAN_WEAPON,1.0);
			A_ZoomFactor(1.0,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			A_Light0();
		}stop;
	unload:
		---- A 0{
			invoker.weaponstatus[0]|=PISF_JUSTUNLOAD;
			if(invoker.weaponstatus[PISS_MAG]>=0)setweaponstate("unmag");
		}goto chamber_manual;
	loadchamber:
		---- A 0 A_JumpIf(invoker.weaponstatus[PISS_CHAMBER]>0,"nope");
		---- A 1 offset(0,34) A_PlaySound("weapons/pocket",CHAN_WEAPON);
		---- A 1 offset(2,36);
		PISG C 3 offset(5,40);
		PISG C 6 offset(4,39){
			if(countinv("HDPistolAmmo")){
				A_TakeInventory("HDPistolAmmo",1,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[PISS_CHAMBER]=2;
				A_PlaySound("weapons/pischamber1",CHAN_WEAPON);
			}
		}
		PISG B 3 offset(2,36);
		PISG B 2 offset(0,34);
		goto readyend;
	reload:
		---- A 0{
			invoker.weaponstatus[0]&=~PISF_JUSTUNLOAD;
			if(invoker.weaponstatus[PISS_MAG]>=15)setweaponstate("nope");
			else if(HDMagAmmo.NothingLoaded(self,"HD9mMag15")){
				if(countinv("HDPistolAmmo"))setweaponstate("loadchamber");
				else setweaponstate("nope");
			}
		}goto unmag;
	unmag:
		---- A 1 offset(0,34){
			A_SetCrosshair(21);
		}
		---- A 1 offset(1,38);
		---- A 2 offset(2,42);
		---- A 3 offset(3,46) A_PlaySound("weapons/pismagclick",CHAN_WEAPON);
		---- A 0{
			int pmg=invoker.weaponstatus[PISS_MAG];
			invoker.weaponstatus[PISS_MAG]=-1;
			if(pmg<0)setweaponstate("magout");
			else if(
				(!PressingUnload()&&!PressingReload())
				||A_JumpIfInventory("HD9mMag15",0,"null")
			){
				HDMagAmmo.SpawnMag(self,"HD9mMag15",pmg);
				setweaponstate("magout");
			}
			else{
				HDMagAmmo.GiveMag(self,"HD9mMag15",pmg);
				A_PlaySound("weapons/pocket",CHAN_WEAPON);
				setweaponstate("pocketmag");
			}
		}
	pocketmag:
		---- AAA 5 offset(0,46) A_MuzzleClimb(frandom(-0.2,0.8),frandom(-0.2,0.4));
		goto magout;
	magout:
		---- A 0{
			if(invoker.weaponstatus[0]&PISF_JUSTUNLOAD)setweaponstate("reloadend");
			else setweaponstate("loadmag");
		}

	loadmag:
		---- A 4 offset(0,46) A_MuzzleClimb(frandom(-0.2,0.8),frandom(-0.2,0.4));
		---- A 0 A_PlaySound("weapons/pocket",CHAN_WEAPON);
		---- A 5 offset(0,46) A_MuzzleClimb(frandom(-0.2,0.8),frandom(-0.2,0.4));
		---- A 3;
		---- A 0{
			let mmm=hdmagammo(findinventory("HD9mMag15"));
			if(mmm){
				invoker.weaponstatus[PISS_MAG]=mmm.TakeMag(true);
				A_PlaySound("weapons/pismagclick",CHAN_BODY);
			}
			if(
				invoker.weaponstatus[PISS_CHAMBER]<1
				&&invoker.weaponstatus[PISS_MAG]>0
			){
				invoker.weaponstatus[PISS_CHAMBER]=2;
				invoker.weaponstatus[PISS_MAG]--;
			}else setweaponstate("reloadend");
		}
		PISG A 3 offset(0,48) A_PlaySound("weapons/pischamber2",CHAN_WEAPON);
		PISG A 2 offset(0,45);
		goto reloadend;
	reloadend:
		---- A 2 offset(3,46);
		---- A 1 offset(2,42);
		---- A 1 offset(2,38);
		---- A 1 offset(1,34);
		---- A 0 A_JumpIf(pressingreload()||pressingunload(),"nope");
		goto ready;


	spawn:
		PIST ABCD -1 nodelay{
			if(invoker.weaponstatus[PISS_CHAMBER]<1){
				if(invoker.weaponstatus[PISS_AUTO]<0)frame=1;
				else frame=3;
			}else{
				if(invoker.weaponstatus[PISS_AUTO]<0)frame=0;
				else frame=2;
			}
		}stop;
	}
	override void initializewepstats(bool idfa){
		int autobak=weaponstatus[PISS_AUTO];
		weaponstatus[0]=0;
		weaponstatus[PISS_MAG]=15;
		weaponstatus[PISS_CHAMBER]=2;
		if(idfa)weaponstatus[PISS_AUTO]=max(0,autobak);
		else weaponstatus[PISS_AUTO]=-1;
	}
}
enum pistolstatus{
	PISF_JUSTUNLOAD=1,

	PISS_FLAGS=0,
	PISS_MAG=1,
	PISS_CHAMBER=2, //0 empty, 1 spent, 2 loaded
	PISS_AUTO=3 //-1 is no fire select
};



//use this to give an autopistol in a custom loadout
class HDAutoPistol:HDWeaponGiver{
	default{
		//$Category "Weapons/Hideous Destructor"
		//$Title "Pistol (select-fire)"
		//$Sprite "PISTA0"
		+hdweapon.fitsinbackpack
		hdweapon.refid HDLD_PISTAUT;
		hdweapon.nicename "Pistol (select-fire)";
		hdweapongiver.bulk 34;
		hdweapongiver.weapontogive "HDPistol";
		inventory.icon "PISTC0";
	}
	override void configureactualweapon(){
		bool startauto=0;
		if(owner)startauto=cvar.getcvar("hd_pistauto",owner.player).getbool();
		actualweapon.weaponstatus[PISS_AUTO]=max(0,startauto,actualweapon.weaponstatus[PISS_AUTO]);
	}
}

