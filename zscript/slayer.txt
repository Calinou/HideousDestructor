// ------------------------------------------------------------
// Super Shotgun
// ------------------------------------------------------------
class Slayer:HDShotgun replaces HDShotgun{
	default{
		//$Category "Weapons/Hideous Destructor"
		//$Title "Slayer"
		//$Sprite "SLAYA0"

		weapon.selectionorder 30;
		weapon.slotnumber 3;
		inventory.pickupmessage "You got the double-barreled shotgun!";
		weapon.bobrangex 0.18;
		weapon.bobrangey 0.7;
		scale 0.6;
		hdweapon.barrelsize 26,1,1;
	}
	override string getobituary(actor victim,actor inflictor,name mod,bool playerattack){
		bool sausage=true;
		for(int i=0;i<MAXPLAYERS;i++){
			if(playeringame[i]&&(players[i].getgender()!=0)){
				sausage=false;
				break;
			}
		}
		if(
			sausage
			&&inflictor is "HDBullet"
		)return "%o doubled over from the turgid size of %k's hot, manly lead.";
		return obituary;
	}
	override void PostBeginPlay(){
		if(Wads.CheckNumForName("SHT2B0",wads.ns_sprites,-1,false)<0){
			if(owner){
				actor ownor=owner;
				ownor.A_GiveInventory("Hunter");
				if(ownor.player&&ownor.player.readyweapon==self)
					ownor.A_SelectWeapon("Hunter");
				if(hd_debug)ownor.A_Log("doom 1 shotty lol",true);
				destroy();
			}else{
				spawn("Hunter",pos,ALLOW_REPLACE);
				destroy();
			}
			return;
		}
		super.postbeginplay();
	}
	override double gunmass(){
		return 6+weaponstatus[SLAYS_SIDESADDLE]*0.06;
	}
	override double,int weaponbulk(){
		return 100+weaponstatus[SLAYS_SIDESADDLE]*ENC_SHELLLOADED,USE_12;
	}
	//so you don't switch to the hunter every IDFA in D1
	override void detachfromowner(){
		if(Wads.CheckNumForName("SHT2B0",wads.ns_sprites,-1,false)<0){
			weapon.detachfromowner();
		}else hdweapon.detachfromowner();
	}
	states{
	select0:
		SHT2 A 0;
		goto select0small;
	deselect0:
		SHT2 A 0;
		goto deselect0small;
	fire:
	altfire:
		TNT1 A 0 A_ClearRefire();
	ready:
		TNT1 A 0; //let the PostBeginPlay handle the presence of the relevant sprite
		SHT2 A 1{
			if(PressingFireMode()){
				invoker.weaponstatus[0]|=SLAYF_DOUBLE;
				if(pressingreload()&&invoker.weaponstatus[SLAYS_SIDESADDLE]<12){
					invoker.weaponstatus[0]&=~SLAYF_DOUBLE;
					setweaponstate("reloadss");
					return;
				}
			}else invoker.weaponstatus[0]&=~SLAYF_DOUBLE;

			int pff=PressingFire()+(PressingAltfire()<<1);
			bool ch1=invoker.weaponstatus[SLAYS_CHAMBER1]==2;
			bool ch2=invoker.weaponstatus[SLAYS_CHAMBER2]==2;
			if(ch1&&ch2){
				if(pff==3){
					A_Overlay(PSP_FLASH,"flashboth");
					return;
				}
				else if(pff&&invoker.weaponstatus[0]&SLAYF_DOUBLE){
					setweaponstate("double");
					return;
				}
			}
			if(ch1&&pff%2)A_Overlay(11,"flashleft");
			else if(ch2&&pff>1)A_Overlay(12,"flashright");
			else A_WeaponReady((WRF_ALL|WRF_NOFIRE)&~WRF_ALLOWUSER2);
		}
		SHT2 A 0 A_WeaponReady();
		goto readyend;
	double:
		SHT2 A 1 offset(0,34);
		SHT2 A 1 offset(0,33);
		SHT2 A 0 A_Overlay(PSP_FLASH,"flashboth");
		goto readyend;

	flashleft:
		SHT1 J 1 bright{
			A_Light2();
			HDFlashAlpha(64,false,overlayid());
			A_PlaySound("weapons/slayersingle",6);
			A_ZoomFactor(0.98,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			invoker.weaponstatus[SLAYS_CHAMBER1]=1;

			actor p=spawn("HDBullet00bfl",pos+(0,0,height-6));
			p.target=self;p.angle=angle;p.pitch=pitch;
			p.vel+=(
				frandom(-1.,1.),frandom(-1.,1.),frandom(-1.,1.)
			);
			p.speed+=frandom(-10.,10.);
			p.vel+=self.vel;
		}
		TNT1 A 1{
			A_Light0();
			A_MuzzleClimb(1.6,-3.9,1.6,-3.9);
		}goto flasheither;
	flashright:
		SHT1 N 1 bright{
			A_Light2();
			HDFlashAlpha(64,false,overlayid());
			A_PlaySound("weapons/slayersingle",6);
			A_ZoomFactor(0.98,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			invoker.weaponstatus[SLAYS_CHAMBER2]=1;

			actor p=spawn("HDBullet00bfr",pos+(0,0,height-6));
			p.target=self;p.angle=angle;p.pitch=pitch;
			p.vel+=(
				frandom(-1.,1.),frandom(-1.,1.),frandom(-1.,1.)
			);
			p.speed+=frandom(-10.,10.);
			p.vel+=self.vel;
		}
		TNT1 A 1{
			A_Light0();
			A_MuzzleClimb(-1.6,-3.9,-1.6,-3.9);
		}goto flasheither;
	flasheither:
		TNT1 A 0{
			A_ZoomFactor(1.0,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			A_AlertMonsters();
			setweaponstate("recoil");
		}stop;
	flashboth:
		SHT2 J 1 bright{
			A_Light2();
			HDFlashAlpha(128);
			A_PlaySound("weapons/slayersingle",5);
			A_PlaySound("weapons/slayersingle",6);
			A_ZoomFactor(0.95,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
			invoker.weaponstatus[SLAYS_CHAMBER1]=1;
			invoker.weaponstatus[SLAYS_CHAMBER2]=1;


			actor p=spawn("HDBullet00bf2",pos+(0,0,height-6));
			p.target=self;p.angle=angle;p.pitch=pitch;
			p.vel+=(
				frandom(-1.,1.),frandom(-1.,1.),frandom(-1.,1.)
			);
			p.speed+=10*frandom(-1.,1.);
			p.vel+=self.vel;
		}
		TNT1 A 1{
			A_Light0();
			A_MuzzleClimb(1.2,-8.,1.2,-8.8);
		}goto flasheither;
	recoil:
		SH2G K 1;
		goto ready;

	altreload:
		SHT2 A 0{
			if(countinv("HDShellAmmo"))
				invoker.weaponstatus[0]|=SLAYF_FROMPOCKETS;
			else setweaponstate("nope");
		}goto reloadstart;
	reload:
		SHT2 A 0{
			if(
				invoker.weaponstatus[SLAYS_CHAMBER1]>1&&
				invoker.weaponstatus[SLAYS_CHAMBER2]>1
			)setweaponstate("reloadss");

			invoker.weaponstatus[0]&=~SLAYF_UNLOADONLY;
			if(invoker.weaponstatus[SLAYS_SIDESADDLE]>0)
				invoker.weaponstatus[0]&=~SLAYF_FROMPOCKETS;
			else if(countinv("HDShellAmmo"))
				invoker.weaponstatus[0]|=SLAYF_FROMPOCKETS;
			else setweaponstate("nope");
		}goto reloadstart;
	reloadstart:
	unloadstart:
		SH2G K 2 offset(0,34) EmptyHand();
		SH2G K 1 offset(0,40);
		SH2G K 3 offset(0,46);
		SH2G K 5 offset(0,47) A_PlaySound("weapons/sshoto",CHAN_AUTO);
		SH2G M 4 offset(0,46) A_MuzzleClimb(
			frandom(0.6,1.2),frandom(0.6,1.2),
			frandom(0.6,1.2),frandom(0.6,1.2),
			frandom(1.2,2.4),frandom(1.2,2.4)
		);
		SH2G L 3 offset(0,36){
			//eject whatever is already loaded
			for(int i=0;i<2;i++){
				int chm=invoker.weaponstatus[SLAYS_CHAMBER1+i];
				invoker.weaponstatus[SLAYS_CHAMBER1+i]=0;
				if(chm>1){
					if(health<90&&countinv("IsMoving"))A_SpawnItemEx("HDFumblingShell",
						cos(pitch)*5,-1,height-7-sin(pitch)*5,
						cos(pitch-45)*cos(angle+random(-2,2))*random(1,4)+vel.x,
						cos(pitch-45)*sin(angle+random(-2,2))*random(1,4)+vel.y,
						-sin(pitch-45)*random(1,4)+vel.z,
						0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
					);else A_SpawnItemEx("HDUnspentShell",
						cos(pitch)*5,0,height-7-sin(pitch)*5,
						vel.x,vel.y,vel.z+1,
						0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
					);
				}else if(chm==1)A_SpawnItemEx("HDSpentShell",
					cos(pitch)*5,0,height-7-sin(pitch)*5,
					cos(pitch-45)*cos(angle+random(-2,2))*random(1,4)+vel.x,
					cos(pitch-45)*sin(angle+random(-2,2))*random(1,4)+vel.y,
					-sin(pitch-45)*random(1,4)+vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}
		}
		SH2G L 2 offset(1,34);
		SH2G L 2 offset(2,34);
		SH2G L 2 offset(4,34);
		SH2G L 8 offset(0,36){
			if(invoker.weaponstatus[0]&SLAYF_UNLOADONLY){
				setweaponstate("unloadend");
				return;
			}

			//play animation to search pockets as appropriate
			if(invoker.weaponstatus[0]&SLAYF_FROMPOCKETS)
				A_PlaySound("weapons/pocket",CHAN_WEAPON);
				else setweaponstate("reloadnopocket");
		}
		SH2G L 4 offset(2,35);
		SH2G L 4 offset(0,35);
		SH2G L 4 offset(0,34);
	reloadnopocket:
		SH2G E 5 offset(1,35);
		SH2G E 2 offset(0,36);
		SH2G F 2 offset(0,40);
		SH2G F 1 offset(0,46);
		SH2G F 2 offset(0,54);

		TNT1 A 4{
			//take up to 2 shells in hand
			int ssh=0;
			if(invoker.weaponstatus[0]&SLAYF_FROMPOCKETS){
				ssh=min(2,countinv("HDShellAmmo"));
				if(ssh>0)A_TakeInventory("HDShellAmmo",ssh);
			}else{
				ssh=min(2,invoker.weaponstatus[SLAYS_SIDESADDLE]);
				invoker.weaponstatus[SLAYS_SIDESADDLE]-=ssh;
			}

			//if the above leaves you with nothing, abort
			if(ssh<1){
				A_SetTics(0);
				return;
			}

			//transfer from hand to chambers
			ssh--;
			while(ssh>=0){
				invoker.weaponstatus[SLAYS_CHAMBER2-ssh]=2;
				ssh--;
			}
		}
		TNT1 A 4 A_PlaySound("weapons/sshotl",CHAN_WEAPON);
		SH2G G 2 offset(0,46);
		SH2G G 1 offset(0,42);
		SH2G H 2 offset(0,42);
		SH2G H 3 A_PlaySound("weapons/sshotc",CHAN_WEAPON);
		SHT2 A 3;
		goto ready;
	unloadend:
		SH2G L 6 A_PlaySound("weapons/sshotl",CHAN_WEAPON);
		SH2G G 2 offset(0,46);
		SH2G H 2 offset(0,42);
		SH2G H 2 offset(0,42);
		SH2G H 0 A_PlaySound("weapons/sshotc",CHAN_WEAPON);
		SH2G H 5;
		SHT2 A 3;
		goto ready;

	reloadss:
		SHT2 A 0 A_JumpIf(invoker.weaponstatus[SLAYS_SIDESADDLE]>=12,"nope");
		SHT2 A 1 offset(1,34);
		SHT2 A 2 offset(2,34);
		SHT2 A 3 offset(3,36);
	reloadssrestart:
		SHT2 A 6 offset(3,35);
		SHT2 A 9 offset(4,34) A_PlaySound("weapons/pocket",CHAN_WEAPON);
	reloadssloop1:
		SHT2 A 0{
			if(invoker.weaponstatus[SLAYS_SIDESADDLE]>=12)setweaponstate("reloadssend");

			//load shells into hand
			int ssh=min(3,countinv("HDShellAmmo"));
			if(ssh<1){
				setweaponstate("reloadssend");
				return;
			}
			ssh=min(3,ssh,max(1,health/20),12-invoker.weaponstatus[SLAYS_SIDESADDLE]);
			invoker.weaponstatus[SLAYS_SIDESADDLE]+=ssh;
			A_TakeInventory("HDShellAmmo",ssh,TIF_NOTAKEINFINITE);
		}
	reloadssend:
		SHT2 A 4 offset(3,34);
		SHT2 A 0{
			if(
				invoker.weaponstatus[SLAYS_SIDESADDLE]<12
				&&!pressingfire()
				&&!pressingaltfire()
				&&!pressingreload()
				&&!pressingunload()
				&&countinv("HDShellAmmo")
			)setweaponstate("reloadssrestart");
		}
		SHT2 A 3 offset(2,34);
		SHT2 A 1 offset(1,34) emptyhand();
		goto nope;
	unloadss:
		SHT2 A 0 EmptyHand();
		SHT2 A 2 offset(2,34) A_JumpIf(invoker.weaponstatus[SLAYS_SIDESADDLE]<1,"nope");
		SHT2 A 1 offset(3,36) A_PlaySound("weapons/pocket",CHAN_WEAPON);
	unloadssloop1:
		SHT2 A 4 offset(4,36);
		SHT2 A 2 offset(5,37) A_UnloadSideSaddle(HUNTS_SIDESADDLE);
		SHT2 A 3 offset(4,36){	//decide whether to loop
			if(
				invoker.weaponstatus[SLAYS_SIDESADDLE]>0
				&&!pressingfire()
				&&!pressingaltfire()
				&&!pressingreload()
//				&&!pressingunload()
			)setweaponstate("unloadssloop1");
		}
		SHT2 A 3 offset(4,35);
		SHT2 A 2 offset(3,35);
		SHT2 A 1 offset(2,34);
		goto nope;
	unload:
		SH2G K 2 offset(0,34){
			if(invoker.weaponstatus[SLAYS_SIDESADDLE]>0)setweaponstate("unloadss");
			else invoker.weaponstatus[0]|=SLAYF_UNLOADONLY;
		}goto unloadstart;

	zoom:
	cannibalize:
		SHT2 A 0 EmptyHand();
		SHT2 A 2 offset(0,36) A_JumpIf(!countinv("Hunter"),"nope");
		SHT2 A 2 offset(0,40) A_PlaySound("weapons/pocket",CHAN_WEAPON);
		SHT2 A 8 offset(0,42);
		SHT2 A 8 offset(0,44);
		SHT2 A 8 offset(0,42);
		SHT2 A 2 offset(0,36) A_CannibalizeOtherShotgun();
		goto ready;

	spawn:
		SLAY ABCDEFG -1 nodelay{
			int ssh=invoker.weaponstatus[SLAYS_SIDESADDLE];
			if(ssh>=11)frame=0;
			else if(ssh>=9)frame=1;
			else if(ssh>=7)frame=2;
			else if(ssh>=5)frame=3;
			else if(ssh>=3)frame=4;
			else if(ssh>=1)frame=5;
			else frame=6;
		}
	}
	override void InitializeWepStats(bool idfa){
		weaponstatus[SLAYS_CHAMBER1]=2;
		weaponstatus[SLAYS_CHAMBER2]=2;
		weaponstatus[SLAYS_SIDESADDLE]=12;
		handshells=0;
	}
}
enum slayerstatus{
	SLAYF_UNLOADONLY=1,
	SLAYF_DOUBLE=2,
	SLAYF_FROMPOCKETS=4,

	SLAYS_CHAMBER1=1,
	SLAYS_CHAMBER2=2,
	SLAYS_SIDESADDLE=3,
	SLAYS_HEAT1=4,
	SLAYS_HEAT2=5,
};
