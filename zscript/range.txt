class HDLoadBox:switchabledecoration{
	default{
		+usespecial
		height 20;radius 20;gravity 0.8;
		activation THINGSPEC_Switch|THINGSPEC_ThingTargets;
	}
	states{
	active:
	inactive:
		AMMO A 5{
			A_PlaySound("misc/chat2",CHAN_AUTO);
			busespecial=false;
		}
		OWWV AA 4{
			A_GiveInventory("Clip");
			vel.z+=3;
		}
		OWWV A 18 A_GiveToTarget("HDLoaded");
	spawn:
		AMMO A -1{
			busespecial=true;
		}
	}
}
class HDLoaded:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			A_PlaySound("misc/w_pkup",CHAN_AUTO);
			A_Print("Weapons reloaded.");
			if(countinv("Lumberjack")>0){A_GiveInventory("LumberCell",1200);}
			if(countinv("HDPistol")>0){
				A_GiveInventory("HDPistolLoaded",15);
				A_GiveInventory("HDPistolChamber");
				A_TakeInventory("HDPistolNoMag");
			}
			if(countinv("HDSecondPistol")>0){
				A_GiveInventory("HDSecondPistolLoaded",15);
				A_GiveInventory("HDSecondPistolChamber");
				A_TakeInventory("HDSecondPistolNoMag");
			}
			if(countinv("HDSMG")>0){
				A_GiveInventory("HDSMGLoaded",30);
				A_GiveInventory("HDSMGChamber");
				A_TakeInventory("HDSMGNoMag");
			}
			if(countinv("Hunter")>0){
				A_GiveInventory("ShotgunQuickAmmo",12);
				A_GiveInventory("ShotgunTube",12);
				A_GiveInventory("ShotgunChamber");
				A_TakeInventory("HunterSpentShell");
			}
			if(countinv("Slayer")>0){
				A_GiveInventory("ShotgunQuickAmmo2",12);
				A_GiveInventory("ShellChamber1");
				A_GiveInventory("ShellChamber2");
				A_TakeInventory("SlayerSpentShell1");
				A_TakeInventory("SlayerSpentShell2");
			}
			if(countinv("ZM66Rifle")+countinv("ZM66GrenadeRifle")+countinv("ZM66SemiRifle")>0){
				A_GiveInventory("ZM66Loaded",50);
				A_GiveInventory("ZM66Chamber");
				A_TakeInventory("ZM66NoMag");
			}
			if(countinv("ZM66GrenadeRifle")>0){A_GiveInventory("ZM66GLoaded");}
			if(countinv("Vulcanette")){
				vulcanette vvv=vulcanette(findinventory("vulcanette"));
				if(vvv)vvv.initializewepstats(true);
			}
			if(countinv("RocquetteLauncher")>0){
				if(countinv("RocketMag")<6){
					A_GiveInventory("RocketMag",6);
				}else{
					A_GiveInventory("HEATLoaded");
					A_TakeInventory("IsUsingRocketUnderslung");
					A_SetInventory("RocketMag",5);
				}
			}
			if(countinv("Blooper")>0){A_GiveInventory("BloopChamber");}
			if(countinv("LiberatorRifle")+countinv("LiberatorGrenadeRifle")>0){
				A_GiveInventory("LiberatorLoaded",30);
				A_GiveInventory("LiberatorChamber");
				A_TakeInventory("LiberatorNoMag");
			}
			if(countinv("LiberatorGrenadeRifle")>0){A_GiveInventory("LiberatorGrenadeChamber");}
			if(countinv("ThunderBuster")){
				A_GiveInventory("PlasmaBattery",60);
				A_TakeInventory("PlasmaEmptyCell");
			}
			if(countinv("Brontornis")>0){A_GiveInventory("BrontornisMag");}
			if(countinv("BFG9k")){
				A_TakeInventory("BFGDemonSummoned");
				A_GiveInventory("BFGChamber",20);
				A_GiveInventory("BFGInternalCharge",20);
				A_GiveInventory("BFGLoaded");
			}
			if(countinv("BossRifle")>0){
				A_GiveInventory("BossMag",10);
				A_GiveInventory("BossChamber");
				A_TakeInventory("BossSpentChamber");
			}
			if(countinv("HDFragGrenadeAmmo")<3){
				A_TakeInventory("HDFragGrenadeAmmo");
				A_GiveInventory("HDFragGrenadeAmmo",3);
			}
		}fail;
	}
}



class TargetBarrel:Actor{
	default{
		+ismonster +nevertarget +shootable +quicktoretaliate +float +nogravity +nodamage +noblood
		height 34;radius 16;mass 25;painchance 256;speed 2;
	}
	states{
	spawn:
		BEXP B 2{
			A_ChangeVelocity(frandom(-0.4,0.4),frandom(-0.4,0.4),frandom(-1,1));
			A_Wander();
		}
		TNT1 A 0 A_JumpIf(vel.x>4||vel.y>4||vel.z>4,"spawn");
		BEXP B 1 A_SetTics(random(10,100));
		loop;
	pain:
		BEXP B 3{
			bnopain=true;
			vel.x+=10;
			bnogravity=false;
			spawn("HDExplosion",pos+(0,0,16));
			spawn("HDSmoke",pos+(0,0,16));
		}
		TNT1 A 0{
			bnopain=0;
			spawn("DistantRocket",pos);
		}
	pain2:
		BEXP B 1{
			spawn("HDSmoke",pos+(0,0,16));
			if(floorz>=pos.z)setstatelabel("pain3");
		}wait;
	pain3:
		BEXP B 10{
			vel.z*=-0.3;
			bnogravity=true;
			A_PlaySound("weapons/smack");
		}goto spawn;
	}
}



