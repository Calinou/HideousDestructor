// ------------------------------------------------------------
// The heart
// ------------------------------------------------------------

extend class HDPlayerPawn{
	const HDCONST_MINHEARTTICS = 5; //35/5*60=420 beats per minute!
	int beatcount;
	int beatmax;
	int beatcap;
	int beatcounter;

	int bloodpressure;

	int stimcount;
	int regenblues;
	int regenbers;

	actor lastthingthatwoundedyou;

	int woundcount;
	int burncount;
	int unstablewoundcount;
	int oldwoundcount;
	int aggravateddamage;

	void ResetHealth(bool downtimeonly=true){
		beatcount=0;beatcounter=0;
		bloodpressure=0;
		stimcount=0;regenbers=0;A_TakeInventory("PowerStrength");
		stunned=0;

		if(downtimeonly){
			//wounds stabilize during downtime
			unstablewoundcount+=woundcount;
			woundcount=0;
			oldwoundcount+=min(5,unstablewoundcount);
			unstablewoundcount=max(unstablewoundcount-5,0);
			oldwoundcount=max(oldwoundcount-2,0);
		}else{
			//just reset everything
			beatmax=35;beatcap=35;
			healthing(100-health);
			regenblues=0;
			woundcount=0;unstablewoundcount=0;oldwoundcount=0;
			burncount=0;
			aggravateddamage=0;
		}
	}

	void HeartTicker(){

		if(bkilled) return; //don't really need this...
		A_SetInventory("HeartBeatCounter",beatcount); //for sbarinfo

		//zerk!!!!
		if(regenbers){
			if(regenbers>0){
				regenbers=min(regenbers-1,3000);
				healthing(1);
				if(stunned)stunned*=0.8;
				if(!regenbers){
					A_SetBlend("20 0a 0f",0.8,35);
					A_TakeInventory("PowerStrength");
					A_Pain();
					regenbers-=700;
					if(!random(0,4))aggravateddamage+=random(1,3);
				}
			}else{
				stunned+=3;
				regenbers++;
				beatcap=HDCONST_MINHEARTTICS+random(1,10);
			}
		}

		//good old jitters!
		if(
			bloodpressure>20||
			health<33
		){
			double jitter=0.3;
			if(health<20) jitter=1;
			A_SetPitch(pitch+frandom(-jitter,jitter),true);
			A_SetAngle(angle+frandom(-jitter,jitter),true);
		}

		//on every beat
		if(beatcount>0){
			beatcount--;
			pitch-=0.0005*bloodpressure;
		}else{
			pitch+=0.0005*beatmax*bloodpressure;
			beatmax=min(beatmax,beatcap);

			//MY ORIFICE IS BLEEDING!!!
			if(woundcount){
				int dm=(random(10,woundcount)-random(0,bloodpressure))*0.4;
				if(dm>0)damagemobj(self,lastthingthatwoundedyou,dm,"bleedout",DMG_THRUSTLESS);
			}

			//remove these once we're done with them
			//lethal didn't mean the same as oldwoundcount but oh well
			if(countinv("WoundCount")){
				woundcount+=countinv("WoundCount");
				A_TakeInventory("WoundCount");
			}
			if(countinv("RegenBlues")){
				regenblues+=countinv("RegenBlues")*12;
				A_TakeInventory("RegenBlues");
			}
			if(countinv("RegenBers")){
				regenbers+=countinv("RegenBers")*10;
				A_TakeInventory("RegenBers");
			}
			if(countinv("RegenStims")){
				stimcount+=countinv("RegenStims");
				A_TakeInventory("RegenStims");
			}
			if(countinv("AggravatedDamage")){
				aggravateddamage+=countinv("AggravatedDamage");
				A_TakeInventory("AggravatedDamage");
			}
			if(countinv("LethalDamage")){
				oldwoundcount+=countinv("LethalDamage");
				A_TakeInventory("LethalDamage");
			}

			//limit beatmax subject to zerk
			if(countinv("PowerStrength")) beatmax=clamp(beatmax,4,14);
			else beatmax=clamp(beatmax,HDCONST_MINHEARTTICS,35);

			if(beatmax<8)DamageMobj(self,self,1,"internal");
			else{
				if(health<100-aggravateddamage-burncount-oldwoundcount-unstablewoundcount){
					healthing(1);
					if(random(1,20)<stimcount)healthing(1);
				}
			}

			//reset beatcount
			beatcount=beatmax;
			A_SetBlend("20 12 0f",0.003*min(bloodpressure,100),beatcount);
			beatcounter++;

			//sprinting
			if(cansprint && runwalksprint>0){
				beatmax--;
				if(beatmax<=HDCONST_SPRINTMAXHEARTRATE)beatmax-=10;
			}

			//blood pressure
			if(stimcount>0 && bloodpressure<12)bloodpressure++;
			if(stimcount<1 && bloodpressure>0)bloodpressure--;

			//don't go negatives
			if(woundcount<0)woundcount=0;
			if(unstablewoundcount<0)unstablewoundcount=0;
			if(burncount<0)burncount=0;
			if(aggravateddamage<0)aggravateddamage=0;

			//magical healing stops imminent danger first
			if(regenblues>0 && unstablewoundcount+woundcount>0){
				if(woundcount)woundcount--;else unstablewoundcount--;
				regenblues-=2;
			}

			//apply stims
			if(stimcount){
				beatcap=clamp(beatcap,beatcap-4,35-stimcount);
				if(bloodpressure<stimcount)bloodpressure+=4;
			}

			if(beatcounter%4==0){	//every 4 beats
				//recovering heart rate
				if(beatmax<beatcap) beatmax++;
				if(bloodpressure>0)bloodpressure--;
				if(regenblues>0 && oldwoundcount+burncount+aggravateddamage>0){
					oldwoundcount-=2;burncount-=2;aggravateddamage--;
					regenblues--;
				}
			}
			if(beatcounter%12==0){	//every 12 beats
				if(stimcount>0)stimcount--;
			}
			if(beatcounter%20==0){	//every 20 beats
				beatcap=clamp(beatcap+8,1,35);

				//updating beatcap (minimum heart rate)
				if(health<40) beatcap=clamp(beatcap,1,24);
				else if(health<60) beatcap=clamp(beatcap,1,32);

				//bandages come undone
				if(unstablewoundcount && countinv("IsMoving")>random(0,12)){
					unstablewoundcount--;
					if(!random(0,1))oldwoundcount++;else woundcount++;
				}

				//wounds start settling
				if(!random(0,unstablewoundcount+woundcount)){
					if(unstablewoundcount+woundcount>0)oldwoundcount++;
					if(unstablewoundcount>0)unstablewoundcount--;  
					else if(woundcount>0)woundcount--;
				}
			}
			if(beatcounter==120){	//every 120 beats
				beatcounter=0;	//reset
				if(random(1,health)>70){
					oldwoundcount--;
					burncount--;
				}
			}
		}
	}
}



class BleedOutAI:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			damagemobj(self,null,1,"bleedout",DMG_THRUSTLESS);
			if(hd_debug)A_Log("Deprecated bleedout.");
		}fail;
	}
}
