// ------------------------------------------------------------
// The heart
// ------------------------------------------------------------
extend class HDPlayerPawn{
	const HDCONST_MINHEARTTICS = 5; //35/5*60=420 beats per minute!
	int beatcount;
	int beatmax;
	int beatcap;
	int beatcounter;

	int bloodpressure;

	int stimcount;
	int regenblues;
	int zerk;
	int haszerked;

	actor lastthingthatwoundedyou;

	int woundcount;
	int burncount;
	int unstablewoundcount;
	int oldwoundcount;
	int aggravateddamage;

	void ResetHealth(bool downtimeonly=true){
		beatcount=0;beatcounter=0;
		bloodpressure=0;
		stimcount=0;zerk=0;A_TakeInventory("PowerStrength");
		stunned=0;

		if(downtimeonly){
			//wounds stabilize during downtime
			unstablewoundcount+=woundcount;
			woundcount=0;
			oldwoundcount+=min(5,unstablewoundcount);
			unstablewoundcount=max(unstablewoundcount-5,0);
			oldwoundcount=max(oldwoundcount-2,0);
		}else{
			//just reset everything
			beatmax=35;beatcap=35;
			healthing(100-health);
			regenblues=0;
			woundcount=0;unstablewoundcount=0;oldwoundcount=0;
			burncount=0;
			aggravateddamage=0;
		}
	}

	void HeartTicker(){
		//zerk!!!!
		if(zerk){
			if(zerk>0){
				zerk=max(zerk-1,0);
				if(zerk>5000){
					zerk--;
					stunned=10;
					angle+=random(-2,2);pitch+=random(-2,2);
					vel+=(frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
					if(!random(0,3)){
						healthing(1);
						A_SetBlend("20 0a 0f",0.4,3);
						if(!random(0,8-zerk*0.0005)){
							woundcount+=random(0,2);
							if(!random(0,4))A_Pain();
							else if(!random(0,3))aggravateddamage++;
						}
						if(!(player.readyweapon is "Ring")){
							A_DropInventory(player.readyweapon.getclass());
							A_SelectWeapon("Ring");
						}
					}
				}else if(zerk>4000){
					zerk--;
					angle+=random(-1,1);pitch+=random(-1,1);
					vel+=(frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-0.5,0.5));
					if(!random(0,3)){
						healthing(1);
						if(!(player.readyweapon is "Ring")){
							A_DropInventory(player.readyweapon.getclass());
							A_SelectWeapon("Ring");
						}
					}
				}else{
					healthing(1);
					if(stunned)stunned*=0.8;
					if(!zerk){
						A_SetBlend("20 0a 0f",0.8,35);
						A_TakeInventory("PowerStrength");
						A_Pain();
						zerk-=700*max(1,haszerked);
						haszerked=0;
						if(!random(0,4))aggravateddamage+=random(1,3);
					}
				}
			}else{
				stunned+=3;
				zerk++;
				beatcap=HDCONST_MINHEARTTICS+random(1,10);
			}
		}



		//on every beat
		if(beatcount>0){
			beatcount--;
			pitch-=0.0005*bloodpressure;
		}else{
			pitch+=0.0005*beatmax*bloodpressure;
			beatmax=min(beatmax,beatcap);

			//heartbeat sound
			if(bloodpressure>5||health<30||beatmax<20){
				float bp=0.05*bloodpressure;
				if(beatmax<15||health<30)bp*=2;
				A_PlaySound("misc/heart",CHAN_BODY,0.05*max(bloodpressure,22-beatmax),false,30);
			}

			//MY ORIFICE IS BLEEDING!!!
			if(woundcount){
				int dm=(random(10,woundcount)-random(0,bloodpressure))*0.4;
				if(dm>0)damagemobj(self,lastthingthatwoundedyou,dm,"bleedout",DMG_THRUSTLESS);
			}

			//ZERK IS NO SUBSTITUTE FOR OXYGEN TO BRAIN
			if(fatigue>HDCONST_DAMAGEFATIGUE*1.4){
				if(zerk)damagemobj(self,self,beatmax+6,"internal");
				else damagemobj(self,self,2,"internal");
				stunned=min(10,stunned);
			}

			//remove these once we're done with them
			if(countinv("WoundCount")){
				woundcount+=countinv("WoundCount");
				A_TakeInventory("WoundCount");
			}
			if(countinv("RegenBlues")){
				regenblues+=countinv("RegenBlues")*12;
				A_TakeInventory("RegenBlues");
			}
			if(countinv("StunCount")){
				stunned+=countinv("StunCount");
				A_TakeInventory("StunCount");
			}

			//limit beatmax subject to zerk
			if(countinv("PowerStrength")) beatmax=clamp(beatmax,4,14);
			else beatmax=clamp(beatmax,HDCONST_MINHEARTTICS,35);

			if(fatigue>0)fatigue--;
			if(
				beatmax<HDCONST_MINHEARTTICS+3
				||fatigue>HDCONST_DAMAGEFATIGUE  
			)DamageMobj(self,self,1,"internal");
			else{
				if(health<100
					-aggravateddamage
					-burncount
					-oldwoundcount
					-unstablewoundcount
					+max(fatigue-HDCONST_SPRINTFATIGUE,0)
				){
					if(random(1,30)+countinv("IsMoving")<beatmax)healthing(1);
					if(random(1,40)<stimcount)healthing(1);
				}
			}

			//reset beatcount
			beatcount=beatmax;
			A_SetBlend("20 12 0f",0.003*min(bloodpressure,100),beatcount);
			beatcounter++;

			//sprinting
			if(cansprint && runwalksprint>0 && (fm||sm)){
				fatigue+=2;
				if(!stimcount && !zerk && fatigue>=HDCONST_SPRINTFATIGUE){
					fatigue+=20;
					A_Pain();
				}
				if(!random(0,35))bloodpressure++;
			}

			//blood pressure
			if(stimcount>0 && bloodpressure<12)bloodpressure++;
			else if(bloodpressure>0)bloodpressure--;

			//don't go negatives
			if(regenblues<0)regenblues=0;
			if(woundcount<0)woundcount=0;
			if(oldwoundcount<0)oldwoundcount=0;
			if(unstablewoundcount<0)unstablewoundcount=0;
			if(burncount<0)burncount=0;
			if(aggravateddamage<0)aggravateddamage=0;
			if(stunned<0)stunned=0;

			//apply stims
			if(stimcount){
				beatcap=clamp(beatcap,beatcap-1,35-stimcount);
				if(bloodpressure<stimcount)bloodpressure+=4;
				if(zerk && stimcount>18)stunned=10;
				else stunned--;
			}

			//magical healing every heartbeat
			if(regenblues){
				if(unstablewoundcount>0||woundcount>0){  
					if(woundcount>0)woundcount--;else unstablewoundcount--;
					regenblues-=2;
				}
				if(beatcap<HDCONST_MINHEARTTICS){
					beatcap=max(beatcap,HDCONST_MINHEARTTICS+3);
					if(!random(0,99))regenblues--;
				}
				if(stimcount>4){  
					stimcount-=4;
					regenblues--;
				}
			}

			//every 4 beats
			if(beatcounter%4==0){
				//recovering heart rate
				if(fatigue){
					beatmax=min(beatmax,33-fatigue);
					if(fatigue>20)stunned=clamp(stunned,stunned+10,fatigue*10);
					if(bloodpressure<fatigue)bloodpressure=fatigue;
				}
				if(beatmax<beatcap) beatmax++;
				if(bloodpressure>0)bloodpressure--;
				if(regenblues>0 && (oldwoundcount>0||burncount>0||aggravateddamage>0)){    
					oldwoundcount-=2;burncount-=2;aggravateddamage--;
					regenblues--;
				}
			}

			//every 12 beats
			if(beatcounter%12==0){
				if(stimcount>0)stimcount--;

				//twitchy zerk
				if(zerk>0 && !countinv("IsMoving")){
					if(zat>=pos.z)A_ChangeVelocity(frandom(-2,3),frandom(-2,2),1,CVF_RELATIVE);
					if(!(input&BT_ATTACK))muzzledrift+=(random(-14,14),random(-24,14));
					else muzzledrift+=(frandom(-2,2),frandom(-3,2));
					if(!random(0,2)){
						int stupid=random(1,100);
						if(stupid<30)A_PlaySound("*grunt",CHAN_VOICE);
						else if(stupid<50)A_PlaySound("*pain",CHAN_VOICE);
						else if(stupid<70)A_PlaySound("*death",CHAN_VOICE);
						else if(stupid<90)A_PlaySound("*xdeath",CHAN_VOICE);
						else if(stupid<100){
							A_PlaySound("*taunt",CHAN_VOICE);
							A_AlertMonsters();
						}
					}
				}
			}
			if(beatcounter%20==0){	//every 20 beats
				beatcap=clamp(beatcap+8,1,35);

				//updating beatcap (minimum heart rate)
				if(health<40) beatcap=clamp(beatcap,1,24);
				else if(health<60) beatcap=clamp(beatcap,1,32);

				//bandages come undone
				if(unstablewoundcount && countinv("IsMoving")>random(0,12)){
					unstablewoundcount--;
					if(!random(0,1))oldwoundcount++;else woundcount++;
				}

				//wounds start settling
				if(!random(0,unstablewoundcount+woundcount)){
					if(unstablewoundcount>0){
						unstablewoundcount--;
						oldwoundcount++;
					}else if(woundcount>0)woundcount--;
				}
			}
			if(beatcounter==120){	//every 120 beats
				beatcounter=0;	//reset
				if(random(1,health)>70){
					oldwoundcount--;
					burncount--;
				}
			}
		}
	}
}



class BleedOutAI:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			damagemobj(self,null,1,"bleedout",DMG_THRUSTLESS);
			if(hd_debug)A_Log("Deprecated bleedout.");
		}fail;
	}
}
