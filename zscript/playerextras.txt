// ------------------------------------------------------------
// Additional player functions
// ------------------------------------------------------------
class PlayerAntenna:HDActor{
	default{
		radius 3;
		height 2;
	}
}
extend class HDPlayerPawn{
	actor antenna;
	void A_MoveAntenna(vector3 newpos){
		if(!antenna)antenna=spawn("PlayerAntenna",newpos);
		else antenna.setorigin(newpos,false);
	}

	//check mantling
	//returns: -1 cannot mantle; 0 cannot mantle but on ground; 1 can mantle
	int MantleCheck(){
		bool onground=false;
		int res=-1;
		if(zat==pos.z){
			onground=true;
			res=0;
		}

		//determine max height
		int mantlemax=36;
		if(
			!mustwalk
			&&barehanded
			&&(
				zerk||
				stimcount>10||
				fatigue<HDCONST_SPRINTFATIGUE
			)
		){
			if(zerk>0||onground)mantlemax=64;
			else mantlemax=56;
		}
		//place the antenna
		A_MoveAntenna(pos+vel+((cos(angle),sin(angle))*(radius+1),mantlemax));

		//check if blocked
		bool checkmovesuccessful=false;
		bool checkmoveunsuccessful=false;
		int mmfinal=mantlemax;
		for(int i=0;i<mantlemax;i++){
			antenna.addz(-1);
			if(antenna.checkmove(antenna.pos.xy)){
				checkmovesuccessful=true;
			}else if(mmfinal>maxstepheight){
				checkmoveunsuccessful=true;
			}
			mmfinal-=1;
		}
		if(!checkmovesuccessful||!checkmoveunsuccessful)return res;
/*
		//the old method in case the other one fails
		mantlemax=antenna.getzat();
		if((mantlemax-pos.z)<=maxstepheight) return res; //don't mantle steppables
		if(antenna.getzat(flags:GZF_CEILING)-mantlemax<1) return res; //can't squeeze fingers in
		if(antenna.pos.z<mantlemax) return res; //too high to reach and grab
*/

		//thrust player upwards and forwards
		if(
			onground
			&&!(oldinput & BT_JUMP)
		){
			if(!countinv("PowerStrength") && fatigue<HDCONST_SPRINTFATIGUE)vel.z+=4;
			else vel.z+=7;
			fatigue+=random(1,2);
		}else vel.z+=3;
		return 1;
	}
	double jumppower(){
		double jumppower=5;
		if(zerk>0)jumppower=10;else{
			if(fatigue>30) jumppower=3;
			else if(fatigue>20) jumppower=3.5;
		}
		if(overloaded)jumppower/=max(1,overloaded);
		return jumppower;
	}
	//and jump. don't separate from mantling.
	override void CheckJump(){}
	void JumpCheck(double fm,double sm){
		if(
			player.cmd.buttons & BT_JUMP
		){
			if(player.crouchoffset){
				// Jumping while crouching will force an un-crouch but not jump
				player.crouching=1;
			}
			else if(waterlevel>=2){
				vel.z=4*speed;
			}
			else if(bnogravity){
				vel.z=3;
			}
			else if(
				!stunned
				&&(
					fatigue<HDCONST_SPRINTFATIGUE
					||zerk
					||cansprint
				)
				&&!MantleCheck()
				&&!countinv("WornRadsuit")
				&&!(oldinput & BT_JUMP)
			){
				double jumppower=jumppower();
				double jz=jumppower*0.65;
				if(!sm){
					if(!fm)vel.z+=jumppower*1.3; //straight up vertical leap
					else if(fm>0){ //forwards
						jumppower*=1.5;
						A_ChangeVelocity(jumppower,0,jz,CVF_RELATIVE);
					}else{ //backwards
						A_ChangeVelocity(-jumppower,0,jz,CVF_RELATIVE);
					}
				}else if(!fm){ //side jump
					if(sm>0) jumppower*=-1;
					stunned+=10;
					A_ChangeVelocity(0,jumppower,jz,CVF_RELATIVE);
				}else{ //diagonal jump
					int smult=1;
					int fmult=1;
					if(fm<0) fmult=-1;
					if(sm>0) smult=-1;
					jumppower*=HDCONST_ONEOVERSQRTTWO;
					stunned+=10;
					A_ChangeVelocity(jumppower*fmult,jumppower*smult,jz,CVF_RELATIVE);
				}
				if(height<40){
					if(bloodpressure<40)bloodpressure+=7;
					fatigue+=7;
				}
				else{
					if(bloodpressure<40)bloodpressure+=4;
					fatigue+=4;
				}
			}
		}
		//slides, too!
		else if(
			player.crouchdir<0&&height>46
			&&(fm||sm)
			&&floorz==pos.z
			&&countinv("IsMoving")>1
		){
			double mm=jumppower()*1.5;
			double fmm=fm>0?mm:fm<0?-mm*0.6:0;
			double smm=sm>0?-mm:sm<0?mm:0;
			A_ChangeVelocity(fmm,smm,-0.6,CVF_RELATIVE);
			if(bloodpressure<40)bloodpressure+=2;
			fatigue++;
			stunned+=10;
			smm*=-0.3;
			if(fmm<0)A_MuzzleClimb((smm*1.2,-7.2),(smm,-4.),(smm,-2.),(smm*0.8,-1.));
			else if(fmm>0){
				A_MuzzleClimb((smm*1.2,7.2),(smm,4.),(smm,2.),(smm*0.8,1.));
				totallyblocked=true;
			}
			else A_MuzzleClimb((smm*1.2,-0.2),(smm,-0.1),(smm,-0.1),(smm*0.8,-0.04));
		}
	}
}



extend class HDHandlers{
	void Taunt(hdplayerpawn ppp){
		ppp.A_PlaySound("*taunt",CHAN_VOICE);
		ppp.A_TakeInventory("powerfrightener");
		ppp.A_SpawnItemEx("DelayedTaunter",12,0,ppp.height-6,
			flags:SXF_NOCHECKPOSITION|SXF_SETTARGET
		);
		if(ppp.findinventory("HDBlurSphere"))
			HDBlursphere(ppp.findinventory("HDBlurSphere")).intensity=-200;
	}
}
class DelayedTaunter:IdleDummy{
	states{
	spawn:
		TNT1 A 18;
		TNT1 A 0 A_AlertMonsters();
		stop;
	}
}


