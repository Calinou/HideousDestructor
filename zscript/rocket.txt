// ------------------------------------------------------------
// Gyro-Grenades and H.E.A.T.
// ------------------------------------------------------------
extend class HDWeapon{
	int airburst;
	action void A_FireHDGL(){
		A_PlaySound("weapons/grenadeshot",CHAN_WEAPON);
		let ggg=gyrogrenade(spawn("GyroGrenade",pos+(0,0,height-8)));
		ggg.angle=angle;ggg.pitch=pitch-2;ggg.target=self;ggg.master=self;
		ggg.primed=false;
		ggg.fuze=invoker.airburst;
		invoker.airburst=0;
	}
}
extend class HDHandlers{
	void SetAirburst(hdplayerpawn ppp,int abi){
		abi=max(0,abi);
		let www=hdweapon(ppp.player.readyweapon);
		if(www){
			www.airburst=abi;
			ppp.A_Log(string.format("Airburst set to %i",abi),true);
		}
		return;
	}
}
class GyroGrenade:SlowProjectile{
	int fuze;bool primed;bool isrocket;
	default{
		-noextremedeath -noteleport +bloodlessimpact
		height 2; radius 2; scale 0.5;
		speed 110; mass 600; accuracy 0; woundhealth 0;
		obituary "%o was fragged by %k.";
		stamina 5; //used for fuel
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(isrocket)speed*=2;
		A_ChangeVelocity(speed*cos(pitch),0,speed*sin(-pitch),CVF_RELATIVE);
	}
	override void ExplodeSlowMissile(line blockingline,actor blockingobject){
		//bounce
		if(!primed&&random(0,20)){
			if(speed>50)painsound="misc/punch";else painsound="misc/fragknock";
			actor a=spawn("IdleDummy",pos);a.stamina=10;a.A_PlaySound(painsound,CHAN_AUTO);
			A_SpawnItemEx("DudRocket",0,0,0,
				random(30,60),random(-10,10),random(-10,10),
				random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,0
			);
			destroy();
			return;
		}

		//damage
		//nothing here - impact is disappearingly tiny compared to frags
		//NOTE: damage calculation is ALREADY in base SlowProjectile!

		//explosion
		if(!inthesky){
			A_SprayDecal("Scorch",16);
			A_Blast(
				pushradius:256,pushamount:128,fullpushradius:96,
				fragradius:HDCONST_SPEEDOFSOUND,
				fragdamage:random(160,200+8*stamina),
				immolateradius:128,immolateamount:random(3,60),
				immolatechance:isrocket?random(1,stamina):25,
				gibradius:16,gibamount:random(1,8)*10
			);
			actor xpl=spawn("Gyrosploder",self.pos-(0,0,1),ALLOW_REPLACE);
			xpl.target=target;xpl.master=master;xpl.stamina=stamina;
		}else{
			spawn("DistantRocket",self.pos,ALLOW_REPLACE);
		}
		A_SpawnChunks("HDBulletFrag",180,100,700+50*stamina);

		destroy();return;
	}
	states{
	spawn:
		ROCQ A 0;
	spawn1:
		#### B 1;
		#### A 0{
			if(isrocket)setstatelabel("spawnrocket");
			primed=true;
		}
	spawn2:
		#### AB 1{
			fuze--;
			if(!fuze)ExplodeSlowMissile();
		}loop;
	spawnrocket:
		---- A 0{
			brockettrail=true;
			Gunsmoke();
			A_PlaySound("weapons/rocklaunch",CHAN_VOICE);
		}
		---- AAA 0{
			actor sss=spawn("HDGunSmoke",pos-vel*0.1+(0,0,-4));
			sss.vel=vel*-0.06+(frandom(-2,2),frandom(-2,2),frandom(-2,2));
		}
	spawnrocket2:
		#### BA 1{
			if(stamina>0){  
				if(!inthesky){
					actor sss=spawn("HDGunsmoke",pos);
					sss.vel=vel*0.1;
				}
				A_ChangeVelocity(
					cos(pitch)*60,0,
					sin(-pitch)*60+1,CVF_RELATIVE
				);
				stamina--;
			}
			fuze--;
			if(!fuze)ExplodeSlowMissile();
		}
		---- A 0{
			if(primed){
				A_PlaySound("weapons/rocklaunch",5);
			}else{
				primed=true;
				brockettrail=false;
				bgrenadetrail=true;
			}
		}loop;
	death:
		TNT1 A 1;
		stop;
	}
}
class HDHEAT:GyroGrenade{
	default{
		+forcepain
		scale 0.4; woundhealth 1800;
		decal "BrontoScorch";
	}
	override void ExplodeSlowMissile(line blockingline,actor blockingobject){
		//bounce
		//nothing here - HEAT will always explode

		//damage
		if(blockingobject){
			int dmgg=random(70,240);
			if(primed&&isrocket){
				double dangle=absangle(angle,angleto(blockingobject));
				if(dangle<20){
					dmgg+=random(2000,4000);
					if(hd_Debug)A_Log("CRIT!");
				}else if(dangle<40)dmgg+=random(200,1200);
			}
			blockingobject.damagemobj(self,target,dmgg,"SmallArms3");
		}

		//explosion
		if(!inthesky){
			A_SprayDecal("BrontoScorch",16);
			A_Blast(
				pushradius:256,pushamount:128,fullpushradius:96,
				fragradius:HDCONST_SPEEDOFSOUND,
				fragdamage:random(60,240),
				immolateradius:128,immolateamount:random(3,60),
				immolatechance:2,
				gibradius:16,gibamount:random(1,8)*10
			);
			actor xpl=spawn("Gyrosploder",self.pos-(0,0,1),ALLOW_REPLACE);
			xpl.target=target;xpl.master=master;xpl.stamina=stamina;
		}else{actor xpl=Spawn("DistantRocket",self.pos,ALLOW_REPLACE);}
		A_SpawnChunks("HDBulletFrag",80,100,600);

		destroy();return;
	}
	states{
	spawn:
		MISL A 0 nodelay{primed=true;}
		goto spawnrocket;
	}
}

class Gyrosploder:HDActor{
	int ud;
	default{
		+noblockmap +missile +nodamagethrust
		gravity 0;height 6;radius 6;
		damagefactor(0);
		decal "BigScorch";
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_ChangeVelocity(1,0,0,CVF_RELATIVE);
		A_SpawnItemEx("DistantRocket",flags:SXF_NOCHECKPOSITION);
	}
	states{
	death:
		TNT1 A 0{
			if(ceilingz-pos.z<(pos.z-floorz)*3) ud=-5;
			else ud=5;
		}
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("HugeWallChunk", -1,0,ud,
			frandom(-7,7),frandom(-7,7),ud*frandom(1,3),
			frandom(0,360),SXF_NOCHECKPOSITION
		);
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("BigWallChunk", -1,0,ud,
			frandom(-1,6),frandom(-4,4),ud*frandom(1,4),
			frandom(0,360),SXF_NOCHECKPOSITION
		);
		TNT1 AA 0 A_SpawnItemEx("HDSmoke", -1,0,ud,
			frandom(-2,2),frandom(-2,2),0,
			frandom(-15,15),SXF_NOCHECKPOSITION
		);
	xdeath:
	spawn:
		TNT1 A 0 nodelay;
		TNT1 AA 0 A_SpawnItemEx("HDExplosion",
			random(-1,1),random(-1,1),2, 0,0,0,
			0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS
		);
		TNT1 A 2 A_SpawnItemEx("HDExplosion",0,0,0,
			0,0,2,
			0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS
		);
		TNT1 AAAAAAAAAAAAAAA 0 A_SpawnItemEx("BigWallChunk",0,0,1,
			random(-1,6),random(-4,4),random(4,18),
			random(-15,15),SXF_NOCHECKPOSITION
		);
	death2:
		TNT1 AA 0 A_SpawnItemEx("HDSmoke",-1,0,1,
			random(-2,3),random(-2,2),0,
			random(-15,15),SXF_NOCHECKPOSITION
		);
		TNT1 A 21{
			A_AlertMonsters();
			DistantQuaker.Quake(self,4,35,512,10);
		}stop;
	}
}





class DudRocketAmmo:Ammo{
	default{
		+inventory.ignoreskill
		inventory.pickupmessage "picked up a defused rocket grenade.";
		inventory.maxamount 6;
		ammo.backpackmaxamount 6;
		radius 2;height 2;
		scale 0.5;
	}
	states{
	spawn:
		ROCQ A 0 nodelay{
			HDF.SplitAmmoSpawn(self);
			A_SpawnItemEx("DRAP",0,0,0,
				vel.x,vel.y,vel.z,
				0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
			);
		}stop;
	}
}
class DRAP:HDUPK{
	default{
		scale 0.5;
		hdupk.pickupmessage "picked up a defused rocket grenade.";
	}
	states{
	spawn:
		ROCQ A -1 A_Stop();
	give:
		---- A 0 A_JumpIfInTargetInventory("DudRocketAmmo",0,"spawn");
		---- A 0 A_GiveToTarget("DudRocketAmmo",1);
		---- A 0 A_PlaySound("weapons/grenopen");
		stop;
	}
}
class DudRocket:HDUPK{
	default{
		projectile; -nogravity -noteleport +bounceonactors
		-noblockmap -grenadetrail -floorclip +forcexybillboard
		+nodamagethrust +noblood
		bouncetype "doom"; decal "none";
		mass 30; pushfactor 3.4; bouncefactor 0.3; gravity 1;
		deathsound "misc/fragknock";
		bouncesound "misc/fragknock";
		wallbouncesound "misc/fragknock";
		obituary "%o was fragged by %k.";
		hdupk.pickupmessage "picked up (and defused) a rocket grenade.";
		damagefactor(0);
		radius 3; height 3; scale 0.5;
	}
	states{
	spawn:
		ROCQ A 0 A_Jump(256,"clean");
		ROCQ AB 2 A_SetAngle(angle+45);
		loop;
	death:
		ROCQ A 0{
			return; //why did we have these flags anyway?
			bmissile=false;
			bpushable=true;
			bshootable=true;
			bsolid=true;
		}
	dead:
		ROCQ A 500 A_Jump(32,"clean");
		ROCQ A 500 A_Jump(8,"Explode");
		loop;
	clean:
		ROCQ A -1;
		stop;
	explode:
		---- A 0{
			A_Blast(
				pushradius:256,pushamount:128,fullpushradius:96,
				fragradius:HDCONST_SPEEDOFSOUND,
				fragdamage:random(160,200+8*stamina),
				immolateradius:128,immolateamount:random(3,60),
				immolatechance:random(1,25),
				gibradius:16,gibamount:random(1,8)*10
			);
			actor xpl=spawn("Gyrosploder",self.pos-(0,0,1),ALLOW_REPLACE);
			xpl.target=target;xpl.master=master;xpl.stamina=stamina;
		}stop;
	give:
		---- A 0 A_JumpIfInTargetInventory("DudRocketAmmo",0,3);
		---- A 0 A_GiveToTarget("DudRocketAmmo",1);
		---- A 0 A_PlaySound("weapons/grenopen");
		stop;
		ROCQ A 0 A_Jump(1,"Explode");
		ROCQ A 0 A_SpawnItemEx("DRAP",2,0,0,0,0,2,0,SXF_NOCHECKPOSITION);
		stop;
	}
}
