// ------------------------------------------------------------
// Basic functional stuff
// ------------------------------------------------------------

//event handler
class HDHandlers:EventHandler{
	//the only thing that should be up here, otherwise use "extend"
	override void worldtick(){
		//for changing fraglimit on the fly
		if(fl!=fraglimit){
			int oldfl;
			fl=fraglimit>=100?fraglimit-100:fraglimit;
			for(int i=0;i<MAXPLAYERS;i++){
				lives[i]+=(fl-oldfl);
			}
		}
	}
	override void WorldLoaded(WorldEvent e){
		textureid dirtyglass=texman.checkfortexture("WALL47_2",texman.type_any);

		//minimum light level 1 for nightvis
		int itmax=level.sectors.size();
		for(int i=0;i<itmax;i++){
			let special=level.sectors[i].special;
			bool dontchangelight=false;
			switch(special){
			// https://doomwiki.org/wiki/Sector#ZDoom
			//these totally break if we change it like this
			case 65:
			case 66:
			case 67:
			case 68:
			case 84:
			case 76:
			case 77:
				dontchangelight=true;
				break;
			default:break;
			}
			if(!dontchangelight&&level.sectors[i].lightlevel<1)level.sectors[i].lightlevel=1;
		}
		itmax=level.lines.size();
		for(int i=0;i<itmax;i++){
			line lll=level.lines[i];

			//no more killer elevators
			if(lll.special){
				switch(lll.special){

				//cap platform speeds
				case Plat_DownByValue:
				case Plat_DownWaitUpStayLip:
				case Plat_DownWaitUpStay:
				case Generic_Lift:
				case Plat_PerpetualRaiseLip:
				case Plat_PerpetualRaise:
				case Plat_RaiseAndStayTx0:
				case Plat_UpByValue:
				case Plat_UpByValueStayTx:
				case Plat_UpNearestWaitDownStay:
				case Plat_UpWaitDownStay:
					if(!hd_safelifts||abs(lll.args[1])>64)break;
					lll.args[1]=clamp(lll.args[1],-24,24);
					break;

				//prevent lights from going below 1
				case Light_ChangeToValue:
				case Light_Fade:
				case Light_LowerByValue:
					lll.args[1]=max(lll.args[1],1);break;
				case Light_Flicker:
				case Light_Glow:
				case Light_Strobe:
					lll.args[2]=max(lll.args[2],1);break;
				case Light_StrobeDoom:
					lll.args[2]=min(lll.args[2],1);break;
				case Light_RaiseByValue:
					if(lll.args[1]>=0)break;
				case Light_LowerByValue:
					sectortagiterator sss=sectortagiterator.create(lll.args[0]);
					int ssss=sss.next();
					int lowestlight=255;
					while(ssss>-1){
						lowestlight=min(lowestlight,level.sectors[ssss].lightlevel);
						ssss=sss.next();
					}
					lll.args[1]=min(lll.args[1],lowestlight-1);

				default: break;
				}
			}
			//if block-all and no midtexture, force add a mostly transparent midtexture
			if(
				hd_dirtywindows
				&&lll.sidedef[1]
				&&(
					lll.flags&line.ML_BLOCKEVERYTHING
					||lll.flags&line.ML_BLOCKPROJECTILE
					||lll.flags&line.ML_BLOCKHITSCAN
//					||lll.flags&line.ML_BLOCKING
				)
				&&!lll.sidedef[0].gettexture(side.mid)
				&&!lll.sidedef[1].gettexture(side.mid)
			){
				lll.flags|=line.ML_WRAP_MIDTEX;
				lll.sidedef[0].settexture(side.mid,dirtyglass);
				lll.sidedef[1].settexture(side.mid,dirtyglass);
				lll.alpha=0.2;
			}
		}
	}
}

//because "extend class Actor" doesn't work
class HDActor:Actor{
	default{
		+noblockmonst
	}
	//advance to the next tic without doing anything
	//don't actually use this, it's just for reference
	void nexttic(){
		if(CheckNoDelay()){
			if(tics>0)tics--;  
			while(!tics){
				if(!SetState(CurState.NextState)){
					return;
				}
			}
		}
	}
	//decorate wrapper for HDMath.CheckLump()
	bool CheckLump(string lumpname){
		return HDMath.CheckLump(lumpname);
	}
	//"After that many drinks anyone would be blowing chunks all night!"
	//"Chunks is the name of my dog."
	//for frags: A_SpawnChunks("HDFrag",42,100,700);
	void A_SpawnChunks(
		class<actor> chunk,
		int number=12,
		double minvel=10,
		double maxvel=20
	){
		if(
			chunk is "WallChunk"
		){
			int ch=0;
			thinkeriterator chit=thinkeriterator.create(chunk,STAT_DEFAULT);
			while(chit.Next(exact:false))ch++;
			if(ch>500){  
				if(hd_debug)A_Log(string.format("%i is too many chunks, %s not spawned",ch,chunk.getclassname()));
				return;
			}
		}
		double burstz=pos.z+height*0.5;
		double minpch=burstz-floorz<56?9:90;
		double maxpch=ceilingz-burstz<56?-9:-90;
		burstz-=pos.z;
		for(int i=0;i<number;i++){
			bool gbg;actor frg;
			double pch=frandom(minpch,maxpch);
			double vl=frandom(minvel,maxvel);
			[gbg,frg]=A_SpawnItemEx(
				chunk,
				0,0,burstz,
				vl*cos(pch),0,vl*sin(-pch),
				frandom(0,360),
				SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_TRANSFERPOINTERS
			);
			frg.vel+=vel;
			frg.bseeinvisible=true; //work around hack that normally lets HDBullet out
		}
	}
	//roughly equivalent to CacoZapper
	static void ArcZap(actor caller){
		caller.A_CustomRailgun((random(4,8)),frandom(-12,12),"","azure",
			RGF_SILENT|RGF_FULLBRIGHT,
			1,4000,"HDArcPuff",180,180,frandom(32,128),4,0.4,0.6
		);
	}
}
class HDArcPuff:HDActor{
	default{
		+nogravity
		+puffgetsowner
		+puffonactors
		+forcepain
		+noblood
		scale 0.4;
		damagetype "Electro";
		radius 0.1;
		height 0.1;
	}
	states{
	spawn:
		TNT1 A 5 A_PlaySound("weapons/plasidle",0,0.1,0,0.4);
		stop;
	}
}

class InventoryFlag:Inventory{
	default{
		+inventory.untossable;+nointeraction;+noblockmap;
		inventory.maxamount 1;inventory.amount 1;
	}
	states{
	spawn:
		TNT1 A 0;
		stop;
	}
}
class ActionItem:CustomInventory{
	default{
		+inventory.untossable -inventory.invbar +noblockmap
	}
	//wrapper for HDWeapon and ActionItem
	//remember: LEFT and DOWN
	//would use vector2s but lol bracketing errors I don't need that kind of negativity in my life
	action void A_MuzzleClimb(
		double mc10=0,double mc11=0,
		double mc20=0,double mc21=0,
		double mc30=0,double mc31=0,
		double mc40=0,double mc41=0
	){
		let hdp=HDPlayerPawn(self);
		if(hdp){
			hdp.A_MuzzleClimb((mc10,mc11),(mc20,mc21),(mc30,mc31),(mc40,mc41));
		}else{ //I don't even know why
			vector2 mc0=(mc10,mc11)+(mc20,mc21)+(mc30,mc31)+(mc40,mc41);
			A_SetPitch(pitch+mc0.y,SPF_INTERPOLATE);
			A_SetAngle(angle+mc0.x,SPF_INTERPOLATE);
		}
	}
	states{
	nope:
		TNT1 A 0;fail;
	spawn:
		TNT1 A 0;stop;
	}
}
class IdleDummy:HDActor{
	default{
		+noclip +nointeraction +noblockmap
		height 0;radius 0;
	}
	states{
	spawn:
		TNT1 A -1 nodelay{
			if(stamina>0)A_SetTics(stamina);  
		}stop;
	}
}
class CheckPuff:IdleDummy{
	default{
		+bloodlessimpact +hittracer +puffonactors +alwayspuff +puffgetsowner
		stamina 1;
	}
}
//mostly for doing TryMove checks to grab blockingline data
class TMChecker:HDActor{
	default{
		radius 3;height 1;maxstepheight 0;
	}
	states{
	spawn:
		TNT1 A 1;
		stop;
	}
}


// Blocker to prevent shotguns from overpenetrating multiple targets
// tempshield.spawnshield(self);
class tempshield:HDActor{
	default{
		-solid +shootable +nodamage
		radius 16;height 50;
		stamina 16;
	}
	static actor spawnshield(
		actor caller,class<actor> type="tempshield",
		bool deathheight=false,int shieldlength=16
	){
		actor sss=caller.spawn(type,caller.pos);
		if(!sss)return null;
		sss.master=caller;
		sss.A_SetSize(
			caller.radius,
			deathheight?getdefaultbytype(caller.getclass()).deathheight
			:getdefaultbytype(caller.getclass()).height
		);
		sss.bnoblood=caller.bnoblood;
		sss.stamina=shieldlength;
		return sss;
	}
	states{
	spawn:
		TNT1 A 1 nodelay{
			if(!master||stamina<1){destroy();return;}
			setorigin(master.pos,false);
			stamina--;
		}wait;
	}
}
class tempshield2:tempshield{
	default{
		radius 18;height 26;
		stamina 10;
	}
}
class tempshieldyellow:tempshield{
	default{bloodcolor "aa 99 22";}
}
class tempshieldgreen:tempshield{
	default{bloodcolor "44 99 22";}
}
class tempshield2green:tempshield2{
	default{bloodcolor "44 99 22";}
}
class tempshieldblue:tempshield{
	default{bloodcolor "00 00 99";}
}
class tempshield2blue:tempshield2{
	default{bloodcolor "00 00 99";}
}
class tempshieldpuff:tempshield{
	default{+noblood}
}
class tempshield2puff:tempshield2{
	default{+noblood}
}


//collection of generic math functions
struct HDMath{
	//return true if lump exists
	//mostly for seeing if we're playing D1 or D2
	//HDMath.CheckLump("SHT2A0")
	static bool CheckLump(string lumpname){
		return Wads.CheckNumForName(lumpname,wads.ns_sprites,-1,false)>=0;
	}
	//return the maximum capacity for this actor and this inventory
	//HDMath.MaxInv(self,"FourMilAmmo")
	static int MaxInv(actor holder,class<inventory> inv){
		if(holder.findinventory(inv))return holder.findinventory(inv).maxamount;
		return getdefaultbytype(inv).maxamount;
	}
	//counts stuff
	//HDMath.CountThinkers("IdleDummy")
	static int Count(name type,bool exactmatch=false){
		int count=0;
		thinkeriterator it=thinkeriterator.create(type);
		while(it.Next(exact:exactmatch))count++;
		return count;
	}
	//get the opposite sector of a line
	static sector OppositeSector(line hitline,sector hitsector){
		if(!hitline||!hitline.backsector)return null;
		if(hitline.backsector==hitsector)return hitline.frontsector;
		return hitline.backsector;
	}
	//calculate the speed at which 2 actors are moving towards each other
	static double TowardsEachOther(actor a1, actor a2){
		vector3 oldpos1=a1.pos;
		vector3 oldpos2=a2.pos;
		vector3 newpos1=oldpos1+a1.vel;
		vector3 newpos2=oldpos2+a2.vel;
		double l1=(oldpos1-oldpos2).length();
		double l2=(newpos1-newpos2).length();
		return l1-l2;
	}
	//return a string indicating a rough cardinal direction
	static string CardinalDirection(int angle){
		angle%=360;
		if(angle<0)angle+=360;
		if(angle>=22&&angle<=66)return("northeast");
		else if(angle>=67&&angle<=113)return("north");
		else if(angle>=114&&angle<=158)return("northwest");
		else if(angle>=159&&angle<=203)return("west");
		else if(angle>=204&&angle<=248)return("southwest");
		else if(angle>=249&&angle<=292)return("south");
		else if(angle>=293&&angle<=338)return("southeast");
		return("east");
	}
}
struct HDF play{
	//transfer inventory: one gets, the other loses
	enum TransferInventoryFlags{
		ITRF_ADD=1,
		ITRF_KEEPGIVER=2,
	};
	static int TransferInventory(
		actor giver,
		actor receiver,
		class<inventory> item,
		int flags=0,
		int amt=0
	){
		//set the amount
		if(amt<1)amt=giver.countinv(item);
		else amt=min(amt,giver.countinv(item));

		//give the amount as appropriate
		if(flags & ITRF_ADD){
			amt=min(amt,hdmath.maxinv(receiver,item)-receiver.countinv(item));
			if(amt)receiver.A_GiveInventory(item,amt);
		}else receiver.A_SetInventory(item,amt);

		//remove from giver if true
		if(!(flags & ITRF_KEEPGIVER)){
			giver.A_TakeInventory(item,amt);
		}
		return amt;
	}
	//because this is 10 times faster than A_GiveInventory
	static void Give(actor whom,class<inventory> what,int howmany=1){
		whom.A_SetInventory(what,whom.countinv(what)+howmany);
	}
	//transfer fire. returns # of fire actors affected.
	static int TransferFire(actor ror,actor ree,int maxfires=-1){
		actoriterator it=actoriterator.create(-7677,"HDFire");
		actor fff;int counter;
		bool eee;if(ree)eee=true;
		while(maxfires && (fff=it.next())){
			maxfires--;
			if(fff.target==ror){
				counter+=fff.stamina;
				if(eee)fff.target=ree;
				else fff.destroy();
			}
		}
		return counter;
	}
	//split inventory item to match pickup sprite
	static void SplitAmmoSpawn(inventory caller,int maxunit=1){
		while(caller.amount>maxunit){
			caller.amount-=maxunit;
			inventory bbb=inventory(caller.spawn(caller.getclass(),caller.pos));
			bbb.amount=maxunit;bbb.vel=caller.vel+(frandom(-1,1),frandom(-1,1),frandom(-1,1));
		}
	}
	//process shields
	//[shields,damage]=hdf.gothroughshields(shields,damage,inflictor,mod);
	static int,int GoThroughShields(int shields,int damage,actor inflictor=null,name mod="none"){
		if(
			shields<1
			||mod=="internal"
			||mod=="falling"
		)return shields,damage;

		int blocked=min(shields,damage,100);
		if(blocked==damage&&!random(0,3))blocked--;
		damage-=blocked;

		shields=clamp(shields-(mod=="BFGBallAttack"?blocked*100:blocked-20),0,shields);

		if(inflictor&&!inflictor.bismonster&&!inflictor.player){
			int shrd=max(random(0,1),damage/50);
			for(int i=0;i<shrd;i++){
				actor aaa=inflictor.spawn("FragShard",inflictor.pos);
				aaa.vel=(frandom(-3,3),frandom(-3,3),frandom(-3,3));
			}
		}
		return shields,damage;
	}
}



class testmarker:idledummy{
	states{
	spawn:
		BAL2 A -1;
	}
}


//just wanted to make sure
//Pop will not crash on an empty array
//Appears to be saved and loaded just fine
class dynarraytester:inventory{
	array<int> testarray;
	array<int> testarray2;
	override void doeffect(){
		if(!(level.time%10)){
			//drop one
			if(!random(0,4)){
				if(testarray.size()>0)A_LogInt(testarray[testarray.size()-1]);
				testarray.pop();
			}
			//add one to the end
			if(!random(0,3)&&testarray.size()<12){
				testarray.push(random(10,99));
			}
			//move one from end to start
			if(!random(0,4)&&testarray.size()>1){
				testarray.insert(0,testarray[testarray.size()-1]);
				testarray.pop();
			}

			string aaa="";
			for(int i=0;i<testarray.size();i++){
				aaa=string.format("%s %i",aaa,testarray[i]);
			}
			if(aaa!="")A_Log(aaa);
		}
	}
}






