// ------------------------------------------------------------
// Cyberdemon
// ------------------------------------------------------------
class Cyberball:SlowProjectile{
	default{
		+rockettrail
		damage 30;
		speed 72;
		radius 5;height 5;
		missileheight 4;
		gravity 0;
		decal "Scorch";
		seesound "weapons/rocklf";
		scale 0.6;
	}
	override void ExplodeSlowMissile(line blockingline=null,actor blockingobject=null){
		//damage
		if(blockingobject){
			int dmgg=random(70,240);
			double dangle=angle;
			A_Face(blockingmobj);
			dangle=abs(deltaangle(angle,dangle));
			if(dangle<10)dmgg+=random(2000,4000);
			else if(dangle<30)dmgg+=random(200,1200);

			blockingobject.damagemobj(self,target,dmgg,"SmallArms3");
		}

		//explosion
		if(!inthesky){
			A_SprayDecal("Scorch",16);
			A_Blast(
				blastradius:512,blastdamage:random(128,256),fullblastradius:96,
				pushradius:256,pushamount:256,fullpushradius:96,
//				fragradius:256,fragdamage:random(160,200),
				immolateradius:128,immolateamount:random(3,60),
				immolatechance:15,
				gibradius:24,gibamount:random(1,8)*10
			);

			//hit map geometry
			if(
				blockingline||
				floorz>=pos.z||
				ceilingz-height<=pos.z
			){
				bmissilemore=true;
			}
		}else{
			spawn("DistantRocket",self.pos,ALLOW_REPLACE);
		}
		A_SpawnChunks("HDBulletFrag",180,100,500+4*stamina);

		//destroy();return;
		bmissile=false;
		setstatelabel("death");
	}
	int fuel;
	vector2 thrust;
	states{
	spawn:
		TNT1 A 0 nodelay{
			A_PlaySound("weapons/rocklf",CHAN_VOICE);
			fuel=100;
			thrust=(cos(pitch),-sin(pitch))*10;
		}
	spawn2:
		MISL A 2 light("ROCKET"){
			A_PlaySound("weapons/rocklaunch",CHAN_AUTO,0.6);
			if(fuel<1)setstatelabel("spawn2");
			fuel--;
			A_ChangeVelocity(thrust.x,0,thrust.y,CVF_RELATIVE);
		}loop;
	spawn3:
		MISL A 3 light("ROCKET")A_Changevelocity(0,0,-1);
		MISL A 2 light("ROCKET")A_Changevelocity(0,0,-1);
		MISL A 1 A_Changevelocity(0,0,-1);
		wait;
	death:
		TNT1 A 1{
			for(int i=0;i<3;i++){
				actor xp=spawn("HDExplosion",pos+(frandom(-2,2),frandom(-2,2),frandom(-2,2)));
				xp.vel.z=frandom(1,3);
			}
			A_PlaySound("world/explode");
			spawn("DistantRocket",pos);
			A_Quake(3,28,0,256);
		}
		TNT1 AAAA 0 A_SpawnItemEx("HDSmokeChunk",
			random(-1,1),random(-1,1),random(-1,1),
			random(-6,6),random(-6,6),random(1,6)
		);
		TNT1 AAAA 0 A_SpawnItemEx("HDSmoke",
			random(-6,6),random(-6,6),random(-2,6),
			random(-1,5),0,random(0,1),
			random(-5,15)
		);
		TNT1 AAAAA 0 A_SpawnItemEx("HugeWallChunk", 0,0,1, 
			random(-2,22),random(-16,16),random(-4,24),
			random(0,360)
		);
		TNT1 A 12 A_JumpIf(bmissilemore,"deathsmash");
		stop;
	deathsmash:
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx("HugeWallChunk", 0,0,1, 
			random(-2,18),random(-12,12),random(8,20),
			random(0,360)
		);
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("BigWallChunk",0,0,0,
			random(-3,20),random(-6,6),random(4,18),
			random(-15,15)
		); 
		TNT1 A 12;
		stop;
	}
}

class SatanRobo:Cyberdemon replaces CyberDemon{
	default{
		+avoidmelee +nofear +seeinvisible +nodropoff
		-noradiusdmg
		+noblood
		damagefactor "Thermal", 0.5;
		damagefactor "SmallArms0", 0.2;
		damagefactor "SmallArms1", 0.3;
		damagefactor "SmallArms2", 0.6;
		damagefactor "SmallArms3", 0.9;
		gibhealth 900;
		health 4000;
		mass 12000;
		speed 15;
		deathheight 110;
		painchance 32;
		painthreshold 200;
		maxtargetrange 0;
		radiusdamagefactor 0.6;
		bloodtype "NotQuiteBloodSplat";
		obituary "%o was experimented upon by a cyberdemon.";
		minmissilechance 196;
	}
	void A_CyberGunSmoke(){
		A_SpawnItemEx("HDSmoke",
			random(-32,32),random(-32,32),random(46,96),0,
			frandom(2,3),0,frandom(2,4),0,SXF_NOCHECKPOSITION,64
		);
	}
	void A_CyberAdjustLead(){
		oldaim=(angle,pitch);
		A_FaceTarget(12,12,0,0,FAF_MIDDLE);
		aimadjust=(deltaangle(oldaim.x,angle),deltaangle(oldaim.y,pitch));
		aimadjust*=distance3d(target)*0.0005;
	}
	void A_CyberRocket(double spread=0.,double aimhorz=0.,double aimvert=0.){
		A_PlaySound("weapons/bronto",CHAN_WEAPON);
		spawn("DistantShotgun",pos);
		if(spread){
			aimhorz=frandom(-spread,spread);
			aimvert=frandom(-spread,spread);
		}
		double dist=max(1,(target?distance3d(target):0));
		A_SpawnProjectile("CyberBall",42,-24,
			aimhorz-(target?atan(24/dist):0),
			CMF_AIMDIRECTION|CMF_SAVEPITCH,
			pitch+aimvert-(dist>2048?dist*0.000001:0)+(target?atan(10/dist):0)
		);
	}
	override void tick(){
		super.tick();
		if(
			bnofear&&
			health<1600&&
			!random(0,max(10,health))
		)A_SpawnItemEx("HDSmoke",
			random(-32,32),random(-32,32),random(46,96),
			0,0,random(2,4),0,160,64
		);
		if(health>0&&shields<HDCB_SHIELDMAX)shields+=2;


		/*
		//indicator
		if(!master||!(master is "IdleDummy")){
			master=spawn("IdleDummy");
			master.sprite=GetSpriteIndex("BFE2A0");
		}else{
			double shieldleft=(shields*1.)/(HDCB_SHIELDMAX*1.);
			master.scale=(shieldleft,shieldleft);master.alpha=shieldleft;
			master.setorigin(pos+(0,0,height+20),true);
		}
		*/
	}
	override int damagemobj(
		actor inflictor,actor source,int damage,
		name mod,int flags=0,double angle=0
	){
		//cheat
		if(source==self)return 0;

		//shields
		if(
			shields>0
			&&mod!="internal"
			&&mod!="falling"
		){
			int blocked=clamp(damage,0,min(shields,500));
			damage=max(damage-blocked,(!random(0,9)?1:0));
			shields=clamp(shields-(mod=="BFGBallAttack"?blocked*100:blocked-20),0,shields);
			if(!inflictor.bismonster&&!inflictor.player){
				int shrd=max(random(0,1),damage/50);
				for(int i=0;i<shrd;i++){
					actor aaa=spawn("FragShard",inflictor.pos);
					aaa.vel=(frandom(-3,3),frandom(-3,3),frandom(-3,3));
				}
			}
		}else{
			bnoblood=false;
		}
		if(damage<1)return 0;
//			else A_Log(string.format("cyberdemon took %i damage",damage));
		return super.damagemobj(
			inflictor,source,damage,mod,flags,angle
		);
	}
	override void postbeginplay(){
		super.postbeginplay();
		shields=HDCB_SHIELDMAX;
		hdmobster.spawnmobster(self);
	}
	vector2 oldaim;vector2 aimadjust;
	int shields;
	enum CyberStats{
		HDCB_SHIELDMAX=2000,
	}
	states{
	spawn:
		CYBR EEEE 10{
			A_Look();
			angle+=frandom(-5,5);
		}
	spawn2:
		CYBR CDDAA 6{hdmobai.wander(self);}
		CYBR B 0 A_PlaySound("spider/walk");
		CYBR BB 6{hdmobai.wander(self);}
		CYBR C 6{
			A_PlaySound("cyber/hoof");
			hdmobai.wander(self);
		}
		CYBR C 0 A_Jump(32,"spawn");
		loop;
	see:
		CYBR E 0{
			if(health<1)setstatelabel("death");
			A_AlertMonsters(0,AMF_TARGETNONPLAYER);
			bfrightening=true;
		}
		CYBR AA 2{
			hdmobai.chase(self);
		}
		CYBR B 5{
			A_PlaySound("spider/walk",5);
			hdmobai.chase(self);
		}
		CYBR C 5{
			A_PlaySound("cyber/hoof",6);
			hdmobai.chase(self);
		}
		CYBR DD 2{
			hdmobai.chase(self);
		}
		CYBR E 0 A_Jump(56,"see");
		CYBR E 0 A_JumpIfTargetInLOS("missile",0);
		loop;
	pain:
		CYBR G 10{
			A_Pain();
			if(health<3500){
				if(health<3000)minmissilechance-=5;
				if(health>1000)speed++;else speed--;
			}
		}goto see;
	missile:
		CYBR A 0 A_JumpIfTargetInLOS("inposition",20);
		CYBR A 4 A_FaceTarget(40,40);
		CYBR B 4{
			A_FaceTarget(40,40);
			A_PlaySound("spider/walk",5);
			A_Recoil(-4);
		}
		CYBR C 0 A_JumpIfTargetInLOS("inposition",20);
		CYBR C 4{
			A_FaceTarget(40,40);
			A_PlaySound("cyber/hoof",6);
			A_Recoil(-4);
		}
		CYBR D 4 A_FaceTarget(40,40);
		CYBR E random(15,25) A_Recoil(-4);
		CYBR E 0 A_JumpIfTargetInLOS("missile");
		CYBR E 0 A_Jump(128,"spray");
		goto see;
	inposition:
		CYBR E 4{
			A_Recoil(1);
			bfrightening=true;
			A_FaceTarget(12,12);
		}
		CYBR E 0 A_Stop();
		CYBR E 0 A_JumpIf(health>1600,3);
		CYBR EE 2 A_CyberGunSmoke();
		CYBR E 4;
		CYBR A 0 A_AlertMonsters(0,AMF_TARGETNONPLAYER);
		CYBR E 0 A_JumpIfTargetInLOS(1,90);
		goto missile;

		CYBR E 0 A_JumpIf(health>1600,3);
		CYBR EE 2 A_CyberGunSmoke();
		CYBR E 4 A_FaceTarget(12,12);
		CYBR E 0 A_JumpIf(health>1600,3);
		CYBR EE 2 A_CyberGunSmoke();
		CYBR E 4;
		CYBR A 0{
			double dist=distance3d(target);

			if(dist<1024&&!random(0,7))setstatelabel("spray");
			else if(dist<8192&&!random(0,2))setstatelabel("leadtarget");
			else setstatelabel("directshots");
		}
	leadtarget:
		CYBR F 3 bright light("ROCKET")A_CyberRocket();
		CYBR E 1 A_SetTics(random(1,4));
		CYBR E 0 A_JumpIf(health>1600,3);
		CYBR EE 0 A_CyberGunSmoke();
	leadtarget2:
		CYBR E 8 A_FaceTarget(12,12,0,0,FAF_MIDDLE);
		CYBR F 3 bright light("ROCKET"){
			A_CyberAdjustLead();
			A_CyberRocket(
				0.,frandom(0,2)*aimadjust.x,frandom(0,2)*aimadjust.y
			);
		}
		CYBR E 1 A_SetTics(random(1,8));
		CYBR E 0 A_JumpIf(health>1600,3);
		CYBR EE 0 A_CyberGunSmoke();
	leadtarget3:
		CYBR E 8 A_FaceTarget(12,12,0,0,FAF_MIDDLE);
		CYBR F 3 bright light("ROCKET"){
			A_CyberAdjustLead();
			A_CyberRocket(
				0.,frandom(1,5)*aimadjust.x,frandom(1,5)*aimadjust.y
			);
		}
		goto fireend;

	directshots:
		CYBR F 3 bright light("ROCKET")A_CyberRocket();
		CYBR E 8 A_FaceTarget(12,12,0,0,FAF_MIDDLE);
		CYBR F 3 bright light("ROCKET")A_CyberRocket(0.3);
		CYBR E 8 A_FaceTarget(12,12,0,0,FAF_MIDDLE);
		CYBR F 3 bright light("ROCKET")A_CyberRocket(0.7);
		goto fireend;

	spray:
		CYBR F 3 bright light("ROCKET")A_CyberRocket(1.);
		CYBR E 6 A_FaceTarget(12,12,0,0,FAF_MIDDLE);
		CYBR F 3 bright light("ROCKET")A_CyberRocket(2.);
		CYBR E 6 A_FaceTarget(12,12,0,0,FAF_MIDDLE);
		CYBR F 3 bright light("ROCKET")A_CyberRocket(3.);
	fireend:
		CYBR E 0 A_JumpIf(health>1600,3);
		CYBR EE 0 A_CyberGunSmoke();
		CYBR E 10;
		goto see;

	death:
		CYBR G 12 A_Pain();
		CYBR DD 6 A_Recoil(-2);
		CYBR A 12;
		CYBR A 0{
			A_FaceTarget(40,0);
			A_SetAngle(angle+random(-10,10));
			A_PlaySound("spider/walk",5);
		}
		CYBR BB 6 A_Recoil(-2);
		CYBR C 12{
			A_FaceTarget(40,0);
			A_PlaySound("cyber/hoof",6);
		}
		CYBR A 0{
			A_FaceTarget(40,0);
			angle+=random(-10,10);
		}
		CYBR DD 6 A_Recoil(-2);
		CYBR A 10{
			A_SpawnItemEx("HDExplosionBoss",
				random(-12,12),random(-12,12),random(60,64),
				random(-1,1),random(-1,1),random(1,3)
			);
			A_SpawnItemEx("HDSmokeChunk",
				random(-10,10),random(-10,10),random(38,60), 
				vel.x+random(-6,6),vel.y+random(-6,6),vel.z+random(3,12),
				0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM,144
			);
			A_PlaySound("cyber/hoof",6);
		}
		CYBR D 0 A_SpawnItemEx("HDExplosionBoss",
			random(-12,12),random(-12,12),random(60,64),
			random(-1,1),random(-1,1), random(1,3)
		);
		CYBR A 5{
			A_FaceTarget(40,0);
			angle+=random(-10,10);
			A_PlaySound("cyber/hoof");
		}
		CYBR B 6 A_PlaySound("spider/walk");
		CYBR B 6{
			A_SpawnItemEx("HDExplosionBoss",
				random(-26,26),random(-26,26),random(60,64),
				random(-1,1),random(-1,1),random(1,3)
			);
			A_SpawnItemEx("HDSmokeChunk",
				random(-10,10),random(-10,10),random(38,60),
				vel.x+random(-6,6),vel.y+random(-6,6),vel.z+random(3,12),
				0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM,144
			);
		}
		CYBR C 16{
			A_FaceTarget(40,0);
			angle+=random(-10,10);
			A_PlaySound("cyber/hoof",6);
		}
		CYBR DD 6 A_Recoil(-2);
		CYBR D 6 A_SpawnItemEx("HDExplosionBoss",
			random(-26,26),random(-26,26),random(60,64),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR A 20 A_SpawnItemEx("HDSmokeChunk",
			random(-10,10),random(-10,10),random(38,60),
			vel.x+random(-6,6),vel.y+random(-6,6),vel.z+random(3,12),
			0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM,144
		);
		CYBR B 0{
			A_FaceTarget(40,0);
			angle+=random(-10,10);
			A_PlaySound("spider/walk",5);
		}
		CYBR BB 6 A_Recoil(-1);
		CYBR C 24{
			A_SpawnItemEx("HDExplosionBoss",
				random(-26,26),random(-26,26),random(70,88),
				random(-1,1),random(-1,1),random(1,3)
			);
			A_PlaySound("cyber/hoof",6);
		}
		CYBR E 14{
			A_PlaySound("spider/walk",5);
		}
		CYBR EEEE 6 A_FaceTarget(10,0);
		CYBR FF 0 A_SpawnItemEx("HDSmoke",54,-24,52,
			random(-1,1),random(-1,1),random(2,4)
		);
		CYBR FFFFFF 0 A_SpawnItemEx("BigWallChunk",54,-24,52,
			random(4,14),random(-3,3),random(1,4),random(0,360)
		);
		CYBR FFFF 0 A_SpawnItemEx("HugeWallChunk",54,-24,52,
			random(4,24),random(-3,3),random(1,4),random(0,360)
		);
		CYBR F 3 bright A_SpawnItemEx("HDExplosion",54,-24,52);
		CYBR E 56;
		CYBR EEEEEE 4 A_SpawnItemEx("HDSmoke",54,-24,52,
			random(-1,1),random(-1,1),random(2,4)
		);
		CYBR EEEEEE 2 A_SpawnItemEx("HDSmoke",54,-24,52,
			random(-1,1),random(-1,1),random(3,6)
		);
	xdeath:
		CYBR EEEEEE 1 A_SpawnItemEx("HDSmoke",54,-24,52,
			random(-1,1),random(-1,1),random(4,8)
		);
		CYBR H 6 A_Scream();
		CYBR H 2 bright A_SpawnItemEx("HDExplosion",
			random(-26,26),random(-26,26),random(56,64),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR H 3 bright A_SpawnItemEx("HDExplosionBoss",
			random(-36,36),random(-36,36),random(40,46),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR H 1 bright A_SpawnItemEx("HDExplosion",
			random(-26,26),random(-46,46),random(30,36),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR I 2{
			A_Explode(512,16);
			DistantQuaker.Quake(self,8,140,4096,7,400,666,256);
		}

		CYBR AAAAAAA 0 A_SpawnItemEx("HDSmokeChunk",
			random(-10,10),random(-10,10),random(38,60),
			random(-6,6),random(-6,6),random(3,12)
		);
		CYBR I 2 bright A_SpawnItemEx("HDExplosionBoss",
			random(-36,36),random(-26,26),random(60,78),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR I 2 bright A_SpawnItemEx("HDExplosion",
			random(-36,36),random(-26,26),random(50,68),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR I 3 bright A_SpawnItemEx("HDExplosion",
			random(-26,26),random(-26,26),random(75,82),
			random(-1,1),random(-1,1),random(1,3)
		);

		CYBR AA 0 A_SpawnItemEx("CyberGibs",
			random(-10,10),random(-10,10),random(38,60),
			random(-6,6),random(-6,6),random(3,12)
		);
		CYBR AA 0 A_SpawnItemEx("HDSmokeChunk",
			random(-10,10),random(-10,10),random(38,60),
			random(-6,6),random(-6,6),random(6,12)
		);

		CYBR J 3 bright A_SpawnItemEx("HDExplosionBoss",
			random(-26,26),random(-46,46),random(45,52),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR AA 0 A_SpawnItemEx("CyberGibs",
			random(-10,10),random(-10,10),random(38,60),
			random(-6,6),random(-6,6),random(3,12)
		);
		CYBR J 3 bright A_SpawnItemEx("HDExplosion",
			random(-36,36),random(-26,26),random(64,82),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR J 3 bright A_SpawnItemEx("HDExplosionBoss",
			random(-36,36),random(-26,26),random(45,82),
			random(-1,1),random(-1,1),random(1,3)
		);

		CYBR KK 0 A_SpawnItemEx("CyberGibs",
			random(-10,10),random(-10,10),random(38,60),
			random(-6,6),random(-6,6),random(3,12)
		);
		CYBR KK 0 A_SpawnItemEx("HDSmokeChunk",
			random(-10,10),random(-10,10),random(38,60),
			random(-6,6),random(-6,6),random(3,12)
		);

		CYBR K 4 bright A_SpawnItemEx("HDExplosion",
			random(-36,36),random(-46,46),random(48,62),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR K 4 A_SpawnItemEx("HDExplosionBoss",
			random(-66,66),random(-66,66),random(15,42),
			random(-1,1),random(-1,1),random(1,3)
		);

		CYBR L 4 A_SpawnItemEx("HDExplosion",
			random(-36,36),random(-36,36),random(62,82),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR L 1 A_SpawnItemEx("HDExplosionBoss",
			random(-16,16),random(-16,16),random(75,82),
			random(-1,1),random(-1,1),random(1,3)
		);

		CYBR LL 0 A_SpawnItemEx("HDSmokeChunk",
			random(-10,10),random(-10,10),random(38,60),
			random(-6,6),random(-6,6),random(3,12)
		);

		CYBR LLLL 3 A_SpawnItemEx("HDSmoke",
			random(-36,36),random(-36,36),random(24,80),
			random(-1,1),random(-1,1),random(3,6)
		);

		CYBR M 0 A_NoBlocking();
		CYBR MMMM 2 A_SpawnItemEx("HDSmoke",
			random(-20,20),random(-20,20),random(24,80),
			random(-1,1),random(-1,1),random(2,4)
		);
		CYBR O 0{
			vel=(0,0,0);
			spawn("CyberRemains",pos);
		}

		CYBR PPPP 4 A_SpawnItemEx("HDSmoke",
			random(-26,26),random(-26,26),random(12,40),
			random(-1,1),random(-1,1),random(1,3)
		);
		CYBR PPPPPPPPPPPPPPPPP 1 A_SpawnItemEx("HDSmoke",
			random(-26,26),random(-26,26),random(32,60),
			random(-2,2),random(-2,2),random(1,6)
		);
		CYBR PPPPPPPPPPPPPPPP 5 A_SpawnItemEx("HDSmoke",
			random(-26,26),random(-26,26),random(1,14),
			random(-1,1),random(-1,1),random(2,6),random(0,255)
		);
		CYBR P 200{bnofear=false;}
		CYBR P -1 A_BossDeath();
		stop;
	}
}


class CyberRemains:Actor{
	default{
		renderstyle "add";
	}
	int smokelag;
	states{
	spawn:
		CYBR NO 4;
	spawn2:
		---- AA 1 A_SpawnItemEx("HDSmoke",
			random(-30,30),random(-30,30),random(12,14),
			random(-1,1),random(-1,1),0,
			0,SXF_NOCHECKPOSITION
		);
		---- A 0 A_PlaySound("misc/firecrkl",1,1.0-(smokelag*0.005));
		---- AAA 0 A_SpawnItemEx("HDFlameRed",
			random(-66,66),random(-56,56),random(12,14),
			random(-1,1),random(-1,1),random(1,3),
			0,SXF_NOCHECKPOSITION
		);
		---- A 0 A_SpawnItemEx("HDSmokeChunk",
			random(-30,30),random(-30,30),random(4,12),
			random(-3,3),random(-3,3),random(2,8),
			0,SXF_NOCHECKPOSITION,160+smokelag
		);
		---- AAA 0 A_SpawnItemEx("HugeWallChunk",
			random(-30,30),random(-30,30),random(4,12),
			random(-6,6),random(-6,6),random(5,12),
			0,SXF_NOCHECKPOSITION,64+smokelag/2
		);
		---- A 0{
			A_SetTics(random(1,smokelag/7));
			smokelag++;
			if(alpha>0.2)alpha-=0.04;
			else A_FadeOut(0.002);
		}
		---- A 0 A_JumpIf(alpha<0.2,1);
		loop; //NOOOOOOOOOOOOOOOOOOOOOOOOOOOOOOO
	spawn3:
		CYBR P 0;
		goto spawn2;
		CYBR E 0;
		stop;
	}
}
class CyberGibs:RedParticleFountain{
	default{
		+rockettrail +grenadetrail
		+corpse -invisible -nogravity
		radius 20;height 16;
	}
	states{
	spawn:
		POSS O 5;
		POSS P -1;
		stop;
	crash:
		POSS QQ 1 A_SpawnItemEx("HDExplosion",
			random(-3,3),random(-3,3),2,vel.x,vel.y,vel.z+1,
			0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
		);
		POSS QRRSSTT 2 A_SpawnItemEx("HDSmoke",
			random(-6,6),random(-6,6),random(1,4),
			random(-1,1),random(-1,1), random(1,3),
			0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
		);
	bluh:
		POSS U 6{
			A_SpawnItemEx("HDSmokeChunk",
				frandom(-3,3),frandom(-3,3),2,
				vel.x+frandom(-6,6),vel.y+frandom(-6,6),vel.z+frandom(5,10),
				0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
			);
			A_FadeOut(0.07);
		}wait;
	}
}