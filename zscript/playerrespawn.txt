// ------------------------------------------------------------
// Respawn, including bot replacements and endgame
// ------------------------------------------------------------

class BotSpawner:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			actor spot;int garbage;
			if(!player.getgender()){
				[garbage,spot]=A_SpawnItemEx("BotBot",flags:SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			}else{
				[garbage,spot]=A_SpawnItemEx("BotBotF",flags:SXF_NOCHECKPOSITION|SXF_TRANSFERTRANSLATION);
			}
			if(spot){
				spot.master=self;
			}
			setz(pos.z+50);
			A_Log(string.format("Bot %s replaced with rifleman.",player.getusername()));
			A_Morph("HDBotSpectator",int.MAX,MRF_FULLHEALTH,"HDBloodTrailFloor","TeleFog");
		}fail;
	}
}
class BotKill:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			bshootable=1;
			A_Die();
		}fail;
	}
}
//hack given by ACS map restart
class RestartMap:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			level.nextmap=level.mapname;
			Exit_Normal(0);
		}fail;
	}
}



//respawn event
extend class HDHandlers{
	override void PlayerRespawned(PlayerEvent e){
		let hde=HDPlayerPawn(players[e.playernumber].mo);
		if(!hde)return;

		HDF.TransferFire(hde,hde.playercorpse);

		hde.consolidateweapons();

		if(wiped[e.playernumber]){
			hde.A_GiveInventory("WipedOut");
			hde.A_GiveInventory("SpecMorph");
		}else{
			setplayerlives(e.playernumber);
			hde.spawn("TeleFog",hde.pos); //HDPP only sets telefog in postbeginplay
		}
	}
	void setplayerlives(int pnum){
		if(fraglimit&&!deathmatch){
			int fl=fraglimit>=100?fraglimit-100:fraglimit;
			players[pnum].mo.A_SetInventory("HDLives",fl-coopdeaths);
		}
		else if(deathmatch&&fraglimit>100){
			int fl=fraglimit-100;
			int dths;
			if(teamplay)dths=teamdeaths[players[pnum].getteam()];
			else dths=ffadeaths[pnum];
			players[pnum].mo.A_SetInventory("HDLives",fl-dths);
		}
	}
}




//Spectator playerpawn
class SpecMorph:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			setz(pos.z+50);
			A_Log(string.format("%s is now a spectator.",player.getusername()));
			A_Morph("HDSpectator",int.MAX,MRF_FULLHEALTH,"HDBloodTrailFloor","TeleportFog");
			
		}fail;
	}
}
class HDBotSpectator:HDSpectator{
	default{
		+nointeraction
	}
}
class HDSpectator:PlayerPawn{
	default{
		-solid -shootable +invisible +notarget +nogravity +noblockmap
		-telestomp +alwaystelefrag -pickup +notrigger
		telefogsourcetype "";
		telefogdesttype "";
		player.viewbob 0;
		player.soundclass "spectator";
		player.userange 0;player.jumpz 0;maxstepheight 1;
		player.morphweapon "NullWeapon";
		player.forwardmove 0.3;player.sidemove 0.3;
		player.startitem "NullWeapon";
		player.viewheight 1;player.attackzoffset 0;
		height 4;radius 4;
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_TakeInventory("IsAlive");
		setz(min(floorz+64,ceilingz-4));
		if(countinv("WipedOut"))ACS_NamedExecuteAlways("WipedSpecMessage");
		else A_Print("Now spectating.\n\n\n\ccpress fire and altfire to\n\n\ccwarp to remaining players\n\n\cc(if any).",6,"SMALLFONT");
	}
	override bool cancollidewith(actor other,bool passive){return false;}
	override void checkcrouch(bool totallyfrozen){}
	int destplayer;
	override void tick(){
		playerpawn.tick();
		if(!player)return;

		int oldinput=getplayerinput(MODINPUT_OLDBUTTONS);
		vel*=0.9;
		if(player.cmd.buttons&BT_CROUCH)vel.z-=1;

		//warp to players
		int chosenplayer=-1;int choosedir;
		if(
			(player.cmd.buttons&BT_ATTACK)&&!(oldinput&BT_ATTACK)
			||(player.cmd.buttons&BT_ALTATTACK)&&!(oldinput&BT_ALTATTACK)
		){
			if(player.cmd.buttons&BT_ATTACK)choosedir=1;
			else choosedir=-1;
	
			for(int i=0;i<MAXPLAYERS&&chosenplayer<0;i++){
				destplayer+=choosedir;
				if(destplayer<0)destplayer=MAXPLAYERS-1;
				else if(destplayer==MAXPLAYERS)destplayer=0;

				actor pmo=players[destplayer].mo;
				if(
					playeringame[destplayer] &&
					pmo &&
					!(pmo is "HDSpectator")
				){
					A_Print(string.format(
						"Now viewing %s.",
						players[destplayer].getusername()
					));
					setorigin(pmo.pos,true);
					angle=pmo.angle;pitch=10;
					A_ChangeVelocity(-6,0,12,CVF_RELATIVE|CVF_REPLACE);
					break;
				}
			}
		}
	}
	states{
	spawn:
	see:
	melee:
	missile:
	pain:
		TNT1 A -1;
	death:
	xdeath:
		TNT1 A 0 A_NoBlocking();
		TNT1 A 10 A_CheckPlayerDone();
		wait;
	}
}