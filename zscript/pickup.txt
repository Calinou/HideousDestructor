//-------------------------------------------------
// Pickup Archetypes
//-------------------------------------------------
const HDCONST_MAXENC=600;
class GrabThinker:Thinker{
	actor picktarget;
	actor pickobj;
	int ticker;
	static void Grab(actor grabber,actor grabee){
		let grabthink=new("GrabThinker");
		grabthink.picktarget=grabber;
		grabthink.pickobj=grabee;
	}
	override void postbeginplay(){
		super.postbeginplay();
	}
	override void tick(){
		if(!picktarget||!picktarget.player||!pickobj){destroy();return;}
		super.tick();
		ticker++;
		if(ticker<4)pickobj.setorigin(
			0.5*(
				(picktarget.pos+(0,0,picktarget.height-10))
				+pickobj.pos
			),true
		);else{
			let pt=hdpickup(pickobj);if(pt)pt.isbeingpickedup=false;
			let wt=hdweapon(pickobj);
			if(
				!pickobj
				||!picktarget
				||picktarget.health<1
			){
				destroy();
				return;
			}
			pickobj.A_CallSpecial(
				pickobj.special,pickobj.args[0],
				pickobj.args[1],pickobj.args[2],
				pickobj.args[3],pickobj.args[4]
			);

			vector2 shiftpk=actor.rotatevector((frandom(-0.4,-0.8),frandom(0.8,1.1)),picktarget.angle);
			pickobj.vel.xy+=shiftpk;
			pickobj.setorigin((pickobj.pos.xy+shiftpk,pickobj.pos.z),true);
			if(
				!wt
				&&hdplayerpawn(picktarget)
				&&hdplayerpawn(picktarget).itemenc>HDCONST_MAXENC
				&&picktarget.countinv(pickobj.getclassname())
				&&(
					!hd_onemanarmy
					||deathmatch
					||skill>=5
				)
				&&(!inventory(pickobj)||!inventory(pickobj).balwayspickup)
				&&(
					!hdpickup(pickobj)
					||hdpickup(pickobj).bulk>0
					||!hdmagammo(pickobj)
					||hdmagammo(pickobj).magbulk>0
					||hdmagammo(pickobj).roundbulk>0
				)
			){
				if(cvar.getcvar("hd_helptext",picktarget.player).getint())picktarget.A_Log("No room in pockets.",true);
				destroy();
				return;
			}

			let ht=hdupk(pickobj);
			let bp=HDBackpack(picktarget.findinventory("HDBackpack"));
			let tt=inventory(pickobj);
			if(ht){
				ht.picktarget=picktarget;
				ht.a_hdupkgive();
			}else if(
				bp&&bp==picktarget.player.readyweapon
				&&bp.ItemToBackpack(tt)!=1
			){
				destroy();
				return;
			}
			if(pt){
				pt.actualpickup(picktarget);
			}else if(wt){
				wt.actualpickup(picktarget);
			}else if(tt){
				if(picktarget.vel==(0,0,0))picktarget.A_ChangeVelocity(0.001,0,0,CVF_RELATIVE);
			}
			destroy();
			return;
		}
	}
}
class HDPickerUpper:Actor{
	default{
		+solid +missile +nogravity
		height 2;
		radius 2;
	}
	override bool cancollidewith(actor other,bool passive){
		return (inventory(other)||hdupk(other));
	}
}



//Usable pickup.
class HDPickup:CustomInventory{
	actor picktarget;
	bool droptranslation;
	property droptranslation:droptranslation;
	double bulk;
	property bulk:bulk;
	int maxunitamount;
	property maxunitamount:maxunitamount;
	bool multipickup; //lets you continue picking up without re-pressing the key
	property multipickup:multipickup;
	string refid;property refid:refid;
	string nicename;property nicename:nicename;
	bool fitsinbackpack;property fitsinbackpack:fitsinbackpack;
	bool isbeingpickedup;
	bool cheatnogive;property cheatnogive:cheatnogive;
	default{
		+solid
		+inventory.invbar +inventory.persistentpower
		+noblockmonst +notrigger +dontgib
		inventory.interhubamount int.MAX;
		inventory.maxamount int.MAX;

		hdpickup.droptranslation true;
		hdpickup.bulk 0;
		hdpickup.multipickup false;
		hdpickup.refid "";
		hdpickup.nicename "";
		hdpickup.fitsinbackpack true;
		hdpickup.cheatnogive false;

		radius 8; height 10; scale 0.8;
		inventory.pickupsound "weapons/pocket";
		hdpickup.maxunitamount 1;
	}
	override bool cancollidewith(actor other,bool passive){
		return HDPickerUpper(other);
	}

	//called on level resets, etc.
	virtual void Consolidate(){}

	//called to get the encumbrance
	virtual double getbulk(){return amount*bulk;}
	override inventory createtossable(int amount){
		inventory iii=super.createtossable(amount);
		if(droptranslation&&owner){
			actor onr=owner;
			if(iii)iii.translation=onr.translation;
		}
		return iii;
	}
	virtual int effectivemaxamount(){
		return bulk?max(1,HDCONST_MAXENC/bulk):maxamount;
	}

	override void doeffect(){
		if(!amount)destroy();
	}
	//for the status bar
	virtual ui int getsbarnum(int flags=0){return -1000000;}
	override void touch(actor toucher){}
	virtual void actualpickup(actor other){
		if(!other)other=picktarget;
		if(!other)return;
		if(heat.getamount(self)>50)return;
		name gcn=getclassname();
		int maxtake=HDMath.MaxInv(other,gcn)-other.countinv(gcn);
		if(balwayspickup){
			inventory.touch(other);
			return;
		}else if(maxtake<1){
			return;
		}else if(maxtake<amount){
			amount-=maxtake;
			HDF.Give(other,gcn,maxtake);
		}else trypickup(other);
		other.A_PlaySound(pickupsound,CHAN_AUTO);
		other.A_Log(string.format("\cg%s",pickupmessage()),true);
	}
	void GotoSpawn(){
		if(findstate("spawn2")){
			if(hd_debug)A_Log(string.format("%s still uses spawn2",getclassname()));
			setstatelabel("spawn2");
		}
	}
	virtual void SplitPickup(){
		int maxpkamt=max(1,maxunitamount);
		while(amount>maxpkamt){
			let aaa=hdpickup(spawn(getclassname(),pos,ALLOW_REPLACE));
			aaa.amount=maxpkamt;amount-=maxpkamt;
			aaa.vel=vel+(frandom(-1,1),frandom(-1,1),frandom(-1,1));
		}
		vel+=(frandom(-0.6,0.6),frandom(-0.6,0.6),frandom(-0.6,0.6));
		GotoSpawn();
	}
	override void postbeginplay(){
		maxamount=min(maxamount,effectivemaxamount());
		super.postbeginplay();
		isbeingpickedup=false;
		let hdps=new("HDPickupSplitter");
		hdps.invoker=self;
	}
	states{
	use:
		TNT1 A 0;
		fail;
	spawn:
		CLIP A -1;
		stop;
	}
}
class HDPickupSplitter:Thinker{
	hdpickup invoker;
	int ticks;
	override void Tick(){
		super.tick();
		if(!!invoker&&!invoker.owner){
			invoker.SplitPickup();
		}
		destroy();
	}
}

//custom ammotype
class HDAmmo:HDPickup{
	default{
		-inventory.invbar
		hdpickup.droptranslation false;
	}
}


class HDItemGiver:HDPickup{
	default{
		+nointeraction
		-inventory.invbar
		inventory.maxamount 1;
	}
	override void actualpickup(actor other){}
	virtual void addtobackpack(hdbackpack bp){
		if(!master)spawnmaster();
		bp.itemtobackpack(inventory(master));
	}
	virtual void spawnmaster(){}
	states{
	spawn:
		TNT1 A 1;
		stop;
	}
}



/*
 Fake pickup for creating different actors that give the same item
 hdupk.pickupsound: pickup sound
 hdupk.pickuptype: default type of inventory item it replaces
 hdupk.pickupmessage: self-explanatory
 hdupk.maxunitamount: max # of pickuptype a single unit can store
 hdupk.amount: amount in this item, if it is a container
*/
class HDUPK:HDActor{
	//do NOT use this for pickupable casings, 1x1 is intolerably pixelbitchy!
	actor picktarget;
	class<inventory> pickuptype;
	string pickupmessage;
	sound pickupsound;
	int maxunitamount;
	int amount;
	bool multipickup;
	property pickuptype:pickuptype;
	property pickupmessage:pickupmessage;
	property pickupsound:pickupsound;
	property maxunitamount:maxunitamount;
	property amount:amount;
	property multipickup:multipickup;
	default{
		+solid
		height 8;radius 8;
		hdupk.pickupsound "weapons/pocket";//"misc/i_pkup";
		hdupk.pickupmessage "Picked something up.";
		hdupk.pickuptype "none";
		hdupk.multipickup false;
	}
	override bool cancollidewith(actor other,bool passive){
		return HDPickerUpper(other);
	}
	override void postbeginplay(){
		super.postbeginplay();

		if(!maxunitamount)return;
		if(maxunitamount<0)maxunitamount*=-1;
		while(amount>maxunitamount){  
			let a=hdupk(spawn(getclassname(),pos,ALLOW_REPLACE));
			a.amount=maxunitamount;
			amount-=maxunitamount;
			a.vel=vel+(frandom(-1,1),frandom(-1,1),frandom(-1,1));
		}
		if(amount>1){  
		}else{
			amount=1;
		}
	}
	virtual void OnGrab(actor grabber){}
	virtual void A_HDUPKGive(){
		if(pickuptype=="none"){
			target=picktarget;
			setstatelabel("give");
			if(!bdestroyed)return;
			picktarget.A_PlaySound(pickupsound,5);
			picktarget.A_Log(string.format("\cg%s",pickupmessage),true);
			return;
		}
		int maxtake=getdefaultbytype(pickuptype).maxamount;
		let hdpk=(class<hdpickup>)(pickuptype);
		let hdp=hdplayerpawn(picktarget);
		if(hdp&&hdpk){
			double hdpkubu=getdefaultbytype(hdpk).bulk;
			let hdpm=(class<hdmagammo>)(pickuptype);
			if(hdpm){
				let hdpmd=getdefaultbytype(hdpm);
				hdpkubu=max(hdpkubu,hdpmd.magbulk+hdpmd.roundbulk*hdpmd.maxperunit);
			}
			maxtake=min(maxtake,(HDCONST_MAXENC-hdp.itemenc)/hdpkubu);
		}
		int increase=amount;
		if(
			!hd_onemanarmy
			||deathmatch
			||skill>=5
		)increase=min(maxtake,amount);
		if(heat.getamount(self)>50)increase=0;
		if(increase<1){ //didn't pick any up
			setstatelabel("spawn");
			return;
		}
		picktarget.A_PlaySound(pickupsound,5);
		picktarget.A_Log(string.format("\cg%s",pickupmessage),true);
		HDF.Give(picktarget,pickuptype,increase);
		amount-=increase;
		if(amount>0){ //only picked some up  
			setstatelabel("spawn");
			return;
		}else if(pickuptype!="none")destroy();
	}
	states{
	give:
		---- A 0;
		stop;
	spawn:
		CLIP A -1;
	spawn2:
		---- A -1;
	}
}


