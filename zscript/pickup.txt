//-------------------------------------------------
// Pickup Archetypes
//-------------------------------------------------
class GrabThinker:Thinker{
	actor picktarget;
	actor pickobj;
	int ticker;
	static void Grab(actor grabber,actor grabee){
		let grabthink=new("GrabThinker");
		grabthink.picktarget=grabber;
		grabthink.pickobj=grabee;
	}
	override void postbeginplay(){
		super.postbeginplay();
	}
	override void tick(){
		if(!picktarget||!picktarget.player||!pickobj){destroy();return;}
		super.tick();
		ticker++;
		if(ticker<4){
			pickobj.setorigin(
				0.5*(
					(picktarget.pos+(0,0,picktarget.height-10))
					+pickobj.pos
				),true
			);
			pickobj.bsolid=false;
		}else{
			let pt=hdpickup(pickobj);if(pt)pt.bisbeingpickedup=false;
			let wt=hdweapon(pickobj);
			let ht=hdupk(pickobj);
			let tt=inventory(pickobj);
			if(
				!pickobj
				||!picktarget
				||picktarget.health<1
			){
				destroy();
				return;
			}
			pickobj.A_CallSpecial(
				pickobj.special,pickobj.args[0],
				pickobj.args[1],pickobj.args[2],
				pickobj.args[3],pickobj.args[4]
			);

			vector2 shiftpk=actor.rotatevector((frandom(-0.4,-0.8),frandom(0.8,1.1)),picktarget.angle);
			pickobj.vel.xy+=shiftpk;
			pickobj.setorigin((pickobj.pos.xy+shiftpk,pickobj.pos.z),true);
			if(
				!wt
				&&hdplayerpawn(picktarget)
				&&hdplayerpawn(picktarget).itemenc>hdplayerpawn(picktarget).maxpocketspace
				&&!(picktarget.player.readyweapon is "HDBackpack")
				&&(
					deathmatch
					||hd_onemanarmy<2
					||skill>=5
				)
				&&(
					!tt
					||(
						!tt.balwayspickup
						&&picktarget.countinv(pickobj.getclassname())
					)
				)
				&&(
					!pt
					||pt.bulk>0
					||(
						hdmagammo(pickobj)&&(
							hdmagammo(pickobj).magbulk>0
							||hdmagammo(pickobj).roundbulk>0
						)
					)
				)
			){
				if(cvar.getcvar("hd_helptext",picktarget.player).getint())picktarget.A_Log("No room in pockets.",true);
				pickobj.bsolid=true;
				destroy();
				return;
			}

			let bp=HDBackpack(picktarget.findinventory("HDBackpack"));
			if(ht){
				ht.picktarget=picktarget;
				ht.a_hdupkgive();
			}else if(
				bp&&bp==picktarget.player.readyweapon
				&&bp.ItemToBackpack(tt)!=1
			){
				destroy();
				return;
			}
			if(pt){
				pt.actualpickup(picktarget);
			}else if(wt){
				wt.actualpickup(picktarget);
			}else if(tt){
				if(picktarget.vel==(0,0,0))picktarget.A_ChangeVelocity(0.001,0,0,CVF_RELATIVE);
			}
			if(pickobj&&(!inventory(pickobj)||!inventory(pickobj).owner))pickobj.bsolid=true;
			destroy();
			return;
		}
	}
}
class HDPickerUpper:Actor{
	default{
		+solid
		+nogravity
		height 2;
		radius 2;
	}
	override bool cancollidewith(actor other,bool passive){
		return (inventory(other)||hdupk(other));
	}
}



//Usable pickup.
class HDPickup:CustomInventory{
	int HDPickupFlags;
	flagdef DropTranslation:HDPickupFlags,0;
	flagdef FitsInBackpack:HDPickupFlags,1;
	flagdef MultiPickup:HDPickupFlags,2; //lets you continue picking up without re-pressing the key
	flagdef IsBeingPickedUp:HDPickupFlags,3;
	flagdef CheatNoGive:HDPickupFlags,4;
	flagdef MustShowInMagManager:HDPickupFlags,5;

	actor picktarget;
	double bulk;
	property bulk:bulk;
	int maxunitamount;
	property maxunitamount:maxunitamount;
	string refid;property refid:refid;
	string nicename;property nicename:nicename;
	default{
		+solid
		+inventory.invbar +inventory.persistentpower
		+noblockmonst +notrigger +dontgib

		+hdpickup.droptranslation
		-hdpickup.multipickup
		+hdpickup.fitsinbackpack
		-hdpickup.cheatnogive

		inventory.interhubamount int.MAX;
		inventory.maxamount int.MAX;

		hdpickup.bulk 0;
		hdpickup.refid "";
		hdpickup.nicename "";

		radius 8; height 10; scale 0.8;
		inventory.pickupsound "weapons/pocket";
		hdpickup.maxunitamount 1;
	}
	override bool cancollidewith(actor other,bool passive){
		return HDPickerUpper(other);
	}

	//called on level resets, etc.
	virtual void Consolidate(){}

	//called to get the encumbrance
	virtual double getbulk(){return amount*bulk;}
	override inventory createtossable(int amount){
		inventory iii=super.createtossable(amount);
		if(bdroptranslation&&owner){
			actor onr=owner;
			if(iii)iii.translation=onr.translation;
		}
		return iii;
	}
	virtual int effectivemaxamount(){
		if(!bulk)return maxamount;
		double gdpsp;
		if(hdplayerpawn(owner))gdpsp=hdplayerpawn(owner).maxpocketspace;
		else gdpsp=getdefaultbytype("hdplayerpawn").maxpocketspace;
		return max(1,gdpsp/bulk);
	}

	override void doeffect(){
		if(!amount)destroy();
	}
	//for the status bar
	virtual ui int getsbarnum(int flags=0){return -1000000;}
	override void touch(actor toucher){}
	virtual void actualpickup(actor other){
		if(!other)other=picktarget;
		if(!other)return;
		if(heat.getamount(self)>50)return;
		name gcn=getclassname();
		int maxtake=HDMath.MaxInv(other,gcn)-other.countinv(gcn);
		if(balwayspickup){
			inventory.touch(other);
			return;
		}else if(maxtake<1){
			return;
		}
		bool gotpickedup=false;
		if(maxtake<amount){
			amount-=maxtake;
			HDF.Give(other,gcn,maxtake);
			gotpickedup=true;
		}else gotpickedup=trypickup(other);
		if(gotpickedup){
			other.A_PlaySound(pickupsound,CHAN_AUTO);
			other.A_Log(string.format("\cg%s",pickupmessage()),true);
		}
	}

	//delete once no longer needed
	void GotoSpawn(){
		if(findstate("spawn2")){
			if(hd_debug)A_Log(string.format("%s still uses spawn2",getclassname()));
			setstatelabel("spawn2");
		}
	}

	//so you don't get a bullet pickup that's 2 bullets somehow
	virtual void SplitPickup(){
		int maxpkamt=max(1,maxunitamount);
		while(amount>maxpkamt){
			let aaa=hdpickup(spawn(getclassname(),pos,ALLOW_REPLACE));
			aaa.amount=maxpkamt;amount-=maxpkamt;
			aaa.vel=vel+(frandom(-1,1),frandom(-1,1),frandom(-1,1));
			if(bdroptranslation)aaa.translation=translation;
		}
		vel+=(frandom(-0.6,0.6),frandom(-0.6,0.6),frandom(-0.6,0.6));
		GotoSpawn();
	}
	override void postbeginplay(){
		maxamount=min(maxamount,effectivemaxamount());
		itemsthatusethis.clear();
		GetItemsThatUseThis();
		super.postbeginplay();
		bisbeingpickedup=false;
		let hdps=new("HDPickupSplitter");
		hdps.invoker=self;
	}

	//This is an array of item names created on an actor's initialization.
	//If you have a sub-mod item that also uses a given ammo type,
	//you can use an event handler to add that item to this array for that ammo type.
	//The IsUsed function can, of course, take in any other circumstances you can write in.
	array<string> itemsthatusethis;
	virtual void GetItemsThatUseThis(){}
	virtual bool IsUsed(){return true;}

	states{
	use:
		TNT1 A 0;
		fail;
	spawn:
		CLIP A -1;
		stop;
	}
}
class HDPickupSplitter:Thinker{
	hdpickup invoker;
	int ticks;
	override void Tick(){
		super.tick();
		if(!!invoker&&!invoker.owner){
			invoker.SplitPickup();
		}
		destroy();
	}
}

//custom ammotype
class HDAmmo:HDPickup{
	default{
		-inventory.invbar
		-hdpickup.droptranslation
	}
	override bool IsUsed(){
		if(!owner)return true;
		for(int i=0;i<itemsthatusethis.size();i++){
			if(owner.countinv(itemsthatusethis[i]))return true;
		}
		return false;
	}
}



/*
 Fake pickup for creating different actors that give the same item
 hdupk.pickupsound: pickup sound
 hdupk.pickuptype: default type of inventory item it replaces
 hdupk.pickupmessage: self-explanatory
 hdupk.maxunitamount: max # of pickuptype a single unit can store
 hdupk.amount: amount in this item, if it is a container
*/
class HDUPK:HDActor{
	int HDUPKFlags;
	flagdef MultiPickup:HDUPKFlags,0;

	actor picktarget;
	class<inventory> pickuptype;
	string pickupmessage;
	sound pickupsound;
	int maxunitamount;
	int amount;
	property pickuptype:pickuptype;
	property pickupmessage:pickupmessage;
	property pickupsound:pickupsound;
	property maxunitamount:maxunitamount;
	property amount:amount;
	default{
		+solid
		-hdupk.multipickup
		height 8;radius 8;
		hdupk.pickupsound "weapons/pocket";//"misc/i_pkup";
		hdupk.pickupmessage "Picked something up.";
		hdupk.pickuptype "none";
	}
	override bool cancollidewith(actor other,bool passive){
		return HDPickerUpper(other);
	}
	override void postbeginplay(){
		super.postbeginplay();

		if(!maxunitamount)return;
		if(maxunitamount<0)maxunitamount*=-1;
		while(amount>maxunitamount){  
			let a=hdupk(spawn(getclassname(),pos,ALLOW_REPLACE));
			a.amount=maxunitamount;
			amount-=maxunitamount;
			a.vel=vel+(frandom(-1,1),frandom(-1,1),frandom(-1,1));
		}
		if(amount>1){  
		}else{
			amount=1;
		}
	}
	virtual void OnGrab(actor grabber){}
	virtual void A_HDUPKGive(){
		//it's not an item container
		if(pickuptype=="none"){
			target=picktarget;
			setstatelabel("give");
			if(!bdestroyed)return;
			picktarget.A_PlaySound(pickupsound,5);
			picktarget.A_Log(string.format("\cg%s",pickupmessage),true);
			return;
		}

		//if placing directly into backpack
		if(
			picktarget.player
			&&picktarget.player.readyweapon is "HDBackpack"
		){
			let bp=hdbackpack(picktarget.player.readyweapon);
			int bpindex=bp.invclasses.find(pickuptype.getclassname());
			if(bpindex<bp.invclasses.size()){
				let hdpk=(class<hdpickup>)(pickuptype);
				double hdpkubu=getdefaultbytype(hdpk).bulk;
				let hdpm=(class<hdmagammo>)(pickuptype);
				if(hdpm){
					let hdpmd=getdefaultbytype(hdpm);
					hdpkubu=max(hdpkubu,hdpmd.magbulk+hdpmd.roundbulk*hdpmd.maxperunit);
				}
				int maxtake=(HDCONST_BPMAX-bp.bulk)/hdpkubu;
				int increase=min(maxtake,amount);
				amount-=increase;
				bp.amounts[bpindex]=""..(bp.amounts[bpindex].toint()+increase);
				bp.updatemessage(bpindex);
				if(amount<1)destroy();
				else setstatelabel("spawn");
				return;
			}
		}

		//check effective maxamount and take as appropriate
		int maxtake=getdefaultbytype(pickuptype).maxamount;
		let hdpk=(class<hdpickup>)(pickuptype);
		let hdp=hdplayerpawn(picktarget);
		if(hdp&&hdpk){
			double hdpkubu=getdefaultbytype(hdpk).bulk;
			let hdpm=(class<hdmagammo>)(pickuptype);
			if(hdpm){
				let hdpmd=getdefaultbytype(hdpm);
				hdpkubu=max(hdpkubu,hdpmd.magbulk+hdpmd.roundbulk*hdpmd.maxperunit);
			}
			maxtake=min(maxtake,(hdp.maxpocketspace-hdp.itemenc)/hdpkubu);
		}
		int increase=amount;
		if(
			!hd_onemanarmy
			||deathmatch
			||skill>=5
		)increase=min(maxtake,amount);
		if(heat.getamount(self)>50)increase=0;
		if(increase<1){ //didn't pick any up
			setstatelabel("spawn");
			return;
		}
		picktarget.A_PlaySound(pickupsound,5);
		picktarget.A_Log(string.format("\cg%s",pickupmessage),true);
		HDF.Give(picktarget,pickuptype,increase);
		amount-=increase;
		if(amount>0){ //only picked some up  
			setstatelabel("spawn");
			return;
		}else if(pickuptype!="none")destroy();
	}
	states{
	give:
		---- A 0;
		stop;
	spawn:
		CLIP A -1;
	spawn2:
		---- A -1;
	}
}





