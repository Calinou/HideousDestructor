//-------------------------------------------------
// Pickup Archetypes
//-------------------------------------------------

//Projectile for using things that aren't switches and doors
class userpickerupper:hdactor{
	default{
		+solid +noblockmonst +notrigger +noteleport +cannotpush
		height 2;radius 2;
	}
	override bool cancollidewith(actor other,bool passive){
		return(
			other is "HDUPK"
			||other is "Inventory"
		);
	}
	override void postbeginplay(){
		if(vel.xy==(0,0))vel.x=minvel;
		for(int i=0;i<10;i++){
			if(!trymove(pos.xy+vel.xy,true,true)){
				if(blockingmobj){
					if(
						blockingmobj is "HDUPK"
					){
						blockingmobj.target=self.target;
						if(blockingmobj.findstate("grab",true))
							blockingmobj.setstatelabel("grab");
						let ppp=HDPlayerPawn(target);if(ppp)ppp.hasgrabbed=true;
						destroy();return;
					}
					else if(
						blockingmobj is "HDPickup"
						||blockingmobj is "HDWeapon"
					){
						tracer=blockingmobj;
						let ppp=HDPlayerPawn(target);if(ppp)ppp.hasgrabbed=true;
						setstatelabel("grabpickup");return;
					}
				}
				if(blockingline){destroy();return;}
			}
			addz(vel.z);
			if(floorz>pos.z && floorz<target.height+target.pos.z)setz(floorz);
		}
		destroy();
	}
	states{
	grabpickup:
		TNT1 A 0{bsolid=false;}
		TNT1 AA 2{
			tracer.vel+=(
				target.pos.x-tracer.pos.x,
				target.pos.y-tracer.pos.y,
				target.pos.z+target.height*0.6-tracer.pos.z+2
			)*0.3;
		}
		TNT1 A 0{
			tracer.A_Stop();
			//in case of death or teleport mid-grab
			if(
				target
				&&!target.bkilled
				&&target.health>0        
				&&tracer.distance2d(target)<32
			){
				let ht=hdupk(tracer);
				if(ht){
					ht.target=target;
					ht.a_hdupkgive();
					return;
				}

				let pt=hdpickup(tracer);
				if(pt){pt.actualpickup(target);return;}

				let wt=hdweapon(tracer);
				if(wt){wt.actualpickup(target);return;}

				let tt=inventory(tracer);
				if(tt){tt.touch(target);return;}
			}
		}stop;
	}
}

/*
 special uses:
 activesound: pickup sound
 hdupk.pickuptype: default type of inventory item it replaces
 hdupk.pickupmessage: self-explanatory
 accuracy: max # of pickuptype a single unit can store
*/
class HDUPK:HDActor{
	//do NOT use this for pickupable casings, 1x1 is intolerably pixelbitchy!
	class<inventory> pickuptype;
	property pickuptype:pickuptype;
	string pickupmessage;
	property pickupmessage:pickupmessage;
	default{
		+solid +noblockmonst +notrigger
		height 8;radius 8;
		activesound "misc/i_pkup";
		hdupk.pickupmessage "Picked something up.";
	}
	override bool cancollidewith(actor other,bool passive){
		return(bpushable || other is "userpickerupper");
	}
	override void postbeginplay(){
		super.postbeginplay();

		if(!accuracy) return;
		if(accuracy<0) accuracy*=-1;
		while(stamina>accuracy){
			actor a=spawn(getclassname(),pos,ALLOW_REPLACE);
			a.stamina=accuracy;
			stamina-=accuracy;
			a.vel=vel+(frandom(-1,1),frandom(-1,1),frandom(-1,1));
		}
		if(stamina>1){
		}else{
			stamina=1;
		}
	}
	virtual void A_HDUPKGive(){
		if(!pickuptype){
			setstatelabel("give");
			return;
		}
		int increase=target.countinv(pickuptype);
		A_GiveToTarget(pickuptype,stamina);
		increase=target.countinv(pickuptype)-increase;
		if(!increase){ //didn't pick any up
			setstatelabel("spawn");
			return;
		}
		A_PlaySound(activesound);
		target.A_Log(string.format("\cg%s",pickupmessage),true);
		stamina-=increase;
		if(stamina){ //only picked some up
			setstatelabel("spawn");
			return;
		}
	}
	states{
	grab:
		---- AA 2{
			vel+=(
				target.pos.x-pos.x,
				target.pos.y-pos.y,
				target.pos.z+target.height*0.6-pos.z
			)*0.3+(0,0,2);
		}
		---- A 0{
			//in case of death or teleport mid-grab
			if(target.bkilled||distance3d(target)>64){
				A_Stop();
				setstatelabel("spawn");
			}else{
				setorigin(target.pos+(0,0,target.height-24),true);
				A_HDUPKGive();
			}
		}
	give:
		---- A 0;
		stop;
	spawn:
		CLIP A -1;
	spawn2:
		---- A -1;
	givegunloopend:
		---- A 0{
			if(hd_debug)A_Log("Deprecated givegunloopend found in %s",getclassname());
			setorigin(target.pos+(0,0,target.height-24),true);
		}
		stop;
	}
}


//some people put keys in the weirdest of sectors.
//One particular Oblige trick makes them impossible to pick up as HDUPKs.
class HDHPK:HDUPK{
	override bool cancollidewith(actor other,bool passive){
		if(other is "userpickerupper") return true;
		if(other is "playerpawn" && other.bpickup){
			target=other;
			let ppp=HDPlayerPawn(target);if(ppp && !ppp.hasgrabbed){
				ppp.hasgrabbed=true;
				if(findstate("give",true))setstatelabel("give");
			}
			return false;
		}return false;
	}
}



//the real hdpickup.
class HDPickup:CustomInventory{
	default{
		+inventory.invbar +inventory.persistentpower
		+solid +noblockmonst +notrigger
		inventory.interhubamount int.MAX;

		radius 8; height 10; scale 0.8;
		inventory.pickupsound "weapons/pocket";
		accuracy 1;
	}
	override void doeffect(){
		if(!amount)goawayanddie();
	}
	override void touch(actor toucher){}
	virtual void actualpickup(actor other){
		name gcn=getclassname();
		int maxtake=HDF.MaxInv(other,gcn)-other.countinv(gcn);
		if(maxtake<1){
			setstatelabel("spawn2");return;
		}else if(maxtake<amount){
			amount-=maxtake;
			other.A_GiveInventory(gcn,maxtake);
			if(amount>0)setstatelabel("spawn2");
		}else trypickup(other);
		other.A_PlaySound(pickupsound,CHAN_AUTO);
		other.A_Log(string.format("\cg%s",pickupmessage()),true);
	}
	override bool cancollidewith(actor other,bool passive){
		return(other is "userpickerupper");
	}
	states{
	spawn:
		TNT1 A 1;
		TNT1 A 0{
			while(invoker.amount>invoker.accuracy){
				inventory aa=inventory(spawn(invoker.getclassname(),invoker.pos));
				aa.amount=invoker.accuracy;invoker.amount-=invoker.accuracy;
				aa.vel=invoker.vel+(frandom(-1,1),frandom(-1,1),frandom(-1,1));
			}
			invoker.setstatelabel("spawn2");
		}stop;
	}
}

//custom ammotype
class HDAmmo:HDPickup{
	int basemaxamount;
	property basemaxamount:basemaxamount;
	default{
		-inventory.invbar
	}
	states{use:TNT1 A 0;fail;}
}

