class HDStatusBar:DoomStatusBar{
	override void Init(){
		BaseStatusBar.Init();
		SetSize(0,320,200);

		// Create the font used for the fullscreen HUD
		Font fnt = "HUDFONT_DOOM";
		mHUDFont = HUDFont.Create(fnt, fnt.GetCharWidth("0"), true, 1, 1);
		fnt = "INDEXFONT_DOOM";
		mIndexFont = HUDFont.Create(fnt, fnt.GetCharWidth("0"), true);
		mAmountFont = HUDFont.Create("INDEXFONT");
		diparms = InventoryBarState.Create();
	}
	override void Draw(int state,double TicFrac){
		if(cplayer.health<1)return;
		cplayer.inventorytics=0;
		if(automapactive){
			DrawAutomapHUD(ticfrac);
		}else if(state==HUD_StatusBar){
			BeginHUD();
			DrawCommonStuff(state);
		}
		else if(state==HUD_Fullscreen){
			BeginHUD();
			DrawCommonStuff(state);
			DrawFullScreenStuff();
		}
	}
	void DrawFullScreenStuff(){
		DrawTexture(GetMugShot(5),(0,-14),DI_ITEM_CENTER_BOTTOM|DI_SCREEN_CENTER_BOTTOM);
	}
	void DrawCommonStuff(int state){
		int mxht=-4-mIndexFont.mFont.GetHeight();
		if(CPlayer.mo.InvSel){
			DrawInventoryIcon(cplayer.mo.InvSel,(25,-4),DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE);
			DrawString(mAmountFont,FormatNumber(CPlayer.mo.InvSel.Amount), (42,mxht),DI_TEXT_ALIGN_RIGHT|DI_SCREEN_CENTER_BOTTOM,Font.CR_OLIVE);
		}

		let cp=HDPlayerPawn(CPlayer.mo);if(!cp)return;

		//health
		drawstring(
			mAmountFont,FormatNumber(CPlayer.mo.health),
			(0,mxht),DI_TEXT_ALIGN_CENTER|DI_SCREEN_CENTER_BOTTOM,
			cp.health>70?Font.CR_OLIVE:(cp.health>33?Font.CR_GOLD:Font.CR_RED)
		);

		//heartbeat/playercolour tracker
		if(cp.beatmax){
			float cpb=cp.beatcount*1./cp.beatmax;
			drawimage(
				"GREENPXL",(-8,-6-cpb*2),DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE,
				cpb,scale:(3,3+cp.bloodpressure*0.03)
			);
		}

		int armour;int maxarmour;string armoursprite;
		class<Inventory> armourtype;
		if(cp.countinv("GreenArmour")){
			armour=0;armoursprite="ARMSA0";
			armourtype="GreenArmour";
			maxarmour=HDCONST_GREENARMOUR;
		}else if(cp.countinv("BlackArmour")){
			armour=1;armoursprite="ARMEA0";
			armourtype="BlackArmour";
			maxarmour=HDCONST_BLACKARMOUR;
		}else if(cp.countinv("BlueArmour")){
			armour=2;armoursprite="ARMCA0";
			armourtype="BlueArmour";
			maxarmour=HDCONST_BLUEARMOUR;
		}
		if(armourtype){
			vector2 armourcoords=(0,-mIndexFont.mFont.GetHeight()*2);
			if(state==HUD_Fullscreen)armourcoords=(-55,-4);
			drawbar(
				armoursprite,string.format("ARMER%i",armour),
				cp.countinv(armourtype),maxarmour,
				armourcoords,-1,
				SHADER_VERT,DI_ITEM_CENTER_BOTTOM|DI_SCREEN_CENTER_BOTTOM
			);
		}

		//weapon readouts!
		let w=cplayer.readyweapon;
		if(w){
			if(cplayer.readyweapon.ammotype1){
				int loaded=cp.countinv(cplayer.readyweapon.ammotype1);
				int maxloaded=cp.findinventory(cplayer.readyweapon.ammotype1).maxamount;
				DrawImage(
					"GREENPXL",
					(-16,-6),
					DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT,
					0.6,scale:(loaded*20/maxloaded,2)
				);
			}
			if(cplayer.readyweapon.ammotype2){
				int loaded2=cp.countinv(cplayer.readyweapon.ammotype2);
				int maxloaded2=cp.findinventory(cplayer.readyweapon.ammotype2).maxamount;
				DrawImage(
					"GREENPXL",
					(-16,-10),
					DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT,
					0.6,scale:(loaded2*20/maxloaded2,2)
				);
			}
		}


		//full hud consequences
		if(!cp.countinv("IsMoving") && cp.countinv("GimmeHud")>9){

			//weapons
			drawwep("Lumberjack","CSAWA0",-163);
			drawwep("HDSMG","SMGNA0",-141);
			drawwep("HDPistol","PISTA0",-143);
			drawwep("Hunter","HUNTA0",-128);
			drawwep("Slayer","SLAYA0",-120);
			drawwep("Vulcanette","VULCA0",-108);
			drawwep("ZM66Rifle","RIFLA0",-103);
			drawwep("ZM66SemiRifle","RIFSA0",-103);
			drawwep("ZM66GrenadeRifle","RIGLA0",-103);
			drawwep("RocquetteLauncher","LAUNA0",-83);
			drawwep("Blooper","BLOPA0",-78);
			drawwep("ThunderBuster","PLASA0",-63);
			drawwep("LiberatorRifle","BRFLA0",-53);
			drawwep("LiberatorGrenadeRifle","BRFLB0",-53);
			drawwep("BFG9k","BFUGA0",-28);
			drawwep("Brontornis","BLSTA0",-23);
			drawwep("BossRifle","BORFA0",-16);
			if(
				cplayer.mo.countinv("HDFragGrenadeAmmo")
				&&!(cplayer.readyweapon is "HDFragGrenades")
			)drawimage(
				"FRAGA0",(8,-11),
				DI_SCREEN_LEFT_BOTTOM|DI_ITEM_LEFT,
				scale:(0.6,0.6)
			);

			//encumbrance
			if(hd_carrylimit)drawstring(
				mAmountFont,string.format("%.2f",cp.overloaded),
				(8,mxht),DI_TEXT_ALIGN_LEFT|DI_SCREEN_LEFT_BOTTOM,
				cp.overloaded<1.5?Font.CR_OLIVE:(cp.overloaded>2?Font.CR_RED:Font.CR_GOLD)
			);

			int mxhtplus=mxht-20;
			drawambar("MAGRED","MAGSTAT","SevenMilAmmo",(-84,mxhtplus));
			drawambar("CLIPRED","CLIPC0","FourMilAmmo",(-64,mxhtplus));
			drawimage("ROQPA0",(-44,mxhtplus),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);
			drawambar("PISARED","HDPISAMO","HDPistolAmmo",(-24,mxhtplus));
			drawimage("BROCA0",(-4,mxhtplus),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);
		
			drawimage("SHELA0",(-64,mxht),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);
			drawambar("CELLRED","CELLA0","HDCellAmmo",(-44,mxht));
			drawimage("FRAGA0",(-24,mxht),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM,scale:(0.7,0.7));
			drawimage("ROCKB0",(-4,mxht),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);
		
			drawimage("",(-4,mxht),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT);
		}
	}
	void drawwep(class<inventory> type,string img,int zoffs){
		if(cplayer.mo.countinv(type) && !(cplayer.readyweapon is type)){
			drawimage(img,(8,zoffs),DI_SCREEN_LEFT_BOTTOM|DI_ITEM_LEFT);
		}
	}
	void drawambar(
		string ongfx,string offgfx,
		class<inventory> type,
		vector2 coords
	){
		inventory inv=cplayer.mo.findinventory(type);
		if(!inv||!inv.amount){
			drawimage(offgfx,coords,DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);
		}else drawbar(
				ongfx,offgfx,
				inv.amount,inv.maxamount,
				coords,-1,
				SHADER_VERT,DI_ITEM_RIGHT_BOTTOM|DI_SCREEN_RIGHT_BOTTOM
		);
	}
}





