class HDStatusBar:DoomStatusBar{
	override void Init(){
		BaseStatusBar.Init();
		SetSize(0,320,200);

		// Create the font used for the fullscreen HUD
		Font fnt = "HUDFONT_DOOM";
		mHUDFont = HUDFont.Create(fnt, fnt.GetCharWidth("0"), true, 1, 1);
		fnt = "INDEXFONT_DOOM";
		mIndexFont = HUDFont.Create(fnt, fnt.GetCharWidth("0"), true);
		mAmountFont = HUDFont.Create("INDEXFONT");
		diparms = InventoryBarState.Create();
	}
	override void Draw(int state,double TicFrac){
		if(cplayer.health<1)return;
		cplayer.inventorytics=0;
		if(automapactive){
			DrawAutomapHUD(ticfrac);
		}else if(state==HUD_StatusBar){
			BeginHUD();
			DrawCommonStuff(state);
		}
		else if(state==HUD_Fullscreen){
			BeginHUD();
			DrawCommonStuff(state);
			DrawFullScreenStuff();
		}
	}
	void DrawFullScreenStuff(){
		DrawTexture(GetMugShot(5),(0,-14),DI_ITEM_CENTER_BOTTOM|DI_SCREEN_CENTER_BOTTOM);
	}
	void DrawCommonStuff(int state){
		int mxht=-4-mIndexFont.mFont.GetHeight();
		if(CPlayer.mo.InvSel){
			DrawInventoryIcon(CPlayer.mo.InvSel,(25,-4),DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE);
			DrawString(mAmountFont, FormatNumber(CPlayer.mo.InvSel.Amount), (35,mxht),DI_TEXT_ALIGN_RIGHT|DI_SCREEN_CENTER_BOTTOM, Font.CR_GOLD);
		}

		let cp=HDPlayerPawn(CPlayer.mo);if(!cp)return;

		//health
		DrawString(
			mAmountFont,FormatNumber(CPlayer.mo.health),
			(6,mxht),DI_TEXT_ALIGN_RIGHT|DI_SCREEN_CENTER_BOTTOM,
			Font.CR_OLIVE,alpha:(cp.beatcount*1./cp.beatmax)
		);

		//heartbeat/playercolour tracker
		if(cp.beatmax)DrawImage(
			"GREENPXL",(-8,-7),DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE,
			(cp.beatcount*1./cp.beatmax),scale:(3,3)
		);

		int armour;int maxarmour;string armoursprite;
		class<Inventory> armourtype;
		if(cp.countinv("GreenArmour")){
			armour=0;armoursprite="ARMSA0";
			armourtype="GreenArmour";
			maxarmour=HDCONST_GREENARMOUR;
		}else if(cp.countinv("BlackArmour")){
			armour=1;armoursprite="ARMEA0";
			armourtype="BlackArmour";
			maxarmour=HDCONST_BLACKARMOUR;
		}else if(cp.countinv("BlueArmour")){
			armour=2;armoursprite="ARMCA0";
			armourtype="BlueArmour";
			maxarmour=HDCONST_BLUEARMOUR;
		}
		if(armourtype){
			vector2 armourcoords=(0,-mIndexFont.mFont.GetHeight()*2);
			if(state==HUD_Fullscreen)armourcoords=(-55,-4);
			drawbar(
				armoursprite,string.format("ARMER%i",armour),
				cp.countinv(armourtype),maxarmour,
				armourcoords,-1,
				SHADER_VERT,DI_ITEM_CENTER_BOTTOM|DI_SCREEN_CENTER_BOTTOM
			);
		}

		let w=cplayer.readyweapon;
		if(w){
			if(cplayer.readyweapon.ammotype1){
				int loaded=cp.countinv(cplayer.readyweapon.ammotype1);
				int maxloaded=cp.findinventory(cplayer.readyweapon.ammotype1).maxamount;
				DrawImage(
					"GREENPXL",
					(-16,-6),
					DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT,
					0.6,scale:(loaded*20/maxloaded,2)
				);
			}
			if(cplayer.readyweapon.ammotype2){
				int loaded2=cp.countinv(cplayer.readyweapon.ammotype2);
				int maxloaded2=cp.findinventory(cplayer.readyweapon.ammotype2).maxamount;
				DrawImage(
					"GREENPXL",
					(-16,-10),
					DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT,
					0.6,scale:(loaded2*20/maxloaded2,2)
				);
			}
		}
	}
}


