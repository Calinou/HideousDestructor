class HDStatusBar:DoomStatusBar{
	override void Init(){
		BaseStatusBar.Init();
		SetSize(0,320,200);

		// Create the font used for the fullscreen HUD
		Font fnt = "HUDFONT_DOOM";
		mHUDFont = HUDFont.Create(fnt, fnt.GetCharWidth("0"), true, 1, 1);
		fnt = "INDEXFONT_DOOM";
		mIndexFont = HUDFont.Create(fnt, fnt.GetCharWidth("0"), true);
		mAmountFont = HUDFont.Create("INDEXFONT");
		diparms = InventoryBarState.Create();
	}
	override void Draw(int state,double TicFrac){
		if(cplayer.health<1)return;
		cplayer.inventorytics=0;
		if(automapactive){
			DrawAutomapHUD(ticfrac);
		}else if(state==HUD_StatusBar){
			BeginHUD();
			DrawCommonStuff(state);
		}
		else if(state==HUD_Fullscreen){
			BeginHUD();
			DrawCommonStuff(state);
			DrawFullScreenStuff();
		}
	}
	void DrawFullScreenStuff(){
		if(cplayer.getgender()==0)DrawTexture(GetMugShot(5),(0,-14),DI_ITEM_CENTER_BOTTOM|DI_SCREEN_CENTER_BOTTOM);
		else if(cplayer.getgender()==1)DrawTexture(GetMugShot(5,Mugshot.CUSTOM,"SFF"),(0,-14),DI_ITEM_CENTER_BOTTOM|DI_SCREEN_CENTER_BOTTOM);
	}
	void DrawCommonStuff(int state){
		int mxht=-4-mIndexFont.mFont.GetHeight();
		int mhht=-4-mHUDFont.mFont.getheight();

		//inventory
		if(CPlayer.mo.InvSel){
			inventory ivs=cplayer.mo.invsel;
			drawinventoryicon(ivs,(25,-4),
				DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE
			);
			if(
				ivs is "GreenArmourStored"||
				ivs is "BlueArmourStored"||
				ivs is "BlackArmourStored"
			)drawstring(
				mAmountFont,FormatNumber(cplayer.mo.countinv("StoredArmour")),
				(42,mxht),
				DI_TEXT_ALIGN_RIGHT|DI_SCREEN_CENTER_BOTTOM,
				Font.CR_OLIVE
			);
			else drawstring(mAmountFont,FormatNumber(ivs.amount),
				(42,mxht),
				DI_TEXT_ALIGN_RIGHT|DI_SCREEN_CENTER_BOTTOM,
				Font.CR_OLIVE
			);
		}

		//keys
		string keytype="";
		if(cplayer.mo.countinv("BlueCard"))keytype="STKEYS0";
		if(cplayer.mo.countinv("BlueSkull")){
			if(keytype=="")keytype="STKEYS3";
			else keytype="STKEYS6";
		}
		if(keytype!="")drawimage(
			keytype,
			(50,-16),
			DI_SCREEN_CENTER_BOTTOM
		);
		keytype="";
		if(cplayer.mo.countinv("YellowCard"))keytype="STKEYS1";
		if(cplayer.mo.countinv("YellowSkull")){
			if(keytype=="")keytype="STKEYS4";
			else keytype="STKEYS7";
		}
		if(keytype!="")drawimage(
			keytype,
			(50,-10),
			DI_SCREEN_CENTER_BOTTOM
		);
		keytype="";
		if(cplayer.mo.countinv("RedCard"))keytype="STKEYS2";
		if(cplayer.mo.countinv("RedSkull")){
			if(keytype=="")keytype="STKEYS5";
			else keytype="STKEYS8";
		}
		if(keytype!="")drawimage(
			keytype,
			(50,-4),
			DI_SCREEN_CENTER_BOTTOM
		);

		//backpack
		if(cplayer.mo.countinv("Backpack"))drawimage("BPAKA0",(-55,-4),DI_SCREEN_CENTER_BOTTOM|DI_ITEM_CENTER_BOTTOM);

		//radsuit
		if(cplayer.mo.countinv("PowerIronFeet"))drawimage("SUITC0",(64,-4),DI_SCREEN_CENTER_BOTTOM|DI_ITEM_CENTER_BOTTOM);


		//health
		drawstring(
			mAmountFont,FormatNumber(CPlayer.mo.health),
			(0,mxht),DI_TEXT_ALIGN_CENTER|DI_SCREEN_CENTER_BOTTOM,
			cplayer.mo.health>70?Font.CR_OLIVE:(cplayer.mo.health>33?Font.CR_GOLD:Font.CR_RED)
		);


		//frags
		if(deathmatch)drawstring(
			mHUDFont,FormatNumber(CPlayer.fragcount),
			(74,mhht),DI_TEXT_ALIGN_LEFT|DI_SCREEN_CENTER_BOTTOM,
			Font.CR_RED
		);

		//lives
		if(
			fraglimit>100||
			(fraglimit>0 && !deathmatch)
		){
			int liv=cplayer.mo.countinv("HDLives");
			drawstring(
				mHUDFont,FormatNumber(liv),
				(-74,mhht),DI_TEXT_ALIGN_RIGHT|DI_SCREEN_CENTER_BOTTOM,
				liv>3?Font.CR_GOLD:(liv?Font.CR_BRICK:Font.CR_DARKGRAY)
			);
			
		}


		let cp=HDPlayerPawn(CPlayer.mo);if(!cp)return;

		//heartbeat/playercolour tracker
		if(cp.beatmax){
			float cpb=cp.beatcount*1./cp.beatmax;
			drawimage(
				"GREENPXL",(-8,-6-cpb*2),DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE,
				cpb,scale:(3,3+cp.bloodpressure*0.03)
			);
		}

		//armour
		int armour;int maxarmour;string armoursprite;
		class<Inventory> armourtype;
		if(cp.countinv("GreenArmour")){
			armour=0;armoursprite="ARMSA0";
			armourtype="GreenArmour";
			maxarmour=HDCONST_GREENARMOUR;
		}else if(cp.countinv("BlackArmour")){
			armour=1;armoursprite="ARMEA0";
			armourtype="BlackArmour";
			maxarmour=HDCONST_BLACKARMOUR;
		}else if(cp.countinv("BlueArmour")){
			armour=2;armoursprite="ARMCA0";
			armourtype="BlueArmour";
			maxarmour=HDCONST_BLUEARMOUR;
		}
		if(armourtype){
			vector2 armourcoords=(0,-mIndexFont.mFont.GetHeight()*2);
			if(state==HUD_Fullscreen)armourcoords=(-55,-4);
			drawbar(
				armoursprite,string.format("ARMER%i",armour),
				cp.countinv(armourtype),maxarmour,
				armourcoords,-1,
				SHADER_VERT,DI_ITEM_CENTER_BOTTOM|DI_SCREEN_CENTER_BOTTOM
			);
		}


		//weapon readouts!
		let w=cplayer.readyweapon;
		if(w){
			if(cplayer.readyweapon.ammotype1){
				int loaded=cp.countinv(cplayer.readyweapon.ammotype1);
				int maxloaded=cp.findinventory(cplayer.readyweapon.ammotype1).maxamount;
				DrawImage(
					"GREENPXL",
					(-16,-6),
					DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT,
					1,scale:(loaded*20/maxloaded,2)
				);
			}
			if(cplayer.readyweapon.ammotype2){
				int loaded2=cp.countinv(cplayer.readyweapon.ammotype2);
				int maxloaded2=cp.findinventory(cplayer.readyweapon.ammotype2).maxamount;
				DrawImage(
					"GREENPXL",
					(-16,-10),
					DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT,
					1,scale:(loaded2*20/maxloaded2,2)
				);
			}
		}


		//full hud consequences
		if(!cp.countinv("IsMoving") && cp.countinv("GimmeHud")>9){

			//weapons
			drawwep("Lumberjack","CSAWA0",-163);
			drawwep("HDSMG","SMGNA0",-141);
			drawwep("HDPistol","PISTA0",-143);
			drawwep("Hunter","HUNTA0",-128);
			drawwep("Slayer","SLAYA0",-120);
			drawwep("Vulcanette","VULCA0",-108);
			drawwep("ZM66Rifle","RIFLA0",-103);
			drawwep("ZM66SemiRifle","RIFSA0",-103);
			drawwep("ZM66GrenadeRifle","RIGLA0",-103);
			drawwep("RocquetteLauncher","LAUNA0",-83);
			drawwep("Blooper","BLOPA0",-78);
			drawwep("ThunderBuster","PLASA0",-63);
			drawwep("LiberatorRifle","BRFLA0",-53);
			drawwep("LiberatorGrenadeRifle","BRFLB0",-53);
			drawwep("BFG9k","BFUGA0",-28);
			drawwep("Brontornis","BLSTA0",-23);
			drawwep("BossRifle","BORFA0",-16);
			if(
				cplayer.mo.countinv("HDFragGrenadeAmmo")
				&&!(cplayer.readyweapon is "HDFragGrenades")
			)drawimage(
				"FRAGA0",(8,-11),
				DI_SCREEN_LEFT_BOTTOM|DI_ITEM_LEFT,
				scale:(0.6,0.6)
			);

			//encumbrance
			if(hd_carrylimit)drawstring(
				mAmountFont,string.format("%.2f",cp.overloaded),
				(8,mxht),DI_TEXT_ALIGN_LEFT|DI_SCREEN_LEFT_BOTTOM,
				cp.overloaded<1.5?Font.CR_OLIVE:(cp.overloaded>2?Font.CR_RED:Font.CR_GOLD)
			);

			//ammo counters
			int mxhtplus=mxht-24;
			drawambar("PISARED","HDPISAMO","HDPistolAmmo",(-76,mxhtplus-1));
			drawambar("CLIPRED","CLIPC0","FourMilAmmo",(-52,mxhtplus));
			drawambar("MAGRED","MAGSTAT","SevenMilAmmo",(-38,mxhtplus-1));
			drawimage("SHELA0",(-10,mxhtplus-1),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);

			drawimage("BROCA0",(-81,mxht),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);		
			drawimage("ROCKB0",(-58,mxht-2),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);
			drawimage("ROQPA0",(-62,mxht+2),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM,scale:(0.85,0.85));
			drawambar("CELLRED","CELLA0","HDCellAmmo",(-30,mxht));
			drawimage("FRAGA0",(-10,mxht),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM,scale:(0.7,0.7));

			drawnum(cp.countinv("HDPistolMag"),-75,mxhtplus-6);
			drawnum(cp.countinv("HDSMGMag"),-75,mxhtplus);

			drawnum(cp.countinv("ZM66RifleMags"),-54,mxhtplus);

			drawnum(cp.countinv("BossClip"),-37,mxhtplus-6);
			drawnum(cp.countinv("LiberatorMag"),-37,mxhtplus);

			if(cp.countinv("Slayer"))drawnum(
				cp.countinv("ShotgunQuickAmmo2"),-4,mxhtplus-14,
				DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM,
				Font.CR_BROWN
			);
			if(cp.countinv("Hunter"))drawnum(
				cp.countinv("ShotgunQuickAmmo"),-4,mxhtplus-8,
				DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM,
				fnt:Font.CR_BROWN
			);
			drawnum(cp.countinv("HDShellAmmo"),-5,mxhtplus,
				DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM
			);

			drawnum(cp.countinv("BrontornisRound"),-80,mxht);

			drawnum(cp.countinv("HEATAmmo"),-60,mxht-6);
			drawnum(cp.countinv("HDRocketAmmo"),-60,mxht);

			drawnum(cp.countinv("HDCellPacks"),-30,mxht,
				DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM
			);
			drawnum(cp.countinv("HDFragGrenadeAmmo"),-6,mxht,
				DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM
			);
		}
	}
	void drawwep(class<inventory> type,string img,int zoffs){
		if(cplayer.mo.countinv(type) && !(cplayer.readyweapon is type)){
			drawimage(img,(8,zoffs),DI_SCREEN_LEFT_BOTTOM|DI_ITEM_LEFT);
		}
	}
	void drawnum(int num,int xpos,int ypos,int flags=-1,int fnt=Font.CR_OLIVE){
		drawstring(
			mAmountFont,formatnumber(num),
			(xpos,ypos),flags<0?DI_TEXT_ALIGN_LEFT|DI_SCREEN_RIGHT_BOTTOM:flags,
			fnt
		);
	}
	void drawambar(
		string ongfx,string offgfx,
		class<inventory> type,
		vector2 coords
	){
		inventory inv=cplayer.mo.findinventory(type);
		if(!inv||!inv.amount){
			drawimage(offgfx,coords,DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);
		}else drawbar(
				ongfx,offgfx,
				inv.amount,inv.maxamount,
				coords,-1,
				SHADER_VERT,DI_ITEM_RIGHT_BOTTOM|DI_SCREEN_RIGHT_BOTTOM
		);
	}
}





