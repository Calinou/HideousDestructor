// ------------------------------------------------------------
// HD HUD. HUD for HD. Hdhdhdhdhhdhdhhdhd
// ------------------------------------------------------------
class HDStatusBar:DoomStatusBar{
	override void Init(){
		BaseStatusBar.Init();
		SetSize(0,320,200);

		// Create the font used for the fullscreen HUD
		Font fnt = "HUDFONT_DOOM";
		mHUDFont = HUDFont.Create(fnt, fnt.GetCharWidth("0"), true, 1, 1);
		fnt = "INDEXFONT_DOOM";
		mIndexFont = HUDFont.Create(fnt, fnt.GetCharWidth("0"), true);
		mAmountFont = HUDFont.Create("INDEXFONT");
		diparms = InventoryBarState.Create();
	}
	override void Draw(int state,double TicFrac){
		if(cplayer.health<1||cplayer.mo.countinv("WipedOut"))return;
		cplayer.inventorytics=0;

		setwepsprites(cplayer.mo);

		if(automapactive){
			DrawAutomapHUD(ticfrac);
			DrawAutomapStuff();
		}else{
			DrawAlwaysStuff();
			BeginHUD();
			if(state==HUD_StatusBar){
				DrawCommonStuff(state);
			}
			else if(state==HUD_Fullscreen){
				DrawCommonStuff(state);
				DrawFullScreenStuff();
			}
		}
	}
	void DrawAutomapStuff(){
		playerpawn cp=playerpawn(cplayer.mo);
		int DI_TOPRIGHT=DI_SCREEN_RIGHT_TOP|DI_ITEM_RIGHT_TOP;
		int DI_TOPLEFT=DI_SCREEN_LEFT_TOP|DI_ITEM_LEFT_TOP;
		int DI_BOTTOMLEFT=DI_SCREEN_LEFT_BOTTOM|DI_ITEM_LEFT_BOTTOM;
		int DI_BOTTOMRIGHT=DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM;
		//KEYS!
		if(cp.countinv("BlueCard"))drawimage("BKEYB0",(10,24),DI_TOPLEFT);
		if(cp.countinv("YellowCard"))drawimage("YKEYB0",(10,44),DI_TOPLEFT);
		if(cp.countinv("RedCard"))drawimage("RKEYB0",(10,64),DI_TOPLEFT);
		if(cp.countinv("BlueSkull"))drawimage("BSKUA0",(6,30),DI_TOPLEFT);
		if(cp.countinv("YellowSkull"))drawimage("YSKUA0",(6,50),DI_TOPLEFT);
		if(cp.countinv("RedSkull"))drawimage("RSKUB0",(6,70),DI_TOPLEFT);

		//lives
		drawlives(30,deathmatch?44:24,DI_TOPLEFT|DI_TEXT_ALIGN_LEFT);

		//frags
		if(deathmatch)drawstring(
			mHUDFont,FormatNumber(CPlayer.fragcount),
			(30,24),DI_TOPLEFT|DI_TEXT_ALIGN_LEFT,
			Font.CR_RED
		);

		//mugshot
		if(cplayer.getgender()==0)DrawTexture(GetMugShot(5),(6,-14),DI_BOTTOMLEFT);
		else if(cplayer.getgender()==1)DrawTexture(GetMugShot(5,Mugshot.CUSTOM,"SFF"),(6,-14),DI_BOTTOMLEFT);

		//heartbeat/playercolour tracker
		let hcp=HDPlayerPawn(cplayer.mo);
		if(hcp && hcp.beatmax){
			float cpb=hcp.beatcount*1./hcp.beatmax;
			drawimage(
				"GREENPXL",(32,-24-cpb*3),DI_BOTTOMLEFT|DI_TRANSLATABLE,
				cpb,scale:(4,4+hcp.bloodpressure*0.05)
			);
		}
		//health
		drawstring(
			mAmountFont,FormatNumber(CPlayer.mo.health),
			(34,-24),DI_BOTTOMLEFT|DI_TEXT_ALIGN_CENTER,
			cplayer.mo.health>70?Font.CR_OLIVE:(cplayer.mo.health>33?Font.CR_GOLD:Font.CR_RED)
		);

		//inventory
		DrawInvSel(6,90,36,106,DI_TOPLEFT);
		if(cp.countinv("WornRadsuit"))drawimage("SUITC0",(14,120),DI_TOPLEFT);

		//guns
		drawselectedweapon(-100,-24,DI_BOTTOMRIGHT);

		drawammocounters(-18);
		drawweaponstash(true,-48);
	}
	void DrawFullScreenStuff(){
		if(cplayer.getgender()==0)DrawTexture(GetMugShot(5),(0,-14),DI_ITEM_CENTER_BOTTOM|DI_SCREEN_CENTER_BOTTOM);
		else if(cplayer.getgender()==1)DrawTexture(GetMugShot(5,Mugshot.CUSTOM,"SFF"),(0,-14),DI_ITEM_CENTER_BOTTOM|DI_SCREEN_CENTER_BOTTOM);
	}
	void DrawAlwaysStuff(){
		let cp=HDPlayerPawn(CPlayer.mo);
		if(!cp)return;

		if(cp.countinv("PowerInvisibility"))return;

		//draw the crosshair.
		BeginHUD();
		DrawHDXHair(cp);

		BeginHUD(forcescaled:true);
		if(cp.countinv("WornRadsuit"))drawimage(
			"DRKGRNPX",(0,0),DI_SCREEN_CENTER|DI_ITEM_CENTER,
			alpha:0.6,scale:(1000,600)
		);

		//draw the goggles when they do something.
		let hdla=portableliteamp(cp.findinventory("PortableLiteAmp"));
		if(hdla && hdla.worn){
			drawimage(
				"GOGGA0",(0,0),DI_SCREEN_CENTER|DI_ITEM_CENTER,
				scale:(2.2,2.4)
			);
		}
	}
	void DrawCommonStuff(int state){
		let cp=HDPlayerPawn(CPlayer.mo);
		if(!cp)return;

		int mxht=-4-mIndexFont.mFont.GetHeight();
		int mhht=-4-mHUDFont.mFont.getheight();

		//inventory
		DrawInvSel(25,-4,42,mxht,DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE);

		//keys
		string keytype="";
		if(cplayer.mo.countinv("BlueCard"))keytype="STKEYS0";
		if(cplayer.mo.countinv("BlueSkull")){
			if(keytype=="")keytype="STKEYS3";
			else keytype="STKEYS6";
		}
		if(keytype!="")drawimage(
			keytype,
			(50,-16),
			DI_SCREEN_CENTER_BOTTOM
		);
		keytype="";
		if(cplayer.mo.countinv("YellowCard"))keytype="STKEYS1";
		if(cplayer.mo.countinv("YellowSkull")){
			if(keytype=="")keytype="STKEYS4";
			else keytype="STKEYS7";
		}
		if(keytype!="")drawimage(
			keytype,
			(50,-10),
			DI_SCREEN_CENTER_BOTTOM
		);
		keytype="";
		if(cplayer.mo.countinv("RedCard"))keytype="STKEYS2";
		if(cplayer.mo.countinv("RedSkull")){
			if(keytype=="")keytype="STKEYS5";
			else keytype="STKEYS8";
		}
		if(keytype!="")drawimage(
			keytype,
			(50,-4),
			DI_SCREEN_CENTER_BOTTOM
		);

		//backpack
		if(cplayer.mo.countinv("Backpack"))drawimage("BPAKA0",(-55,-4),DI_SCREEN_CENTER_BOTTOM|DI_ITEM_CENTER_BOTTOM);

		//radsuit
		if(cp.countinv("WornRadsuit"))drawimage(
			"SUITC0",(64,-4),DI_SCREEN_CENTER_BOTTOM|DI_ITEM_CENTER_BOTTOM
		);


		//health
		drawstring(
			mAmountFont,FormatNumber(CPlayer.mo.health),
			(0,mxht),DI_TEXT_ALIGN_CENTER|DI_SCREEN_CENTER_BOTTOM,
			cplayer.mo.health>70?Font.CR_OLIVE:(cplayer.mo.health>33?Font.CR_GOLD:Font.CR_RED)
		);


		//frags
		if(deathmatch)drawstring(
			mHUDFont,FormatNumber(CPlayer.fragcount),
			(74,mhht),DI_TEXT_ALIGN_LEFT|DI_SCREEN_CENTER_BOTTOM,
			Font.CR_RED
		);

		//lives
		drawlives(-74,mhht,DI_TEXT_ALIGN_RIGHT|DI_SCREEN_CENTER_BOTTOM);


		//heartbeat/playercolour tracker
		if(cp.beatmax){
			float cpb=cp.beatcount*1./cp.beatmax;
			drawimage(
				"GREENPXL",(-8,-6-cpb*2),DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE,
				cpb,scale:(3,3+cp.bloodpressure*0.03)
			);
		}

		//armour
		int armour;int maxarmour;string armoursprite;
		class<Inventory> armourtype;
		if(cp.countinv("GreenArmour")){
			armour=0;armoursprite="ARMSA0";
			armourtype="GreenArmour";
			maxarmour=HDCONST_GREENARMOUR;
		}else if(cp.countinv("BlackArmour")){
			armour=1;armoursprite="ARMEA0";
			armourtype="BlackArmour";
			maxarmour=HDCONST_BLACKARMOUR;
		}else if(cp.countinv("BlueArmour")){
			armour=2;armoursprite="ARMCA0";
			armourtype="BlueArmour";
			maxarmour=HDCONST_BLUEARMOUR;
		}
		if(armourtype){
			vector2 armourcoords=(0,-mIndexFont.mFont.GetHeight()*2);
			if(state==HUD_Fullscreen)armourcoords=(-55,-4);
			drawbar(
				armoursprite,string.format("ARMER%i",armour),
				cp.countinv(armourtype),maxarmour,
				armourcoords,-1,
				SHADER_VERT,DI_ITEM_CENTER_BOTTOM|DI_SCREEN_CENTER_BOTTOM
			);
		}


		//weapon readouts!
		if(cplayer.readyweapon)drawweaponstatus(cplayer.readyweapon);

		//weapon sprite
		if(!r_drawplayersprites||cp.countinv("HUDSprite"))
		drawselectedweapon(80,
-4,//			state==HUD_Fullscreen?-55:-35,
			DI_SCREEN_LEFT_BOTTOM|DI_ITEM_LEFT_BOTTOM
		);


		//full hud consequences
		if(cp.countinv("GimmeHud")>9){
			drawweaponstash();
			drawammocounters(mxht);

			//encumbrance
			if(hd_carrylimit)drawstring(
				mAmountFont,string.format("%.2f",cp.overloaded),
				(8,mxht),DI_TEXT_ALIGN_LEFT|DI_SCREEN_LEFT_BOTTOM,
				cp.overloaded<1.5?Font.CR_OLIVE:(cp.overloaded>2?Font.CR_RED:Font.CR_GOLD)
			);
		}
	}
	void drawnum(int num,int xpos,int ypos,int flags=-1,int fnt=Font.CR_OLIVE){
		drawstring(
			mAmountFont,formatnumber(num),
			(xpos,ypos),flags<0?DI_TEXT_ALIGN_LEFT|DI_SCREEN_RIGHT_BOTTOM:flags,
			fnt
		);
	}
	void drawlives(int posx,int posy,int flags){
		if(
			fraglimit>100||
			(fraglimit>0 && !deathmatch)
		){
			int liv=cplayer.mo.countinv("HDLives");
			drawstring(
				mHUDFont,FormatNumber(liv),
				(posx,posy),flags,
				liv>3?Font.CR_GOLD:(liv?Font.CR_BRICK:Font.CR_DARKGRAY)
			);
			
		}
	}
	void drawambar(
		string ongfx,string offgfx,
		class<inventory> type,
		vector2 coords
	){
		inventory inv=cplayer.mo.findinventory(type);
		if(!inv||!inv.amount){
			drawimage(offgfx,coords,DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);
		}else drawbar(
				ongfx,offgfx,
				inv.amount,inv.maxamount,
				coords,-1,
				SHADER_VERT,DI_ITEM_RIGHT_BOTTOM|DI_SCREEN_RIGHT_BOTTOM
		);
	}
	void DrawInvSel(int posx,int posy,int numposx,int numposy,int flags){
		if(CPlayer.mo.InvSel){
			inventory ivs=cplayer.mo.invsel;
			drawinventoryicon(ivs,(posx,posy),flags|DI_ITEM_CENTER_BOTTOM|DI_TRANSLATABLE);
			if(
				ivs is "GreenArmourStored"||
				ivs is "BlueArmourStored"||
				ivs is "BlackArmourStored"
			)drawstring(
				mAmountFont,FormatNumber(cplayer.mo.countinv("StoredArmour")),
				(numposx,numposy),flags|DI_TEXT_ALIGN_RIGHT,Font.CR_OLIVE
			);
			else if(ivs is "PortableLiteAmp"){
				let pla=PortableLiteAmp(ivs);
				if(pla)drawstring(
					mAmountFont,FormatNumber((84000-pla.spent)/840),
					(numposx,numposy),flags|DI_TEXT_ALIGN_RIGHT,Font.CR_OLIVE
				);
			}
			else drawstring(mAmountFont,FormatNumber(ivs.amount),
				(numposx,numposy),flags|DI_TEXT_ALIGN_RIGHT,Font.CR_OLIVE
			);
		}
	}
	string huntersprite;string slayersprite;
	string smgsprite;string zm66sprite;string liberatorsprite;
	void setwepsprites(playerpawn cp){
		//smg
		if(cp.countinv("HDSMGNoMag"))smgsprite="SMGNB0";else smgsprite="SMGNA0";

		//zm66
		string zm66pre="RIFL";
		if(cp.countinv("ZM66SemiRifle"))zm66pre="RIFS";
		else if(cp.countinv("ZM66GrenadeRifle"))zm66pre="RIGL";
		if(cp.countinv("ZM66NoMag"))zm66sprite="D";else zm66sprite="A";
		zm66sprite=string.format("%s%s0",zm66pre,zm66sprite);

		//liberator
		if(cp.countinv("LiberatorGrenadeRifle")){
			if(cp.countinv("LiberatorNoMag"))liberatorsprite="BRFLD0";
			else liberatorsprite="BRFLB0";
		}else{
			if(cp.countinv("LiberatorNoMag"))liberatorsprite="BRFLC0";
			else liberatorsprite="BRFLA0";
		}

		//hunter
		int shl=cp.countinv("ShotgunQuickAmmo");
		if(shl>10)huntersprite="HUNTA0";
		else if(shl>8)huntersprite="HUNTB0";
		else if(shl>6)huntersprite="HUNTC0";
		else if(shl>4)huntersprite="HUNTD0";
		else if(shl>2)huntersprite="HUNTE0";
		else if(shl)huntersprite="HUNTF0";
		else huntersprite="HUNTG0";

		//hunter
		shl=cp.countinv("ShotgunQuickAmmo2");
		if(shl>10)slayersprite="SLAYA0";
		else if(shl>8)slayersprite="SLAYB0";
		else if(shl>6)slayersprite="SLAYC0";
		else if(shl>4)slayersprite="SLAYD0";
		else if(shl>2)slayersprite="SLAYE0";
		else if(shl)slayersprite="SLAYF0";
		else slayersprite="SLAYG0";
	}
	void drawselectedweapon(int posx,int posy,int flags){
		weapon w=cplayer.readyweapon;
		string wx="";
		if(w is "")wx="";
		else if(w is "Lumberjack")wx="CSAWA0";
		else if(w is "HDPistol"){
			if(cplayer.mo.countinv("HDPistolFullAuto"))wx="PISTC0";else wx="PISTA0";
		}
		else if(w is "HDSMG")wx=smgsprite;
		else if(w is "Slayer")wx=slayersprite;
		else if(w is "Hunter")wx=huntersprite;
		else if(w is "ZM66GrenadeRifle")wx=zm66sprite;
		else if(w is "Vulcanette")wx="VULCA0";
		else if(w is "Blooper")wx="BLOPA0";
		else if(w is "RocquetteLauncher")wx="LAUNA0";
		else if(w is "LiberatorGrenadeRifle")wx=liberatorsprite;
		else if(w is "ThunderBuster")wx="PLASA0";
		else if(w is "Brontornis")wx="BLSTA0";
		else if(w is "BFG9k")wx="BFUGA0";
		else if(w is "BossRifle")wx="BORFA0";
		else if(w is "HDFragGrenades" || w is "Tripwire")wx="FRAGA0";
		else if(w is "HDMedikitter")wx="MEDIB0";
		else if(w is "HDBerserker")wx="PSTRA0";
		else if(w is "HDBlueBottler")wx="BON1A0";
		else if(w is "HDStimpacker")wx="STIMA0";
		else if(w is "SelfBandage")wx="BLUDC0";
		if(wx!="")drawimage(wx,(posx,posy),flags,
			scale:wx=="CSAWA0"?(0.7,0.7):(wx=="FRAGA0"||wx=="MEDIB0"?(0.6,0.6):(1,1))
		);
	}
	void drawwep(class<inventory> type,string img,int zoffs,float scl=1.,bool right=false){
		if(cplayer.mo.countinv(type) && !(cplayer.readyweapon is type)){
			drawimage(img,(right?-8:8,zoffs),
				(right?DI_SCREEN_RIGHT_BOTTOM:DI_SCREEN_LEFT_BOTTOM)|
				(right?DI_ITEM_RIGHT:DI_ITEM_LEFT),
				scale:(scl,scl)
			);
		}
	}
	void drawweaponstash(bool rt=false,int yofs=0){
		drawwep("Lumberjack","CSAWA0",-163+yofs,scl:0.6,right:rt);
		drawwep("HDSMG",smgsprite,-141+yofs,scl:0.8,right:rt);
		if(cplayer.mo.countinv("HDPistol")){
			string pispr="PISTA0";
			if(cplayer.mo.countinv("HDSecondPistolFullAuto"))pispr="PISTC0";
			if(cplayer.mo.countinv("HDSecondPistol"))drawimage(
				pispr,(16,-139+yofs),
				(rt?DI_SCREEN_RIGHT_BOTTOM:DI_SCREEN_LEFT_BOTTOM)|
				(rt?DI_ITEM_RIGHT:DI_ITEM_LEFT),
				scale:(0.7,0.7)
			);
			if(cplayer.mo.countinv("HDPistolFullAuto"))pispr="PISTC0";else pispr="PISTA0";
			drawwep("HDPistol",pispr,-143+yofs,scl:0.7,right:rt);
		}
		drawwep("Hunter",huntersprite,-128+yofs,right:rt);
		drawwep("Slayer",slayersprite,-120+yofs,right:rt);
		drawwep("Vulcanette","VULCA0",-108+yofs,right:rt);
		drawwep("ZM66SemiRifle",zm66sprite,-103+yofs,right:rt);
		drawwep("ZM66Rifle",zm66sprite,-103+yofs,right:rt);
		drawwep("ZM66GrenadeRifle",zm66sprite,-103+yofs,right:rt);
		drawwep("RocquetteLauncher","LAUNA0",-83+yofs,scl:0.9,right:rt);
		drawwep("Blooper","BLOPA0",-78+yofs,right:rt);
		drawwep("ThunderBuster","PLASA0",-63+yofs,scl:0.8,right:rt);
		drawwep("LiberatorRifle",liberatorsprite,-53+yofs,right:rt);
		drawwep("LiberatorGrenadeRifle",liberatorsprite,-53+yofs,right:rt);
		drawwep("BFG9k","BFUGA0",-28+yofs,right:rt);
		drawwep("Brontornis","BLSTA0",-23+yofs,right:rt);
		drawwep("BossRifle","BORFA0",-16+yofs,right:rt);
		if(
			cplayer.mo.countinv("HDFragGrenadeAmmo")
			&&!(cplayer.readyweapon is "HDFragGrenades")
		)drawimage(
			"FRAGA0",(rt?-8:8,-11+yofs),
			(rt?DI_SCREEN_RIGHT_BOTTOM:DI_SCREEN_LEFT_BOTTOM)|
			(rt?DI_ITEM_RIGHT:DI_ITEM_LEFT),
			scale:(0.6,0.6)
		);
	}
	void drawammocounters(int mxht){
		int mxhtplus=mxht-24;
		actor cp=cplayer.mo;
		drawimage("CLP3A0",(-78,mxhtplus+4),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM,scale:(2.4,2.4));
		drawimage("CLP2A0",(-74,mxhtplus),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM,scale:(2.4,2.4));
		drawambar("PISARED","HDPISAMO","HDPistolAmmo",(-76,mxhtplus-1));
		drawambar("CLIPRED","CLIPC0","FourMilAmmo",(-54,mxhtplus));
		drawimage("RMAGA0",(-32,mxhtplus+3),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM,scale:(1.5,1.5));
		drawambar("MAGRED","MAGSTAT","SevenMilAmmo",(-35,mxhtplus-1));
		drawimage("SHELA0",(-10,mxhtplus-1),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);

		drawimage("BROCA0",(-81,mxht),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);		
		drawimage("ROCKB0",(-58,mxht-2),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM);
		drawimage("ROQPA0",(-62,mxht+2),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM,scale:(0.85,0.85));
		drawambar("CELLRED","CELLA0","HDCellAmmo",(-30,mxht));
		drawimage("FRAGA0",(-10,mxht),DI_SCREEN_RIGHT_BOTTOM|DI_ITEM_RIGHT_BOTTOM,scale:(0.7,0.7));

		drawnum(cp.countinv("HDPistolMag"),-75,mxhtplus-6);
		drawnum(cp.countinv("HDSMGMag"),-75,mxhtplus);

		drawnum(cp.countinv("ZM66RifleMags"),-56,mxhtplus);

		drawnum(cp.countinv("BossClip"),-37,mxhtplus-6);
		drawnum(cp.countinv("LiberatorMag"),-37,mxhtplus);

		if(cp.countinv("Slayer"))drawnum(
			cp.countinv("ShotgunQuickAmmo2"),-4,mxhtplus-14,
			DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM,
			Font.CR_BROWN
		);
		if(cp.countinv("Hunter"))drawnum(
			cp.countinv("ShotgunQuickAmmo"),-4,mxhtplus-8,
			DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM,
			fnt:Font.CR_BROWN
		);
		drawnum(cp.countinv("HDShellAmmo"),-5,mxhtplus,
			DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM
		);

		drawnum(cp.countinv("BrontornisRound"),-80,mxht);

		drawnum(cp.countinv("HEATAmmo"),-60,mxht-6);
		drawnum(cp.countinv("HDRocketAmmo"),-60,mxht);

		drawnum(cp.countinv("HDCellPacks"),-30,mxht,
			DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM
		);
		drawnum(cp.countinv("HDCellPackEmpty"),-40,mxht,
			DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM,
			fnt:Font.CR_BROWN
		);
		drawnum(cp.countinv("HDFragGrenadeAmmo"),-6,mxht,
			DI_TEXT_ALIGN_RIGHT|DI_SCREEN_RIGHT_BOTTOM
		);
	}
}





