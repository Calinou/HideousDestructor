//-------------------------------------------------
// Frag on a string: the real IED
//-------------------------------------------------
class TripwirePlacerPuff:IdleDummy{
	default{
		-alwayspuff -puffonactors +bloodlessimpact
		stamina 1;radius 0.1;height 0.1;
	}
}
class TripwireCheckerPuff:IdleDummy{
	default{
		-alwayspuff +puffonactors +hittracer +bloodlessimpact
		stamina 1;radius 0.1;height 0.1;
	}
}
class Tripwire:HDWeapon{
	actor grenade;
	actor gumspot;
	default{
		weapon.ammotype1 "HDFragGrenadeAmmo";
	}
	action void UndoAll(){
		if(invoker.grenade){
			invoker.grenade.master=null;
			invoker.grenade=null;
		}
		if(invoker.gumspot){
			invoker.gumspot.destroy();
			A_Log("Setup aborted.",true);
		}
	}
	states{
	altfire:
		TNT1 A 0 UndoAll();
		goto nope;
	deselect:
		TNT1 A 10{
			UndoAll();
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			if(countinv("NulledWeapon"))A_SetTics(0);
		}goto super::deselect;
	select:
		TNT1 A 10{
			if(!countinv("HDFragGrenadeAmmo")){
				if(getcvar("hd_helptext"))A_Print("No grenades.");
				A_SelectWeapon("Ring");
			}else if(getcvar("hd_helptext"))A_Print("\cp\--- \cqTRIPWIRES \cp---\c-\n\n\nHit fire to set one end of the line,\n\nthen hit fire again to plant the grenade.\nby human hands.\n\nMove carefully.");
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			if(countinv("NulledWeapon"))A_SetTics(0);
		}goto super::select;
	ready:
		TNT1 A 1{
			if(!countinv("HDFragGrenades"))A_SelectWeapon("Ring");
			else A_SetCrosshair(0);
			if(!invoker.gumspot)A_TakeInventory("WeaponBusy");else A_GIveInventory("WeaponBusy");
			A_WeaponReady();
		}wait;
	fire:
		TNT1 A 0{
			actor a;int b;actor aaa;
			if(!invoker.gumspot){
				[a,b]=LineAttack(angle,64,pitch,0,"none",
					"TripwirePlacerPuff",flags:LAF_NORANDOMPUFFZ
				);
				if(!a){
					A_Log("You need to stick the wire on to something stable.",true);
					return;
				}
				aaa=spawn("GumAndString",a.pos);
				aaa.target=self;aaa.master=self;
				aaa.A_PlaySound("misc/bulletflesh",CHAN_BODY);
				invoker.gumspot=aaa;
				A_Log("Wire end secured. Now to set the grenade...",true);
			}else{
				[a,b]=LineAttack(angle,64,pitch,0,"none",
					"TripwirePlacerPuff",flags:LAF_NORANDOMPUFFZ
				);
				if(!a){
					A_Log("You need to stick the grenade on to something stable.",true);
					return;
				}
				aaa=spawn("TrippingFrag",a.pos);
				aaa.target=self;aaa.master=self;
				aaa.A_PlaySound("weapons/rifleclick2",CHAN_BODY);
				invoker.grenade=aaa;
				invoker.gumspot.tracer=invoker.grenade;
				invoker.grenade.tracer=invoker.gumspot;
				invoker.grenade.master=self;
				invoker.grenade.target=self;
				A_Log("Grenade secured! Now be very, very careful...",true);
				A_TakeInventory("HDFragGrenadeAmmo",1,TIF_NOTAKEINFINITE);
				invoker.gumspot=null;
			}
			a.destroy();
		}goto nope;
	}
}
class GumAndString:IdleDummy{
	bool trapisset;
	int stringlength;
	vector3 ddd;int bbb;float ii;
	default{
		+spriteangle
		spriteangle 0;
		scale 0.1;translation 2;
		radius 5;height 2;
	}
	override void postbeginplay(){
		super.postbeginplay();

		//and now a BIG FUCKING HACK to find some shit!
		vector3 posbak=pos;
		bmissile=true;bnointeraction=false;bnoclip=false;
		if(master && !trymove(pos.xy+(pos.xy-master.pos.xy).unit(),true,true)){
			if(blockingline.backsector)A_Log("YAY");
		}
		bmissile=false;bnointeraction=true;bnoclip=true;
		setorigin(posbak,false);
	}
	states{
	spawn:
		BAL7 A 1 nodelay{
			if(
				!tracer&&
				(!master||master.health<1)
			){
				destroy();
				return;
			}

			//check for the actual thing
			if(tracer)A_FaceTracer(0,0,flags:FAF_TOP);
			else if(master)A_FaceMaster(0,0,flags:FAF_TOP);
			actor a;int b;
			[a,b]=LineAttack(angle,512,pitch,0,"none",
				"TripwireCheckerPuff",
				flags:LAF_NORANDOMPUFFZ|LAF_OVERRIDEZ
			);
			if(a){
				bool carefulplayer;
				let atpl=HDPlayerPawn(a.tracer);
				if(
					atpl && atpl!=master && atpl.runwalksprint<0
					&& a.pos.z-atpl.pos.z<24
					&& max(abs(atpl.vel.x),abs(atpl.vel.y),abs(atpl.vel.z))<5
				){
					carefulplayer=true;
					atpl.stunned=max(atpl.stunned,10);
				}

				if(!master && !carefulplayer && tracer && a.tracer!=tracer){
					actor ggg=tracer.spawn("HDFragGrenadeRoller",tracer.pos);
					ggg.target=tracer.target;
					ggg.vel=(pos-ggg.pos).unit();
					actor hhh=tracer.spawn("HDFragSpoon",tracer.pos);
					hhh.vel=ggg.vel*5;
					ggg.vel*=3;ggg.vel.z++;
					ggg.A_PlaySound("weapons/rifleclick");
					tracer.destroy();
					destroy();return;
				}
				else if(tracer && a.tracer==tracer)master=null;

				if(
					!a.tracer
					||(tracer && a.tracer==master)
					||(!tracer && a.tracer!=master)
				){
					ForceAbort();
					return;
				}
				a.destroy();
			}else{
				ForceAbort();
				destroy();return;
			}


			//draw a line of particles
			if(!tracer && !master)return;

			//set all the numbers
			if(!trapisset){
				if(tracer)ddd=tracer.pos+(0,0,4);
				else ddd=master.pos+(0,0,master.height-8);

				ddd-=pos;
				bbb=max(ddd.length(),1)*0.3;
				ddd/=bbb;

				ii=min(bbb,40)*0.0001;

				if(tracer && !master)trapisset=true;
			}

			vector3 ccc;
			for(int i=0;i<bbb;i++){
				ccc=ddd*i*frandom(1-ii,1+ii);
				A_SpawnParticle(
					"white",
					lifetime:1,size:0.5,
					xoff:ccc.x,yoff:ccc.y,zoff:ccc.z,
					startalphaf:0.8
				);
			}
		}wait;
	}
	void ForceAbort(){
		A_PlaySound("weapons/bigcrack");
		if(tracer)tracer.A_PlaySound("weapons/bigcrack");
		else if(master)master.A_PlaySound("weapons/bigcrack");
		master.A_Log("Welp, there goes that one. Try again?",true);
		destroy();
	}
}
class TrippingFrag:FragP{
	default{
		+nogravity +shootable +noblood +nodamage +notargetswitch
		health 1;painchance 256;
	}
	override bool cancollidewith(actor other,bool passive){
		if(other is "userpickerupper"){
			spawn("FragP",pos);
			if(tracer)tracer.destroy();
			destroy();return false;
		}
		return false;
	}
	states{
	pain:
		---- A 0{
			if(!random(0,9)){
				actor ggg=spawn("HDFragGrenadeRoller",pos);
				ggg.target=target;
				ggg.vel=(pos-ggg.pos).unit();
				actor hhh=spawn("HDFragSpoon",tracer.pos);
				hhh.vel=ggg.vel*5;
				ggg.vel*=3;ggg.vel.z++;
				ggg.A_PlaySound("weapons/rifleclick");
				tracer.destroy();
				destroy();return;
			}else if(!random(0,4)){
				tracer.destroy();
				spawn("FragP",pos);
				destroy();return;
			}
		}goto spawn;
	}
}
