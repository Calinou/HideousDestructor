// ------------------------------------------------------------
// 7.76mm Ammo
// ------------------------------------------------------------
class LiberatorMag:HDAmmo{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Liberator Magazine"
		//$Sprite "RMAGA0"

		+inventory.ignoreskill
		inventory.maxamount 5;
		scale 0.8;
		inventory.pickupmessage "Picked up a 7.76mm magazine.";
	}states{
	spawn2:
		RMAG A -1;
		stop;
	}
}
class LiberatorEmptyMag:HD4mmMagEmpty{
	default{scale 0.8;}
	states{
	spawn:
		RMAG B 2 A_SetRoll(roll+30,SPF_INTERPOLATE);
		wait;
	}
}
class LiberatorMagP:HDUPK{
	default{
		stamina 30;
		scale 0.8;
		hdupk.pickupmessage "Picked up a 7.76mm magazine.";
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(stamina<1){
			actor emg=spawn("LiberatorEmptyMag",pos);
			if(emg)emg.vel=vel;
			destroy();return;
		}
	}
	states{
	spawn:
		RMAG A -1 A_Stop();
	give:
		---- A 0{
			if(stamina>=30){    
				if(A_JumpIfInTargetInventory("LiberatorMag",0,"null")){
					setstatelabel("spawn");return;
				}else{
					target.A_GiveInventory("LiberatorMag",1);
					destroy();return;
				}
			}else{
				if(magmanager.givemmmag(self,picktarget,HDMMS_LIB,stamina))return;
				int al=target.countinv("SevenMilAmmo");
				target.A_GiveInventory("SevenMilAmmo",stamina);
				stamina-=target.countinv("SevenMilAmmo")-al;
				if(!stamina){destroy();return;}
				else setstatelabel("spawn");
			}
		}stop;
	}
}


class BossClip:HDAmmo{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Boss Clip"
		//$Sprite "RCLPA0"

		+inventory.ignoreskill;
		inventory.maxamount 12;

		scale 0.5;
		inventory.pickupmessage "Picked up a clip.";
	}
	states{
	spawn2:
		RCLP A -1;
		stop;
	}
}

class SevenMilAmmo:Ammo{
	default{
		+forcexybillboard +cannotpush
		+inventory.ignoreskill
		ammo.backpackmaxamount 80;ammo.backpackamount 0;
		height 16; radius 8;
		inventory.maxamount 40;
		xscale 0.8;yscale 0.88;
		inventory.pickupmessage "Picked up a 7.76mm round.";
	}
	states{
	spawn:
		RBRS A -1 nodelay{HDF.SplitAmmoSpawn(self);}
	}
}
class HDSpent7mm:HDDebris{
	override void postbeginplay(){
		super.postbeginplay();
		A_ChangeVelocity(random(-3,3),frandom(-0.4,0.4),0,CVF_RELATIVE);
	}
	default{
		bouncesound "misc/casing";
		xscale 0.8;yscale 0.88;
		maxstepheight 0.6;
	}
	states{
	spawn:
		RBRS A 2{angle+=45;}
		wait;
	death:
		RBRS A -1{
			actor p=spawn("SevenMilBrass",self.pos,ALLOW_REPLACE);
			p.vel=self.vel;p.vel.xy*=3;
			p.A_FaceMovementDirection();p.angle+=90;
			destroy();
		}stop;
	}
}
class SevenMilBrass:Ammo{
	default{
		+inventory.ignoreskill +forcexybillboard +cannotpush
		height 16;radius 8;ammo.backpackamount 0;
		ammo.backpackmaxamount 120;
		inventory.maxamount 60;
		xscale 0.8;yscale 0.88;
		inventory.pickupmessage "Picked up some brass.";
	}
	states{
	spawn:
		RBRS A -1 nodelay{
			HDF.SplitAmmoSpawn(self);
			A_ChangeVelocity(0,frandom(-1,1),0,CVF_RELATIVE);
		}stop;
	}
}
class HDLoose7mm:HDSpent7mm{
	override void postbeginplay(){
		HDDebris.postbeginplay();
	}
	default{
		bouncefactor 0.6;
	}
	states{
	death:
		---- A 1{
			actor a=spawn("SevenMilAmmo",pos,ALLOW_REPLACE);
			a.angle=self.angle;a.vel=self.vel;
			destroy();
		}stop;
	}
}



