// ------------------------------------------------------------
// 7.76mm Ammo
// ------------------------------------------------------------
class SevenMilAmmo:HDAmmo{
	default{
		+forcexybillboard +cannotpush
		+inventory.ignoreskill
		inventory.maxamount 70;
		xscale 0.7;yscale 0.8;
		inventory.pickupmessage "Picked up a 7.76mm round.";
		hdpickup.multipickup true;
		hdpickup.refid HDLD_SEVNMIL;
		hdpickup.nicename "7.76mm Round";
	}
	states{
	spawn:
		RBRS A -1;
	}
}
class HD7mMag:HDMagAmmo{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Liberator Magazine"
		//$Sprite "RMAGA0"

		hdmagammo.maxperunit 30;
		hdmagammo.roundtype "SevenMilAmmo";
		hdmagammo.roundbulk ENC_776_LOADED;
		hdmagammo.magbulk ENC_776MAG_EMPTY;
		hdpickup.refid HDLD_SEVNMAG;
		hdpickup.nicename "7.76mm Magazine";
		inventory.maxamount 6;
		inventory.pickupmessage "Picked up a 7.76mm magazine.";
		scale 0.8;
	}
	states{
	spawn:
		RMAG A -1;
		stop;
	spawnempty:
		RMAG B -1{
			brollsprite=true;brollcenter=true;
			roll=randompick(0,0,0,0,2,2,2,2,1,3)*90;
		}stop;
	}
}
class HD7mClip:HDMagAmmo{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Boss Clip"
		//$Sprite "RCLPA0"

		hdmagammo.maxperunit 10;
		hdmagammo.roundtype "SevenMilAmmo";
		hdmagammo.roundbulk ENC_776;
		hdmagammo.magbulk ENC_776CLIP_EMPTY;
		hdpickup.refid HDLD_SEVCLIP;
		hdpickup.nicename "7.76mm Clip";
		inventory.maxamount 20;
		inventory.pickupmessage "Picked up a 7.76mm clip.";
		scale 0.6;
	}
	states(actor){
	spawn:
		RCLP ABCDE -1 nodelay{
			int amt=mags[0];
			if(amt>8)frame=0;
			else if(amt>6)frame=1;
			else if(amt>4)frame=2;
			else if(amt>2)frame=3;
			else if(amt>0)frame=4;
		}stop;
	spawnempty:
		RCLP F -1{
			brollsprite=true;brollcenter=true;
			roll=randompick(1,1,1,1,3,3,3,3,0,2)*90;
		}stop;
	}
}
class SevenMilBrass:HDAmmo{
	default{
		+inventory.ignoreskill +forcexybillboard +cannotpush
		height 16;radius 8;
		hdpickup.multipickup true;
		inventory.maxamount 60;
		xscale 0.7;yscale 0.8;
		inventory.pickupmessage "Picked up some brass.";
	}
	states{
	spawn:
		RBRS A -1;
		stop;
	}
}

class LiberatorEmptyMag:IdleDummy{
	override void postbeginplay(){
		super.postbeginplay();
		HDMagAmmo.SpawnMag(self,"HD7mMag",0);
		destroy();
	}
}
class HDSpent7mm:HDUPK{
	override void postbeginplay(){
		super.postbeginplay();
		A_ChangeVelocity(random(-3,3),frandom(-0.4,0.4),0,CVF_RELATIVE);
	}
	default{
		+missile
		height 4;radius 2;
		bouncetype "doom";
		hdupk.pickuptype "SevenMilBrass";
		hdupk.pickupmessage "Picked up some brass.";
		hdupk.multipickup true;

		bouncesound "misc/casing";
		xscale 0.7;yscale 0.8;
		maxstepheight 0.6;
	}
	states{
	spawn:
		RBRS A 2{
			angle+=45;
			if(floorz==pos.z&&!vel.z)A_Countdown();
		}wait;
	death:
		RBRS A -1{
			actor p=spawn("SevenMilBrass",self.pos,ALLOW_REPLACE);
			p.vel=self.vel;p.vel.xy*=3;
			p.A_FaceMovementDirection();p.angle+=90;
			destroy();
		}stop;
	}
}
class HDLoose7mm:HDSpent7mm{
	override void postbeginplay(){
		HDUPK.postbeginplay();
	}
	default{
		bouncefactor 0.6;
		hdupk.pickuptype "SevenMilAmmo";
		hdupk.pickupmessage "Picked up a 7.76mm round.";
	}
	states{
	death:
		---- A 1{
			actor a=spawn("SevenMilAmmo",pos,ALLOW_REPLACE);
			a.angle=self.angle;a.vel=self.vel;
			destroy();
		}stop;
	}
}

class HD7mBoxPickup:HDUPK{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Box of 7.76mm"
		//$Sprite "7BOXA0"

		scale 0.4;
		hdupk.amount 50;
		hdupk.pickupsound "weapons/pocket";
		hdupk.pickupmessage "Picked up some 7.76mm ammo.";
		hdupk.pickuptype "SevenMilAmmo";
	}
	states{
	spawn:
		7BOX A -1;
	}
}

