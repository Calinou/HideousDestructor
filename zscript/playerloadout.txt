// ------------------------------------------------------------
// Loadout-related stuff!
// ------------------------------------------------------------
extend class HDPlayerPawn{
	//basic stuff every player should have
	void GiveBasics(){
		if(!player)return;
		A_GiveInventory("HDFist");
		A_GiveInventory("SelfBandage");
		A_GiveInventory("HDFragGrenades");
		A_GiveInventory("MagManager");
	}
}

//loadout common to all soldier classes
class SoldierExtras:HDPickup{
	default{
		hdpickup.refid HDLD_SOLDIER;
		hdpickup.nicename "Elite Soldier Kit";
		hdpickup.fitsinbackpack false;
	}
	states{
	pickup:
		TNT1 A 0{
			A_SetInventory("PortableMedikit",max(1,countinv("PortableMedikit")));
			A_SetInventory("PortableStimpack",max(2,countinv("PortableStimpack")));
			A_SetInventory("GreenArmourWorn",1);

			if(!countinv("HDPistol"))A_GiveInventory("HDPistol",1);
			A_SetInventory("HD9mMag15",max(3,countinv("HD9mMag15")));

			A_SetInventory("HDFragGrenadeAmmo",max(3,countinv("HDFragGrenadeAmmo")));
			A_SetInventory("DERPUsable",max(1,countinv("DERPUsable")));
			A_SetInventory("PortableLadder",max(1,countinv("PortableLadder")));
		}fail;
	}
}



//reset inventory
class InvReset:Inventory{
	void ReallyClearInventory(actor resetee,bool keepkeys=false){
		for(inventory item=resetee.inv;item!=null;item=!item?null:item.inv){
			if(
				(!keepkeys||!(item is "Key"))
			){
				item.destroy();
				item=resetee.inv;
			}
		}
		resetee.ClearInventory();
	}
	void GiveStartItems(actor resetee){
		//now get all the "dropitems" (i.e. player's startitems) and give them
		let drop=getdefaultbytype(resetee.getclass()).getdropitems();
		if(drop){
			for(dropitem di=drop;di;di=di.Next){
				if(di.Name=='None')continue;
				resetee.A_GiveInventory(di.Name,di.Amount);
			}
		}
		let d=HDPlayerPawn(resetee);
		if(d)d.GiveCustomItems(d.classloadout);
	}
	override void attachtoowner(actor other){
		reallyclearinventory(other);
		givestartitems(other);
		destroy();
	}
}
class DoomguyLoadout:InvReset{
	override void attachtoowner(actor other){
		reallyclearinventory(other,true);
		let d=HDPlayerPawn(other);
		if(d)d.GiveBasics();
		other.A_GiveInventory("HDPistol");
		other.A_GiveInventory("HD9mMag15",2);
		other.A_GiveInventory("HDPistolAmmo",4);
		HDWeaponSelector.Select(other,"HDPistol");
		destroy();
	}
}
//wait 1 tick and then select a weapon
//used to override default to fist on weapon removal
class HDWeaponSelector:Thinker{
	actor other;
	class<Weapon> weptype;
	static void Select(actor caller,class<Weapon> weptype){
		let thth=new("HDWeaponSelector");
		thth.weptype=weptype;
		thth.other=caller;
	}
	int ticker;
	override void Tick(){
		ticker++;
		if(ticker<10)return;
		other.A_SelectWeapon(weptype);
		destroy();
	}
}




/*
	CUSTOM LOADOUTS!
	000s: misc
	100s: weapons, in format 1xy: x=slot, y=priority
	200s: ammo, no spares allowed
*/
const HDCONST_CUSTOMWEPOFFSET=200;
class CustomLoadout:InventoryFlag{
	override void tick(){
		let hdp=HDPlayerPawn(owner);
		if(hdp){
			hdp.GiveBasics();
			hdp.GiveCustomLoadout();
			hdp.A_GiveInventory("LoadoutCode",999);
		}destroy();
	}
}
extend class HDPlayerPawn{
	void GiveCustomLoadout(){
		int ldt;int lda;
		name newwep="";
		for(int i=100;i>=0;i--){ //reverse order so top weapons are added last
			ldt=getcvar(string.format("hd_load%i",i));
			lda=max(0,getcvar(string.format("hd_load%ia",i)));

			name nw=NumForInv(ldt,lda);
			if(nw!="")newwep=nw;
		}
		if(player && newwep!=""){
			player.pendingweapon=weapon(findinventory(newwep));
		}
	}
	name NumForInv(int num,int amt){
		name nw="";
		if(!num||!amt)return "";
		else if(num>=HDCONST_CUSTOMWEPOFFSET){ //weapons
			num-=HDCONST_CUSTOMWEPOFFSET;
			switch(num){
			case 0:
				nw="HDFragGrenades";A_GiveInventory(nw);
				A_GiveInventory("HDFragGrenadeAmmo",max(1,amt));
				break;
			case 10:
				nw="Lumberjack";A_GiveInventory(nw);
				break;
			case 20:
			case 21:
				int which=0;
				//give 2 pistols at once
				if(amt>1){
					A_GiveInventory("HDPistol",2);
					nw="HDPistol";
					which=3;
				}else if(countinv("HDPistol")){
					A_GiveInventory("HDPistol");
					which=2;
				}else{
					nw="HDPistol";A_GiveInventory(nw);
					which=1;
				}
				//give auto to either or both
				if(num==21){
					let ppp=hdpistol(findinventory("HDPistol"));
					if(which&2)ppp.weaponstatus2[PISS_AUTO]=0;
					if(which&1)ppp.weaponstatus[PISS_AUTO]=0;
				}
				break;
			case 22:
				nw="HDSMG";A_GiveInventory(nw);
				break;
			case 30:
				nw="Slayer";A_GiveInventory(nw);
				break;
			case 31:
				nw="Hunter";A_GiveInventory(nw);
				break;
			case 40:
			case 41:
			case 42:
				nw="ZM66AssaultRifle";A_GiveInventory(nw);
				let zzz=hdweapon(findinventory("ZM66AssaultRifle"));
				if(zzz){
					if(num<42){
						zzz.weaponstatus[0]|=ZM66F_NOLAUNCHER;
						if(num==40)zzz.weaponstatus[0]|=ZM66F_NOFIRESELECT;
					}
				}
				break;
			case 43:
				nw="Vulcanette";A_GiveInventory(nw);
				break;
			case 50:
				nw="Blooper";A_GiveInventory(nw);
				break;
			case 51:
				nw="HDRL";A_GiveInventory(nw);
				break;
			case 60:
			case 61:
				nw="LiberatorRifle";A_GiveInventory(nw);
				let llb=hdweapon(findinventory("LiberatorRifle"));
				if(llb){
					if(num==60){
						llb.weaponstatus[0]|=LIBF_NOLAUNCHER;
					}
				}
				break;
			case 62:
				nw="ThunderBuster";A_GiveInventory(nw);
				break;
			case 70:
				nw="Brontornis";A_GiveInventory(nw);
				break;
			case 71:
				nw="BFG9k";A_GiveInventory(nw);
				break;
			case 80:
				nw="BossRifle";A_GiveInventory(nw);
				break;
			case 90:
				nw="HDMedikitter";A_GiveInventory(nw);
				break;
			default:
				break;
			}
			return nw;
		}
		else if(num>=100){ //ammo
			switch(num){
				case 100:A_GiveInventory("HDBattery",amt);break;
				case 101:A_GiveInventory("HD9mMag15",amt);break;
				case 102:A_GiveInventory("HD9mMag30",amt);break;
				case 103:A_GiveInventory("HDShellAmmo",amt);break;
				case 104:A_GiveInventory("HD4mMag",amt);break;
				case 105:A_GiveInventory("HDRocketAmmo",amt);break;
				case 106:A_GiveInventory("HEATAmmo",amt);break;
				case 107:A_GiveInventory("HD7mMag",amt);break;
				case 108:A_GiveInventory("HD7mClip",amt);break;
				case 109:A_GiveInventory("BrontornisRound",amt);break;
				case 110:A_GiveInventory("HDFragGrenadeAmmo",amt);break;
				case 111:A_GiveInventory("HDPistolAmmo",amt);break;
				case 112:A_GiveInventory("FourMilAmmo",amt);break;
				case 113:A_GiveInventory("SevenMilAmmo",amt);break;
				default:break;
			}
		}
		else{ //all other shit
			switch(num){
				case 1:
					A_TakeInventory("HDArmourWorn");
					A_GiveInventory("GreenArmourWorn");break;
				case 2:
					A_TakeInventory("HDArmourWorn");
					A_GiveInventory("BlueArmourWorn");break;
				case 3: A_GiveInventory("PortableRadsuit",amt);break;

				case 4: A_GiveInventory("PortableMedikit",amt);break;
				case 5: A_GiveInventory("PortableStimpack",amt);break;
				case 6: A_GiveInventory("PortableBerserkPack",amt);break;
				case 7: A_GiveInventory("PortableLadder",amt);break;
				case 8: A_GiveInventory("PortableLiteAmp");break;
				case 9: A_GiveInventory("HDBPPreset",999);break;
				case 10: A_GiveInventory("DERPUsable",amt);break;
				case 11: A_GiveInventory("HERPUsable",amt);break;
				case 12: A_GiveInventory("DoorBuster",amt);break;
				case 13: A_GiveInventory("HDIEDKit",amt);break;

				case 14: A_GiveInventory("MapRevealer",amt);break;
				case 15: A_GiveInventory("RedCard",amt);break;
				case 16: A_GiveInventory("BlueCard",amt);break;
				case 17: A_GiveInventory("YellowCard",amt);break;

				case 18: A_GiveInventory("GreenArmourWorn");break;
				case 19:
					if(!countinv("HDArmourWorn"))A_GiveInventory("BlueArmourWorn",HDCONST_BLUEARMOUR);
					break;
				case 99:
					A_GiveInventory("SoldierExtras",amt);
					return "HDPistol";
					break;
				default:break;
			}
		}
		return "";
	}
}








//this is the new shit


//these constants need to be defined ONLY
//where an item needs to be selectable
//through custom loadouts.

const HDLD_SOLDIER="sol";

const HDLD_NINEMIL="9mm";
const HDLD_NIMAG15="915";
const HDLD_NIMAG30="930";

const HDLD_SEVNMIL="7mm";
const HDLD_SEVNMAG="730";
const HDLD_SEVCLIP="710";

const HDLD_FOURMIL="4mm";
const HDLD_FOURMAG="450";

const HDLD_BATTERY="bat";
const HDLD_SHOTSHL="shl";
const HDLD_ROCKETS="rkt";
const HDLD_HEATRKT="rkh";
const HDLD_BROBOLT="brb";
const HDLD_GREFRAG="frg";

const HDLD_STIMPAK="stm";
const HDLD_MEDIKIT="med";
const HDLD_BERSERK="zrk";
const HDLD_RADSUIT="rad";
const HDLD_LITEAMP="lit";
const HDLD_LADDER= "lad";
const HDLD_DOORBUS="dbs";
const HDLD_IEDKIT= "ied";
const HDLD_BACKPAK="bak";

const HDLD_DERPBOT="drp";
const HDLD_HERPBOT="hrp";

const HDLD_ARMGINV="arm";
const HDLD_ARMGREN="arg";
const HDLD_ARMBLUE="arb";
const HDLD_ARWGREN="awg";
const HDLD_ARWBLUE="awb";

const HDLD_FIST=    "fis";
const HDLD_CHAINSW= "cha";
const HDLD_PISTOL= "pis"; 
const HDLD_PISTAUT="pia";
const HDLD_SMG    ="smg";
const HDLD_HUNTER= "hun";
const HDLD_SLAYER= "sla";
const HDLD_ZM66GL= "z66";
const HDLD_ZM66AUT="z6a";
const HDLD_ZM66SMI="z6s";
const HDLD_VULCETT="vul";
const HDLD_LAUNCHR="lau";
const HDLD_BLOOPER="blo";
const HDLD_THUNDER="thu";
const HDLD_LIBGL=  "lib";
const HDLD_LIBNOGL="lia";
const HDLD_BFG=    "bfg";
const HDLD_BRONTO= "bro";
const HDLD_BOSS=   "bos";



class clg:CustomLoadoutGiver{}
class CustomLoadoutGiver:Inventory{
	//must be DoEffect as AttachToOwner and Pickup are not called during a range reset!
	override void doeffect(){
		let hdp=HDPlayerPawn(owner);
		if(hdp)hdp.GiveCustomItems(hdp.classloadout);
		destroy();
	}
}
extend class HDPlayerPawn{
	string startingloadout;property startingloadout:startingloadout;
	void GiveCustomItems(string loadinput){
		if(!player)return;
		if(HDPlayerPawn(self))HDPlayerPawn(self).GiveBasics();
		if(hd_forcepistolstart){
			A_GiveInventory("DoomguyLoadout");
			return;
		}
		if(loadinput=="")return;

		array<string> whichitem;whichitem.clear();
		array<int> howmany;howmany.clear();
		array<string> loadlist;loadlist.clear();

		if(loadinput.left(3)~=="hd_"){
			loadinput=cvar.getcvar(loadinput,player).getstring();
		}
		string firstwep="";

		loadinput.replace(" ","");
		loadinput.split(loadlist,"-");
		loadlist[0].split(whichitem,",");
		if(hd_debug)A_Log("Loadout: "..loadlist[0]);
		for(int i=0;i<whichitem.size();i++){
			howmany.push((whichitem[i].mid(3,whichitem[i].length())).toint(10));
			whichitem[i]=whichitem[i].left(3);

			//handle it directly now if keys or map
			if(whichitem[i]=="map")A_GiveInventory("MapRevealer");
			else if(whichitem[i]=="key"){
				int whichkeys=howmany[i];
				if(whichkeys&4)A_GiveInventory("BlueCard");
				if(whichkeys&2)A_GiveInventory("YellowCard");
				if(whichkeys&1)A_GiveInventory("RedCard");
			}
		}
		for(int i=0;i<allactorclasses.size();i++){
			class<actor> reff=allactorclasses[i];
			if(reff is "HDPickup"||reff is "HDWeapon"){
				string ref;
				if(reff is "HDPickup")ref=getdefaultbytype((class<hdpickup>)(reff)).refid;
				else ref=getdefaultbytype((class<hdweapon>)(reff)).refid;
				if(ref~=="none"||ref=="")continue;
				for(int j=0;j<whichitem.size();j++){
					if(whichitem[j]~==ref){
						A_GiveInventory(reff.getclassname(),howmany[j]);
						if(!j){
							//some codes give weapons but are not themselves weapons
							if(ref~==HDLD_LIBNOGL)firstwep="LiberatorRifle";
							else if(
								ref~==HDLD_SOLDIER
								||ref~==HDLD_PISTAUT
							)firstwep="HDPistol";
							else if(
								ref~==HDLD_ZM66AUT
								||ref~==HDLD_ZM66SMI
							)firstwep="ZM66AssaultRifle";

							//otherwise, just look to the weapon
							else if(reff is "HDWeapon")firstwep=reff.getclassname();
						}
					}
				}
			}
		}

		//attend to backpack and contents
		if(loadinput.indexof("-")>=0){
			if(hd_debug)A_Log("Backpack Loadout: "..loadlist[1]);
			A_GiveInventory("HDBackpack");
			hdbackpack(findinventory("HDBackpack")).initializeamount(loadlist[1]);
		}

		//select the correct weapon
		HDWeaponSelector.Select(self,firstwep);
	}
}


class LoadoutCode:custominventory{
	default{
		inventory.maxamount 999;
	}
	states{
	pickup:
		TNT1 A 0{
			string lll="";
			bool first=true;
			for(inventory hdppp=inv;hdppp!=null;hdppp=hdppp.inv){
				let hdw=hdweapon(hdppp);
				let hdp=hdpickup(hdppp);
				string refid=(hdw?hdw.refid:hdp?hdp.refid:"");
				if(refid=="")continue;
				if(first){
					lll=refid.." "..hdppp.amount;
					first=false;
				}else if(hdw&&hdw==player.readyweapon){
					//readyweapon gets first position
					lll=hdw.refid.." 1, "..lll;
				}else{
					//append all items to end
					lll=lll..", "..refid.." "..hdppp.amount;
				}
			}

			let bp=HDBackpack(findinventory("HDBackpack"));
			if(bp){
				int imax=bp.invclasses.size();
				bool first=true;
				if(bp.bulk>0){
					lll=lll.." - ";
					for(int i=0;i<imax;i++){
						if(bp.havenone(i))continue;
						let wep=(class<hdweapon>)(bp.invclasses[i]);
						let mag=(class<hdmagammo>)(bp.invclasses[i]);
						let pkup=(class<hdpickup>)(bp.invclasses[i]);
						int amt;
						if(wep||mag){
							array<string> amts;amts.clear();
							bp.amounts[i].split(amts," ");
							if(wep)amt=amts.size()/8;
							else amt=amts.size();
						}else{
							amt=bp.amounts[i].toint(10);
						}
						if(first)first=false;else lll=lll..", "; 
						lll=lll..bp.refids[i].." "..amt;
					}
				}
			}

			string outstring="The loadout code for your current gear is:\n"..(lll==""?"nothing, you're naked.":"\cy"..lll);
			A_Log(outstring,true);
			if(invoker.amount>900){
				string warning="\cxPLEASE UPDATE YOUR \cyHD_LOADOUT1\cx SETTING ACCORDINGLY.";
				A_Log(warning);
				A_Print(outstring.."\n\n\n"..warning.."\n\n\cuThis custom loadout playerclass will be \cgREMOVED\cu in a future version.",10,"smallfont");
			}
		}fail;
	}
}


class LoadoutItemList:CustomInventory{
	states{
	pickup:
		TNT1 A 0{
			string blah="All loadout codes for all items including loaded mods:";
			for(int i=0;i<allactorclasses.size();i++){
				class<actor> reff=allactorclasses[i];
				string ref="";
				string nnm="";
				if(reff is "HDPickup"){
					let gdb=getdefaultbytype((class<hdpickup>)(reff));
					nnm=gdb.nicename;if(nnm=="")nnm=gdb.getclassname();
					ref=gdb.refid;
				}else if(reff is "HDWeapon"){
					let gdb=getdefaultbytype((class<hdweapon>)(reff));
					nnm=gdb.nicename;if(nnm=="")nnm=gdb.getclassname();
					ref=gdb.refid;
				}
				if(ref!=""){
					blah=blah.."\n"..ref.."   "..nnm;
				}
			}
			A_Log(blah,true);
		}fail;
	}
}






//this is the new... backpack


const HDCONST_BPMAX=1000;
class HDBackpack:HDWeapon{
	int index;

	array<string> nicenames;
	array<string> refids;
	array<string> invclasses;

	array<string> amounts;
	//because the last time I tried nested dynamic arrays it did not work.
	//"10" for 10 rounds/medikits/whatever; "0 10 20" for 3 mags
	//"0 0 0 0 0 0 0 0 0" for a single weapon: first # is bulk

	default{
		//$Category "Items/Hideous Destructor/Gear"
		//$Title "Backpack"
		//$Sprite "BPAKA0"

		+inventory.invbar +weapon.wimpy_weapon
		+weapon.no_auto_switch
		weapon.selectionorder 1010;
		inventory.icon "BPAKA0";
		inventory.pickupmessage "Picked up a backpack to help fill your life with ammo!";
		inventory.pickupsound "weapons/pocket";

		hdweapon.nicename "Backpack";
		hdweapon.refid HDLD_BACKPAK;
		hdweapon.fitsinbackpack true;
	}
	override inventory createtossable(int amount){
		target=owner;
		let ct=hdbackpack(super.createtossable(amount));
		ct.weaponbulk();
		if(ct&&target&&ct.bulk>0){
			ct.target=target;
			ct.translation=target.translation;
		}
		return ct;
	}

	int bpindex;
	override void InitializeWepStats(bool idfa){
		if(idfa)return;
		nicenames.clear();
		refids.clear();
		invclasses.clear();
		for(int i=0;i<allactorclasses.size();i++){
			class<actor> reff=allactorclasses[i];
			string ref="";
			string nnm="";
			if(reff is "HDPickup"){
				let gdb=getdefaultbytype((class<hdpickup>)(reff));
				if(gdb.fitsinbackpack){
					nnm=gdb.nicename;
					ref=gdb.refid;
				}
			}else if(reff is "HDWeapon"){
				let gdb=getdefaultbytype((class<hdweapon>)(reff));
				if(gdb.fitsinbackpack){
					nnm=gdb.nicename;
					ref=gdb.refid;
				}
			}
			if(nnm!=""){
				invclasses.push(reff.getclassname());
				nicenames.push(nnm);
				refids.push(ref);
				amounts.push("");
			}
		}
		bpindex=invclasses.find(getclassname());
	}
	void initializeamount(string loadlist){
		array<string> whichitem;whichitem.clear();
		array<int> howmany;howmany.clear();
		loadlist.replace(" ","");
		loadlist.split(whichitem,",");
		for(int i=0;i<whichitem.size();i++){
			howmany.push((whichitem[i].mid(3,whichitem[i].length())).toint(10));
			whichitem[i]=whichitem[i].left(3);
		}
		for(int i=0;i<whichitem.size();i++){
			string ref=whichitem[i];
			if(ref=="")continue;
			int refindex=refids.find(ref);
			if(refindex>=refids.size())continue;

			let wep=(class<hdweapon>)(invclasses[refindex]);
			let mag=(class<hdmagammo>)(invclasses[refindex]);
			let pkup=(class<hdpickup>)(invclasses[refindex]);

			if(wep||mag){
				for(int j=0;j<howmany[i];j++){
					let iii=inventory(spawn(invclasses[refindex],pos,ALLOW_REPLACE));
					if(owner)iii.attachtoowner(owner);
					itemtobackpack(iii);
					if(iii)iii.destroy();
					
				}
			}else if(pkup){
				double bulkmax=(HDCONST_BPMAX-bulk)/max(1,getdefaultbytype(pkup).bulk);
				int addamt=max(0,min(bulkmax,howmany[i]));
				int amt=amounts[refindex].toint(10);
				if(addamt>0)amounts[refindex]=""..amt+addamt;
				updatemessage(index);
			}
		}
	}

	double bulk;
	override double,int weaponbulk(){
		double blk=0;
		for(int i=0;i<invclasses.size();i++){
			if(havenone(i))continue;
			class<actor> reff=invclasses[i];
			array<string> theseamounts;
			theseamounts.clear();
			if(((class<hdmagammo>)(reff))){
				amounts[i].split(theseamounts," ");
				let mmm=getdefaultbytype((class<hdmagammo>)(reff));
				bool armour=!!((class<HDArmour>)(reff));
				for(int j=0;j<theseamounts.size();j++){
					if(armour)blk+=theseamounts[j].toint()>=1000?ENC_BLUEARMOUR/2:ENC_GREENARMOUR;
					else blk+=mmm.magbulk+theseamounts[j].toint()*mmm.roundbulk;
				}
			}else if(((class<hdweapon>)(reff))){
				amounts[i].split(theseamounts," ");
				for(int j=0;j<theseamounts.size();j++){
					if(!((j+1)%9))blk+=theseamounts[j].toint();
				}
			}else if(((class<hdpickup>)(reff))){
				let classref=((class<hdpickup>)(reff));
				//presets required for default ammos because of One Man Army
				double unitbulk;
				if(classref is "HDShellAmmo")unitbulk=ENC_SHELL;
				else if(classref is "FourMilAmmo")unitbulk=ENC_426;
				else if(classref is "SevenMilAmmo")unitbulk=ENC_776;
				else if(classref is "HDPistolAmmo")unitbulk=ENC_9;
				else if(classref is "HDRocketAmmo")unitbulk=ENC_ROCKET;
				else if(classref is "HDBattery")unitbulk=ENC_BATTERY;
				else if(classref is "BrontornisRound")unitbulk=ENC_BRONTOSHELL;
				else if(classref is "HEATAmmo")unitbulk=ENC_HEATROCKET;
				else if(classref is "HDFragGrenadeAmmo")unitbulk=ENC_FRAG;
				else unitbulk=getdefaultbytype(classref).bulk;
				blk+=amounts[i].toint()*unitbulk;
			}
		}
		bulk=blk;
		return max(blk*0.6,100),0;
	}
	void UpdateMessage(int num){
		if(!owner)return;
		int nnsiz=nicenames.size();
		int maxindex=nicenames.size()-1;
		if(num==index)weaponbulk();
		if(num!=index&&owner){
			int toadd=num>index?1:-1;
			do{
				index+=toadd;
				if(index>maxindex)index=0;
				else if(index<0)index=maxindex;
			}while(index!=bpindex&&havenone(index)&&(!owner.countinv(invclasses[index])));
		}else if(max(0-num,num-nnsiz)<maxindex){
			while(num<0)num+=nnsiz;
			while(num>maxindex)num-=nnsiz;
			index=num;
		}

		//translate "amounts" into an actual number of items
		string amt=amounts[index];
		let thisclass=((class<actor>)(invclasses[index]));
		if(havenone(index))amt="\cunone";
		else if(thisclass is "HDWeapon"||thisclass is "HDMagAmmo"){
			array<string>mags;
			amt.split(mags," ");
			if(thisclass is "HDWeapon")amt="\ce"..(mags.size()/9);
			else amt="\ce"..mags.size();
		}else amt="\ce"..amt;

		//look for an actual amount on the player
		string ininv;
		if(!owner.countinv(invclasses[index]))ininv="\cunone";
		else ininv="\cc"..owner.countinv(invclasses[index]);
		string str="\n\cx"..nicenames[index]..(hd_debug?(" \cr"..invclasses[index]):"").."\n"..amt.." in pack   "..ininv.." carried\n";

		int aaa=index;int bbb=index;
		for(int i=0;i<3;i++){
			aaa--;
			if(aaa<0)aaa=maxindex;
			bool has=(amounts[aaa]!=""||(owner&&owner.countinv(invclasses[aaa])));
			str=(has?"\cs":"")..nicenames[aaa].."\n"..str;
			bbb++;
			if(bbb>maxindex)bbb=0;
			has=(amounts[bbb]!=""||(owner&&owner.countinv(invclasses[bbb])));
			str=str.."\n"..(has?"\cs":"")..nicenames[bbb];
		}

		//display selected item name and amounts carried
		str="\cs[] [] [] \cbBackpack \cs[] [] []\nzoom=fast scroll  unload/reload=take/insert\n\n\n\n"
			..str
			.."\n\nTotal Bulk: \cf"..int(bulk);

		wepmsg=str;
	}
	//generic code for inserting into backpack
	int ItemToBackpack(inventory item){
		if(item==self&&amount<2)return 1;
		let wep=HDWeapon(item);
		let mag=HDMagAmmo(item);
		let pkup=HDPickup(item);
		if(!wep&&!pkup)return false;
		index=invclasses.find(item.getclassname());
		if(index>=invclasses.size())return 1;
		if(wep){
			if(wep is "HDBackpack"&&HDBackpack(wep).bulk>0){
				if(owner)owner.A_Log("Empty this backpack first.",true);
				return 1;
			}
			if(wep.weaponbulk()+bulk>HDCONST_BPMAX){
				owner.A_Log("Your backpack is too full.",true);
				return 1;
			}
			if(wep.owner)wep=HDWeapon(owner.dropinventory(wep));
			string newwep=
				(amounts[index]==""?"":" ")..
				wep.weaponstatus[0].." "..
				wep.weaponstatus[1].." "..
				wep.weaponstatus[2].." "..
				wep.weaponstatus[3].." "..
				wep.weaponstatus[4].." "..
				wep.weaponstatus[5].." "..
				wep.weaponstatus[6].." "..
				wep.weaponstatus[7].." "..
				int(wep.weaponbulk());
			amounts[index]=amounts[index]..newwep;
			if(hd_debug){
				A_Log(nicenames[index]..":  "..wep.getclassname().."  "..newwep);
				A_Log(amounts[index]);
			}
			wep.amount--;if(wep.amount<1)wep.destroy();
			weaponbulk();
			UpdateMessage(index);
			return 12;
		}else if(mag){
			if(mag.magbulk+bulk>HDCONST_BPMAX){
				if(owner)owner.A_Log("Your backpack is too full.",true);
				return 0;
			}
			int tookmag=mag.TakeMag(false);if(item.amount<1)item.destroy();
			if(amounts[index]=="")amounts[index]=""..tookmag;
			else amounts[index]=tookmag.." "..amounts[index];
		}else{
			int units=item.owner?1:item.amount;
			if(pkup.bulk*units+bulk>HDCONST_BPMAX){
				if(owner)owner.A_Log("Your backpack is too full.",true);
				return 0;
			}
			amounts[index]=""..amounts[index].toint()+units;
			item.amount-=units;if(item.amount<1)item.destroy();
		}
		weaponbulk();
		UpdateMessage(index);
		if(pkup.multipickup)return 4;else return 10;
	}
	//returns whether empty
	bool havenone(int which){
		return(
			amounts[which]==""
			||(
				amounts[which].toint()<1
				&&!((class<HDMagAmmo>)(invclasses[which]))
				&&!((class<HDWeapon>)(invclasses[which]))
			)
		);
	}
	//generic code for removing from backpack
	int RemoveFromBackpack(int which=-1,bool trytopocket=true){
		if(which<0||which>invclasses.size())which=index;
		if(havenone(which))return 1;
		array<string>tempamounts;
		bool basicpickup=false;
		amounts[which].split(tempamounts," ");
		if(tempamounts.size()<1)return 1;
		int ticks=0;
		let wepth=(class<hdweapon>)(invclasses[which]);
		let thisclass=(class<hdpickup>)(invclasses[which]);
		if(wepth){
			let newp=HDWeapon(spawn(wepth,owner.pos+(0,0,owner.height-12),ALLOW_REPLACE));
			newp.angle=owner.angle;newp.A_ChangeVelocity(1,1,1,CVF_RELATIVE);
			for(int i=0;i<9;i++){
				if(i<newp.weaponstatus.size())newp.weaponstatus[i]=tempamounts[0].toint();
				tempamounts.delete(0);
			}
			if(trytopocket&&owner.countinv(wepth)<getdefaultbytype(wepth).maxamount){
				newp.actualpickup(owner);
				ticks=12;
			}else{
				ticks=10;
			}
		}
		if(thisclass){
			int thisamt=tempamounts[0].toint();
			bool multipi=getdefaultbytype(thisclass).multipickup;
			if(thisclass is "HDMagAmmo"){
				if(trytopocket)HDMagAmmo.GiveMag(owner,thisclass,thisamt);
				else HDMagAmmo.SpawnMag(owner,thisclass,thisamt);
				tempamounts.delete(0);
			}else{
				basicpickup=true;
				thisamt--;
				if(!trytopocket||A_JumpIfInventory(thisclass,0,"null")){
					int moar=0;
					if(multipi&&thisamt>0)moar=min(random(10,50),thisamt);
					let iii=inventory(spawn(thisclass,owner.pos+(0,0,owner.height-20),ALLOW_REPLACE));
					iii.angle=owner.angle;iii.vel=owner.vel;
					iii.A_ChangeVelocity(1,0,1,CVF_RELATIVE);
					iii.amount=1+moar;
					thisamt-=moar;
				}
				else HDF.Give(owner,thisclass,1);
				tempamounts[0]=""..thisamt;
			}
			//allow continuous move for smaller items
			if(multipi)ticks=6;else ticks=10;
		}
		//put the string back together
		string newamounts="";
		if(basicpickup){
			if(tempamounts[0].toint()>0)newamounts=tempamounts[0];
		}else{
			for(int i=0;i<tempamounts.size();i++){
				newamounts=newamounts..(i?" ":"")..tempamounts[i];
			}
		}
		amounts[index]=newamounts;
		UpdateMessage(index);
		return ticks;
	}
	//main interface
	action void A_BPReady(){
		if(pressingzoom()){
			int inputamt=player.cmd.pitch>>5;
			invoker.UpdateMessage(invoker.index-inputamt);
			HijackMouse();
		}
		int ttt=1;
		if(justpressed(BT_ATTACK))invoker.UpdateMessage(invoker.index-1);
		else if(justpressed(BT_ALTATTACK))invoker.UpdateMessage(invoker.index+1);
		else if(pressingreload()&&countinv(invoker.invclasses[invoker.index])){
			ttt=invoker.ItemToBackpack(findinventory(invoker.invclasses[invoker.index]));
		}else if(pressingunload()){
			ttt=invoker.RemoveFromBackpack(invoker.index,true);
		}else if(pressing(BT_ALTRELOAD)){
			invoker.RemoveFromBackpack(invoker.index,false);
			invoker.updatemessage(invoker.index+1);
			if(self is "HDPlayerPawn")ttt=randompick(0,0,0,0,0,1);
			if(!invoker.bulk){
				DropInventory(invoker);
				return;
			}
		}
		if(!pressing(BT_ALTRELOAD)){
			A_WeaponMessage(invoker.wepmsg,ttt+1);
			A_SetTics(max(1,ttt));
		}else A_SetTics(ttt);
		A_WeaponReady(
			WRF_NOFIRE|WRF_ALLOWUSER3
			|((player.cmd.buttons&(BT_RELOAD|BT_UNLOAD|BT_USE))?WRF_DISABLESWITCH:0)
		);
	}
	states{
	spawn:
		BPAK ABC -1 nodelay{
			invoker.weaponbulk();
			if(!invoker.bulk)frame=1;
			else if(target){
				translation=target.translation;
				frame=2;
			}
			invoker.bno_auto_switch=false;
		}
	select0:
		TNT1 A 10{
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			if(invoker.bulk>(HDBPC_CAPACITY*0.7))A_SetTics(20);
			if(invoker.havenone(invoker.index))invoker.updatemessage(invoker.index+1);
			else invoker.UpdateMessage(invoker.index);
		}goto super::select0;
	ready:
		TNT1 A 1 A_BPReady();
		goto readyend;
	nope:
		TNT1 A 0 A_WeaponMessage(invoker.wepmsg,5);
		goto super::nope;
	}
}









//semi-filled backpacks at random
class WildBackpack:IdleDummy replaces Backpack{
		//$Category "Items/Hideous Destructor/Gear"
		//$Title "Backpack (Random Spawn)"
		//$Sprite "BPAKC0"
	override void postbeginplay(){
		super.postbeginplay();
		let aaa=HDBackpack(spawn("HDBackpack",pos,ALLOW_REPLACE));
		aaa.RandomContents();
	}
}

//THIS GIVES WRONG RESULTS.
//The problem is that the bulk is not actually set on ammo - see encumbrance.txt
extend class HDBackpack{
	void RandomContents(){
		for(int i=0;i<5;i++){
			int thisitem=random(1,invclasses.size())-1;
			let wep=(class<hdweapon>)(invclasses[thisitem]);
			let pkup=(class<hdpickup>)(invclasses[thisitem]);
			let mag=(class<hdmagammo>)(pkup);
			if(wep){
				let iii=inventory(spawn(wep));
				itemtobackpack(iii);
			}else if(mag){
				int howmany=max(1,random(1,
					min(
						getdefaultbytype(mag).maxamount>>1,
						HDCONST_BPMAX/(
							max(1.,getdefaultbytype(mag).roundbulk)
							*max(1.,getdefaultbytype(mag).magbulk)
							*3.
						)
					)
				));
				int magmax=getdefaultbytype(mag).maxperunit;
				string ati=amounts[thisitem];
				for(int i=0;i<howmany;i++){
					int mmx=min(magmax,magmax*frandom(0.1,5.));
					if(ati=="")ati=ati..mmx;
					else ati=ati.." "..mmx;
				}
				amounts[thisitem]=ati;
			}else if(pkup){
				int howmany=max(1,random(1,
					min(
						getdefaultbytype(pkup).maxamount>>1,
						HDCONST_BPMAX/(max(1.,getdefaultbytype(pkup).bulk)*3.)
					)
				));
				if(
					pkup is "BluePotion"
					||pkup is "SquadSummoner"
				){
					howmany=random(-2,howmany);
				}
				amounts[thisitem]=""..howmany;
				if(amounts[thisitem].toint()<1)amounts[thisitem]="";
			}
		}
		weaponbulk();
		updatemessage(index);
	}
}



class InsurgentLoadout:Inventory{
	override void Tick(){
		if(!owner){destroy();return;}
		//pick one or two random weapons
		class<inventory> ammoforwep=null;
		for(int i=0;i<randompick(1,1,1,1,1,1,1,1,1,2,2,2,3);i++){
			switch(4+random(0,13)){
			case 0:
				owner.A_GiveInventory("HDPistol");
				ammoforwep="HD9mMag15";
				break;
			case 1:
				owner.A_GiveInventory("ZM66Semi");
				ammoforwep="HD4mMag";
				break;
			case 2:
				owner.A_GiveInventory("Hunter");
				ammoforwep="HDShellAmmo";
				break;
			case 3:
				owner.A_GiveInventory("Slayer");
				ammoforwep="HDShellAmmo";
				break;
			case 4:
				owner.A_GiveInventory("Lumberjack");
				ammoforwep="HDBattery";
				break;
			case 5:
				owner.A_GiveInventory("LiberatorNoGL");
				ammoforwep="HD7mMag";
				break;
			case 6:
				owner.A_GiveInventory("ZM66Regular");
				ammoforwep="HD4mMag";
				break;
			case 7:
				owner.A_GiveInventory("HDSMG");
				ammoforwep="HD9mMag30";
				break;
			case 8:
				owner.A_GiveInventory("BossRifle");
				ammoforwep="HD7mClip";
				break;
			case 9:
				owner.A_GiveInventory("HDAutoPistol");
				ammoforwep="HD9mMag15";
				break;
			case 10:
				owner.A_GiveInventory("ZM66AssaultRifle");
				ammoforwep="HD4mMag";
				owner.A_SetInventory("HDRocketAmmo",max(owner.countinv("HDRocketAmmo"),random(0,4)));
				break;
			case 11:
				owner.A_GiveInventory("LiberatorRifle");
				ammoforwep="HD7mMag";
				owner.A_SetInventory("HDRocketAmmo",max(owner.countinv("HDRocketAmmo"),random(0,4)));
				break;
			case 12:
				owner.A_GiveInventory("Vulcanette");
				ammoforwep="HD4mMag";
				owner.A_SetInventory("HDBattery",max(owner.countinv("HDBattery"),random(0,1)));
				break;
			case 13:
				owner.A_GiveInventory("Blooper");
				ammoforwep="HDRocketAmmo";
				break;
			case 14:
				owner.A_GiveInventory("HDRL");
				ammoforwep="HDRocketAmmo";
				break;
			case 15:
				owner.A_GiveInventory("ThunderBuster");
				ammoforwep="HDBattery";
				break;
			case 16:
				owner.A_GiveInventory("Brontornis");
				ammoforwep="BrontornisRound";
				break;
			case 17:
				owner.A_GiveInventory("BFG9k");
				ammoforwep="HDBattery";
				break;
			default:
				break;
			}
			//give some random ammo for the new weapon
			if(ammoforwep)owner.A_SetInventory(
				ammoforwep,
				max(1,random(0,getdefaultbytype(ammoforwep).maxamount/3))
			);
		}
		//give random other gear, make 3 rolls, amount max(1,random(0,maxamount/3))
		array<string> supplies;supplies.clear();
		for(int i=0;i<allactorclasses.size();i++){
			let thisclass=((class<hdpickup>)(allactorclasses[i]));
			if(thisclass && getdefaultbytype(thisclass).refid!=""){
				supplies.push(thisclass.getclassname());
			}
		}
		int imax=random(3,6);
		int smax=supplies.size()-1;
		for(int i=0;i<imax;i++){
			let thisclass=supplies[random(0,smax)];
			int maxamt=getdefaultbytype((class<inventory>)(thisclass)).maxamount;
			owner.A_GiveInventory(thisclass,max(1,random(0,maxamt/3)));
		}
		//randomize integrity of armour
		let armourstored=HDArmour(owner.findinventory("HDArmour"));
		if(armourstored){
			armourstored.syncamount();
			bool nomega=armourstored.amount>2;
			for(int i=0;i<armourstored.amount;i++){
				if(!nomega&&!random(0,12)){
					armourstored.mags[i]=random(1001,1000+HDCONST_BLUEARMOUR);
				}else{
					armourstored.mags[i]=random(1,HDCONST_GREENARMOUR);
				}
			}
		}
		let armourworn=HDArmourWorn(owner.findinventory("HDArmourWorn"));
		if(armourworn){
			armourworn.mega=!random(0,12);
			armourworn.durability=random(1,armourworn.mega?HDCONST_BLUEARMOUR:HDCONST_GREENARMOUR);
		}
		destroy();
	}
}
