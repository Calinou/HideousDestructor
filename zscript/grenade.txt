// ------------------------------------------------------------
//   GRENADE!
// ------------------------------------------------------------

//delete this once all things have been scriptified
class frags:idledummy{
	states{
	spawn:
		TNT1 A 1 nodelay {
			A_SpawnChunks("HDBulletFrag",242,100,700);
			if(hd_debug)A_Log("deprecated frags. Please use A_SpawnChunks directly.");
		}stop;
	}
}

class HDFragGrenadeRoller:HDActor{
	int fuze;
	vector3 keeprolling;
	default{
		-noextremedeath -floorclip +shootable +noblood
		+activatemcross;-noteleport;
		+missile +bounceonactors +usebouncestate 
			bouncetype "doom";bouncesound "misc/fragknock";
		radius 4;height 4;damagetype "none";
		scale 0.3;
		obituary "%o was fragged by %k.";
		radiusdamagefactor 0.04;pushfactor 1.4;maxstepheight 2;mass 30;
	}
	states{
	spawn:
		FRAG A 0 nodelay{
			A_Frighten(256);
		}
	spawn2:
		#### BCD 2{
			if(abs(vel.z-keeprolling.z)>10)A_PlaySound("misc/fragknock",CHAN_BODY);
			else if(floorz>=pos.z)A_PlaySound("misc/fragroll");
			keeprolling=vel;
			if(abs(vel.x)<0.4 && abs(vel.y)<0.4) setstatelabel("death");
		}loop;
	bounce:
		---- A 0{
			bmissile=false;
			vel*=0.3;
		}goto spawn2;
	death:
		---- A 2{
			if(abs(vel.z-keeprolling.z)>3){
				A_PlaySound("misc/fragknock",CHAN_BODY);
				keeprolling=vel;
			}
			if(abs(vel.x)>0.4 || abs(vel.y)>0.4) setstatelabel("spawn");
		}wait;
	destroy:
		TNT1 A 1{
			bsolid=false;bpushable=false;bmissile=false;bnointeraction=true;bshootable=false;
			spawn("DistantRocket",pos);
			A_DistantQuake(4,35,512,10);
			A_PlaySound("world/explode",1,7);
			A_AlertMonsters();
			actor xpl=spawn("WallChunker",self.pos-(0,0,1),ALLOW_REPLACE);
				xpl.target=target;xpl.master=master;xpl.stamina=stamina;
			xpl=spawn("HDExplosion",self.pos-(0,0,1),ALLOW_REPLACE);
				xpl.target=target;xpl.master=master;xpl.stamina=stamina;
			A_Blast(
				pushradius:256,pushamount:128,fullpushradius:96,
				fragradius:256,fragdamage:256,
				gibradius:24,gibamount:random(2,9)*10
			);
			A_SpawnChunks("BigWallChunk",14,4,12);
			A_SpawnChunks("HDBulletFrag",360,100,800);
		}
		stop;
	}
	override void tick(){
		ClearInterpolation();
		if(globalfreeze||level.Frozen) return;
		else if(bnointeraction){
			nexttic();
			return;
		}else{
			fuze++;
			if(fuze>=140 && !bnointeraction){
				setstatelabel("destroy");
				nexttic();
				return;
			}else super.tick();
		}
	}
}
class HDFragGrenade:SlowProjectile{
	int fuze;
	vector3 keeprolling;
	default{
		-noextremedeath -floorclip +bloodlessimpact
		+shootable -noblockmap +noblood
		radius 5;height 5;damagetype "none";
		scale 0.3;
		obituary "%o was fragged by %k.";
		mass 500;
		+activatemcross;-noteleport;
	}
	override void tick(){
		ClearInterpolation();
		if(globalfreeze||level.Frozen)return;
		if(fuze<140){
			fuze++;
			keeprolling=vel;
			super.tick();
		}else{
			if(inthesky){
				spawn("DistantRocket",pos);
				A_SpawnChunks("HDBulletFrag",360,400,800);
				destroy();return;
			}
			actor gr=spawn("HDFragGrenadeRoller",pos);
			gr.target=self.target;gr.master=self.master;gr.vel=self.vel;
			gr.setstatelabel("destroy");
			destroy();return;
		}
	}
	override void postbeginplay(){
		actor.postbeginplay();
		shootz = height*0.5+8;
		grav=getgravity();
		if(target){
			speed=2+target.countinv("grenadeforce");
			pitch=target.pitch;
			if(target.countinv("powerstrength")) speed*=2;
			fuze=target.countinv("fragtimer");
			target.A_TakeInventory("fragtimer");
			target.A_TakeInventory("grenadeforce");
			a_changevelocity(cos(pitch)*speed,0,-sin(pitch)*speed,CVF_RELATIVE);
		}
	}
	states{
	spawn:
		FRAG BCD 2;
		loop;
	death:
		TNT1 A 10{
			bmissile=false;
			let gr=HDFragGrenadeRoller(spawn("HDFragGrenadeRoller",self.pos));
			if(!gr)return;
			gr.target=self.target;gr.master=self.master;
			gr.fuze=self.fuze;
			gr.vel=self.keeprolling;
			gr.keeprolling=self.keeprolling;
			gr.A_Frighten(256);
			gr.A_PlaySound("misc/fragknock",CHAN_BODY);
		}stop;
	}
}

class HDFragSpoon:HDDebris{
	default{
		scale 0.3;bouncefactor 0.8;
		bouncesound "misc/casing4";
	}
	override void postbeginplay(){
		super.postbeginplay();
		A_PlaySound("weapons/grenopen",0,2);
	}
	states{
	spawn:
		FRGP A 2{roll+=40;}wait;
	death:
		FRGP A -1;
	}
}



