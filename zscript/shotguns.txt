// ------------------------------------------------------------
// Shotgun (Common)
// ------------------------------------------------------------
class HDShotgun:HDWeapon{
	default{
		weapon.bobrangex 0.21;
		weapon.bobrangey 0.86;
		scale 0.6;
		inventory.pickupmessage "You got a shotgun!";
		obituary "%o shot with %k's gun";
	}
	int handshells;
	action void EmptyHand(int amt=-1){
		if(amt>0)invoker.handshells=amt;
		while(invoker.handshells>0){
 			if(invoker.handshells>=4){
				invoker.handshells-=4;
				A_SpawnItemEx("ShellPickup",
					cos(pitch)*1,1,height-7-sin(pitch)*1,
					cos(pitch)*cos(angle)*frandom(1,2)+vel.x,
					cos(pitch)*sin(angle)*frandom(1,2)+vel.y,
					-sin(pitch)+vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}else{
				invoker.handshells--;
				A_SpawnItemEx("HDFumblingShell",
					cos(pitch)*5,1,height-7-sin(pitch)*5,
					cos(pitch)*cos(angle)*frandom(1,4)+vel.x,
					cos(pitch)*sin(angle)*frandom(1,4)+vel.y,
					-sin(pitch)*random(1,4)+vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}
		}
	}
	action void A_UnloadSideSaddle(int slot){
		int uamt=clamp(invoker.weaponstatus[slot],0,4);
		if(!uamt)return;
		invoker.weaponstatus[slot]-=uamt;
		int maxpocket=min(uamt,ammocap("HDShellAmmo")-countinv("HDShellAmmo"));
		if(maxpocket>0&&pressingunload()){
			A_SetTics(16);
//			A_WeaponReady(WRF_NONE);
			uamt-=maxpocket;
			A_GiveInventory("HDShellAmmo",maxpocket);
		}
		A_PlaySound("weapons/pocket");
		EmptyHand(uamt);
	}
	action void A_CannibalizeOtherShotgun(){
		let hhh=hdweapon(findinventory(invoker is "Hunter"?"Slayer":"Hunter"));
		if(hhh){
			int sidesaddle=hhh is "Slayer"?SLAYS_SIDESADDLE:HUNTS_SIDESADDLE;
			int totake=min(
				hhh.weaponstatus[sidesaddle],
				AmmoCap("HDShellAmmo")-countinv("HDShellAmmo"),
				4
			);
			if(totake>0){
				hhh.weaponstatus[sidesaddle]-=totake;
				A_GiveInventory("HDShellAmmo",totake);
			}
		}
	}
	override void DetachFromOwner(){
		if(handshells>0){
			if(owner)owner.A_DropItem("HDShellAmmo",handshells);
			else A_DropItem("HDShellAmmo",handshells);
		}
		handshells=0;
		super.detachfromowner();
	}
}

