// ------------------------------------------------------------
// Sight picture crosshairs
// ------------------------------------------------------------
extend class HDStatusBar{
	virtual void DrawHDXHair(hdplayerpawn hpl){
		let wp=hdweapon(cplayer.readyweapon);
		bool sightbob=cvar.getcvar("hd_sightbob",cplayer).getbool();
		vector2 bob=hpl.hudbob;

		//have no crosshair at all
		if(
			!wp
			||hpl.barehanded
			||hpl.nocrosshair>0
			||(!sightbob&&hpl.countinv("IsMoving"))
			||abs(bob.x)>50
		)return;


		//multiple weapons use this
		string whichdot="redpxl";
		int whichdotthough=cvar.getcvar("hd_crosshair",hpl.player).getint();
		switch(whichdotthough){
			case 1:case 2:case 3:case 4:
			whichdot=string.format("riflsit%i",whichdotthough);
			default:break;
		}


		//all weapon sights go here
		SetSize(0,400,250);
		BeginHUD(forcescaled:true);

		actor hpc=hpl.scopecamera;
		int cpbt=cplayer.cmd.buttons;
		int nscp=cvar.getcvar("hd_noscope",cplayer).getint();
		float scl=clamp(cvar.getcvar("hd_xhscale",cplayer).getfloat(),0.1,3.0);
		bool scopeview=!!hpc&&(
			!nscp||
			(nscp==1&&!gimmehud)||
			cpbt&BT_ZOOM
		);
		if(hpl.findinventory("PortableLiteAmp")&&PortableLiteAmp(hpl.findinventory("PortableLiteAmp")).worn)scopeview=false;

		if((wp is "HDPistol")){
			int cx,cy,cw,ch;
			[cx,cy,cw,ch]=screen.GetClipRect();
			SetClipRect(
				-8*scl+bob.x,-4*scl+bob.y,16*scl,10*scl,
				DI_SCREEN_CENTER
			);
			vector2 bobb=bob*1.6;
			bobb.y=clamp(bobb.y,-8,8);
			drawimage(
				"frntsite",(0,0)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
				alpha:0.9,scale:(scl,scl)*0.6
			);
			screen.SetClipRect(cx,cy,cw,ch);
			drawimage(
				"backsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_TOP,
				scale:(scl,scl)*0.6
			);
		}
		else if((wp is "HDSMG"||wp is "Hunter")){
			int cx,cy,cw,ch;
			[cx,cy,cw,ch]=screen.GetClipRect();
			SetClipRect(
				-16*scl+bob.x,-4*scl+bob.y,32*scl,16*scl,
				DI_SCREEN_CENTER
			);
			vector2 bobb=bob*3;
			bobb.y=clamp(bobb.y,-8,8);
			drawimage(
				"frntsite",(0,0)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
				alpha:0.9,scale:(scl,scl)
			);
			screen.SetClipRect(cx,cy,cw,ch);
			drawimage(
				"backsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_TOP,
				scale:(scl,scl)
			);
		}
		else if((wp is "Slayer")){
			drawimage(
				"XH23",(0,1.4)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
				scale:(1.8,1.6)*scl
			);
		}
		else if(
			wp is "ZM66AssaultRifle"
		){
			int scaledyoffset=47*scl;
			if(wp.weaponstatus[0]&ZM66F_GLMODE){
				drawimage(
					"XH27",(0,1.8)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(1.6,1.6)
				);
			}else{
				double dotoff=max(abs(bob.x),abs(bob.y));
				if(dotoff<20){
					drawimage(
						whichdot,(0,0)+bob*1.6,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						alpha:0.8-dotoff*0.04,scale:(scl,scl)
					);
				}
				drawimage(
					"riflsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(scl,scl)
				);
				if(scopeview){
					double degree=(35-wp.weaponstatus[ZM66S_ZOOM])*0.2;
					texman.setcameratotexture(hpc,"HDXHCAM3",degree);
					drawimage(
						"HDXHCAM3",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.31,0.31)*scl
					);
					int scaledwidth=57*scl;
					int cx,cy,cw,ch;
					[cx,cy,cw,ch]=screen.GetClipRect();
					SetClipRect(
						-28*scl+bob.x,19*scl+bob.y,scaledwidth,scaledwidth,
						DI_SCREEN_CENTER
					);
					drawimage(
						"scophole",(0,scaledyoffset)+bob*3,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.78,0.78)*scl
					);
					screen.SetClipRect(cx,cy,cw,ch);
					drawimage(
						"zm66scop",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.82,0.82)*scl
					);
					drawnum(degree*10,
						4+bob.x,74*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
					);
				}
			}
		}
		else if((wp is "Vulcanette")){
			double dotoff=max(abs(bob.x),abs(bob.y));
			if(dotoff<20){
				drawimage(
					whichdot,(0,0)+bob*1.6,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					alpha:0.8-dotoff*0.04,scale:(scl,scl)
				);
			}
			drawimage(
				"riflsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
				scale:(scl,scl)
			);
			int scaledyoffset=47*scl;
			if(scopeview){
				double degree=(35-wp.weaponstatus[2])*0.2;
				texman.setcameratotexture(hpc,"HDXHCAM3",degree);
				drawimage(
					"HDXHCAM3",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(0.31,0.31)*scl
				);
				int scaledwidth=57*scl;
				int cx,cy,cw,ch;
				[cx,cy,cw,ch]=screen.GetClipRect();
				SetClipRect(
					-28*scl+bob.x,19*scl+bob.y,scaledwidth,scaledwidth,
					DI_SCREEN_CENTER
				);
				drawimage(
					"scophole",(0,scaledyoffset)+bob*3,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(0.78,0.78)*scl
				);
				screen.SetClipRect(cx,cy,cw,ch);
				drawimage(
					"zm66scop",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(0.8,0.8)*scl
				);
				drawnum(degree*10,
					4+bob.x,74*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
				);
			}
		}
		else if((wp is "Blooper")){
			drawimage(
				"XH27",(0,1.8*scl)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
				scale:(1.6,1.6)*scl
			);
		}
		else if((wp is "HDRL")){
			if(wp.weaponstatus[0]&RLF_GRENADEMODE)drawimage(
				"XH27",(0,1.8*scl)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
				scale:(1.6,1.6)*scl
			);
			else if(wp.weaponstatus[0]&RLF_ZOOM){
				int scaledyoffset=4*scl;
				texman.setcameratotexture(hpc,"HDXHCAM6",1.6);
				drawimage(
					"HDXHCAM6",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(0.35,0.35)*scl
				);
				drawimage(
					"rlstadia",bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(0.77,0.77)*scl
				);
			}
			else{
				double dotoff=max(abs(bob.x),abs(bob.y));
				if(dotoff<10){
					drawimage(
						"riflsit3",(0,0)+bob*1.6,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						alpha:0.8-dotoff*0.04,scale:(scl,scl)*0.8
					);
				}
				drawimage(
					"xh25",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(scl,scl)*1.6
				);
			}
		}
		else if(wp is "ThunderBuster"){
			int cx,cy,cw,ch;
			[cx,cy,cw,ch]=screen.GetClipRect();
			SetClipRect(
				-16*scl+bob.x,-4*scl+bob.y,32*scl,16*scl,
				DI_SCREEN_CENTER
			);
			vector2 bobb=bob*2;
			bobb.y=clamp(bobb.y,-8,8);
			drawimage(
				"frntsite",(0,0)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
				alpha:0.9,scale:(scl*1.6,scl*2)
			);
			screen.SetClipRect(cx,cy,cw,ch);
			drawimage(
				"backsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_TOP,
				scale:(scl*2,scl)
			);

			if(scopeview){
				int scaledyoffset=36*scl;
				texman.setcameratotexture(hpc,"HDXHCAM1",3);
				drawimage(
					"HDXHCAM1",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1,1)*scl
				);
				drawimage(
					"tbwindow",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1,1)*scl
				);
				bobb*=3.7;
				double dotoff=max(abs(bobb.x),abs(bobb.y));
				if(dotoff<40)drawimage(
					"redpxl",(0,scaledyoffset)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
					alpha:0.9*(1.-dotoff*0.04),scale:(scl*2,scl*2)
				);
			}
		}
		else if(wp is "LiberatorRifle"){
			if(wp.weaponstatus[0]&LIBF_GRENADEMODE){
				drawimage(
					"XH27",(0,1.8*scl)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(1.6,1.6)*scl
				);
			}else{
				double dotoff=max(abs(bob.x),abs(bob.y));
				if(dotoff<20){
					drawimage(
						whichdot,(0,0)+bob*1.6,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						alpha:0.8-dotoff*0.04,scale:(scl,scl)
					);
				}
				drawimage(
					"riflsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(scl,scl)
				);
				if(scopeview){
					int scaledyoffset=60*scl;
					int scaledwidth=72*scl;
					double degree=4.-0.06*wp.weaponstatus[LIBS_ZOOM];
					double deg=1/degree;
					int cx,cy,cw,ch;
					[cx,cy,cw,ch]=screen.GetClipRect();
					SetClipRect(
						-36*scl+bob.x,24*scl+bob.y,scaledwidth,scaledwidth,
						DI_SCREEN_CENTER
					);
					string reticle=
						wp.weaponstatus[0]&LIBF_ALTRETICLE?"reticle2":"reticle1";
					if(wp.weaponstatus[0]&LIBF_FRONTRETICLE){
						texman.setcameratotexture(hpc,"HDXHCAM3",degree);
						drawimage(
							"HDXHCAM3",(0,scaledyoffset)+bob,
							DI_SCREEN_CENTER|DI_ITEM_CENTER,
							scale:(0.4,0.4)*scl
						);
						drawimage(
							reticle,(0,scaledyoffset)+bob*5,
							DI_SCREEN_CENTER|DI_ITEM_CENTER,
							scale:(2.,2.)*deg*scl
						);
					}else{
						degree=(35-wp.weaponstatus[LIBS_ZOOM])*0.2;
						texman.setcameratotexture(hpc,"HDXHCAM3",degree);
						drawimage(
							"HDXHCAM3",(0,scaledyoffset)+bob,
							DI_SCREEN_CENTER|DI_ITEM_CENTER,
							scale:(0.4,0.4)*scl
						);
						drawimage(
							reticle,(0,scaledyoffset)+bob,
							DI_SCREEN_CENTER|DI_ITEM_CENTER,
							scale:(0.52,0.52)*scl
						);
					}
					drawimage(
						"scophole",(0,scaledyoffset)+bob*5,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.95,0.95)*scl
					);
					screen.SetClipRect(cx,cy,cw,ch);
					drawimage(
						"libscope",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(scl,scl)
					);
					drawnum(degree*10,
						4+bob.x,95*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
					);
					drawnum(wp.weaponstatus[LIBS_DROPADJUST]*5,
						6+bob.x,17*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
					);
				}
			}
		}
		else if((wp is "Brontornis")){
			int scaledyoffset=-4*scl;
			if(wp.weaponstatus[0]&BRONF_ZOOM){
				texman.setcameratotexture(hpc,"HDXHCAM5",2.6);
				drawimage(
					"HDXHCAM5",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(0.42,0.42)*scl
				);
				scaledyoffset=-6*scl;
				drawimage(
					"bstadia",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(1.2,1.2)*scl
				);
			}else{
				double dotoff=max(abs(bob.x),abs(bob.y));
				if(dotoff<10){
					drawimage(
						"riflsit4",(0,0)+bob*1.6,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						alpha:0.8-dotoff*0.04,scale:(scl,scl)*1.6
					);
				}
				drawimage(
					"xh25",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(scl,scl)*1.6
				);
			}
		}
		else if(wp is "BossRifle"){

			int cx,cy,cw,ch;
			[cx,cy,cw,ch]=screen.GetClipRect();
			SetClipRect(
				-16*scl+bob.x,-4*scl+bob.y,32*scl,16*scl,
				DI_SCREEN_CENTER
			);
			vector2 bobb=bob*2;
			bobb.y=clamp(bobb.y,-8,8);
			drawimage(
				"frntsite",(0,0)+bobb,DI_SCREEN_CENTER|DI_ITEM_TOP,
				alpha:0.9,scale:(scl*1.6,scl*2)
			);
			screen.SetClipRect(cx,cy,cw,ch);
			drawimage(
				"backsite",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_TOP,
				scale:(scl*2,scl)
			);

			if(scopeview){
				int scaledyoffset=60*scl;
				int scaledwidth=89*scl;
				vector2 sclhalf=(0.5,0.5)*scl;
				double degree=4.-0.05*wp.weaponstatus[BOSSS_ZOOM];
				double deg=1/degree;
				int cx,cy,cw,ch;
				[cx,cy,cw,ch]=screen.GetClipRect();
				SetClipRect(-44*scl+bob.x,16*scl+bob.y,scaledwidth,scaledwidth,
					DI_SCREEN_CENTER);
				if(wp.weaponstatus[0]&BOSSF_FRONTRETICLE){
					texman.setcameratotexture(hpc,"HDXHCAM3",degree);
					drawimage(
						"HDXHCAM3",(0,scaledyoffset)+bob,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:sclhalf
					);
					drawimage(
						"reticle1",(0,scaledyoffset)+bob*deg*5,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(1.6,1.6)*deg*scl
					);
				}else{
					degree=6.-0.1*wp.weaponstatus[BOSSS_ZOOM];
					texman.setcameratotexture(hpc,"HDXHCAM3",degree);
					drawimage(
						"HDXHCAM3",(0,scaledyoffset)+bob,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:sclhalf
					);
					drawimage(
						"reticle1",(0,scaledyoffset)+bob,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:sclhalf
					);
				}
				drawimage(
					"scophole",(0,scaledyoffset)+bob*5,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1.5,1.5)*scl
				);
				screen.SetClipRect(cx,cy,cw,ch);
				drawimage(
					"libscope",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1.24,1.24)*scl
				);
				drawnum(degree*10,
					4+bob.x,105*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
				);
				drawnum(wp.weaponstatus[BOSSS_DROPADJUST]*5,
					6+bob.x,9*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
				);
			}
			// the scope display is in 10ths of an arcminute.
			// every 2 BossMOA (item) should be one arcminute.
			// every 60 BossMOA (scope) should be one dot.
			// one dot = 6 arcminutes.
		}
	}
}
