// ------------------------------------------------------------
// Sight picture crosshairs
// ------------------------------------------------------------
extend class HDStatusBar{
	virtual void DrawHDXHair(hdplayerpawn hpl){
		let wp=hdweapon(cplayer.readyweapon);
		bool sightbob=cvar.getcvar("hd_sightbob",cplayer).getbool();

		if(
			!wp
			||hpl.barehanded
			||hpl.nocrosshair>0  
			||(!sightbob&&hpl.countinv("IsMoving"))
		)return;

		SetSize(0,400,250);
		BeginHUD(forcescaled:true);

		actor hpc=hpl.scopecamera;
		int cpbt=cplayer.cmd.buttons;
		int nscp=cvar.getcvar("hd_noscope",cplayer).getint();
		float scl=clamp(cvar.getcvar("hd_xhscale",cplayer).getfloat(),0.1,3.0);
		bool scopeview=(
			!nscp||
			(nscp==1&&!gimmehud)||
			cpbt&BT_ZOOM
		);

		vector2 bob=sightbob?hpl.hudbob:(0,0);

		if((wp is "HDPistol")){
			drawimage(
				"XH26",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
				scale:(2,2)*scl
			);
		}
		else if((wp is "HDSMG"||wp is "Hunter")){
			drawimage(
				"XH23",(0,1.8)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
				scale:(1.6,1.7)*scl
			);
		}
		else if((wp is "Slayer")){
			drawimage(
				"XH23",(0,1.4)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
				scale:(1.8,1.6)*scl
			);
		}
		else if(
			wp is "ZM66AssaultRifle" //&&ismoving<7
		){
			int scaledyoffset=47*scl;
			if(wp.weaponstatus[0]&ZM66F_GLMODE){
//				if(ismoving>4)return;
				drawimage(
					"XH27",(0,1.8)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(1.6,1.6)
				);
			}else{
				drawimage(
					"XH25",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(2.6,2.7)*scl
				);
				if(scopeview){
					double degree=(35-wp.weaponstatus[ZM66S_ZOOM])*0.2;
					texman.setcameratotexture(hpc,"HDXHCAM3",degree);
					drawimage(
						"HDXHCAM3",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.31,0.31)*scl
					);
					drawimage(
						"XH29",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(0.8,0.8)*scl
					);
					drawnum(degree*10,
						4+bob.x,79*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
					);
				}
			}
		}
		else if((wp is "Vulcanette")){ //&&ismoving<3){
			int scaledyoffset=47*scl;
			drawimage(
				"XH25",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
				scale:(1.7,1.7)*scl
			);
			if(scopeview){
				double degree=(35-wp.weaponstatus[2])*0.2;
				texman.setcameratotexture(hpc,"HDXHCAM3",degree);
				drawimage(
					"HDXHCAM3",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(0.31,0.31)*scl
				);
				drawimage(
					"XH29",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(0.8,0.8)*scl
				);
				drawnum(degree*10,
					4+bob.x,79*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
				);
			}
		}
		else if((wp is "Blooper")){ //&&ismoving<10){
			drawimage(
				"XH27",(0,1.8*scl)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
				scale:(1.6,1.6)*scl
			);
		}
		else if((wp is "HDRL")){ //&&ismoving<6){
			if(wp.weaponstatus[0]&RLF_GRENADEMODE)drawimage(
				"XH27",(0,1.8*scl)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
				scale:(1.6,1.6)*scl
			);
			else if(wp.weaponstatus[0]&RLF_ZOOM){
				int scaledyoffset=-2*scl;
				texman.setcameratotexture(hpc,"HDXHCAM5",2.);
				drawimage(
					"HDXHCAM5",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(0.35,0.35)*scl
				);
				drawimage(
					"stadia",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(1.,1.)*scl
				);
			}
			else drawimage(
				"XH25",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
				scale:(1.9,1.9)*scl
			);
		}
		else if(wp is "ThunderBuster"){ //&&ismoving<6){
			drawimage(
				"XH26",(0,1*scl)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
				scale:(5,1)*scl
			);
			if(scopeview){
				int scaledyoffset=36*scl;
				texman.setcameratotexture(hpc,"HDXHCAM1",3);
				drawimage(
					"HDXHCAM1",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1,1)*scl
				);
				drawimage(
					"XH28",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1,1)*scl
				);
			}
		}
		else if(wp is "LiberatorRifle"){ //&&ismoving<6){
			if(wp.weaponstatus[0]&LIBF_GRENADEMODE){
//				if(ismoving>4)return;  
				drawimage(
					"XH27",(0,1.8*scl)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(1.6,1.6)*scl
				);
			}else{
				drawimage(
					"XH25",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(2.5,2.5)*scl
				);
				if(scopeview){
					int scaledyoffset=60*scl;
					int scaledwidth=72*scl;
					double degree;
					string reticle=
						wp.weaponstatus[0]&LIBF_ALTRETICLE?"reticle2":"reticle1";
					if(wp.weaponstatus[0]&LIBF_FRONTRETICLE){
						degree=4.-0.06*wp.weaponstatus[LIBS_ZOOM];
						texman.setcameratotexture(hpc,"HDXHCAM3",degree);
						drawimage(
							"HDXHCAM3",(0,scaledyoffset)+bob,
							DI_SCREEN_CENTER|DI_ITEM_CENTER,
							scale:(0.4,0.4)*scl
						);
						int cx,cy,cw,ch;
						[cx,cy,cw,ch]=screen.GetClipRect();
						SetClipRect(
							-36*scl+bob.x,24*scl+bob.y,scaledwidth,scaledwidth,
						DI_SCREEN_CENTER);
						double deg=1/degree;
						drawimage(
							reticle,(0,scaledyoffset)+bob*deg*5,
							DI_SCREEN_CENTER|DI_ITEM_CENTER,
							scale:(2.,2.)*deg*scl
						);
						screen.SetClipRect(cx,cy,cw,ch);
					}else{
						degree=(35-wp.weaponstatus[LIBS_ZOOM])*0.2;
						texman.setcameratotexture(hpc,"HDXHCAM3",degree);
						drawimage(
							"HDXHCAM3",(0,scaledyoffset)+bob,
							DI_SCREEN_CENTER|DI_ITEM_CENTER,
							scale:(0.4,0.4)*scl
						);
						int cx,cy,cw,ch;
						[cx,cy,cw,ch]=screen.GetClipRect();
						SetClipRect(
							-36*scl+bob.x,24*scl+bob.y,scaledwidth,scaledwidth,
						DI_SCREEN_CENTER);
						drawimage(
							reticle,(0,scaledyoffset)+bob,
							DI_SCREEN_CENTER|DI_ITEM_CENTER,
							scale:(0.52,0.52)*scl
						);
						screen.SetClipRect(cx,cy,cw,ch);
					}
					drawimage(
						"XH31",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(1.,1.)*scl
					);
					drawnum(degree*10,
						4+bob.x,102*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
					);
					drawnum(wp.weaponstatus[LIBS_DROPADJUST]*5,
						6+bob.x,14*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
					);
				}
			}
		}
		else if((wp is "Brontornis")){ //&&ismoving<10){
			int scaledyoffset=-2*scl;
			if(wp.weaponstatus[0]&BRONF_ZOOM){
				texman.setcameratotexture(hpc,"HDXHCAM5",2.6);
				drawimage(
					"HDXHCAM5",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(0.35,0.35)*scl
				);
				drawimage(
					"stadia",(0,scaledyoffset)+bob,
					DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
					scale:(1.,1.)*scl
				);
			}else drawimage(
				"XH25",(0,0)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
				scale:(1.9,1.9)*scl
			);
		}
		else if(wp is "BossRifle"){ //&&ismoving<5){
			drawimage(
				"XH23",(0,-0.5*scl)+bob,DI_SCREEN_CENTER|DI_ITEM_HCENTER|DI_ITEM_TOP,
				scale:(3,1.6)*scl
			);
			if(scopeview){
				int scaledyoffset=68*scl;
				int scaledwidth=89*scl;
				vector2 sclhalf=(0.5,0.5)*scl;
				double degree;
				if(wp.weaponstatus[0]&BOSSF_FRONTRETICLE){
					degree=4.-0.05*wp.weaponstatus[BOSSS_ZOOM];
					texman.setcameratotexture(hpc,"HDXHCAM3",degree);
					drawimage(
						"HDXHCAM3",(0,scaledyoffset)+bob,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:sclhalf
					);
					int cx,cy,cw,ch;
					[cx,cy,cw,ch]=screen.GetClipRect();
					SetClipRect(-44*scl+bob.x,23*scl+bob.y,scaledwidth,scaledwidth,
						DI_SCREEN_CENTER);
					double deg=1/degree;
					drawimage(
						"reticle1",(0,scaledyoffset)+bob*deg*5,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:(1.6,1.6)*deg*scl
					);
					screen.SetClipRect(cx,cy,cw,ch);
				}else{
					degree=6.-0.1*wp.weaponstatus[BOSSS_ZOOM];
					texman.setcameratotexture(hpc,"HDXHCAM3",degree);
					drawimage(
						"HDXHCAM3",(0,scaledyoffset)+bob,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:sclhalf
					);
					drawimage(
						"reticle1",(0,scaledyoffset)+bob,
						DI_SCREEN_CENTER|DI_ITEM_CENTER,
						scale:sclhalf
					);
				}
				drawimage(
					"XH31",(0,scaledyoffset)+bob,DI_SCREEN_CENTER|DI_ITEM_CENTER,
					scale:(1.24,1.24)*scl
				);
				drawnum(degree*10,
					4+bob.x,119*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
				);
				drawnum(wp.weaponstatus[BOSSS_DROPADJUST]*5,
					6+bob.x,12*scl+bob.y,DI_SCREEN_CENTER,Font.CR_BLACK
				);
			}
			// the scope display is in 10ths of an arcminute.
			// every 2 BossMOA (item) should be one arcminute.
			// every 60 BossMOA (scope) should be one dot.
			// one dot = 6 arcminutes.
		}
	}
}
