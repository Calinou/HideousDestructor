// ------------------------------------------------------------
// Stuff related to player turning
// ------------------------------------------------------------
extend class HDPlayerPawn{
	vector2 muzzleclimb1;
	vector2 muzzleclimb2;
	vector2 muzzleclimb3;
	vector2 muzzleclimb4;
	vector2 hudbobrecoil1;
	vector2 hudbobrecoil2;
	vector2 hudbobrecoil3;
	vector2 hudbobrecoil4;
	vector2 muzzledrift;
	bool muzzlehit;
	bool totallyblocked;

	enum muzzleblock{
		MB_TOP=1,
		MB_BOTTOM=2,
		MB_LEFT=4,
		MB_RIGHT=8,
	}
	void TurnCheck(){
		//get input based on previous angle
		//do NOT use input because that breaks recoil
		double yaw=deltaangle(lastangle,angle);
		double ptch=deltaangle(lastpitch,pitch);
		double vdv=vel dot vel;

		//reduced turning while supported.
		if(
			player.cmd.buttons&BT_USE&&
			!countinv("IsMoving")
		){
			yaw-=iyaw*0.9;ptch+=ipitch*0.9;
			if(!gunbraced){
				vector2 iyp=(iyaw,-ipitch)*0.05;
				A_MuzzleClimb(iyp*2,iyp,iyp,iyp);
			}
		}
		//reduced turning while crouched.
		else if(height<40 && abs(yaw)>5){
			yaw-=iyaw*0.4;ptch-=ipitch*0.4;
		}


		//temporary variables
		double anglechange=yaw;
		double pitchchange=ptch;


		//get weapon size
		double amt=0;
		double barrellength;
		double barrelwidth;
		double barreldepth;
		bool notnull=false;
		let wp=HDWeapon(player.readyweapon);
		if(wp){
			amt=wp.gunmass();
			barrellength=wp.barrellength;
			barrelwidth=wp.barrelwidth;
			barreldepth=wp.barreldepth;
			notnull=!(wp is "NullWeapon");
		}

		//better anticipation
		if(vdv>0.25){
			barrelwidth*=clamp(vdv*10,0,2.6);
		}

		//inertia adjustments for other things
		if(stunned){
			amt=max(17,amt);
		}else if(hd_noturninertia==1){
			amt=0;
		}else if(countinv("PowerStrength")||countinv("PowerInvulnerable")){
			amt*=0.2;
		}
		if(notnull&&countinv("WeaponBusy")){
			barrellength=radius+2;
			barrelwidth*=2;
			amt=min(amt*1.5,20);
		}


		//muzzle inertia
		//how much to scale movement
		double proportion=1-min(0.042*amt,0.99);
		double proportion2=0.05*amt;
		double proportion3=1-proportion*0.7;

		//take the proportion and use that OR crouch+support, whichever is LESS
		if(hd_noturninertia==2)proportion2*=0.1;else{
				double yawcap=abs(yaw*proportion);
				double ptchcap=abs(ptch*proportion);
				anglechange=clamp(anglechange,-yawcap,yawcap);
				pitchchange=clamp(pitchchange,-ptchcap,ptchcap);
		}

		//see if there's any change in velocity overall
		vector3 muzzlevel=lastvel-vel;

		//apply crouch/jump
		double curheight=height;
		muzzlevel.z-=(lastheight-curheight)*0.3;
		lastheight=curheight;

		//determine velocity-based horizontal drift
		//how to tell if it is to left or right?
		//rotate vector so that player is facing east
		//the y value is what we need
		muzzlevel.xy=rotatevector(muzzlevel.xy,-angle);

		muzzledrift+=(muzzlevel.y,muzzlevel.z)*proportion2;


		//NOW apply the drift!
		muzzledrift+=(anglechange,pitchchange);
		anglechange+=muzzledrift.x*proportion2;
		pitchchange+=muzzledrift.y*proportion2;
		muzzledrift*=proportion3;

		hudbobrecoil1.x-=muzzledrift.x;
		hudbobrecoil1.y+=muzzledrift.y;

		//good old jitters!
		//gotta do this after inertia and before collision,
		//to keep this from clipping your gun into the geometry.
		if(
			beatmax<10||
			fatigue>20||
			bloodpressure>20||  
			health<33
		){
			double jitter=0.3;
			if(fatigue)jitter=clamp(0.01*fatigue,0.3,6.);
			if(gunbraced)jitter=0.05;
			else if(health<20)jitter=1;
			anglechange+=frandom(-jitter,jitter);
			pitchchange+=frandom(-jitter,jitter);
		}


		//muzzle climb - gotta keep this here to make it subject to caps
		pitchchange+=muzzleclimb1.y;
		anglechange+=muzzleclimb1.x;
		muzzleclimb1=muzzleclimb2;
		muzzleclimb2=muzzleclimb3;
		muzzleclimb3=muzzleclimb4;
		muzzleclimb4=(0,0);


		//weapon collision
		if(!(player.cheats&CF_NOCLIP2 || player.cheats&CF_NOCLIP)){
			double highheight=height-6;
			double midheight=highheight-max(1,barreldepth)*0.5;
			double lowheight=highheight-max(1,barreldepth);
			double testangle=lastangle+anglechange;
			double testpitch=lastpitch+pitchchange;


			//and now uh do stuff
			vector3 barrelbase=pos+(0,0,midheight);
			actor muzpuf;
			int stuff;
			int muzzleblocked=0;

			double distleft=barrellength;;
			double distright=barrellength;;
			double disttop=barrellength;
			double distbottom=barrellength;

			flinetracedata ltl;
			flinetracedata ltr;
			flinetracedata ltt;
			flinetracedata ltb;


			//top
			linetrace(
				testangle,barrellength,testpitch,flags:TRF_NOSKY,
				offsetz:highheight+cos(pitch)*barreldepth,
				offsetforward:-sin(pitch)*barreldepth,
				offsetside:0,
				data:ltt
			);
			if(ltt.hittype==Trace_CrossingPortal)return;
			disttop=ltt.distance;
			if(ltt.distance<barrellength)muzzleblocked|=MB_TOP;

			//bottom
			linetrace(
				testangle,barrellength,testpitch,flags:TRF_NOSKY,
				offsetz:lowheight-cos(pitch)*barreldepth,
				offsetforward:sin(pitch)*barreldepth,
				offsetside:0,
				data:ltb
			);
			if(ltb.hittype==Trace_CrossingPortal)return;
			distbottom=ltb.distance;
			if(ltb.distance<barrellength)muzzleblocked|=MB_BOTTOM;


			//left
			linetrace(
				testangle,barrellength,testpitch,flags:TRF_NOSKY,
				offsetz:midheight,
				offsetside:-barrelwidth,
				data:ltl
			);
			if(ltl.hittype==Trace_CrossingPortal)return;
			distleft=ltl.distance;
			if(ltl.distance<barrellength)muzzleblocked|=MB_LEFT;

			//right
			linetrace(
				testangle,barrellength,testpitch,flags:TRF_NOSKY,
				offsetz:midheight,
				offsetside:barrelwidth,
				data:ltr
			);
			if(ltr.hittype==Trace_CrossingPortal)return;
			distright=ltr.distance;
			if(ltr.distance<barrellength)muzzleblocked|=MB_RIGHT;


			//totally caught
			totallyblocked=muzzleblocked==15;


			//set angles
			bool mvng=(lastheight!=height || vdv > 0.25);
			double mvng2=vdv*0.001;
			bool hitsnd=(max(abs(anglechange),abs(pitchchange))>1);

//A_Log(string.format("%i %i %i %i",distleft,distright,disttop,distbottom));

			if(notnull){
				if(muzzleblocked&MB_LEFT){
					anglechange=mvng?min(distleft-barrellength,-4):0;
					A_ChangeVelocity(0,-0.05,0,CVF_RELATIVE);
				}
				if(muzzleblocked&MB_RIGHT){
					anglechange=mvng?max(barrellength-distright,4):0;
					A_ChangeVelocity(0,0.05,0,CVF_RELATIVE);
				}

				if(muzzleblocked&MB_BOTTOM){
					pitchchange=mvng?max(distbottom-barrellength,-4):0;
				}
				if(muzzleblocked&MB_TOP){
					pitchchange=mvng?min(barrellength-disttop,4):0;
				}
				pitchchange=clamp(pitchchange,-45,45);

				if(totallyblocked){
					vector2 cv=angletovector(pitch,
						clamp(barrellength-disttop,0,barrellength)*0.005);
					A_ChangeVelocity(-cv.x,0,0,CVF_RELATIVE);
				}
			}



			//bump
			if(muzzleblocked>=4){  
				muzzlehit=false;
			}else if(!muzzlehit){
				if(hitsnd)A_PlaySound("weapons/rifleclick2",CHAN_AUTO,0.6);
				muzzlehit=true;
				gunbraced=true;
			}
		}

//if(abs(anglechange)>45)A_Log(string.format("angle %i",anglechange));
//if(abs(pitchchange)>45)A_Log(string.format("pitch %i",pitchchange));

//if(anglechange)A_Log(string.format("angle %i",anglechange));


		//set everything and update old
		A_SetAngle(lastangle+anglechange,SPF_INTERPOLATE);
		A_SetPitch(
			clamp(lastpitch+pitchchange,player.minpitch,player.maxpitch),
			SPF_INTERPOLATE
		);


		//feet angle
		double fac=deltaangle(feetangle,angle);
		if(abs(fac)>(player.crouchfactor<0.7?30:50)){
			vel+=rotatevector((0,fac>0?0.1:-0.1),angle);  
			A_GiveInventory("IsMoving",2);
			feetangle=angle;
			PlayRunning();

			//if on appropriate terrain, easier to quench a fire
			if(player.crouchfactor<0.7){
				int douse=3;
				//the below is equivalent to "if(CheckLiquidTexture())douse=6;"
				//replicated because of the virtual subfunction processing problem
				int lqlength=lq.size();
				for(int i=0;i<lqlength;i++){
					TextureID tx=TexMan.CheckForTexture(lq[i],TexMan.Type_Flat);
					if (tx && floorpic==tx)douse=5;
				}
				A_GiveInventory("HDFireDouse",douse);
			}
		}

		//move pivot point a little behind the player's view
		anglechange=deltaangle(angle,lastangle);
		if(!teleported && player.onground && zat-pos.z==0){
			if(abs(anglechange)>1){      
				int dir=90;
				if(anglechange<0) dir=-90;
				trymove(self.pos.xy-(cos(angle+dir),sin(angle+dir))*0.8,false);
			}
			int ptchch=clamp(abs(pitchchange),0,10); //THE CLAMP IS A BANDAID
			if(ptchch>1 && -30<pitch<30){  
				trymove(pos.xy-(cos(angle)*ptchch,sin(angle)*ptchch)*0.1,false);
				PlayRunning();
			}
		}
	}



	//seeing if you're standing on a liquid texture
	static const string lq[]={
		"MFLR8_4","MFLR8_2",
		"SFLR6_1","SFLR6_4",
		"SFLR7_1","SFLR7_4",
		"FWATER1","FWATER2","FWATER3","FWATER4",
		"BLOOD1","BLOOD2","BLOOD3",
		"SLIME1","SLIME2","SLIME3","SLIME4",
		"SLIME5","SLIME6","SLIME7","SLIME8"
	};
	bool CheckLiquidTexture(){
		int lqlength=lq.size();
		let fp=floorpic;
		for(int i=0;i<lqlength;i++){
			TextureID tx=TexMan.CheckForTexture(lq[i],TexMan.Type_Flat);
			if (tx&&fp==tx){
				return true;
			}
		}
		return false;
	}

	//Muzzle climb!
	void A_MuzzleClimb(vector2 mc1,vector2 mc2,vector2 mc3,vector2 mc4,bool wepdot=false){
		double mult=1.;
		if(gunbraced)mult=0.2;
		else if(countinv("IsMoving"))mult=1.6;
		if(stunned)mult*=1.6;
		if(mult){
			mc1*=mult;
			mc2*=mult;
			mc3*=mult;
			mc4*=mult;
		}
		muzzleclimb1+=mc1;
		muzzleclimb2+=mc2;
		muzzleclimb3+=mc3;
		muzzleclimb4+=mc4;
		if(wepdot){
			hudbobrecoil1+=(-mc1.x,mc1.y*2)*mult;
			hudbobrecoil2+=(-mc2.x,mc2.y*2)*mult;
			hudbobrecoil3+=(-mc3.x,mc3.y*2)*mult;
			hudbobrecoil4+=(-mc4.x,mc4.y*2)*mult;
		}
	}
}

class MuzzleBlockerPuff:IdleDummy{
	default{
		+nodecal +puffonactors +notrigger +bloodlessimpact +hittracer
	}
}

