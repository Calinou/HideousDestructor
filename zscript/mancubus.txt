// ------------------------------------------------------------
// Mancu, mancu very much.
// ------------------------------------------------------------
class manjuicetrail:hdfireballtail{
	default{
		renderstyle "translucent";
		deathheight 0.9;
		gravity 0;
	}
	states{
	spawn:
		SMOK ABC -1{
			roll=random(0,360);
			scale.x*=randompick(-1,1);
		}wait;
	}
}

class manjuicelight:PointLight{
	override void postbeginplay(){
		args[0]=164;
		args[1]=66;
		args[2]=18;
		args[3]=0;
		args[4]=0;
	}
	override void tick(){
		super.tick();
		if(!target){
			args[3]+=random(-20,4);
			if(args[3]<1)destroy();
		}else if(target.bmissile)args[3]=random(28,44);
		else args[3]=random(32,64);
	}
}
class manjuiceburner:PersistentDamager{
	default{
		height 24;radius 64;threshold 3;damage 1;accuracy 0;gibhealth 3;
		damagetype "thermal";
	}
}
class manjuice:hdfireball{
	default{
		missiletype "manjuicetrail";
		damagetype "thermal";
		activesound "misc/firecrkl";
		decal "scorch";
		gravity 0.1;
		speed 27;
	}
	actor trailburner;
	override void ondestroy(){
		if(trailburner)trailburner.destroy();
		super.ondestroy();
	}
	states{
	spawn:
		MANF A 0 nodelay{
			actor mjl=spawn("manjuicelight",pos);mjl.target=self;
			trailburner=spawn("manjuiceburner",pos);
			trailburner.target=target;trailburner.master=self;
		}
		MANF AABBAABB 1 A_FBTail();
	spawn2:
		MANF AB 2 A_FBFloat();
		loop;
	death:
		MISL B 0{
			vel.z+=1.;
			A_Blast(
				128,96,16,"thermal",
				immolateradius:128,random(4,80),100,
				16,1,
				false
			);
			A_SpawnChunks("HDSmokeChunk",random(2,4),6,20);
			A_PlaySound("misc/fwoosh",CHAN_WEAPON);
			scale=(0.9*randompick(-1,1),0.9);
		}
		MISL BBBB 1{
			vel.z+=0.5;
			scale*=1.05;
		}
		MISL CCCDDD 1{
			alpha-=0.15;
			scale*=1.01;
		}
		TNT1 A 0 A_Immolate(null,target,480);
		TNT1 AAAAAAAAAAAAAAA 4{
			A_SpawnItemEx("HDFlameRed",
				random(-2,2),random(-2,2),random(-2,2),
				frandom(2,-4),frandom(-2,2),frandom(1,4),0,SXF_NOCHECKPOSITION
			);
			A_SpawnItemEx("HDSmoke",
				random(-2,2),random(-2,2),random(-2,2),
				frandom(2,-4),frandom(-2,2),frandom(1,4),0,SXF_NOCHECKPOSITION
			);
		}stop;
	}
}

class Mancubus:Fatso replaces Fatso{
	default{
		+dontharmspecies
		deathheight 20;
		radius 38;
		damagefactor "Thermal", 0.7;
		obituary "%o was smoked by a mancubus.";
		painchance 16;
	}
	vector2 firsttargetaim;
	vector2 secondtargetaim;
	vector2 leadoffset;
	double targdist;
	states{
	see:
		FATT AABBCCDDEEFF 3 {hdmobai.chase(self);}
		loop;
	missile:
		FATT ABCD 3{
			A_FaceTarget(30);
			if(A_JumpIfTargetInLOS("null",10))setstatelabel("raiseshoot");
		}
		FATT E 0 A_JumpIfTargetInLOS("raiseshoot",30);
		FATT E 0 A_JumpIfTargetInLOS("missile");
		goto see;
	raiseshoot:
		FATT G 4{
			A_PlaySound("fatso/raiseguns",CHAN_VOICE);
			A_FaceTarget(40,40);
		}
		FATT G 4 A_FaceTarget(20,20);
		FATT GGGGGG 1 A_SpawnItemEx("HDSmoke",
			24,randompick(24,-24),18,
			random(2,4),flags:SXF_NOCHECKPOSITION
		);
		FATT G 1{
			vector2 aimbak=(angle,pitch);
			A_FaceTarget(0,0);
			firsttargetaim=(angle,pitch);
			angle=aimbak.x;pitch=aimbak.y;
		}
		FATT G 2{
			vector2 aimbak=(angle,pitch);
			A_FaceTarget(0,0);
			secondtargetaim=(angle,pitch);
			angle=aimbak.x;pitch=aimbak.y;

			targdist=(target?distance3d(target):2000);

			leadoffset=(
				deltaangle(firsttargetaim.x,secondtargetaim.x),
				deltaangle(firsttargetaim.y,secondtargetaim.y)
			)*targdist*0.07;

			angle+=leadoffset.x;pitch+=leadoffset.y;
		}
		FATT H 10 bright{
			//fire both at same point
			actor ppp;int bluh;
			for(int i=-1;i<2;i+=2){
				[bluh,ppp]=A_SpawnItemEx(
					"manjuice",0,24*i,32,
					cos(pitch)*27,0,-sin(pitch
						-(targdist<1024?min(60,targdist*0.002):0)
					)*27,
					i<0?-atan(24/targdist):atan(24/targdist),
					flags:SXF_NOCHECKPOSITION|SXF_SETTARGET
				);
				ppp.angle=angle+i*atan(24/targdist);
			}
		}
		FATT G 10;
		goto see;
	death:
		FATT K 6{
			hdmobai.CorpseFlags(self);
			A_SpawnItemEx("tempshield",flags:SXF_NOCHECKPOSITION|SXF_SETMASTER);
			A_SpawnItemEx("HDExplosion",0,0,36,flags:SXF_SETTARGET);
		}
		FATT L 6 A_Scream();
		FATT M 4 A_NoBlocking();
		FATT M 2 A_SpawnItemEx("tempshield2",flags:SXF_NOCHECKPOSITION|SXF_SETMASTER);
		FATT NOPQRS 6 A_SpawnItemEx("HDSmoke",
			frandom(-4,4),frandom(-4,4),frandom(26,32),
			0,0,frandom(1,4),
			0,SXF_NOCHECKPOSITION
		);
		FATT TTT 8 A_SpawnItemEx("HDSmoke",
			frandom(-4,4),frandom(-4,4),frandom(26,32),
			0,0,frandom(1,4),
			0,SXF_NOCHECKPOSITION
		);
		FATT T -1{
			A_BossDeath();
			balwaystelefrag=true; //not needed?
		}stop;
	raise:
		FATT T 0{
			if(countinv("VileCount")<2){
				a_die("notvileenough");
				return;
			}
			hdmobai.CorpseFlags(self);
			A_TakeInventory("VileCount");
		}
		FATT ST 14 A_SpawnItemEx("MegaBloodSplatter",
			frandom(-1,1),frandom(-1,1),frandom(10,16),
			vel.x,vel.y,vel.z,0,SXF_NOCHECKPOSITION
		);
		FATT TTSSRRQPONMLK 5;
		goto see;
	death.notvileenough:
		FATT S 0{
			A_NoBlocking();
			hdmobai.CorpseFlags(self);
			A_GiveInventory("VileCount");
		}
		FATT STST 14 A_SpawnItemEx("MegaBloodSplatter",
			frandom(-1,1),frandom(-1,1),frandom(10,16),
			vel.x,vel.y,vel.z,0,SXF_NOCHECKPOSITION
		);
		FATT T -1;
		stop;
	}
}
