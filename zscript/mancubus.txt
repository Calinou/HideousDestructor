// ------------------------------------------------------------
// Mancubus
// ------------------------------------------------------------
class manjuicetrail:hdfireballtail{
	default{
		renderstyle "translucent";
		deathheight 0.9;
		gravity 0;
	}
	states{
	spawn:
		SMOK ABC -1{
			roll=random(0,360);
			scale.x*=randompick(-1,1);
		}wait;
	}
}
class manjuice:hdfireball{
	default{
		missiletype "manjuice";
		damagetype "thermal";
		activesound "misc/firecrkl";
		decal "scorch";
		gravity 0.1;
		speed 27;
	}
	actor trailburner;
	override void ondestroy(){
		if(trailburner)trailburner.destroy();
		super.ondestroy();
	}
	states{
	spawn:
		MANF A 0 nodelay{
			actor mjl=spawn("ManJuiceLight",pos);mjl.target=self;
			trailburner=spawn("BaleBallBurner",pos);
			trailburner.target=target;trailburner.master=self;
		}
		MANF AABBAABB 1 A_FBTail();
	spawn2:
		MANF AB 2;
		loop;
	death:
		MISL B 4{
			vel.+=1.;
			A_Blast(
				96,96,16,"thermal",
				immolateradius:100,random(0,6),95,
				16,1,
				false
			);
			A_SpawnChunks("HDSmokeChunk",random(2,4),6,20);
		}
		MISL CCCDDD 1{alpha-=0.15;}
		TNT1 AAAAAAAAAAAAAAA 4{
			A_SpawnItemEx("HDSmoke",
				random(-2,2),random(-2,2),random(-2,2),
				frandom(2,-4),frandom(-2,2),frandom(1,4),0,SXF_NOCHECKPOSITION
			);
		}stop;
	}
}

class Mancubus:Fatso replaces Fatso{
	default{
		+donthurtspecies
		deathheight 20;
		radius 38;
		damagefactor "Thermal", 0.7;
		obituary "%o was smoked by a mancubus.";
		painchance 16;
	}
	states{
	see:
		FATT AABBCCDDEEFF 4 {hdmobai.chase(self)};
		loop;
	missile:
		FATT G 4 A_PlaySound("fatso/raiseguns",CHAN_VOICE);
		FATT G 4 A_FaceTarget(20,20);
		FATT GGGGGG 1 A_SpawnItemEx ("HDSmoke", 24, ((random(0,1)*2-1)*24), 18, random(2,4), 0,0,0, 0,160)	
		FATT H 0 A_Jump (256, "Missile1", "Missile2", "Missile3")
	Missile1:
		FATT H 0{
			A_SpawnProjectile("ManJuice",32,-24,random(0,12),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
			A_SpawnProjectile("ManJuice",32,24,random(0,-12),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
		}goto MissilePost
	Missile2:
		FATT H 0{
			A_SpawnProjectile("ManJuice",32,-24,random(8,24),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
			A_SpawnProjectile("ManJuice",32,24,random(0,-2),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
		}goto MissilePost
	Missile3:
		FATT H 0{
			A_SpawnProjectile("ManJuice",32,-24,random(0,2),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
			A_SpawnProjectile("ManJuice",32,24,random(-8,-24),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
		}goto MissilePost
	MissilePost:
		FATT H 10 bright A_GiveInventory("Clip",1)
		FATT H 0 A_JumpIfInventory("Clip",3,"MissileStop")
		FATT IG 5 A_FaceTarget(20,20)
		FATT H 0 A_Jump(256,"Missile2","Missile3")
	MissileStop:
		FATT GGGG 1 BRIGHT A_SpawnItemEx ("HDSmoke", 24, ((random(0,1)*2-1)*24), 18, random(2,4), 0, 0)	
		FATT I 5 A_FaceTarget(20,20)
		FATT G 5 A_TakeInventory ("Clip")
		goto See
	death:
		FATT K 6{
			hdmobai.CorpseFlags(self);
			A_SpawnItemEx("tempshield",flags:SXF_NOCHECKPOSITION|SXF_SETMASTER);
			A_SpawnItemEx("HDExplosion",0,0,36,flags:SXF_SETTARGET);
		}
		FATT L 6 A_Scream();
		FATT M 4 A_NoBlocking();
		FATT M 2 A_SpawnItemEx("tempshield2",flags:SXF_NOCHECKPOSITION|SXF_SETMASTER);
		FATT NOPQRS 6 A_SpawnItemEx("HDSmoke",
			frandom(-4,4),frandom(-4,4),frandom(26,32),
			0,0,frandom(1,4),
			0,SXF_NOCHECKPOSITION
		);
		FATT TTT 8 A_SpawnItemEx("HDSmoke",
			frandom(-4,4),frandom(-4,4),frandom(26,32),
			0,0,frandom(1,4),
			0,SXF_NOCHECKPOSITION
		);
		FATT T -1{
			A_BossDeath();
			balwaystelefrag=true; //not needed?
		}stop;
	raise:
		FATT T 0{
			if(countinv("VileCount")<2){
				a_die("notvileenough");
				return;
			}
			hdmobai.CorpseFlags(self);
			A_TakeInventory("VileCount");
		}
		FATT ST 14 A_SpawnItemEx("MegaBloodSplatter",
			frandom(-1,1),frandom(-1,1),frandom(10,16),
			vel.x,vel.y,vel.z,0,SXF_NOCHECKPOSITION
		);
		FATT TTSSRRQPONMLK 5
		goto see
	death.notvileenough:
		FATT S 0{
			A_NoBlocking();
			hdmobai.CorpseFlags(self);
			A_GiveInventory("VileCount");
		}
		FATT STST 14 A_SpawnItemEx("MegaBloodSplatter",
			frandom(-1,1),frandom(-1,1),frandom(10,16),
			vel.x,vel.y,vel.z,0,SXF_NOCHECKPOSITION
		);
		FATT T -1;
		stop;
	}
}
