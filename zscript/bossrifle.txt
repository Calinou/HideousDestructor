// ------------------------------------------------------------
// Bolt-Action Rifle
// ------------------------------------------------------------
class BossRifleSpawner:IdleDummy{
	states{
	spawn:
		TNT1 A 0 nodelay{
			A_SpawnItemEx("BossClipPickup",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("BossClipPickup",3,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("BossClipPickup",1,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("BossRifle",-3,0,0,0,0,0,0,SXF_NOCHECKPOSITION);
		}stop;
	}
}
class BossRifle:HDWeapon{
	default{
		weapon.slotnumber 8;
		weapon.kickback 15;
		weapon.selectionorder 80;
		inventory.pickupSound "misc/w_pkup";
		inventory.pickupMessage "You got the bolt-action rifle!";
		weapon.bobrangex 0.28;
		weapon.bobrangey 1.1;
		scale 0.7;
		Obituary "%o sure showed %k who was the boss!";
	}
	int pickuprounds;
	override void tick(){
		super.tick();
		drainheat(BOSSS_HEAT,4);
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(owner&&owner.player){
			if(cvar.getcvar("hd_bossfrontreticle",owner.player).getbool())
				weaponstatus[0]|=BOSSF_FRONTRETICLE;
				else weaponstatus[0]&=~BOSSF_FRONTRETICLE;
			weaponstatus[BOSSS_ZOOM]=
				clamp(cvar.getcvar("hd_bosszoom",owner.player).getint(),0,55);
			weaponstatus[BOSSS_DROPADJUST]=
				clamp(cvar.getcvar("hd_bossmoa",owner.player).getint(),0,120);
		}
	}
	states{
	select0:
		BARG A 0;
		goto select0bfg;
	deselect0:
		BARG A 0;
		goto deselect0big;

	ready:
		BARG A 1A_WeaponReady(WRF_ALL);
		goto readyend;
	user3:
		---- A 0 A_MagManager(HDMMS_BOSS);
		goto ready;
	altreload:
		BARG A 0 A_JumpIfInventory("SevenMilAmmo",0,"nope");
		BARG A 0 A_JumpIfInventory("BossClip",1,"altreloadgetclip");
		BARG A 0 A_JumpIfInventory("LiberatorMag",1,"altreloadgetmag");
		goto nope;
	altreloadgetmag:
		BARG A 0{
			A_TakeInventory("LiberatorMag",1);
			invoker.pickuprounds=30;
		}goto altreloadgenerate;
	altreloadgetclip:
		BARG A 0{
			A_TakeInventory("BossClip",1);
			invoker.pickuprounds=10;
		}goto altreloadgenerate;
	altreloadgenerate:
		BARG A 2 offset(4,36){
			invoker.pickuprounds--;
			A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
			if(A_JumpIfInventory("SevenMilAmmo",0,"null"))
				A_SpawnItemEx(
					"HDLoose7mm",random(4,8),0,32,
					frandom(2,3),frandom(1,2),frandom(1,3),
					0,SXF_NOCHECKPOSITION
				);
				else A_GiveInventory("SevenMilAmmo",1);
			if(invoker.pickuprounds<1)setweaponstate("nope");
		}loop;
	fire:
		BARG A 1 A_JumpIf(invoker.weaponstatus[BOSSS_CHAMBER]==2,"shoot");
		goto ready;
	shoot:
		BARG A 1{
			A_Gunflash();
			invoker.weaponstatus[BOSSS_CHAMBER]=1;
			A_PlaySound("weapons/bigrifle",6);
			A_AlertMonsters();

			actor p=spawn("HDBullet776",pos+(0,0,height-6));
			p.target=self;p.angle=angle;p.pitch=pitch;
			p.speed=1400;
			p.vel+=self.vel;
			p.pitch-=(1./120.)*invoker.weaponstatus[BOSSS_DROPADJUST];

			A_MuzzleClimb(
				0,0,
				-frandom(0.2,0.4),-frandom(0.6,1.),
				-frandom(0.4,0.7),-frandom(1.2,2.1),
				-frandom(0.4,0.7),-frandom(1.2,2.1)
			);
		}
		BARG F 1;
		BARG F 1 A_JumpIf(gunbraced(),"ready");
		goto ready;
	flash:
		BARF A 1 bright{
			A_Light1();
			HDFlashAlpha(-96);
			A_ZoomFactor(0.95,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
		}
		TNT1 A 1{
			A_Light0();
			A_ZoomFactor(1.0,ZOOM_INSTANT|ZOOM_NOSCALETURNING);
		}stop;
	altfire:
		BARG A 2 offset(0,34);
		BARG B 1 offset(2,36) A_GiveInventory("WeaponBusy");
		BARG B 1 offset(4,38){
			if(invoker.weaponstatus[BOSSS_CHAMBER]>2)setweaponstate("jamderp");
		}
		BARG B 1 offset(0,34);
		BARG B 0 A_Refire("chamber");
		goto ready;
	althold:
		BARG E 1 A_WeaponReady(WRF_NOFIRE);
		BARG E 1 A_Refire();
		BARG DCB 3 A_WeaponReady(WRF_NOFIRE);
		goto ready;
	jam:
		BARG A 0{
			int chm=invoker.weaponstatus[BOSSS_CHAMBER];
			if(chm<1)setweaponstate("chamber");
			else if(chm<3)invoker.weaponstatus[BOSSS_CHAMBER]+=2;
		}
	jamderp:
		BARG A 0 A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
		BARG D 1 offset(4,38);
		BARG D 2 offset(2,36);
		BARG D 2 offset(4,38)A_MuzzleClimb(frandom(-0.5,0.6),frandom(-0.3,0.6));
		BARG D 3 offset(2,36){
			A_MuzzleClimb(frandom(-0.5,0.6),frandom(-0.3,0.6));
			if(!random(0,3)){
				setweaponstate("chamber");
				if(invoker.weaponstatus[BOSSS_CHAMBER]>2)  
					invoker.weaponstatus[BOSSS_CHAMBER]-=2;
			}
		}
		BARG D 2 offset(4,38);
		BARG D 3 offset(2,36);
		BARG A 0 A_Refire("jamderp");
		goto ready;
	chamber:
		BARG C 2 offset(4,38){
			if(
				!random(0,4)
				&&invoker.weaponstatus[BOSSS_CHAMBER]>0  
			){
				invoker.weaponstatus[BOSSS_CHAMBER]+=2;
				A_MuzzleClimb(
					-frandom(0.6,2.3),-frandom(0.6,2.3),
					-frandom(0.6,1.3),-frandom(0.6,1.3),
					-frandom(0.6,1.3),-frandom(0.6,1.3)
				);
				setweaponstate("jamderp");
			}else A_PlaySound("weapons/boltback",CHAN_WEAPON);
		}
		BARG D 2 offset(6,42);
		BARG D 1 offset(6,42){
			if(gunbraced())A_MuzzleClimb(
				frandom(-0.1,0.3),frandom(-0.1,0.3)
			);else A_MuzzleClimb(
				frandom(-0.2,0.8),frandom(-0.4,0.8)
			);
			if(!random(0,31))setweaponstate("jam");
		}
		BARG D 2 offset(6,42){
			//eject
			int chm=invoker.weaponstatus[BOSSS_CHAMBER];
			if(chm>1){  
				A_SpawnItemEx(
					"HDLoose7mm",cos(pitch)*8,1,height-7-sin(pitch)*8,
					cos(pitch)*cos(angle-80)*4+vel.x,
					cos(pitch)*sin(angle-80)*4+vel.y,
					-sin(pitch)*4+vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}else if(chm==1){
				A_SpawnItemEx(
					"HDSpent7mm",cos(pitch)*8,1,height-7-sin(pitch)*8,
					cos(pitch)*cos(angle-80)*6+vel.x,
					cos(pitch)*sin(angle-80)*6+vel.y,
					-sin(pitch)*6+vel.z,
					0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
				);
			}
			//cycle new
			if(invoker.weaponstatus[BOSSS_MAG]>0){  
				invoker.weaponstatus[BOSSS_CHAMBER]=2;
				invoker.weaponstatus[BOSSS_MAG]--;
			}else invoker.weaponstatus[BOSSS_CHAMBER]=0;
		}
		BARG E 1 offset(6,42) A_WeaponReady(WRF_NOFIRE);
		BARG E 0 A_Refire("chamber4");
		BARG E 1 offset(3,34) A_WeaponReady(WRF_NOFIRE);
		BARG E 1 offset(3,34) A_PlaySound("weapons/boltfwd",CHAN_WEAPON);
		BARG E 3 offset(4,38);
		BARG D 2 offset(3,37){
			if(!gunbraced())
				A_MuzzleClimb(frandom(-0.4,0.8),frandom(-0.2,0.8));
		}
		BARG C 3 offset(2,36);
		BARG B 3 offset(0,34);
		goto ready;
	zoom:
	zoomadjust:
		BARG A 1 A_WeaponReady(WRF_NOFIRE);
		BARG A 0{
			if(PressingFire()){
				invoker.weaponstatus[BOSSS_ZOOM]=
					clamp(invoker.weaponstatus[BOSSS_ZOOM]+1,0,55);
			}else if(PressingAltfire()){
				invoker.weaponstatus[BOSSS_ZOOM]=
					clamp(invoker.weaponstatus[BOSSS_ZOOM]-1,0,55);
			}
			if(PressingZoom())setweaponstate("zoomadjust");
		}goto nope;
	reload:
		BARG A 1 offset(0,34);
		BARG A 1 offset(2,36);
		BARG A 1 offset(4,40);
		BARG A 2 offset(8,42){
			A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
		}
		BARG A 4 offset(14,46){
			A_PlaySound("weapons/rifleload",CHAN_WEAPON);
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
		}
		BARG A 0{
			int mg=invoker.weaponstatus[BOSSS_MAG];
			if(mg==10)setweaponstate("reloaddone");
			else if(mg>0)setweaponstate("loadhand");
			else if(countinv("BossClip"))setweaponstate("loadclip");
		}
	loadhand:
		BARG A 0{
			if(countinv("SevenMilAmmo"))setweaponstate("loadhandloop");
			else if(countinv("BossClip")||(
					countinv("MagManager")&&
					magmanager(findinventory("magmanager")).weaponstatus[HDMMS_BOSS]>0
				)
			)setweaponstate("stripbossclip");
		}
		BARG A 0 A_JumpIfInventory("LiberatorMag",1,"striplibmag");
		goto reloaddone;
	stripbossclip:
		BARG AAA 3 A_PlaySound("weapons/rifleclick2");
		BARG A 0{
			let mmm=magmanager(findinventory("magmanager"));
			if(mmm&&mmm.weaponstatus[HDMMS_BOSS]>0){
				A_GiveInventory("SevenMilAmmo",mmm.weaponstatus[HDMMS_BOSS]);
				mmm.weaponstatus[HDMMS_BOSS]=0;
			}else{
				A_TakeInventory("BossClip",1);
				A_GiveInventory("SevenMilAmmo",10);
			}
		}
		goto loadhandloop;
	striplibmag:
		BARG AA 4 A_PlaySound("weapons/rifleclick2");
		BARG AA 3 A_PlaySound("weapons/rifleclick2");
		BARG AAAAAA 2 A_PlaySound("weapons/rifleclick2");
		BARG A 0 A_TakeInventory("LiberatorMag",1);
		BARG A 0 A_GiveInventory("SevenMilAmmo",30);
		goto loadhandloop;
	loadhandloop:
		BARG A 4{
			int hnd=min(
				countinv("SevenMilAmmo"),3,
				10-invoker.weaponstatus[BOSSS_MAG]
			);
			if(hnd<1){
				setweaponstate("reloaddone");
				return;
			}else{
				A_TakeInventory("SevenMilAmmo",hnd,TIF_NOTAKEINFINITE);
				invoker.weaponstatus[BOSSS_HAND]=hnd;
				A_PlaySound("weapons/pocket",CHAN_WEAPON);
			}
		}
	loadone:
		BARG A 2 offset(16,50) A_JumpIf(invoker.weaponstatus[BOSSS_HAND]<1,"loadhandnext");
		BARG A 4 offset(14,46){
			invoker.weaponstatus[BOSSS_HAND]--;
			invoker.weaponstatus[BOSSS_MAG]++;
			A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		}loop;
	loadhandnext:
		BARG A 8 offset(16,48){
			if(
				PressingReload()||
				PressingFire()||
				PressingAltFire()||
				PressingZoom()||
				!countinv("SevenMilAmmo")	//don't strip clips automatically
			)setweaponstate("reloaddone");
			else A_PlaySound("weapons/pocket");
		}goto loadhandloop;
	loadclip:
		BARG AA 4 offset(16,50) A_PlaySound("weapons/rifleclick2");
		BARG AA 3 offset(17,52) A_PlaySound("weapons/rifleclick2");
		BARG AAAAAA 2 offset(16,50) A_PlaySound("weapons/rifleclick2");
		BARG A 4 offset(14,46){
			A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
			A_TakeInventory("BossClip",1);
			invoker.weaponstatus[BOSSS_MAG]=10;
		}goto reloaddone;
	reloaddone:
		BARG A 1 offset(4,40);
		BARG A 1 offset(2,36);
		BARG A 1 offset(0,34);
		goto ready;
	unload:
		BARG A 1 offset(0,34);
		BARG A 1 offset(2,36);
		BARG A 1 offset(4,40);
		BARG A 2 offset(8,42){
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
			A_PlaySound("weapons/rifleclick2",CHAN_WEAPON);
		}
		BARG A 4 offset (14,46){
			A_MuzzleClimb(-frandom(0.4,0.8),frandom(0.4,1.4));
			A_PlaySound("weapons/rifleload",CHAN_WEAPON);
		}
	unloadloop:
		BARG A 4 offset(3,41){
			if(invoker.weaponstatus[BOSSS_MAG]<1)setweaponstate("unloaddone");
			else{
				A_PlaySound("weapons/rifleclick2");
				invoker.weaponstatus[BOSSS_MAG]--;
				if(A_JumpIfInventory("SevenMilAmmo",0,"null")){
					A_SpawnItemEx(
						"HDLoose7mm",cos(pitch)*8,0,height-7-sin(pitch)*8,
						cos(pitch)*cos(angle-40)*1+vel.x,
						cos(pitch)*sin(angle-40)*1+vel.y,
						-sin(pitch)*1+vel.z,
						0,SXF_ABSOLUTEMOMENTUM|
						SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH
					);
				}else A_GiveInventory("SevenMilAmmo",1);
			}
		}
		BARG A 2 offset(2,42);
		BARG A 0{
			if(
				PressingReload()||
				PressingFire()||
				PressingAltFire()||
				PressingZoom()
			)setweaponstate("unloaddone");
		}loop;
	unloaddone:
		BARG A 2 offset(2,42);
		BARG A 3 offset(3,41);
		BARG A 1 offset(4,40) A_PlaySound("weapons/rifleclick",CHAN_WEAPON);
		BARG A 1 offset(2,36);
		BARG A 1 offset(0,34);
		goto ready;
	firemode:
	moaadjust:
		BARG A 2 A_WeaponReady(WRF_NOFIRE);
		BARG A 0{
			if(PressingFire()){
				invoker.weaponstatus[BOSSS_DROPADJUST]=
					clamp(invoker.weaponstatus[BOSSS_DROPADJUST]+1,0,120);
			}else if(PressingAltfire()){
				invoker.weaponstatus[BOSSS_DROPADJUST]=
					clamp(invoker.weaponstatus[BOSSS_DROPADJUST]-1,0,120);
			}
			if(PressingFireMode())setweaponstate("moaadjust");
		}goto nope;

	spawn:
		BORF A -1;
	}
	override void InitializeWepStats(bool idfa){
		weaponstatus[BOSSS_CHAMBER]=2;
		weaponstatus[BOSSS_MAG]=10;
		if(!idfa){
			weaponstatus[BOSSS_HEAT]=0;
		}
		if(!owner){
			if(randompick(0,0,1))weaponstatus[0]&=~BOSSF_FRONTRETICLE;
				else weaponstatus[0]|=BOSSF_FRONTRETICLE;
			weaponstatus[BOSSS_ZOOM]=20;
			weaponstatus[BOSSS_DROPADJUST]=24;
		}
	}
}
enum bossstatus{
	BOSSF_FRONTRETICLE=1,
	BOSSF_UNLOADONLY=4,

	BOSSS_CHAMBER=1, //0=nothing, 1=brass, 2=loaded, 3/4=jammed brass/round
	BOSSS_MAG=2,
	BOSSS_ZOOM=3,
	BOSSS_DROPADJUST=4,
	BOSSS_HEAT=5,
	BOSSS_HAND=6,
}



