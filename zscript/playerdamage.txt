
extend class HDPlayerPawn{
//	int playerdmob(
	override int DamageMobj(
		actor inflictor,
		actor source,
		int damage,
		name mod,
		int flags=0,
		double angle=0
	){
		if(!skill || hd_lowdamage)damage*=0.3;

		int towound=0;
		int toburn=0;

		int armourlevel=0;
		if(countinv("BlackArmourFlag")){
			armourlevel=3;
		}
		else if(countinv("BlueArmourFlag")){
			armourlevel=2;
		}
		else if(countinv("GreenArmourFlag")){
			armourlevel=1;
		}

		//spiritual armour
		if(countinv("SpiritualArmourFlag")){
			towound=0;
			toburn=0;
			damage*=0.3;
			if(damage>health)damage=health-1;  
			if(mod!="bleedout" && !random(0,2))A_TakeInventory("SpiritualArmourFlag",1);
		}
		else if(
			mod=="bleedout"
		){
			//bleeding
			if(!skill || hd_nobleed){
				A_TakeInventory("WoundCount");
				woundcount=0;
				return 0;
			}
			if(!checkliquidtexture() && !waterlevel){
				spawn("HDBloodTrailFloor",pos+(random(-2,2),random(-2,2),0),ALLOW_REPLACE);
			}
		}else if(
			mod=="smallarms0"||
			mod=="smallarms1"||
			mod=="smallarms2"||
			mod=="smallarms3"||
			mod=="bullet"||
			mod=="gunshot"
		){
			//shot
			double mult=1;

			int type=0;
			if(mod=="smallarms1")type=1;
			else if(mod=="smallarms2")type=2;
			else if(mod=="smallarms3")type=3;

			//1/6 chance to hit exposed area
			if(random(0,5)){
				int netdamagelevel=max(0,type-armourlevel);
				if(armourlevel==3){
					mult=min(0.3,0.3);
				}
				else if(armourlevel==2){
					mult=min(0.25*type,0.3);
				}
				else if(armourlevel==1){
					mult=min(0.3*(1+type),1);
				}
			}
			towound=damage*(1-mult)*0.05;
			damage*=mult;
		}else if(
			mod=="thermal"||
			mod=="fire"||
			mod=="ice"||
			mod=="plasma"||
			mod=="burning"
		){
			//burned
			//the heat still has to hit the armour
			if(random(0,5)){
				if(countinv("BlackArmourFlag")){
					damage-=30;
				}
				else if(countinv("BlueArmourFlag")){
					damage-=30;
				}
				else if(countinv("GreenArmourFlag")){
					damage-=30;
				}
			}
			if(damage<1)damage=1;
			toburn=damage;
		}else if(
			mod=="electro"||
			mod=="electrical"||
			mod=="lightning"||
			mod=="bolt"
		){
			//electrocuted
		}else if(
			mod=="balefire"||
			mod=="hellfire"||
			mod=="unholy"
		){
			//balefired
		}else{
			//anything else
			damage*=(1-(armourlevel*0.2));
		}


		//abort if damage is less than zero
		if(damage<0) return 0;

		//add to wounds and burns
		if(towound){
			A_GiveInventory("WoundCount",towound);
			woundcount+=towound;
		}
		burncount+=toburn;

		//stun the player randomly
		if(damage>50 || (!random(0,2) && damage>20)){  
			stunned+=damage;
			A_GiveInventory("stuncount",damage);
		}

		if(hd_debug){
			A_Log(string.format("%s took %d %s damage",
				player.getusername(),
				damage,
				mod
			));
		}

		//finally call the real one but ignore all armour
		return super.DamageMobj(
			inflictor,
			source,
			damage,
			mod,
			flags,//|DMG_NO_ARMOR,
			angle
		);
	}
	states{
	pain.bleedout:
	pain.invisiblebleedout:
		"----" A 0{
			if(random(1,128)==1){A_GiveInventory("LethalDamage",1);}
			A_GiveInventory("Fatigue",2);
			if(health<40){A_GiveInventory("IsHurt",1);}
			if(countinv("Fatigue")<30){A_GiveInventory("Fatigue",20);}
			if(countinv("IsMoving")){setstatelabel("see");}
			else setstatelabel("spawn");
		}
	pain:
	pain.drowning:
	pain.falling:
	pain.internal:
		"----" A 0{
			if(random(1,128)==1){A_GiveInventory("LethalDamage",1);}
			A_GiveInventory("Fatigue",2);
		}
	painend:
		"####" G 1;
		"####" G 2{
			A_SetBlend("00 00 00",0.8,40,"00 00 00");
			A_SetPitch(pitch+random(-4,4),SPF_FORCECLAMP|SPF_INTERPOLATE);
			A_SetAngle(angle+random(-4,4),SPF_INTERPOLATE);
			A_TakeInventory("PowerFrightener");
			A_TakeInventory("NotShot");
			if(health<40){A_GiveInventory("IsHurt");}
			A_TakeInventory("SpiritualArmourFlag",1);
		}
		"####" G 3 A_Pain();
		"----" A 0 A_Jump(256,"spawn");
	}
}

