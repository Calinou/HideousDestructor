//-------------------------------------------------
// [insert homestar reference here]
//-------------------------------------------------
extend class HDPlayerPawn{
	//cheats to give or take away bad stuff
	bool cheatgivestatusailments(string name,int amount){
		if(name~=="wound"||name~=="wounds"||name~=="woundcount"){
			woundcount+=amount;return true;
		}
		if(name~=="oldwound"||name~=="oldwounds"||name~=="oldwoundcount"){
			oldwoundcount+=amount;return true;
		}
		if(name~=="unstablewound"||name~=="unstablewounds"||name~=="unstablewoundcount"){
			unstablewoundcount+=amount;return true;
		}
		if(name~=="burn"||name~=="burns"||name~=="burncount"){
			burncount+=amount;return true;
		}
		if(name~=="aggro"||name~=="aggravateddamage"){
			aggravateddamage+=amount;return true;
		}
		if(name~=="stun"||name~=="stuncount"){
			stunned+=amount;return true;
		}
		return false;
	}
	override void CheatGive(string name, int amount){
		Class<Inventory> type;
		let player=self.player;
		if(!player.mo||player.health<1)return;

		if(cheatgivestatusailments(name,amount)) return;

		//apply giveammo principles to HDAmmo
		if(name~=="ammo"){
			if(hd_carrylimit)A_GiveInventory("Backpack");
			for(int i=0;i<AllActorClasses.Size();++i){
				type=(class<Inventory>)(AllActorClasses[i]);
				if (type&&type.GetParentClass()=="HDAmmo"){
					let ammoitem=FindInventory(type);
					if(!ammoitem){
						ammoitem=Inventory(Spawn(type));
						ammoitem.AttachToOwner(self);
						ammoitem.Amount=ammoitem.MaxAmount;
					}
					else if(ammoitem.Amount<ammoitem.MaxAmount){
						ammoitem.Amount=ammoitem.MaxAmount;
					}
				}
			}
		}
		//just work around armour
		if(name~=="armor"||name~=="armour"){
			A_GiveInventory("BlackArmour",HDCONST_BLACKARMOUR);
			A_TakeInventory("GreenArmour");
			A_TakeInventory("BlueArmour");
			A_TakeInventory("BasicArmor");
			return;
		}

		//pistol hack part 1
		int p1auto=countinv("HDPistolFullAuto");
		int p2auto=countinv("HDSecondPistolFullAuto");
		if(name~=="weapons"||name~=="all"||name~=="everything"){
			p1auto=max(p1auto,1);
			p2auto=max(p2auto,1);
		}

		//super call
		super.cheatgive(name,amount);

		//pistol hack part 2
		inventory ppp=findinventory("HDPistolFullAuto");
		if(ppp)ppp.amount=p1auto;
		ppp=findinventory("HDSecondPistolFullAuto");
		if(ppp)ppp.amount=p2auto;

		//clean up some stuff
		if(!hd_carrylimit)A_TakeInventory("backpack");
		A_TakeInventory("BasicArmor");
	}
	override void CheatTake(string name, int amount){
		if(!cheatgivestatusailments(name,-amount))super.cheattake(name,amount);
	}
}