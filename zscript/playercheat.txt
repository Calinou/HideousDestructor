//-------------------------------------------------
// [insert homestar reference here]
//-------------------------------------------------
extend class HDPlayerPawn{
	//cheats to give or take away bad stuff
	bool cheatgivestatusailments(string name,int amount=1){
		if(name~=="fire"){
			if(!scopecamera){
				scopecamera=spawn("ViewDummy",pos);
				scopecamera.changetid(playernumber()-32000);
				scopecamera.target=self;
			}
			let hdsc=hdactor(scopecamera);
			if(hdsc)hdsc.A_Immolate(self,self,amount);
		}
		if(name~=="wound"||name~=="wounds"||name~=="woundcount"){
			woundcount+=amount;return true;
		}
		if(name~=="oldwound"||name~=="oldwounds"||name~=="oldwoundcount"){
			oldwoundcount+=amount;return true;
		}
		if(name~=="unstablewound"||name~=="unstablewounds"||name~=="unstablewoundcount"){
			unstablewoundcount+=amount;return true;
		}
		if(name~=="burn"||name~=="burns"||name~=="burncount"){
			burncount+=amount;return true;
		}
		if(name~=="aggro"||name~=="aggravateddamage"){
			aggravateddamage+=amount;return true;
		}
		if(name~=="stun"||name~=="stuncount"){
			stunned+=amount;return true;
		}
		if(name~=="zerk"){
			zerk+=amount;return true;
		}
		return false;
	}
	override void CheatGive(string name, int amount){
		let player=self.player;
		if(!player.mo||player.health<1)return;

		if(cheatgivestatusailments(name,amount)) return;

		bool allthings=(name~=="all"||name~=="everything");

		//apply giveammo principles to HDAmmo
		class<inventory> type;
		if(allthings||name~=="ammo"){
			if(hd_carrylimit)A_GiveInventory("Backpack");
			for(int i=0;i<AllActorClasses.Size();++i){
				type=(class<Inventory>)(AllActorClasses[i]);
				if (type&&type.GetParentClass()=="HDAmmo"){
					let ammoitem=FindInventory(type);
					if(!ammoitem){
						ammoitem=Inventory(Spawn(type));
						ammoitem.AttachToOwner(self);
						ammoitem.Amount=ammoitem.MaxAmount;
					}
					else if(ammoitem.Amount<ammoitem.MaxAmount){
						ammoitem.Amount=ammoitem.MaxAmount;
					}
				}
			}
		}

		//just work around armour
		if(allthings||name~=="armor"||name~=="armour"){
			A_GiveInventory("BlackArmour",HDCONST_BLACKARMOUR);
			A_TakeInventory("GreenArmour");
			A_TakeInventory("BlueArmour");
			A_TakeInventory("BasicArmor");
			return;
		}

		//super call
		super.cheatgive(name,amount);

		//load weapons that use variables instead of ammo types
		if(allthings||name~=="ammo"){
			thinkeriterator it=thinkeriterator.create("HDWeapon");
			actor hdw;
			while(
				hdw=inventory(it.Next())
			){
				let hdww=hdweapon(hdw);
				if(
					hdww
					&& hdww.owner==self
				)hdww.initializewepstats(true);
			}
		}

		//pistol hack
		if(allthings||name~=="weapons"){
			A_SetInventory("HDPistolFullAuto",max(1,countinv("HDPistolFullAuto")));
			A_SetInventory("HDSecondPistolFullAuto",max(1,countinv("HDSecondPistolFullAuto")));
		}

		//clean up some stuff
		if(!hd_carrylimit)A_TakeInventory("backpack");
		A_TakeInventory("BasicArmor");
	}
	override void CheatTake(string name, int amount){
		if(!cheatgivestatusailments(name,-amount))super.cheattake(name,amount);
	}
}