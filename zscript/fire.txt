// ------------------------------------------------------------
// Setting things on fire
// ------------------------------------------------------------
class ImmunityToFire:InventoryFlag{
	override void attachtoowner(actor user){
		super.attachtoowner(user);
		if(owner){
			actoriterator it=actoriterator.create(-7677,"HDFire");
			actor fff;
			while(fff=it.next()){
				if(fff.target==owner){
					fff.destroy();
				}
			}
		}
	}
}
class HDFireEnder:InventoryFlag{
	default{
		inventory.maxamount 5;
	}
}
class HDFireDouse:InventoryFlag{
	default{
		inventory.maxamount 20;
	}
	override void DoEffect(){
		if(amount>0)amount--;
	}
}


class HDFireStarter:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(!self) return;
			if(hd_debug)a_log("deprecated fire. please use A_Immolate() directly.");
			actor f=spawn("HDFire",pos);
			f.target=self;f.master=self.target;
			f.stamina=random(40,80);
			return;
		}fail;
	}
}


//how to immolate
extend class HDActor{
	//should be in HDActor once all conversions are done
	//A_Immolate(tracer,target);
	virtual void A_Immolate(
		actor victim,
		actor perpetrator,
		int duration=0
	){
		if(!victim
			||(
				perpetrator&&
				perpetrator.bdontharmspecies&&
				perpetrator.getspecies()==victim.getspecies()
			)
		){
			victim=spawn("PersistentDamager",self.pos);
			victim.target=perpetrator;
		}
		actor f=victim.spawn("HDFire",victim.pos);
		f.target=victim;f.master=perpetrator;
		if(duration<1) f.stamina=random(40,80);
		else f.stamina=duration;
		if(victim is "PlayerPawn")f.changetid(-7677);
	}
}
//fire actor
class HDFire:IdleDummy{
	int halfrad;int minz;int maxz;int lastheight;
	default{
		+bloodlessimpact
		obituary "%o was burned by %k.";
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(target){
			stamina=target.ApplyDamageFactor("Thermal",stamina);
			if(target is "PlayerPawn" || target is "HDPlayerCorpse"){
				changetid(-7677);
				if(!skill||hd_lowdamage)stamina=max(1,stamina*0.3);
			}
			if(!target.bshootable && stamina>20)stamina=20;
		}
		if(
			A_CheckProximity("null","HDFire",64,12,CPXF_CHECKSIGHT|CPXF_SETTRACER)
			&&(!target||tracer.target==target)
		){
			tracer.stamina+=stamina;
			destroy();
			return;
		}
		if(hd_debug)A_Log(string.format("fire duration \ci%i",stamina));
	}
	override void ondestroy(){
		if(target&&target is "PersistentDamager")target.destroy();
		super.ondestroy();
	}
	states{
	spawn:
		TNT1 A 3{
			if(target&&target.countinv("ImmunityToFire")){
				destroy();return;
			}

			if(waterlevel){
				destroy();
				if(waterlevel<2)spawn("HDSmoke",pos);
				return;
			}
			if(!master)master=self;
			if(!target){
				target=spawn("PersistentDamager",self.pos);
				target.target=master;
				if(stamina>20)stamina=20;
			}
			setorigin(target.pos,false);
			A_SetTics(clamp(random(3,30-stamina*0.1),2,12));
			if(stamina<=0 || target.countinv("HDFireEnder")){
				A_TakeFromTarget("HDFireEnder");
				spawn("HDSmoke",pos);
				destroy();
				return;
			}
			int ds=target.countinv("HDFireDouse");
			if(ds){
				target.A_TakeInventory("HDFireDouse",ds);
				stamina-=ds;
			}
			stamina--;

			//set flame spawn point
			if(lastheight!=target.height){ //poll only height
				halfrad=max(4,target.radius*0.5);
				lastheight=target.height;
				minz=lastheight*0.2;
				maxz=max(lastheight*0.75,4);
			}

			//position and spawn flame
			setorigin(pos+(
					random(-halfrad,halfrad),
					random(-halfrad,halfrad),
					random(minz,maxz)
			),false);
			actor sp=spawn("HDFlameRed",pos);
			sp.vel+=target.vel+(frandom(-2,2),frandom(-2,2),frandom(-1,3));
			A_PlaySound("misc/firecrkl",CHAN_AUTO,0.4,0,6);

			//check if player
			let tgt=HDPlayerPawn(target);
			if(tgt){
				if(tgt.playercorpse){
					target=tgt.playercorpse;
				}
				A_AlertMonsters();
				A_TakeFromTarget("PowerFrightener");
				A_GiveToTarget("WeaponBusy",1);
				A_GiveToTarget("IsMoving",4);
			}else stamina-=3; //monsters assumed to be trying to douse
			//damage the target
			target.damagemobj(self,master,randompick(0,0,0,1),"Thermal",flags:DMG_NO_FACTOR);
			target.A_GiveInventory("Heat",10);
		}
		wait;
	}
}




//an invisible actor that constantly damages anything it collides with
/* special uses:
	threshold = damage interval
	damage = *exact* damage amount each time
	accuracy = temporary number tracking interval
	gibhealth = % chance to immolate
*/
class PersistentDamager:HDActor{
	vector3 relpos;
	default{
		+noblockmap +missile
		height 8;radius 8;threshold 35;damage 1;accuracy 0;gibhealth 5;
		damagetype "thermal";
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(master)relpos=self.pos-master.pos;
	}
	override void tick(){
		if(globalfreeze||level.Frozen) return;

		if(master)setorigin(master.pos+relpos,false);
		trymove(pos.xy,true);

		//nexttic
		if(CheckNoDelay()){
			if(tics>0)tics--;  
			while(!tics){
				if(!SetState(CurState.NextState)){
					return;
				}
			}
		}
	}
	override bool CanCollideWith(actor other,bool passive){
		if(
			passive
			||(
				target &&
				target.bdontharmspecies &&
				other.getspecies()==target.getspecies()
			)
		){
			return false;
		}
		other.A_GiveInventory("Heat",1);
		return false;
	}
	states{
	spawn:
		TNT1 A -1;
	}
}





//new shit


class Heat:InventoryFlag{
	default{
		+inventory.untossable
		inventory.maxamount 1000000;
	}
	int flamethreshold;
	int hurtthreshold;
	override void AttachToOwner(actor other){
		super.attachtoowner(other);
		if(!owner){destroy();return;}

		//mass, height, etc. for flamethreshold
		flamethreshold=(
			(
				max(
					owner.mass,
					getdefaultbytype(owner.getclass()).health,
					getdefaultbytype(owner.getclass()).gibhealth
				)
				+owner.health
			)
			*getdefaultbytype(owner.getclass()).radius
			*getdefaultbytype(owner.getclass()).height
		)/1000;

		//hurtthreshold pinned on flamethreshold
		int ht=flamethreshold*0.4;
		hurtthreshold=ApplyDamageFactor("Thermal",ht);
		int althurtthreshold=ApplyDamageFactor("Fire",ht);
		if(althurtthreshold<hurtthreshold)hurtthreshold=althurtthreshold;
		althurtthreshold=ApplyDamageFactor("Burning",ht);
		if(althurtthreshold<hurtthreshold)hurtthreshold=althurtthreshold;

		hurtthreshold=max(1,hurtthreshold);

		//used to calculated movement
		setorigin(owner.pos,false);
		angle=owner.angle;
	}
	override void DoEffect(){
		if(!owner){destroy();return;}
		if(amount<1)return;

		if(
			owner.health<1
			&&hdplayerpawn(owner)
			&&hdplayerpawn(owner).playercorpse
		){
			target=hdplayerpawn(owner).playercorpse;
			target.A_GiveInventory("Heat",amount);
			amount=0;
			return;
		}

		amount--;
		if(owner.health>0)amount-=2;
		int mv=max(absangle(owner.angle,angle)*0.05,
			clamp(distance3dsquared(owner)*0.05,0,15)
		);
		amount-=random(0,mv);

		angle=owner.angle;
		setorigin(owner.pos,false);

		if(amount>hurtthreshold){
			int dmgamt=randompick(0,1,amount/hurtthreshold);
			owner.damagemobj(self,null,dmgamt,"Thermal",DMG_NO_ARMOR|DMG_THRUSTLESS);

			if(
				owner&&
				amount>(flamethreshold*frandom(1,(HEAT_FLAMEMULT-1)))
			){
				//spawn a flame
				if(accuracy<1){
					accuracy=random(2,5);
					double or=owner.radius;
					actor aaa=spawn("HDFlameRed",owner.pos
						+(frandom(-or,or),frandom(-or,or),frandom(0,owner.height))
					);
					aaa.A_PlaySound("misc/firecrkl",CHAN_VOICE,min(0.4,0.0001*amount));
					if(random(-flamethreshold,amount)>flamethreshold*2){
						owner.A_GiveInventory("SawGib",1);
						owner.A_SpawnItemEx(owner.bloodtype,
							frandom(0,owner.radius),0,
							frandom(0,owner.height),
							1,0,2,frandom(0,360),
							SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR
						);
						aaa.A_PlaySound("misc/bulletflesh",CHAN_BODY,0.2);
						owner.vel.z+=frandom(-1,1);
	
						//structural damage
						if(owner.health<1)hurtthreshold++;
					}
				}else accuracy--;
				amount=clamp(amount+max(1,amount/10),0,flamethreshold*(HEAT_FLAMEMULT+1));
			}
		}
	}
	enum HeatThresholds{
		HEAT_FLAMEMULT = 5,
	}
}



