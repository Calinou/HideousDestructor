// ------------------------------------------------------------
// "I call the big one Bitey!"
// ------------------------------------------------------------
class Babuin:HDActor{
	vector3 lastpos;
	vector3 latchpos;
	double targangle;
	bool latched;
	double latchforce;
	override void postbeginplay(){
		super.postbeginplay();
		hdmobai.resize(self,0.9,1.1);
		let hdmb=hdmobster(hdmobster.spawnmobster(self));
		hdmb.meleethreshold=200;
	}
	void TryLatch(){
		if(
			!target
			||target.health<1
			||distance2d(target)-target.radius-radius>12
		){
			latched=false;
			return;
		}else{
			latched=true;
			bnotargetswitch=true;
			latchpos.xy=
				rotatevector(pos.xy-target.pos.xy,-target.angle).unit()
				*(target.radius+radius)
			;
			latchpos.z=random(8,target.height-12);
			targangle=target.angle;
			latchforce=min(0.4,mass*0.02/max(1,target.mass));
			lastpos=pos;
			setstatelabel("latched");
		}
	}
	override bool cancollidewith(actor other,bool passive){
		return(
			other!=target
			||(
				!latched
				&&max(
					abs(other.pos.x-pos.x),
					abs(other.pos.y-pos.y)
				)>=other.radius+radius
			)
		);
	}
	override void Die(actor source,actor inflictor,int dmgflags=0){
		latched=false;
		bnoclip=false;
		super.Die(source,inflictor,dmgflags);
	}
	override void Tick(){
		if(!latched||!target||target.health<1){
			latched=false;
			bnotargetswitch=false;
		}
		if(latched){
			A_FaceTarget();
			vector3 lp=target.pos;
			targangle=(targangle+target.angle)*0.5;
			lp.xy+=rotatevector(latchpos.xy,target.angle);
			latchpos.z=clamp(latchpos.z+random(-2,2),12,max(floorz,target.height-height));
			lp.z+=latchpos.z+frandom(-0.1,0.1);

			//don't interpolate teleport
			if(
				abs(lp.x-pos.x)>100||
				abs(lp.y-pos.y)>100||
				abs(lp.z-pos.z)>100
			){
				setorigin(lp,false);
			}else setorigin((lp+pos)*0.5,true);

			//can try to bump or shake it off
			if(
				absangle(target.angle,targangle)>random(30,180)||
				floorz>pos.z||
				ceilingz<pos.z+height||
				(!trymove(pos.xy,true)&&(
						blockingmobj!=target
					)
				)
			){
				latched=false;
				A_FaceTarget(0,0);
				A_Changevelocity(-6,random(-2,2),4,CVF_RELATIVE);
			}else{
				//fun!
				target.A_SetAngle(frandom(
					target.angle,targangle)+random(-8,8),SPF_INTERPOLATE
				);
				target.A_SetPitch(target.pitch+random(-6,10),SPF_INTERPOLATE);
				target.vel+=(pos-lastpos)*latchforce;
				lastpos=pos;
				//lift the victim as circumstances permit
				if(
					floorz>=pos.z
					&&mass>target.mass
				){
					target.addz(random(-1,2),true);
				}
			}
			nexttic();return;
		}
		else super.Tick();
	}

	default{
		monster;
		+cannotpush +pushable
		health 90;radius 16;
		height 44;deathheight 10;
		scale 0.6;
		speed 14;
		mass 70;
		meleerange 40;
		maxtargetrange 128;
		painchance 90; pushfactor 0.2;
		maxstepheight 32;maxdropoffheight 32;
		seesound "demon/sight";painsound "demon/pain";
		deathsound "demon/death";activesound "demon/active";
		obituary "%o was mauled by a babuin.";
		damagefactor "Thermal",0.76;
		damagefactor "SmallArms0",0.9;
	}
	states{
	spawn:
		SRG2 A 0 nodelay A_JumpIf(bambush,"spawnstill");
	spawnwander:
		SRG2 AABBCCDD random(2,3){
			blookallaround=false;
			hdmobai.wander(self);
		}
		SRG2 A 0{
			if(!random(0,5))setstatelabel("spawnsniff");
			else if(!random(0,9))A_PlaySound(activesound,CHAN_VOICE);
		}loop;
	spawnsniff:
		SRG2 A 0{blookallaround=true;}
		SRG2 EEEEEEEE 2{
			angle+=frandom(-2,2);
			A_Look();
		}
		SRG2 F 2{
			angle+=frandom(-20,20);
			if(!random(0,9))A_PlaySound(activesound,CHAN_VOICE);
		}
		SRG2 FFF 2 A_Look();
		SRG2 A 0{
			blookallaround=false;
			if(!random(0,6))setstatelabel("spawnwander");
		}loop;
	spawnstill:
		SRG2 AABB 4 A_Look();
		loop;
	see:
		SRG2 A 0{
			blookallaround=false;
			if(
				(target&&checksight(target))
				||!random(0,7)
			)setstatelabel("seechase");
			else setstatelabel("roam");
		}goto roam;
	seechase:
		SRG2 AABBCCDD random(1,2){hdmobai.chase(self);}
		goto seeend;
	roam:
		SRG2 AABBCCDD random(1,3){hdmobai.wander(self,true);}
		goto seeend;
	seeend:
		SRG2 A 0{
			if(!random(0,120)){
				A_PlaySound(seesound,CHAN_VOICE);
				A_AlertMonsters();
			}
			HealThing(random(2,12));
		}
		goto see;
	melee:
		SRG2 E 7{
			A_FaceTarget(0,0);
			A_PlaySound("demon/melee");
			A_Changevelocity(cos(pitch)*4,0,sin(-pitch)*4,CVF_RELATIVE);
		}
		SRG2 F 6;
		SRG2 G 2{
			TryLatch();
			A_CustomMeleeAttack(random(5,15),"","","teeth",true);
		}
	postmelee:
		SRG2 G 6;
		goto see;

	latched:
		#### EF random(1,2){
			if(latched){
				if(!random(0,30))A_Pain();
				target.damagemobj(
					self,self,random(0,2),random(0,3)?"teeth":"falling"
				);
			}else{
				setstatelabel("pain");
			}
		}loop;

	missile:
		SRG2 ABCD 2{
			A_FaceTarget(16,16);
			A_Changevelocity(1,0,0,CVF_RELATIVE);
			if(A_JumpIfTargetInLOS("null",20,0,128))setstatelabel("jump");
		}goto see;
	jump:
		SRG2 AE 3{
			A_FaceTarget(16,16);
			A_Changevelocity(cos(pitch)*3,0,sin(-pitch)*3,CVF_RELATIVE);
		}
		SRG2 E 2{
			A_FaceTarget(6,6);
			A_PlaySound("babuin/sight");
		}
		SRG2 E 0 A_ChangeVelocity(cos(pitch)*16,0,sin(-pitch)*16+random(3,8),CVF_RELATIVE);
	fly:
		SRG2 F 1{
			TryLatch();
			if(floorz>=pos.z)setstatelabel("land");
		}wait;
	land:
		SRG2 FEH 3{vel.xy*=0.8;}
		SRG2 D 4{vel.xy=(0,0);}
		goto see;
	pain:
		SRG2 H 2 A_SetSolid();
		SRG2 H 6 A_Pain();
		SRG2 H 0 A_CheckFloor("missile");
		goto see;
	death:
		SRG2 I 5{
			A_Scream();
			bpushable=false;
			hdmobai.corpseflags(self);
			A_SpawnItemEx("tempshield2", 0,0,0, vel.x,vel.y,vel.z, 0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("BFGVileShard",flags:SXF_TRANSFERPOINTERS|SXF_SETMASTER,240);
		}
	deathend:
		SRG2 J 5 A_NoBlocking();
		SRG2 KLM 5;
	dead:
		SRG2 M 3 canraise{
			if(abs(vel.z)<2)frame++;
		}loop;
	raise:
		---- A 0{
			hdmobai.corpseflags(self);
			if(countinv("IsGibbed"))setstatelabel("raisegibbed");
			else bpushable=true;
		}
		SRG2 NMLKJI 5;
		goto see;
	raisegibbed:
		TROO U 6{
			A_TakeInventory("IsGibbed");
			A_SpawnItemEx("MegaBloodSplatter",0,0,4,
				vel.x,vel.y,vel.z+3,0,
				SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
			);
		}
		TROO UT 8;
		TROO SRQ 6;
		TROO PO 4;
		SRG2 H 4 A_Die("Ungibbed");
	death.ungibbed:
		SRG2 I 5{
			bpushable=false;
			bnodropoff=false;
			hdmobai.corpseflags(self);
			A_SpawnItemEx("tempshield2",flags:SXF_NOCHECKPOSITION|SXF_SETMASTER);
		}goto deathend;
	XDeathBrewtleLulz: //it's identical
	xdeath:
		TROO O 0{
			bpushable=false;
			bnodropoff=false;
			hdmobai.corpseflags(self);
			A_GiveInventory("IsGibbed");
			A_XScream();
			A_NoBlocking();
		}
		TROO OPQ 4{spawn("MegaBloodSplatter",pos+(0,0,34));}
		TROO RST 4;
	xdead:
		TROO T 5 canraise{
			if(abs(vel.z)<2)frame++;
		}loop;
	death.spawndead:
		---- A 0{
			bpushable=false;
			A_NoBlocking();
			hdmobai.corpseflags(self);
		}goto dead;
	}
}


class SpecBabuin:Babuin{
	default{
		renderstyle "fuzzy";
		dropitem "HDBlurSphere",1;
	}
	states{
	see:
		TNT1 A 0 A_SetTranslucent(1,2);
		TNT1 A 0 A_JumpIfCloser(128,2);
		TNT1 A 0 A_Jump(200,2);
		TNT1 A 0;
		goto super::see;
		TNT1 A 4 A_Chase();
		loop;
	death:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(2,14),
			vel.x,vel.y,vel.z+random(1,3),0,
			SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION
		);
		TNT1 A 0 A_SetTranslucent(1,0);
		goto super::death;
	xdeath:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(2,14),
			vel.x,vel.y,vel.z+random(1,3),0,
			SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION
		);
		TNT1 A 0 A_SetTranslucent(1,0);
		goto super::xdeath;
	}
}
class DeadBabuin:Babuin{
	override void postbeginplay(){
		super.postbeginplay();
		A_Die("spawndead");
	}
}
class DeadSpecBabuin:SpecBabuin{
	override void postbeginplay(){
		super.postbeginplay();
		A_Die("spawndead");
	}
}


class DeadDemonSpawner:RandomSpawner replaces DeadDemon{
	default{
		+ismonster
		dropitem "DeadBabuin",256,5;
		dropitem "DeadSpecBabuin",256,2;
		dropitem "DeadSpectre",256,1;
	}
}