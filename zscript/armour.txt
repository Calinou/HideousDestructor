//-------------------------------------------------
// Armour
//-------------------------------------------------
const HDCONST_BLUEARMOUR=33;
const HDCONST_GREENARMOUR=144;

class HDArmour:HDPickup{
	int doubleclicktimer;
	override void doeffect(){
		if(!amount)goawayanddie();
		if(doubleclicktimer>0)doubleclicktimer--;
	}
	default{
		+inventory.invbar
		inventory.maxamount HDCONST_GREENARMOUR;
		inventory.amount HDCONST_GREENARMOUR;
		accuracy HDCONST_GREENARMOUR;

		inventory.icon "ARMSB0";

		meleethreshold 1; //used as armour level
	}
	override string pickupmessage(){
		if(meleethreshold==3)return "Picked up the combat armour.";
		else return "Picked up the security armour.";
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(meleethreshold==3){
			icon=texman.checkfortexture("ARMCB0",TexMan.Type_MiscPatch);
			maxamount=HDCONST_BLUEARMOUR;
			amount=min(maxamount,amount);
		}
	}
	override inventory createtossable(int amount){
		inventory iii=super.createtossable(amount);
		if(iii){
			iii.translation=0;
			iii.target=owner;
			iii.meleethreshold=meleethreshold;
			if(iii.amount<self.amount){
				iii.amount+=self.amount;
				self.amount=0;
			}
		}
		return iii;
	}
	override void actualpickup(actor other){
		if(!other)other=picktarget;
		if(!other)return;
		if(other.countinv(getclassname()))return;

		if(trypickup(other)){
			other.A_PlaySound(pickupsound,CHAN_AUTO);
			other.A_Log(string.format("\cg%s",pickupmessage()),true);
		}
	}
	states{
	spawn:
		ARMC A 0;
		ARMS A -1{
			if(meleethreshold==3){
				sprite=getspriteindex("ARMCA0");
			}
		}stop;
	use:
		TNT1 A 0{
			if(countinv("WornRadsuit")){
				if(getcvar("hd_helptext"))A_Log("Take off your environment suit first!",true);
			}else if(countinv("HDArmourWorn")){
				if(
					getcvar("hd_autostrip")
					||invoker.doubleclicktimer>25
				){
					if(getcvar("hd_helptext"))A_Log("Taking off armour first.",true);
					A_DropInventory("HDArmourWorn");
					invoker.doubleclicktimer=0;
				}else{
					if(getcvar("hd_helptext")&&!invoker.stamina) A_Log("You are already wearing armour. \cdhd_strip\c- in the console or double-tap the inventory icon to remove your current armour.",true);
					invoker.doubleclicktimer+=35;
				}
			}else{
				if(invoker.master!=self)A_TakeInventory("SpiritualArmour");
				A_SetBlend("00 00 00",1,6,"00 00 00");
				A_PlaySound("weapons/pocket");
				A_ChangeVelocity(0,0,2);
				let onr=HDPlayerPawn(self);
				if(onr)onr.stunned+=40;


				A_SetInventory("HDArmourWorn",invoker.amount);
				inventory iii=findinventory("HDArmourWorn");
				int armourlevel=invoker.meleethreshold;
				iii.meleethreshold=armourlevel;

				int qual=armourlevel==3?HDCONST_BLUEARMOUR:HDCONST_GREENARMOUR;

				if(getcvar("hd_helptext")){
					if(qual)qual=invoker.amount/qual;else if(hd_debug)A_Log("armour calculation error.");
					if(qual<0.3)A_Log("You cover your shameful nakedness with your filthy rags.",true);
					else if(qual<0.6)A_Log("It's better than nothing.");
					else if(qual<0.75)A_Log("This armour has definitely seen better days.",true);
					else if(qual<0.95)A_Log("This armour does not pass certification.",true);
				}
				invoker.destroy();
			}
		}fail;
	}
}
class HDArmourWorn:HDArmour{
	default{
		-inventory.invbar
	}
	override void doeffect(){
		if(!amount)goawayanddie();
	}
	override inventory createtossable(int amount){
		if(!owner)return null;
		inventory iiii=inventory(spawn("HDArmour",owner.pos+(0,0,owner.height*0.6),ALLOW_REPLACE));
		if(iiii){
			owner.A_SetBlend("00 00 00",1,6,"00 00 00");
			owner.A_PlaySound("weapons/pocket");
			owner.vel.z+=2;
			let onr=HDPlayerPawn(owner);
			if(onr)onr.stunned+=40;

			iiii.amount=self.amount;
			iiii.angle=owner.angle;
			iiii.A_ChangeVelocity(1,0,1,CVF_RELATIVE);
			iiii.vel+=owner.vel;
			iiii.meleethreshold=meleethreshold;
			iiii.master=owner;
			HDF.TransferFire(owner,iiii);

			self.amount=0;
		}
		return iiii;
	}
	states{
	use:
		TNT1 A 0;
		stop;
	}
}


class BlueArmour:Inventory replaces BlueArmor{
	default{
		meleethreshold 3;
		inventory.maxamount HDCONST_GREENARMOUR;
		inventory.amount HDCONST_GREENARMOUR;
	}
	override void postbeginplay(){
		super.postbeginplay();
		inventory aaa=null;
		if(owner){
			owner.A_SetInventory("HDArmourWorn",amount);
			aaa=owner.findinventory("HDArmourWorn");
		}else{
			aaa=inventory(spawn("HDArmour",pos));
		}
		if(aaa)aaa.meleethreshold=meleethreshold;
		destroy();
	}
}
class GreenArmour:BlueArmour replaces GreenArmor{default{meleethreshold 1;}}

