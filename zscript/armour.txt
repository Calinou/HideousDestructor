//-------------------------------------------------
// Armour
//-------------------------------------------------
const HDCONST_BLUEARMOUR=33;
const HDCONST_GREENARMOUR=144;

class HDArmour:HDMagAmmo{
	default{
		+inventory.invbar
		inventory.amount 1;
		inventory.maxamount 3;
		hdmagammo.maxperunit HDCONST_GREENARMOUR;
		hdmagammo.magbulk ENC_GREENARMOUR;
		hdpickup.nicename "Armour";
		hdpickup.refid HDLD_ARMGINV;
		inventory.icon "ARMSB0";
		inventory.pickupmessage "Picked up the security armour.";
	}
	bool mega;
	int cooldown;
	override int getsbarnum(int flags){
		if(mags.size()<1)return -1000000;
		return mags[0]%1000;
	}
	override string pickupmessage(){
		if(mags[0]>=1000)return "Picked up the combat armour!";
		return super.pickupmessage();
	}
	//because it can intentionally go over the maxperunit amount
	override void AddAMag(int addamt){
		if(addamt<0)addamt=maxperunit;
		mags.push(addamt);
		amount=mags.size();
	}
	action void A_WearArmour(){
		invoker.syncamount();
		int dbl=invoker.mags[0];
		//if holding use, cycle to next armour
		if(player.cmd.buttons&BT_USE){
			invoker.mags.push(dbl);
			invoker.mags.delete(0);
			invoker.syncamount();
			return;
		}
		//strip intervening items on doubleclick
		string dropfirst="";
		if(countinv("HDBackpack"))dropfirst="HDBackpack";
		else if(countinv("WornRadsuit"))dropfirst="WornRadsuit";
		else if(countinv("HDArmourWorn"))dropfirst="HDArmourWorn";
		if(dropfirst!=""){
			if(invoker.cooldown)A_DropInventory(dropfirst);
			else{
				invoker.cooldown=10;
				A_Log(string.format(
					"Take off your %s first!%s",
					dropfirst=="HDBackpack"?"backpack":
					dropfirst=="WornRadsuit"?"environment suit":
					"current armour",
					getcvar("hd_helptext")?" (double-tap to do this)":""
				),true);
			}
			return;
		}
		//and finally put on the actual armour
		HDArmour.ArmourChangeEffect(self);
		let worn=HDArmourWorn(GiveInventoryType("HDArmourWorn"));
		if(dbl>=1000){
			dbl-=1000;
			worn.mega=true;
		}
		worn.durability=dbl;
		invoker.amount--;
		invoker.mags.delete(0);

		if(getcvar("hd_helptext")){
			string blah=string.format("You put on the %s armour. ",worn.mega?"combat":"security");
			double qual=double(worn.durability)/(worn.mega?HDCONST_BLUEARMOUR:HDCONST_GREENARMOUR);
			if(qual<0.1)A_Log(blah.."Just don't get hit.",true);
			else if(qual<0.3)A_Log(blah.."You cover your shameful nakedness with your filthy rags.",true);
			else if(qual<0.6)A_Log(blah.."It's better than nothing.");
			else if(qual<0.75)A_Log(blah.."This armour has definitely seen better days.",true);
			else if(qual<0.95)A_Log(blah.."This armour does not pass certification.",true);
		}

		invoker.syncamount();
	}
	override void doeffect(){
		if(cooldown>0)cooldown--;
		if(!amount)destroy();
	}
	override void syncamount(){
		if(amount<1){destroy();return;}
		super.syncamount();
		mega=mags[0]>=1000;
		icon=texman.checkfortexture(mega?"ARMCB0":"ARMSB0",TexMan.Type_MiscPatch);
	}
	override void beginplay(){
		cooldown=0;
		mega=icon==texman.checkfortexture("ARMCB0",TexMan.Type_MiscPatch);
		mags.push((mega?(1000+HDCONST_BLUEARMOUR):HDCONST_GREENARMOUR));
		super.beginplay();
	}
	override void consolidate(){}
	override double getbulk(){
		syncamount();
		double blk=0;
		for(int i=0;i<amount;i++){
			if(mags[i]>=1000)blk+=ENC_BLUEARMOUR;
			else blk+=ENC_GREENARMOUR;
		}
		return blk;
	}
	override void actualpickup(actor other){
		cooldown=0;
		if(!other)return;
		int durability=mags[0];
		HDArmour aaa=HDArmour(other.findinventory("HDArmour"));
		//put on the armour right away
		if(
			other.player&&other.player.cmd.buttons&BT_USE
			&&!other.countinv("HDArmourWorn")
		){
			HDArmour.ArmourChangeEffect(other);
			let worn=HDArmourWorn(other.GiveInventoryType("HDArmourWorn"));
			if(durability>=1000){
				durability-=1000;
				worn.mega=true;
			}
			worn.durability=durability;
			destroy();
			return;
		}
		//one megaarmour = 2 armour for maxinv purposes
		if(aaa){
			int totalbulk=(durability>=1000)?2:1;
			for(int i=0;i<aaa.mags.size();i++){
				totalbulk+=(aaa.mags[i]>=1000)?2:1;
			}
			if(totalbulk>aaa.maxamount)return;
		}
		//super.actualpickup(other); //WHY IS THIS NOT PICKING UP
		if(!trypickup(other))return;
		aaa=HDArmour(other.findinventory("HDArmour"));
		aaa.syncamount();
		aaa.mags[0]=durability;
		other.A_PlaySound(pickupsound,CHAN_AUTO);
		other.A_Log(string.format("\cg%s",pickupmessage()),true);
	}
	static void ArmourChangeEffect(actor owner){
		owner.A_SetBlend("00 00 00",1,6,"00 00 00");
		owner.A_PlaySound("weapons/pocket",CHAN_BODY);
		owner.A_ChangeVelocity(0,0,2);
		let onr=HDPlayerPawn(owner);
		if(onr)onr.stunned+=40;
	}
	states{
	spawn:
		ARMS A -1 nodelay A_JumpIf(invoker.mega,1);
		ARMC A -1;
		stop;
	use:
		TNT1 A 0 A_WearArmour();
		fail;
	}
}
class HDArmourWorn:HDPickup{
	int durability;
	bool mega;property ismega:mega;
	default{
		-inventory.invbar
		HDArmourworn.ismega false;
		hdpickup.refid HDLD_ARWGREN;
		hdpickup.nicename "Green Armour";
	}
	override void beginplay(){
		durability=mega?HDCONST_BLUEARMOUR:HDCONST_GREENARMOUR;
		super.beginplay();
	}
	override inventory createtossable(int amount){
		if(owner.countinv("HDBackpack")){
			owner.A_DropInventory("HDBackpack");
			return null;
		}
		if(owner.countinv("WornRadsuit")){
			owner.A_DropInventory("WornRadsuit");
			return null;
		}
		HDArmour.ArmourChangeEffect(owner);
		let tossed=HDArmour(owner.spawn("HDArmour",
			(owner.pos.x,owner.pos.y,owner.pos.z+owner.height-20)
		));
		tossed.mags.clear();
		tossed.mags.push(mega?durability+1000:durability);
		tossed.amount=1;
		destroy();
		return tossed;
	}
	states{
	spawn:
		TNT1 A 0;
		stop;
	}
}


class BlueArmour:HDPickup replaces BlueArmor{
	default{
		+missilemore
		hdpickup.fitsinbackpack false;
		hdpickup.refid HDLD_ARMBLUE;
		hdpickup.nicename "Blue Armour (spare)";
		inventory.pickupmessage "Picked up the combat armour.";
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(owner){
			owner.A_GiveInventory("HDArmour");
			let ga=HDArmour(owner.findinventory("HDArmour"));
			ga.syncamount();
			ga.mags[0]=(bmissilemore?(1000+HDCONST_BLUEARMOUR):HDCONST_GREENARMOUR);
		}else{
			let aaa=HDArmour(spawn("HDArmour",pos,ALLOW_REPLACE));
			aaa.mags.clear();
			aaa.mags.push(bmissilemore?(1000+HDCONST_BLUEARMOUR):HDCONST_GREENARMOUR);
			aaa.syncamount();
			aaa.changetid(tid);
		}
		destroy();
	}
}
class BlueArmourWorn:HDPickup{
	default{
		+missilemore
		hdpickup.fitsinbackpack false;
		hdpickup.refid HDLD_ARWBLUE;
		hdpickup.nicename "Blue Armour";
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(owner){
			owner.A_GiveInventory("HDArmourWorn");
			let ga=HDArmourWorn(owner.findinventory("HDArmourWorn"));
			ga.durability=(bmissilemore?HDCONST_BLUEARMOUR:HDCONST_GREENARMOUR);
			ga.mega=bmissilemore;
		}
		destroy();
	}
}

class GreenArmour:BlueArmour replaces GreenArmor{
	default{
		-missilemore
		hdpickup.fitsinbackpack false;
		hdpickup.refid HDLD_ARMGREN;
		hdpickup.nicename "Green Armour (spare)";
		inventory.pickupmessage "Picked up the security armour.";
	}
}
class GreenArmourWorn:BlueArmourWorn{
	default{
		-missilemore
		hdpickup.fitsinbackpack false;
		inventory.icon "ARMCB0";
		hdpickup.refid HDLD_ARWGREN;
		hdpickup.nicename "Green Armour";
	}
}

