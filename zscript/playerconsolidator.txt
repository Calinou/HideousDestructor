// this one gets its own file because it's sooooo sprawly.

extend class HDPlayerPawn{
	void ConsolidateWeapons(){
		int sparepowder=countinv("FourMilAmmo");
		int usablebrass=countinv("HDSpent7mmPickup");
		bool backpack=countinv("Backpack");

		//misc. maintenance
		A_TakeInventory("BossJam");
		if(countinv("ZM66BrokenRound")){
			A_TakeInventory("ZM66BrokenRound");
			sparepowder++;
		}
		A_TakeInventory("ZM66Heat");
		A_TakeInventory("ChaingunHeat");
		A_TakeInventory("BrontornisHeat");
		A_TakeInventory("HunterSpentShell");
		A_TakeInventory("SlayerSpentShell1");
		A_TakeInventory("SlayerSpentShell2");
		A_TakeInventory("BrontornisSpent");
		if(countinv("BossSpentChamber")){
			A_TakeInventory("BossSpentChamber");
			usablebrass++;
		}
		if(countinv("LiberatorSpentChamber")){
			A_TakeInventory("LiberatorSpentChamber");
			usablebrass++;
		}

		//make as many mags and clips as possible to make room for more 7mm
		if(countinv("SevenMilAmmo")>10){  
			while(
				countinv("SevenMilAmmo")>29 &&  
				!A_JumpIfInventory("LiberatorMag",0,"null")
			){
				A_TakeInventory("SevenMilAmmo",30);
				A_GiveInventory("LiberatorMag");
			}
			if(countinv("BossRifle")){
				while(
					countinv("SevenMilAmmo")>10 &&  
					!A_JumpIfInventory("BossClip",0,"null")
				){
					A_TakeInventory("SevenMilAmmo",10);
					A_GiveInventory("BossClip");
				}
			}
		}
		//downtime-only: reload brass
		if(
			usablebrass && sparepowder>=3 &&
			countinv("LiberatorRifle")+countinv("LiberatorGrenadeRifle")+countinv("BossRifle")>0  
		){
			int newbullets=min(
				usablebrass,
				sparepowder*0.3,
				backpack?80:40
			);
			A_GiveInventory("SevenMilAmmo",newbullets);
			//then subtract used supplies
			A_TakeInventory("HDSpent7mmPickup",newbullets);
			A_TakeInventory("FourMilAmmo",newbullets*random(2,5));
		}

		//downtime-only: recharge cells

		//downtime-only: repair *ERPs
		if (countinv("DERPDEAD")){
			A_TakeInventory("DERPDEAD");
			A_GiveInventory("DERPUsable");
		}
		if (countinv("HERPDEAD")){
			A_TakeInventory("HERPDEAD");
			A_GiveInventory("HERPUsable");
		}

		//load pistols, then mags, then SMG, rest in SMG mags
		//fuck this is complicated
		PistolConsolidator();
		if(countinv("HDPistol")){
			if(!countinv("HDPistolChamber")){
				if(countinv("HDPistolLoaded"))A_TakeInventory("HDPistolLoaded",1);
				else if(countinv("HDPistolAmmo"))A_TakeInventory("HDPistolAmmo",1);
				A_GiveInventory("HDPistolChamber");
			}
			if(countinv("HDPistolLoaded")<15){
				if(countinv("HDPistolMag")){
					A_TakeInventory("HDPistolMag",1);
					A_GiveInventory("HDPistolLoaded",15);
				}
				else if(countinv("HDSMGMag")){
					A_TakeInventory("HDSMGMag",1);
					A_GiveInventory("HDPistolLoaded",30);
				}
			}
			int btg=min(15-countinv("HDPistolLoaded"),countinv("HDPistolAmmo"));
			if(btg){
				A_TakeInventory("HDPistolAmmo",btg);
				A_GiveInventory("HDPistolLoaded",btg);
			}
		}
		if(countinv("HDSecondPistol")){
			if(!countinv("HDSecondPistolChamber")){
				if(countinv("HDSecondPistolLoaded"))A_TakeInventory("HDSecondPistolLoaded",1);
				else if(countinv("HDPistolAmmo"))A_TakeInventory("HDPistolAmmo",1);
				A_GiveInventory("HDSecondPistolChamber");
			}
			if(countinv("HDSecondPistolLoaded")<15){
				if(countinv("HDPistolMag")){
					A_TakeInventory("HDPistolMag",1);
					A_GiveInventory("HDSecondPistolLoaded",15);
				}
				else if(countinv("HDSMGMag")){
					A_TakeInventory("HDSMGMag",1);
					A_GiveInventory("HDSecondPistolLoaded",30);
				}
			}
			int btg=min(15-countinv("HDSecondPistolLoaded"),countinv("HDPistolAmmo"));
			if(btg){
				A_TakeInventory("HDPistolAmmo",btg);
				A_GiveInventory("HDSecondPistolLoaded",btg);
			}
		}
		if(countinv("HDPistolAmmo")>=15 && (countinv("HDPistol")||countinv("DERPUsable"))){
			bool done=false;
			int mc;
			while(!done){
				mc=countinv("HDPistolMag");
				A_GiveInventory("HDPistolMag");
				if(countinv("HDPistolMag")>mc)A_TakeInventory("HDPistolAmmo",15);
				else done=true;
				if(countinv("HDPistolAmmo")<15)done=true;
			}
		}
		if(countinv("HDSMG")){
			if(!countinv("HDSMGChamber")){
				if(countinv("HDSMGLoaded"))A_TakeInventory("HDSMGLoaded",1);
				else if(countinv("HDPistolAmmo"))A_TakeInventory("HDPistolAmmo",1);
				A_GiveInventory("HDSMGChamber");
			}
			if(countinv("HDSMGLoaded")<30){
				if(countinv("HDSMGMag")){
					A_TakeInventory("HDSMGMag",1);
					A_GiveInventory("HDPistolAmmo",30);
				}
				else if(countinv("HDPistolMag")>=2){
					A_TakeInventory("HDPistolMag",2);
					A_GiveInventory("HDPistolAmmo",30);
				}
			}
			int btg=min(30-countinv("HDSMGLoaded"),countinv("HDPistolAmmo"));
			if(btg){
				A_TakeInventory("HDPistolAmmo",btg);
				A_GiveInventory("HDSMGLoaded",btg);
			}
		}
		if(countinv("HDPistolAmmo")>=30){
			bool done=false;
			int mc;
			while(!done){
				mc=countinv("HDSMGMag");
				A_GiveInventory("HDSMGMag");
				if(countinv("HDSMGMag")>mc)A_TakeInventory("HDPistolAmmo",30);
				else done=true;
				if(countinv("HDPistolAmmo")<30)done=true;
			}
		}


		//fill up bronto
		if(countinv("Brontornis")&&!countinv("BrontornisMag")&&countinv("BrontornisRound")){
			A_TakeInventory("BrontornisRound",1);
			A_GiveInventory("BrontornisMag");
		}
		//just max out the bfg who cares
		if(countinv("BFGInternalCharge")>6||countinv("BFGChamber")>6){
			A_GiveInventory("BFGInternalCharge",20);
			A_GiveInventory("BFGChamber",20);
		}

		//zm66 and vulc
		//if zm has less than 10 bullets, reload zm
		//if vulc has less than 10 bullets, reload vulc - one mag only
		int inzedmag=countinv("ZM66Loaded");
		if(inzedmag<10&&countinv("ZM66RifleMags") 
			&&(countinv("ZM66Rifle")||countinv("ZM66SemiRifle")||countinv("ZM66GrenadeRifle"))
		){
			A_GiveInventory("ZM66Loaded",50);
			A_TakeInventory("ZM66RifleMags",1);
		}
		if(countinv("Vulcanette")){
			inzedmag=countinv("VulcLoaded1");
			if(inzedmag<10&&countinv("ZM66RifleMags")){
				A_GiveInventory("VulcLoaded1",50);
				A_TakeInventory("ZM66RifleMags",1);
			}
			if(countinv("VulcJuice")<120&&countinv("HDCellPacks")){
				A_GiveInventory("VulcJuice",2100);
				A_TakeInventory("HDCellPacks",1);
			}
		}

		//plasma and chainsaw
		if(countinv("ThunderBuster")){
			if(countinv("PlasmaBattery")<10&&countinv("HDCellPacks")){
				A_GiveInventory("PlasmaBattery",60);
				A_TakeInventory("HDCellPacks",1);
			}
		}
		if(countinv("Lumberjack")){
			if(countinv("LumberCell")<400&&countinv("HDCellPacks")){
				A_GiveInventory("LumberCell",1200);
				A_TakeInventory("HDCellPacks",1);
			}
		}

		//fill up liberator with whatever mags or spares are present
		//fill up boss with whatever clips or spares are left
		if(
			countinv("SevenMilAmmo")||countinv("LiberatorMag")||countinv("BossClip")||
			countinv("LiberatorRifle")||countinv("LiberatorGrenadeRifle")||countinv("BossRifle")
		){
			int totalspare=
				countinv("LiberatorMag")*30+
				countinv("BossClip")*10+
				countinv("SevenMilAmmo");
			if(totalspare){
				bool boss=countinv("BossRifle");
				bool liberator=countinv("LiberatorGrenadeRifle");
				if(countinv("LiberatorRifle"))liberator=true;
				int originalmags=countinv("LiberatorMag");
				int originalclips=countinv("BossClip");
				int toload=totalspare;

				if(boss){
					if(!countinv("BossChamber")){
						A_GiveInventory("BossChamber");
						totalspare--;
					}
					toload=min(10-countinv("BossMag"),totalspare);
					if(toload){
						A_GiveInventory("BossMag",toload);
						totalspare-=toload;
					}
				}
				if(totalspare && liberator){
					if(!countinv("LiberatorChamber")){
						A_GiveInventory("LiberatorChamber");
						totalspare--;
					}
					toload=min(30-countinv("LiberatorLoaded"),totalspare);
					if(toload){
						A_GiveInventory("LiberatorLoaded",toload);
						totalspare--;
					}
				}

				//load original clips
				A_TakeInventory("BossClip");
				while(totalspare>=10 && countinv("BossClip")<originalclips){
					A_GiveInventory("BossClip");
					totalspare-=10;
				}

				//load original mags
				A_TakeInventory("LiberatorMag");
				int maxmags=backpack?12:5;
				while(totalspare>=30 && countinv("LiberatorMag")<originalmags){
					A_GiveInventory("LiberatorMag");
					totalspare-=30;
				}

				//load more clips or mags
				if(player.readyweapon is "BossRifle" || !liberator){
					int maxclips=backpack?30:12;
					while(totalspare>=10 && countinv("BossClip")<maxclips){
						A_GiveInventory("BossClip");
						totalspare-=10;
					}
				}else while(totalspare>=30 && countinv("LiberatorMag")<originalmags){
					A_GiveInventory("LiberatorMag");
					totalspare-=30;
				}

				A_SetInventory("SevenMilAmmo",totalspare);
			}
		}


		//fill up shotguns using whatever spares are present
		//dbs chambers first, then shotguntube
		//if 24+ left, fill both saddles, else split between saddles
		A_TakeInventory("HunterSpentShell");
		A_TakeInventory("SlayerSpentShell1");
		A_TakeInventory("SlayerSpentShell2");
		if(countinv("HDShellAmmo")){
			if(countinv("Slayer")){
				if(!countinv("ShellChamber1")){
					A_TakeInventory("HDShellAmmo",1);
					A_GiveInventory("ShellChamber1");
				}
				if(countinv("HDShellAmmo")&&!countinv("ShellChamber2")){
					A_TakeInventory("HDShellAmmo",1);
					A_GiveInventory("ShellChamber2");
				}
			}
			if(countinv("Hunter")&&countinv("HDShellAmmo")){
				if(!countinv("ShotgunChamber")){
					A_TakeInventory("HDShellAmmo",1);
					A_GiveInventory("ShotgunChamber");
				}
				int gs=min(7-countinv("ShotgunTube"),countinv("HDShellAmmo"));
				if(gs){
					A_GiveInventory("ShotgunTube",gs);
					A_TakeInventory("HDShellAmmo",gs);
				}
			}
		}
		if(countinv("HDShellAmmo")){
			int g1=0;int g2=0;
			int hsa=countinv("HDShellAmmo");
			if(countinv("Hunter"))g1=12-countinv("ShotgunQuickAmmo");
			if(countinv("Slayer"))g2=12-countinv("ShotgunQuickAmmo2");
			int gt=g1+g2;
			if(gt>hsa){
				hsa*=0.5;
				g1=min(hsa,g1);g2=min(hsa,g2);
				gt=g1+g2;
			}
			if(g1)A_GiveInventory("ShotgunQuickAmmo",g1);
			if(g2)A_GiveInventory("ShotgunQuickAmmo2",g2);
			if(gt)A_TakeInventory("HDShellAmmo",gt);
		}

		//reload rocket weapons
		//blooper > zm66 > liberator > RL ONLY if no HEAT loaded
		if(countinv("Blooper")&&!countinv("BloopChamber")&&countinv("HDRocketAmmo")){
			A_TakeInventory("HDRocketAmmo",1);
			A_GiveInventory("BloopChamber");
		}
		if(countinv("ZM66GrenadeRifle")&&!countinv("ZM66GLoaded")&&countinv("HDRocketAmmo")){
			A_TakeInventory("HDRocketAmmo",1);
			A_GiveInventory("ZM66GLoaded");
		}
		if(countinv("LiberatorGrenadeRifle")&&!countinv("LiberatorGrenadeChamber")&&countinv("HDRocketAmmo")){
			A_TakeInventory("HDRocketAmmo",1);
			A_GiveInventory("LiberatorGrenadeChamber");
		}
		if(countinv("RocquetteLauncher")&&!countinv("HEATLoaded")){
			while(countinv("RocketMag")<6&&countinv("HDRocketAmmo")){
				A_TakeInventory("HDRocketAmmo",1);
				A_GiveInventory("RocketMag");
			}
		}
	}
}



