// ------------------------------------------------------------
// Because HD weapons aren't complicated enough.
// ------------------------------------------------------------
extend class HDStatusBar{
	int oddtic;
	void drawweaponstatus(weapon w){

		playerpawn pp=cplayer.mo;
		oddtic++;if(oddtic>6)oddtic=0;

		if(w is "Lumberjack"){
			if(pp.countinv("LumberEmptyCell"))drawstring(
				mamountfont,"00000",
				(-16,-9),DI_TEXT_ALIGN_RIGHT|DI_TRANSLATABLE|DI_SCREEN_CENTER_BOTTOM,
				Font.CR_DARKGRAY
			);
			drawwepnum(pp.countinv("LumberCell"),1200);
		}
		else if(w is "HDPistol"){
			drawwepcounter(pp.countinv("HDPistolFullAuto"),
				-22,-10,"blank","RBRSA3A7","STFULAUT"
			);
			if(pp.countinv("HDSecondPistol")){
				drawwepdot(-16,-13,(4,2.6));
				if(pp.countinv("HDSecondPistolFullAuto"))
					drawwepdot(-21,-13,(2,2.6));
			}
			drawwepnum(pp.countinv("HDPistolLoaded"),15);
			if(pp.countinv("HDPistolChamber"))drawwepdot(-16,-10,(3,1));
		}
		else if(w is "HDSMG"){
			drawwepcounter(pp.countinv("SMGFullAuto"),
				-22,-10,"blank","STBURAUT","STFULAUT"
			);
			drawwepnum(pp.countinv("HDSMGLoaded"),30);
			if(pp.countinv("HDSMGChamber"))drawwepdot(-16,-10,(3,1));
		}
		else if(w is "Slayer"){
			if(pp.countinv("ShellChamber1")){
				drawwepdot(-28,-10,(3,5));
				drawwepdot(-28,-7,(3,2));
			}else if(pp.countinv("SlayerSpentShell1")){
				drawwepdot(-28,-7,(3,2));
			}
			if(pp.countinv("ShellChamber2")){
				drawwepdot(-16,-10,(3,5));
				drawwepdot(-16,-7,(3,2));
			}else if(pp.countinv("SlayerSpentShell2")){
				drawwepdot(-16,-7,(3,2));
			}
			for(int i=pp.countinv("ShotgunQuickAmmo2");i>0;i--){
				drawwepdot(-10-i*2,-2,(1,3));
			}
		}
		else if(w is "Hunter"){
			if(pp.countinv("ShotgunChamber")){
				drawwepdot(-19,-11,(5,3));
				drawwepdot(-16,-11,(2,3));
			}
			else if(pp.countinv("HunterSpentShell")){
				drawwepdot(-16,-11,(2,3));
			}
			drawwepcounter(pp.countinv("ShotgunAuto"),
				-24,-12,"blank","STFULAUT"
			);
			drawwepnum(pp.countinv("ShotgunTube"),7,posy:-7);
			for(int i=pp.countinv("ShotgunQuickAmmo");i>0;i--){
				drawwepdot(-15-i*2,-2,(1,3));
			}
		}
		else if(w is "ZM66Rifle" || w is "ZM66GrenadeRifle" || w is "ZM66SemiRifle"){
			if(!(w is "ZM66SemiRifle"))drawwepcounter(pp.countinv("ZM66FullAuto"),
				-22,-10,"RBRSA3A7","STFULAUT","STBURAUT"
			);
			if(pp.countinv("ZM66GLoaded"))drawwepdot(-16,-13,(4,2.6));
			int lod=pp.countinv("ZM66Loaded");
			drawwepnum(pp.countinv("ZM66Loaded"),50);
			if(pp.countinv("ZM66Chamber")){
				drawwepdot(-16,-10,(3,1));
				lod++;
			}
			if(pp.countinv("ZM66JailbrokenMag"))lod=random[shitgun](10,99);
			drawnum(lod,-16,-22,DI_SCREEN_CENTER_BOTTOM|DI_TEXT_ALIGN_RIGHT,Font.CR_RED);
			if(pp.countinv("ZM66GLMode"))drawnum(pp.countinv("AirburstCount"),
				-30,-22,DI_SCREEN_CENTER_BOTTOM|DI_TEXT_ALIGN_RIGHT,Font.CR_DARKGRAY
			);else drawnum(pp.countinv("ZM66ZoomDegree"),
				-30,-22,DI_SCREEN_CENTER_BOTTOM|DI_TEXT_ALIGN_RIGHT,Font.CR_DARKGRAY
			);
		}
		else if(w is "Vulcanette"){
			if(pp.countinv("VulcLoaded5"))drawwepdot(-32,-13,(3,2));
			if(pp.countinv("VulcLoaded4"))drawwepdot(-28,-13,(3,2));
			if(pp.countinv("VulcLoaded3"))drawwepdot(-24,-13,(3,2));
			if(pp.countinv("VulcLoaded2"))drawwepdot(-20,-13,(3,2));
			drawwepnum(pp.countinv("VulcLoaded1"),50,posy:-10);
			drawwepcounter(pp.countinv("ChaingunFast"),
				-28,-16,"blank","STFULAUT"
			);
			drawwepcounter(pp.countinv("VulcChamber3"),-14,-15,"blank","RBRSA3A7");
			drawwepcounter(pp.countinv("VulcChamber4"),-14,-17,"blank","RBRSA3A7");
			drawwepcounter(pp.countinv("VulcChamber5"),-14,-19,"blank","RBRSA3A7");
			if(pp.countinv("ChaingunOn")){
				int lod=random[shitgun](10,99);
				if(!pp.countinv("VulcJB1"))lod=pp.countinv("VulcLoaded1");
				drawnum(lod,-20,-22,
					DI_SCREEN_CENTER_BOTTOM|DI_TEXT_ALIGN_RIGHT,Font.CR_RED
				);
				if(pp.countinv("VulcJuice"))drawwepnum(pp.countinv("VulcJuice"),2100);
			}
			if(pp.countinv("VulcEmptyCell"))drawstring(
				mamountfont,"00000",(-16,-8),
				DI_TEXT_ALIGN_RIGHT|DI_TRANSLATABLE|DI_SCREEN_CENTER_BOTTOM,
				Font.CR_DARKGRAY
			);
		}
		else if(w is "Blooper"){
			if(pp.countinv("BloopChamber"))drawwepdot(-16,-10,(5,3));
			drawnum(pp.countinv("AirburstCount"),
				-28,-15,DI_SCREEN_CENTER_BOTTOM|DI_TEXT_ALIGN_RIGHT,Font.CR_DARKGRAY
			);
			drawwepnum(
				pp.countinv("HDRocketAmmo"),
				pp.findinventory("HDRocketAmmo").maxamount
			);
		}
		else if(w is "RocquetteLauncher"){
			if(!pp.countinv("IsUsingRocketUnderslung")){
				if(pp.countinv("HEATLoaded")){
					drawwepdot(-19,-13,(3,2));
					drawwepdot(-16,-13,(2,2));
					drawwepdot(-22,-11,(4,6));
					drawwepdot(-26,-12,(4,4));
				}else{
					drawwepdot(-19,-12,(3,1));
					drawwepdot(-16,-12,(2,1));
					drawwepdot(-22,-11,(4,3));
				}
			}
			drawnum(pp.countinv("AirburstCount"),
				-32,-15,DI_SCREEN_CENTER_BOTTOM|DI_TEXT_ALIGN_RIGHT,Font.CR_DARKGRAY
			);
			drawwepnum(pp.countinv("RocketMag"),6);
		}
		else if(w is "LiberatorRifle" || w is "LiberatorGrenadeRifle"){
			drawwepcounter(pp.countinv("LiberatorFullAuto"),
				-22,-10,"RBRSA3A7","STFULAUT"
			);
			if(pp.countinv("LiberatorGrenadeChamber"))drawwepdot(-16,-13,(4,2.6));
			drawwepnum(pp.countinv("LiberatorLoaded"),30);
			if(pp.countinv("LiberatorChamber"))drawwepdot(-16,-10,(3,1));
			if(pp.countinv("LiberatorGrenadeMode"))drawnum(pp.countinv("AirburstCount"),
				-30,-22,DI_SCREEN_CENTER_BOTTOM|DI_TEXT_ALIGN_RIGHT,Font.CR_DARKGRAY
			);else drawnum(pp.countinv("LiberatorZoomDegree"),
				-30,-22,DI_SCREEN_CENTER_BOTTOM|DI_TEXT_ALIGN_RIGHT,Font.CR_DARKGRAY
			);
		}
		else if(w is "ThunderBuster"){
			drawwepcounter(pp.countinv("PlasmaAlt"),
				-22,-10,"blank","STBURAUT"
			);
			if(pp.countinv("PlasmaBattery"))drawwepnum(pp.countinv("PlasmaBattery"),60);
			else if(pp.countinv("PlasmaEmptyCell"))drawstring(
				mamountfont,"00000",
				(-16,-9),DI_TEXT_ALIGN_RIGHT|DI_TRANSLATABLE|DI_SCREEN_CENTER_BOTTOM,
				Font.CR_DARKGRAY
			);
		}
		else if(w is "BFG9k"){
			if(pp.countinv("BFGChamber"))drawwepnum(pp.countinv("BFGChamber"),20,posy:-10);
			else if(pp.countinv("BFGLoaded"))drawstring(
				mamountfont,"00000",
				(-16,-14),DI_TEXT_ALIGN_RIGHT|DI_TRANSLATABLE|DI_SCREEN_CENTER_BOTTOM,
				Font.CR_DARKGRAY
			);
			drawwepnum(pp.countinv("BFGInternalCharge"),20);
		}
		else if(w is "Brontornis"){
			if(pp.countinv("BrontornisMag"))drawwepdot(-16,-10,(5,3));
			drawwepnum(
				pp.countinv("BrontornisRound"),
				pp.findinventory("BrontornisRound").maxamount
			);
		}
		else if(w is "BossRifle"){
			drawwepnum(pp.countinv("BossMag"),10);
			drawwepcounter(pp.countinv("BossChamber"),
				-16,-10,"blank","RBRSA3A7"
			);
			drawnum(pp.countinv("BossZoomDegree"),
				-30,-18,DI_SCREEN_CENTER_BOTTOM|DI_TEXT_ALIGN_RIGHT,Font.CR_DARKGRAY
			);
			drawnum(pp.countinv("BossMOA"),
				-16,-18,DI_SCREEN_CENTER_BOTTOM|DI_TEXT_ALIGN_RIGHT,Font.CR_WHITE
			);
		}
		else if(w is "HDFragGrenades" && pp.countinv("HDFragGrenadeAmmo")){
			drawwepnum(
				pp.countinv("HDFragGrenadeAmmo"),
				pp.findinventory("HDFragGrenadeAmmo").maxamount
			);
			drawwepnum(pp.countinv("GrenadeForce"),50,posy:-10);
			if(!pp.countinv("SpoonOff")){
				drawwepdot(-16,-15,(5,4));
				if(!pp.countinv("FragOut"))drawwepdot(-22,-16,(3,2));
			}else{
				int timer=pp.countinv("FragTimer");
				if(timer%3)drawwepnum(140-timer,140,posy:-15);
			}
		}
		else if(w is "Ring"){
			let ww=Ring(w);
			if(ww && ww.targethealth)drawwepnum(ww.targethealth,ww.targetspawnhealth);
		}
		else if(w is "SelfBandage"){
			int of=0;
			let hp=hdplayerpawn(pp);
			if(hp && hp.woundcount){
				drawimage(
					"BLUDC0",(-17,-6),
					DI_SCREEN_CENTER_BOTTOM|DI_ITEM_RIGHT,
					0.6
				);
				of=clamp(hp.woundcount*0.2,1,3);
				if(oddtic%3)of=-of;
			}
			drawwepdot(-22,-8+of,(2,10));
			drawwepdot(-17,-12+of,(12,2));
		}
		else if(w is "HDMedikitter"){
			drawwepdot(-27,-11,(2,6));
			drawwepdot(-25,-13,(6,2));
			hdmedikitter ww=hdmedikitter(w);
			if(ww.useonother){
				drawwepdot(-22,-20,(5,6));
				drawwepdot(-24,-25,(1,7));
				if(ww.usedon)drawimage(
					"BLUDC0",(-22,-19),
					DI_SCREEN_CENTER_BOTTOM|DI_ITEM_RIGHT,
					0.4
				);
			}
			if(ww.usedon)drawimage(
				"BLUDC0",(-14,-7),
				DI_SCREEN_CENTER_BOTTOM|DI_ITEM_RIGHT,
				0.8
			);
			drawwepnum(100-ww.usedpatches,100);
			if(!ww.usedflesh)drawwepdot(-16,-10,(4,3));
		}
		else if(w is "HDBlueBottler"){
			drawimage(
				"BON1A0",(-14,-7),
				DI_SCREEN_CENTER_BOTTOM|DI_ITEM_RIGHT
			);
		}
		else if(w is "HDBerserker"){
			drawimage(
				"PSTRA0",(-14,-7),
				DI_SCREEN_CENTER_BOTTOM|DI_ITEM_RIGHT
			);
		}
		else if(w is "HDStimpacker"){
			drawimage(
				"STIMA0",(-14,-7),
				DI_SCREEN_CENTER_BOTTOM|DI_ITEM_RIGHT
			);
		}
		else{
			if(cplayer.readyweapon.ammotype1)drawwepnum(
				pp.countinv(cplayer.readyweapon.ammotype1),
				pp.findinventory(cplayer.readyweapon.ammotype1).maxamount
			);
			if(cplayer.readyweapon.ammotype2)drawwepnum(
				pp.countinv(cplayer.readyweapon.ammotype2),
				pp.findinventory(cplayer.readyweapon.ammotype2).maxamount,
				posy:-10
			);
		}
	}
	void drawwepdot(int posx,int posy,vector2 dotscale=(3.,3.)){
		drawimage(
			"GREENPXL",(posx,posy),
			DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT,
			1,scale:dotscale
		);
	}
	void drawwepnum(int value,int maxvalue,int posx=-16,int posy=-6,bool num=false){
		hdplayerpawn cp=hdplayerpawn(cplayer.mo);if(!cp)return;
		if(num){
			drawstring(
				mAmountFont,formatnumber(value),
				(posx,posy),DI_TEXT_ALIGN_RIGHT|DI_TRANSLATABLE|DI_SCREEN_CENTER_BOTTOM,
				cp.overloaded<1.5?Font.CR_OLIVE:(cp.overloaded>2?Font.CR_RED:Font.CR_GREEN)
			);
		}else drawimage(
			"GREENPXL",
			(posx,posy),
			DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT,
			1,scale:(value*25/maxvalue,2)
		);
	}
	//"" means ignore this value and move on to the next check.
	//"blank" means stop here and render nothing.
	//(do we really need 6???)
	void drawwepcounter(
		int input,
		int posx,int posy,
		string zero="",string one="",string two="",string three="",
		string four="",string five="",string six="",
		bool binary=false
	){
		string types[7];types[0]=zero;types[1]=one;types[2]=two;
		types[3]=three;types[4]=four;types[5]=five;types[6]=six;
		input=min(input,6);
		string result="";
		for(int i=input;i>=0;i--){
			if(input==i){
				if(types[i]=="blank")break;
				else if(types[i]=="")input--;
				else result=types[i];
			}
		}
		if(result!="")drawimage(
			result,
			(posx,posy),
			DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT
		);
	}
}