// ------------------------------------------------------------
// Because HD weapons aren't complicated enough.
// ------------------------------------------------------------
extend class HDStatusBar{
	virtual void drawweaponstatus(weapon w){
		let hdw=hdweapon(w);
		if(!hdw){
			if(cplayer.readyweapon.ammotype1)drawwepnum(
				hpl.countinv(cplayer.readyweapon.ammotype1),
				hdmath.maxinv(hpl,cplayer.readyweapon.ammotype1)
			);
			if(cplayer.readyweapon.ammotype2)drawwepnum(
				hpl.countinv(cplayer.readyweapon.ammotype2),
				hdmath.maxinv(hpl,cplayer.readyweapon.ammotype2),
				posy:-10
			);
			return;
		}

		bool helptext=cvar.getcvar("hd_helptext",cplayer).getbool();
		hudfont pSmallFont=HUDFont.Create("SmallFont");

		let sb=self; //easier to convert the stuff below to the new DrawHUDStuff override
	if(w is "HDFist"){
		let ww=HDFist(w);
		if(ww && ww.targethealth)sb.drawwepnum(ww.targethealth,ww.targetspawnhealth);
	}
	else if(w is "MagManager"){
		let thismag=magmanager(w).thismag;
		if(!thismag||thismag.mags.size()<1)return;
		int countermaxx=thismag.mags.size();
		int countermax=countermaxx-1;
		double scl=2.;
		string roundsprite="";
		name roundtype="";
		int howmanylines=countermax/5;
		int linecounter=countermax%5;
		if(linecounter<0)linecounter=4;

		int offx=-64-18*howmanylines;
		int offy=80;
		for(int i=0;i<countermaxx;i++){

			bool imax=i==countermax;
			if(imax){
				offx=-6;
				offy=50;
			}else if(
				linecounter<1
			){
				howmanylines--;
				offx=-64-18*howmanylines;
				offy=80;
				linecounter=4;
			}else{
				if(i>0){
					offx+=2;
					offy-=9;
				}
				linecounter--;
			}

			int thismagamt=thismag.mags[i];
			string magsprite="";
			if(thismag is "HD9mMag30"){
				roundtype="HDPistolAmmo";
				roundsprite="PBRSA0";
				if(thismagamt>0)magsprite="CLP3A0";
				else magsprite="CLP3B0";
			}else if(thismag is "HD9mMag15"){
				roundtype="HDPistolAmmo";
				roundsprite="PBRSA0";
				if(thismagamt>0)magsprite="CLP2A0";
				else magsprite="CLP2B0";
			}else if(thismag is "HD4mMag"){
				roundtype="FourMilAmmo";;
				roundsprite="RBRSICE";
				if(thismagamt>=51)magsprite="ZMAGA0";
				else if(thismagamt>0)magsprite="ZMAGB0";
				else magsprite="ZMAGC0";
			}else if(thismag is "HD7mMag"){
				roundtype="SevenMilAmmo";
				roundsprite="RBRSA3A7";
				if(thismagamt>0)magsprite="RMAGA0";
				else magsprite="RMAGB0";
				scl=1.7;
			}else if(thismag is "HD7mClip"){
				roundtype="SevenMilAmmo";
				roundsprite="RBRSA3A7";
				if(thismagamt>8)magsprite="RCLPA0";
				else if(thismagamt>6)magsprite="RCLPB0";
				else if(thismagamt>4)magsprite="RCLPC0";
				else if(thismagamt>2)magsprite="RCLPD0";
				else if(thismagamt>0)magsprite="RCLPE0";
				else magsprite="RCLPF0";
				scl=1.5;
			}else if(thismag is "HDBattery"){
				roundsprite="CELPA0";
				if(thismagamt>13)magsprite="CELLA0";
				else if(thismagamt>6)magsprite="CELLB0";
				else if(thismagamt>0)magsprite="CELLC0";
				else magsprite="CELLD0";
				scl=0.8;
			}
			sb.drawimage(magsprite,(offx,offy),
				sb.DI_SCREEN_CENTER|sb.DI_ITEM_RIGHT_TOP,
				scale:(scl,scl)*(imax?1.6:1.)
			);
			sb.drawstring(
				imax?pSmallFont:sb.mamountfont,FormatNumber(thismag.mags[i]),
				(offx+2,offy),sb.DI_SCREEN_CENTER|sb.DI_TEXT_ALIGN_LEFT,
				imax?font.CR_SAPPHIRE:font.CR_BROWN
			);
		}

		if(roundsprite!=""){
			offx+=40;
			if(roundsprite=="CELPA0"){
				scl=0.4;
				let battt=HDBattery(thismag).chargemode;
				string batts="uNone";
				if(battt==hdbattery.BATT_CHARGEMAX)batts="eAuto";
				else if(battt==hdbattery.BATT_CHARGETOP)batts="ySelected";
				sb.drawstrings(
					pSmallFont,string.format("%s\c%s%s",helptext?"Charging: ":"",batts,helptext?"\n(\cqReload\cu to cycle)":""),
					(offx+2,offy),sb.DI_SCREEN_CENTER|sb.DI_TEXT_ALIGN_LEFT
				);
			}else{
				scl*=1.6;
				sb.drawstring(
					pSmallFont,FormatNumber(hpl.countinv(roundtype)),
					(offx+2,offy),sb.DI_SCREEN_CENTER|sb.DI_TEXT_ALIGN_LEFT,
					font.CR_BROWN
				);
			}
			sb.drawimage(roundsprite,(offx,offy),
				sb.DI_SCREEN_CENTER|sb.DI_ITEM_RIGHT_TOP,
				scale:(scl,scl)
			);
		}
	}
		else hdw.DrawHUDStuff(self,hdw,hpl);
	}
	void drawwepdot(int posx,int posy,vector2 dotscale=(3.,3.)){
		drawimage(
			"GREENPXL",(posx,posy),
			DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT,
			1,scale:dotscale
		);
	}
	void drawwepnum(int value,int maxvalue,int posx=-16,int posy=-6,bool num=false,bool alwaysprecise=false){
		if(!maxvalue)return;
		hdplayerpawn cp=hdplayerpawn(cplayer.mo);if(!cp)return;
		if(num){
			drawstring(
				mAmountFont,formatnumber(value),
				(posx,posy),DI_TEXT_ALIGN_RIGHT|DI_TRANSLATABLE|DI_SCREEN_CENTER_BOTTOM,
				cp.overloaded<1.5?Font.CR_OLIVE:(cp.overloaded>2?Font.CR_RED:Font.CR_GREEN)
			);
		}else drawimage(
			"GREENPXL",
			(posx,posy),
			DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT,
			1,scale:(
				!alwaysprecise&&(
					gimmehud
					||cplayer.buttons&BT_ATTACK
					||cplayer.buttons&BT_ALTATTACK
				)
				?max(((value*6/maxvalue)<<2),(value>0)):
				(value*24/maxvalue)
			,2)
		);
	}
	//"" means ignore this value and move on to the next check.
	//"blank" means stop here and render nothing.
	//(do we really need 6???)
	void drawwepcounter(
		int input,
		int posx,int posy,
		string zero="",string one="",string two="",string three="",
		string four="",string five="",string six="",
		bool binary=false
	){
		string types[7];types[0]=zero;types[1]=one;types[2]=two;
		types[3]=three;types[4]=four;types[5]=five;types[6]=six;
		input=min(input,6);
		string result="";
		for(int i=input;i>=0;i--){
			if(input==i){
				if(types[i]=="blank")break;
				else if(types[i]=="")input--;
				else result=types[i];
			}
		}
		if(result!="")drawimage(
			result,
			(posx,posy),
			DI_SCREEN_CENTER_BOTTOM|DI_TRANSLATABLE|DI_ITEM_RIGHT
		);
	}
	//return value of the mag that would be selected on reload
	int GetNextLoadMag(hdmagammo maggg){
		if(!maggg||maggg.mags.size()<1)return -1;
		int maxperunit=maggg.maxperunit;
		int maxindex=maggg.mags.find(maxperunit);
		if(maxindex==maggg.mags.size())return maggg.mags[0];
		return maxperunit;
	}
	/*
			if(gimmehud){
				int nextmagloaded=GetNextLoadMag();
				if(nextmagloaded>=15){
					drawimage("CLP2A0",(-46,-3),DI_SCREEN_CENTER_BOTTOM,scale:(3,3));
				}else if(nextmagloaded<1){
					drawimage("CLP2B0",(-46,-3),DI_SCREEN_CENTER_BOTTOM,alpha:0.3,scale:(3,3));
				}else drawbar(
					CLP2A0,CLP2GREY,
					nextmagloaded,15,
					(-46,-3),-1,
					SHADER_VERT,DI_SCREEN_CENTER_BOTTOM,scale:(3,3)
				);
				drawnum(hpl.countinv("HD9mMag15"),-43,-8,DI_SCREEN_CENTER_BOTTOM,font.CR_BLACK);
			}
	*/
}
