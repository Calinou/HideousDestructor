// ------------------------------------------------------------
// Because sometimes, things get caught in map geometry.
// ------------------------------------------------------------
class SectorDamageCounter:IdleDummy{}
class doordestroyer:hdactor{
	default{
		+solid +nogravity +dontgib +ghost
		+actlikebridge
		height 0.1;radius 0.1;
	}
	vector2 v1pos;
	vector2 v2pos;
	vector2 vfrac;
	double llength;
	int llit;
	double bottom;
	double top;
	override void postbeginplay(){
		super.postbeginplay();
		vector2 vvv=(v2pos-v1pos);
		llit=max(1,llength/10); //see chunkspeed
		vfrac=vvv/llit;
	}
	override bool cancollidewith(actor other,bool passive){
		return false;
	}
	enum doorbusternums{
		SECTORDAMAGE_TID=4440,
	}
	void DoorChunk(class<actor>chunktype,int numpercolumn=1,double chunkspeed=10){
		chunkspeed*=0.1; //see vfrac
		for(int i=0;i<llit;i++){
			for(int j=0;j<numpercolumn;j++){
				actor aaa=spawn(chunktype,(
					(v1pos+vfrac*i)+(frandom(-3,3),frandom(-3,3)),
					frandom(bottom,top)
				));
				aaa.vel.xy=rotatevector(
						vfrac,randompick(90,-90)+frandom(-60,60)
					)*chunkspeed*frandom(0.4,1.4);
				aaa.vel.z=frandom(-6,12);
			}
			spawn("HDSmoke",(
				(v1pos+vfrac*i),
				bottom+frandom(1,32)
			));
		}
	}
	states{
	spawn:
		TNT1 A 0;
		TNT1 A 0 DoorChunk("HDExplosion",1,3);
		TNT1 A 0 DoorChunk("HDSmoke",5,3);
		TNT1 AAA 1 DoorChunk("HugeWallChunk",20,15);
		TNT1 A 0 DoorChunk("HDExplosion",1,3);
		TNT1 A 0 DoorChunk("HDSmoke",5,2);
		TNT1 A 0 DoorChunk("HDSmokeChunk",random(0,3),7);
		TNT1 AAAA 1 DoorChunk("HDSmoke",5,1);
		TNT1 A 0 DoorChunk("HDSmoke",5,1);
		TNT1 A 0 DoorChunk("HDSmokeChunk",random(0,3),7);
		TNT1 AAAA 2 DoorChunk("HugeWallChunk",20);
		TNT1 A -1;
		stop;
	}
	static const int doorspecials[]={
		//from https://github.com/coelckers/gzdoom/blob/master/src/p_lnspec.cpp#L3432
		//only affects raising doors, not lowering platforms designed to function as doors

		//these are all arg0
		10,11,12,13,14,
		105,106,194,195,198,
		202,
		249,252,262,263,265,266,268,274
	};
	static bool checkdoorspecial(int linespecial){
		int dlsl=doordestroyer.doorspecials.size();
		for(int i=0;i<dlsl;i++){
			if(linespecial==doordestroyer.doorspecials[i]){
				return true;
				break;
			}
		}
		return false;
	}
	static bool destroydoor(actor caller,double maxwidth=140,double maxdepth=32,double range=0){
		if(hd_nodoorbuster)return false;
		if(!range)range=max(4,caller.radius*2);
		flinetracedata dlt;
		caller.linetrace(
			caller.angle,range,0,
			flags:TRF_THRUACTORS,
			offsetz:caller.height*0.5,data:dlt
		);
		if(!dlt.hitline)return false;
		sector othersector=hdmath.oppositesector(dlt.hitline,dlt.hitsector);
		if(!othersector)return false;

		//the first point within the othersector is used a few times
		vector2 justoverthere=dlt.hitlocation.xy+dlt.hitdir.xy;

		//figure out if it hit above or below
		//0 top, 1 middle, 2 bottom
		if(dlt.linepart==1)return false;
		bool floornotdoor=dlt.linepart==2;//othersector.floorplane.zatpoint(justoverthere)>dlt.hitlocation.z;

		//see if there are at least 2 2-sided lines
		int num2sided=0;
		for(int i=0;i<othersector.lines.size();i++){
			if(othersector.lines[i].sidedef[1]){
				num2sided++;
			}
		}if(num2sided<2)return false;

		//see how wide the door is
		vector2 doorline=dlt.hitline.v1.p-dlt.hitline.v2.p;
		maxwidth*=frandom(0.4,1.8);
		if(doorline dot doorline > maxwidth*maxwidth)return false;

		bool isdoor=false;

		//see how thick the door is
		vector2 depthcheck=dlt.hitdir.xy*maxdepth*frandom(0.8,1.2);
		isdoor=(othersector!=
			sector.pointinsector(dlt.hitlocation.xy+depthcheck)
		);
		if(!isdoor){
			if(othersector!=sector.pointinsector(dlt.hitlocation.xy+depthcheck*frandom(0,2))){
				//add a damage point
				actor surprise=spawn("SectorDamageCounter",(justoverthere,dlt.hitlocation.z));
				int checktid=SECTORDAMAGE_TID+othersector.index();
				surprise.changetid(checktid);

				//count how many
				actoriterator buttesecs=actoriterator.create(checktid,"SectorDamageCounter");
				actor buttesec=null;
				array<actor>buttecracked;
				while(buttesec=buttesecs.next()){
					if(buttesec.cursector==othersector)buttecracked.push(buttesec);
				}
				if(buttecracked.size()>random(5,7)){
					isdoor=true;
					for(int i=0;i<buttecracked.size();i++){
						buttecracked[i].destroy();
					}
				}
			}
		}
		if(!isdoor)return false;

/*
		//see if a door special applies
		//currently unused - it's a "door" destroyer without limitation
		//keeping it in case we can disable remote door switches with this
		isdoor=checkdoorspecial(dlt.hitline.special);
		if(!isdoor){
			//find the sector tag
			int brutalforcery=0;
			bool gotit=false;
			for(brutalforcery=0;brutalforcery<100;brutalforcery++){
				sectortagiterator sss=
					sectortagiterator.create(brutalforcery);
				int ssss=sss.next();
				while(!gotit&&ssss>-1){
					if(level.sectors[ssss]==othersector)gotit=true;
					ssss=sss.next();
				}
				if(gotit)break;
			}
			if(brutalforcery>0){
				lineiditerator lll=lineiditerator.create(brutalforcery);
				int l=0;
				while(!isdoor&&l>-1){
					isdoor=checkdoorspecial(level.lines[l].special);
					l=lll.next();
				}
			}
		}
		if(!isdoor)return false;
*/

		//blow that shit up
		double doorwidth=doorline.length();
		double holeheight=clamp(frandom(0.01,0.02)*maxdepth*maxwidth,32,doorwidth+frandom(-12,12));
		double blockpoint;
		othersector.flags|=sector.SECF_SILENTMOVE;
		if(floornotdoor){
			double lowestsurrounding;vertex garbage;
			[lowestsurrounding,garbage]=othersector.findlowestfloorsurrounding();
			double justoverthereheight=othersector.floorplane.zatpoint(justoverthere);
			blockpoint=min(
				max(
					justoverthereheight-holeheight,
					lowestsurrounding-frandom(3,6)
				),
				justoverthereheight-32
			);
			holeheight=justoverthereheight-blockpoint;
			othersector.MoveFloor(abs(holeheight),abs(blockpoint),0,-1,false,true);
		}else{
			double justoverthereheight=othersector.ceilingplane.zatpoint(justoverthere);
			blockpoint=justoverthereheight+holeheight;
			othersector.MoveCeiling(holeheight,blockpoint,0,1,false);
		}

		//not intended by the mapper
		textureid shwal=texman.checkfortexture("ASHWALL2",texman.type_any);
		if(int(shwal)<1)
			shwal=texman.checkfortexture("ASHWALL",texman.type_any);
		othersector.settexture(floornotdoor?sector.floor:sector.ceiling,shwal,true);
		for(int i=0;i<othersector.lines.size();i++){
			othersector.lines[i].special=0;
			for(int j=0;j<2;j++){
				side sdd=othersector.lines[i].sidedef[j];
				if(sdd){
					int notmid=floornotdoor?side.bottom:side.top;
					if(int(sdd.gettexture(notmid))<1)sdd.settexture(notmid,shwal);
					if(!floornotdoor){
						sdd.settextureyoffset(side.mid,
							sdd.gettextureyoffset(side.mid)-holeheight
						);
						sdd.settextureyoffset(side.top,
							sdd.gettextureyoffset(side.top)-holeheight
						);
					}else{
						sdd.settextureyoffset(side.bottom,
							sdd.gettextureyoffset(side.bottom)+holeheight
						);
					}
				}
			}
		}

		//fancy effects
		let db=doordestroyer(spawn("doordestroyer",(
			(justoverthere+dlt.hitdir.xy),blockpoint
		)));
		db.setz(blockpoint);
		db.v1pos=dlt.hitline.v1.p;
		db.v2pos=dlt.hitline.v2.p;
		db.target=caller.target;
		if(floornotdoor){
			db.top=db.floorz+holeheight;
			db.bottom=db.floorz;
		}else{
			db.bottom=blockpoint-holeheight;
			db.top=blockpoint;
		}

		db.llength=doorwidth;
		hdactor.HDBlast(caller,
			pushradius:doorwidth,pushamount:24,
			fragradius:doorwidth,fragdamage:doorwidth,
			immolateradius:doorwidth,
			immolateamount:random(10,30),
			immolatechance:12,
			gibradius:doorwidth*0.4,gibamount:10
		);
		return true;
	}
}
class doorball:doomimpball{
	states{
	death:
		BAL1 C 0{if(doordestroyer.destroydoor(self))A_Log("Ding dong, motherfucker!");}
		goto super::death;
	}
}




// ------------------------------------------------------------
// It's called a "D.B." because it lets you edit the map.
// ------------------------------------------------------------
class DoorBuster:HDPickup{
	int botid;
	default{
		+inventory.invbar
		inventory.maxamount 5;
		hdpickup.bulk ENC_DOORBUSTER;
		inventory.pickupmessage "Picked up a Door Buster.";
		inventory.icon "BGRNA3A7";
	}
	action void A_PlantDB(){
		if(invoker.amount<1){
			invoker.destroy();return;
		}
		flinetracedata dlt;
		linetrace(
			angle,48,pitch,flags:TRF_THRUACTORS,
			offsetz:height-9,
			data:dlt
		);
		if(!dlt.hitline){
			A_Log(string.format("Find a wall to stick the DoorBuster on."),true);
			return;
		}
		vector3 plantspot=dlt.hitlocation-dlt.hitdir*3;
		let ddd=DoorBusterPlanted(spawn("DoorBusterPlanted",plantspot));
		if(!ddd){
			A_Log("Can't deploy here.",true);
			return;
		}
		ddd.botid=invoker.botid;
		ddd.ChangeTid(HDDB_TID);
		ddd.A_PlaySound("weapons/rockreload",CHAN_BODY);
		ddd.stuckline=dlt.hitline;
		ddd.angle=angle;
		ddd.translation=translation;
		ddd.master=self;
		if(!dlt.hitline.backsector){
			ddd.stuckheight=ddd.pos.z;
			ddd.stucktier=0;
		}else{
			sector othersector=hdmath.oppositesector(dlt.hitline,dlt.hitsector);
			ddd.stuckpoint=plantspot.xy;
			double stuckceilingz=othersector.ceilingplane.zatpoint(ddd.stuckpoint);
			double stuckfloorz=othersector.floorplane.zatpoint(ddd.stuckpoint);
			ddd.stuckbacksector=othersector;
			double dpz=ddd.pos.z;
			if(dpz-ddd.height>stuckceilingz){
				ddd.stuckheight=dpz-ddd.height-stuckceilingz;
				ddd.stucktier=1;
			}else if(dpz<stuckfloorz){
				ddd.stuckheight=dpz-stuckfloorz;
				ddd.stucktier=-1;
			}else{
				ddd.stuckheight=ddd.pos.z;
				ddd.stucktier=0;
			}
		}
		string feedback=string.format("DoorBuster planted with tag \cy%i",ddd.botid);
		if(cvar.getcvar("hd_helptext",player).getint()) feedback.appendformat(string.format("\cj. Use \cddb 999 \cy%i\cj to detonate.",ddd.botid));
		A_Log(feedback,true);
		invoker.amount--;
		if(invoker.amount<1)invoker.destroy();
	}
	states{
	spawn:
		BGRN A -1;
		stop;
	use:
		TNT1 A 0 A_PlantDB();
		fail;
	}
}
class DoorBusterPlanted:HDUPK{
	int botid;
	line stuckline;
	sector stuckbacksector;
	double stuckheight;
	int stucktier;
	vector2 stuckpoint;
	default{
		+nogravity
		height 4;radius 3;
		scale 0.6;
	}
	override void OnGrab(actor grabber){
		actor dbbb=spawn("DoorBuster",pos,ALLOW_REPLACE);
		dbbb.translation=self.translation;
		GrabThinker.Grab(grabber,dbbb);
		destroy();
	}
	void A_DBStuck(){
		if(
			!stuckline
			||ceilingz<pos.z
			||floorz>pos.z
		){
			if(!stuckline)setstatelabel("death");
			else setstatelabel("unstucknow");
			stuckline=null;
			return;
		}
		setz(
			stucktier==1?stuckbacksector.ceilingplane.zatpoint(stuckpoint)+stuckheight:
			stucktier==-1?stuckbacksector.floorplane.zatpoint(stuckpoint)+stuckheight:
			stuckheight
		);
	}
	states{
	spawn:
		BGRN A 1 A_DBStuck();
		loop;
	unstucknow:
		---- A 2 A_PlaySound("misc/fragknock",5);
		---- A 1{
			actor dbs=spawn("DoorBuster",pos,ALLOW_REPLACE);
			dbs.angle=angle;dbs.translation=translation;
			dbs.A_ChangeVelocity(1,0,0,CVF_RELATIVE);
			A_SpawnChunks("BigWallChunk",15);
			A_PlaySound("weapons/bigcrack",4);
		}
		stop;
	death:
		---- A 2 A_PlaySound("misc/fragknock",5);
		---- A 1{
			bnointeraction=true;
			bool busted=doordestroyer.destroydoor(self);

			A_SprayDecal(busted?"Scorch":"BrontoScorch",16);

			actor dbs=spawn("DoorBusterFlying",pos,ALLOW_REPLACE);
			dbs.target=target;dbs.angle=angle;dbs.translation=translation;
			if(busted){
				dbs.A_ChangeVelocity(-8,0,frandom(2,3),CVF_RELATIVE);
			}else{
				dbs.A_ChangeVelocity(-20,frandom(-4,4),frandom(-4,4),CVF_RELATIVE);
			}
			A_PlaySound("weapons/bigcrack",4);
			A_PlaySound("word/explode",CHAN_VOICE);

			A_SpawnChunks("HDExplosion",busted?1:6,0,1);
			if(!busted){
				A_ChangeVelocity(-7,0,1,CVF_RELATIVE);
				A_SpawnChunks("HugeWallChunk",30,1,40);
				DistantQuaker.Quake(self,4,35,512,10);
				A_HDBlast(
					pushradius:256,pushamount:128,fullpushradius:96,
					fragradius:128,fragdamage:random(32,128),
					gibradius:16,gibamount:random(1,20)
				);
			}else{
				DistantQuaker.Quake(self,2,35,256,10);
				A_HDBlast(
					pushradius:128,pushamount:64,fullpushradius:16,
					fragradius:64,fragdamage:random(12,24),
					gibradius:16,gibamount:random(1,20)
				);
			}
		}
		stop;
	}
}
class DoorBusterFlying:SlowProjectile{
	default{
		mass 400;scale 0.6;
		height 2;radius 2;
	}
	states{
	spawn:
		BGRN A 1 A_SpawnItemEx("HDGunSmokeStill");
		wait;
	death:
		TNT1 A 0 A_SpawnItemEx("DoorBusterSpent",0,0,2,
			frandom(4,8),frandom(-8,8),frandom(-2,8),
			0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS
		);
		stop;
	}
}
class DoorBusterSpent:HDDebris{
	default{
		radius 1;height 1;
		+rollsprite +rollcenter
		scale 0.6;
		stamina 100;
	}
	void A_Spin(){
		roll+=20;
	}
	override void Tick(){
		if(stamina>0){
			if(!(stamina%5))A_SpawnItemEx("HDGunSmokeStill",0,0,3,
				frandom(-0.3,0.3),frandom(-0.3,0.3),frandom(2,4)
			);
			stamina--;
		}
		super.Tick();
	}
	states{
	spawn:
		BGRN B 2 A_Spin();
		loop;
	death:
		BGRN B -1;
		stop;
	}
}
extend class HDHandlers{
	void SetDB(hdplayerpawn ppp,int cmd=111,int cmd2=-1){
		//set DB tag number with -#
		//e.g., "db -123" will set tag to 123
		if(cmd<0){
			let dbu=DoorBuster(ppp.findinventory("DoorBuster"));
			if(dbu){
				dbu.botid=-cmd;
				ppp.A_Log(string.format("DoorBuster tag set to  \cy%i",-cmd),true);
			}
			return;
		}
		let dbbb=DoorBuster(ppp.findinventory("DoorBuster"));
		int botid=dbbb?dbbb.botid:1;
		if(cmd!=999&&cmd!=123){
			ppp.A_Log(string.format("DoorBuster Command format:\n\cu db <option> <tag number> \n\cjOptions:\n 999 = DETONATE\n 123 = QUERY\n -n = set tag number\n\cj  tag number on next deployment: \cy%i",botid),true);
		}
		actoriterator it=actoriterator.create(HDDB_TID,"DoorBusterPlanted");
		actor dbs;
		while(dbs=it.Next()){
			let dbss=DoorBusterPlanted(dbs);
			if(
				dbss.master==ppp&&(
					cmd2<0
					||cmd2==dbss.botid
				)
			){
				if(cmd==999){
					dbss.setstatelabel("death");
					ppp.A_Log(
						string.format("DoorBuster detonated at [%i,%i].",
							dbss.pos.x,dbss.pos.y
						),true
					);
				}else if(cmd==123){
					ppp.A_Log(
						string.format("DoorBuster with tag \cy%i at [%i,%i].",
							dbss.botid,dbss.pos.x,dbss.pos.y
						),true
					);
				}
			}
		}
	}
}
enum HDDBConst{
	HDDB_TID=8442,
}
