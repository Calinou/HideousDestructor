// ------------------------------------------------------------
// Cell ammo for Lumberjack and others
// ------------------------------------------------------------
class HDCellAmmo:Ammo{
	default{
		+inventory.ignoreskill
		inventory.maxamount 30;
		ammo.backpackmaxamount 60;
		ammo.backpackamount 0;
	}
	override void detachfromowner(){
		target=owner;
		super.detachfromowner();
	}
	states{
	spawn:
		TNT1 A 1 nodelay{
			if(target)target.A_GiveInventory("HDSpareCellDropper");
		}
		stop;
	}
}
class HDCellpacks:Ammo replaces Cell{
	default{
		+inventory.ignoreskill
		inventory.maxamount 6;
		ammo.backpackmaxamount 20;
		ammo.backpackamount 0;
		inventory.pickupmessage "Picked up a cell.";
		scale 0.4;
	}
	states{
	spawn:
		CELL A 0 nodelay A_SpawnItemEx("CelP",0,0,0,vel.x,vel.y,vel.z,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
		stop;
	}
}
class HDCellpackEmpty:Ammo{
	default{
		+inventory.ignoreskill
		inventory.maxamount 6;
		ammo.backpackmaxamount 20;
		ammo.backpackamount 0;
		inventory.pickupmessage "Picked up a depleted cell.";
		scale 0.4;
	}
	states{
	spawn:
		CELL A 0 nodelay A_SpawnItemEx("CelE",0,0,0,vel.x,vel.y,vel.z,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
		stop;
	}
}
class CelP:HDUPK replaces HDCellpacks{
	default{scale 0.4;}
	states{
	spawn:
		CELL A -1 A_Stop();
	give:
		---- A 0 A_JumpIfInTargetInventory("HDCellpacks",0,"spawn");
		---- A 0{
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			A_GiveToTarget("HDCellpacks",1);
		}stop;
	}
}
class CelE:CelP replaces HDCellpackEmpty{
	default{translation 0;}
	states{
	give:
		---- A 0 A_JumpIfInTargetInventory("HDCellpackEmpty",0,"spawn");
		---- A 0{
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			A_GiveToTarget("HDCellpackEmpty",1);
		}stop;
	}
}


//replace the derivatives where you can
//spawn a HDCellPickup from the weapon, give it a Clip plus whatever # you want
class HDCellPickup:HDUPK{
	default{scale 0.4;}
	states{
	spawn2:
		CELL CBA -1{
			int clm=countinv("HDCellAmmo");
			if(!clm){
				actor a=spawn("CelE",pos);a.vel=vel;
				destroy();return;
			}
			if(clm>19){  
				actor a=spawn("CelP",pos);a.vel=vel;
				destroy();return;
			}
			if(clm>13)frame=0;  
			else if(clm>6)frame=1;
		}
	spawn:
		CELL C 0 A_Stop();
		goto spawn2;
	give:
		---- A 0{
			if(A_JumpIfInTargetInventory("HDCellAmmo",0,"null")){
				setstatelabel("spawn");
				return;
			}
			if(countinv("HDCellAmmo")>=20){  
				if(A_JumpIfInTargetInventory("HDCellPacks",0,"null")){
					setstatelabel("spawn");
					return;
				}else target.A_GiveInventory("HDCellPacks",1);
			}else{
				target.A_GiveInventory("HDCellAmmo",countinv("HDCellAmmo"));
			}
			A_PlaySound("weapons/pocket");
		}stop;
	}
}
class HDCelXB:HDCellPickup{
	states{
	spawn:
		CELL C 0 A_Stop();
		---- A 0{
			if(!countinv("Clip")){
				A_GiveInventory("Clip");
				if(target is "PlayerPawn"){
					ExtractAmmo();
				}else A_GiveInventory("HDCellAmmo",20);
			}
		}goto spawn2;
	}
	virtual void ExtractAmmo(){
		A_SetInventory("HDCellAmmo",target.countinv("BFGChamber"));
		target.A_TakeInventory("BFGChamber");
	}
}
class HDCelXS:HDCelXB{
	override void ExtractAmmo(){
		A_SetInventory("HDCellAmmo",target.countinv("LumberCell")/60);
		target.A_TakeInventory("LumberCell");
	}
}
class HDCelXV:HDCelXB{
	override void ExtractAmmo(){
		A_SetInventory("HDCellAmmo",target.countinv("VulcJuice")/150);
		target.A_TakeInventory("VulcJuice");
	}
}
class HDCelXD:HDCelXB{
	override void ExtractAmmo(){
		A_SetInventory("HDCellAmmo",target.countinv("HERPCell")/750);
		target.A_TakeInventory("HERPCell");
	}
}

//for the single drop
//rename this to HDSpareCellDropper
class HDSpareCellDropper:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			actor a;int b;
			while(countinv("HDCellAmmo")){
				[b,a]=A_SpawnItemEx("HDCellPickup",
					random(8,12),random(0,12),24,5,0,0,random(-6,-12)
				);a.vel+=vel;
				b=min(countinv("HDCellAmmo"),random(1,19));
				a.A_SetInventory("HDCellAmmo",b);
				A_TakeInventory("HDCellAmmo",b);
			}
		}fail;
	}
}

