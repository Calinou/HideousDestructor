// ------------------------------------------------------------
// Cell ammo for Lumberjack and others
// ------------------------------------------------------------
class HDCellAmmo:HDAmmo{
	default{
		inventory.maxamount 50;
		inventory.pickupmessage "Picked up a partially depleted battery.";
	}
	override void detachfromowner(){
		target=owner;
		super.detachfromowner();
	}
	override inventory createtossable(int amount){
		inventory iii=super.createtossable(amount);
		if(iii)iii.target=owner;
		return iii;
	}
	states(actor){
	spawn:
		TNT1 A 1 nodelay{
			//do not let the player choose how much they drop
			if(target){
				int ontarget=target.countinv("HDCellAmmo");
				if(ontarget){
					int total=amount+ontarget;
					amount=clamp(max(amount,9)+random(-10,10),1,total);
					target.setinventory("HDCellAmmo",total-amount);
				}
			}

			actor a;int b;
			//spawn randoms
			while(amount>19){  
				int amt=clamp(random(1,19),1,amount);
				a=spawn("HDCellPickup",pos);
				a.vel+=vel+(frandom(-1,1),frandom(-1,1),frandom(-1,1));
				a.stamina=amt;
				amount-=amt;
			}

			//spawn final
			a=spawn("HDCellPickup",pos);
			a.vel+=vel;
			a.stamina=amount;
		}stop;
	}
}
class HDCellpacks:HDAmmo{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Cell Battery"
		//$Sprite "CELLA0"

		inventory.maxamount 6;
		inventory.pickupmessage "Picked up a battery.";
		scale 0.4;
	}
	states(actor){
	spawn2:
		CELL A -1;
		stop;
	}
}
class HDCellpackEmpty:HDCellpacks{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Cell Battery (Spent)"
		//$Sprite "CELLC0"

		inventory.pickupmessage "Picked up a depleted battery.";
		translation 0;
	}
}


//replace the derivatives where you can
//spawn a HDCellPickup from the weapon, give it a Clip plus whatever # you want
class HDCellPickup:HDUPK{
	default{scale 0.4;stamina 20;}
	states{
	spawn2:
		CELL CBA -1{
			if(stamina<1){
				actor a=spawn("HDCellpackEmpty",pos);a.vel=vel;
				destroy();return;
			}
			else if(stamina>=20){  
				actor a=spawn("HDCellpacks",pos);a.vel=vel;
				destroy();return;
			}
			else if(stamina>13)frame=0;  
			else if(stamina>6)frame=1;
		}
	spawn:
		CELL C 0 A_Stop();
		goto spawn2;
	give:
		---- A 0{
			if(stamina>=20){    
				if(A_JumpIfInTargetInventory("HDCellPacks",0,"null")){
					setstatelabel("spawn");
					return;
				}else target.A_GiveInventory("HDCellPacks",1);
			}else if(stamina<1){
				if(A_JumpIfInTargetInventory("HDCellPackEmpty",0,"null")){
					setstatelabel("spawn");
					return;
				}else target.A_GiveInventory("HDCellPackEmpty",1);
			}else{
				if(A_JumpIfInTargetInventory("HDCellAmmo",0,"null")){
					setstatelabel("spawn");
					return;
				}
				target.A_GiveInventory("HDCellAmmo",stamina);
			}
			A_PlaySound("weapons/pocket");
		}stop;
	}
}



