// ------------------------------------------------------------
// Cell ammo for Lumberjack and others
// ------------------------------------------------------------
class HDBattery:HDMagAmmo{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Cell Battery"
		//$Sprite "CELLA0"

		hdmagammo.maxperunit 20;
		hdmagammo.roundtype "";
		hdmagammo.nicename "Batteries";
		inventory.pickupmessage "Picked up a battery.";
		scale 0.4;
	}
	override double getbulk(){
		return ENC_BATTERY*amount;
	}
	bool BFGChargeable(){
		for(int i=0;i<amount;i++){
			if(mags[i]>=BFGC_MINCHARGE)return true;
		}
		return false;
	}
//	override bool Insert(){return false;}
	override bool Extract(){return false;}

//testing
	override void postbeginplay(){
		super.postbeginplay();
		mags.clear();
		mags.push(11);
		mags.push(1);
		mags.push(10);
		amount=mags.size();
		LogAmounts();
	}
//
	enum BatteryChargeModes{
		BATT_DONTCHARGE=0,
		BATT_CHARGEMAX=1,
		BATT_CHARGETOP=2,
	}
	int ticker;
	int lastamount;
	int chargemode;
	override void doeffect(){
		//testingdoeffect();return;
		if(!chargemode)return;
		if(lastamount!=amount){
			ticker=0;
			lastamount=amount;
		}else if(ticker>35.0){
			ticker=0;
			ChargeBattery(1,chargemode==BATT_CHARGETOP);
		}else ticker++;
		super.doeffect();
	}
	override bool Insert(){
		chargemode++;
		if(chargemode>BATT_CHARGETOP)chargemode=0;
		string msg="Charging now off.";
		if(chargemode==1)msg="Now charging highest battery.";
		else if(chargemode==2)msg="Now charging selected battery.";
		owner.A_Log(msg,true);
		return false;
	}

	//thanks to Quarki for explaining how the principle should work
	//as long as the chargors total more than the chargee's desired amount,
	//a transfer may happen in favour of the chargee.
	override void Consolidate(){
		ChargeBattery(usetop:true);
		ChargeBattery();
	}
	void ChargeBattery(int chargestodo=-1,bool usetop=false){
		SyncAmount();
		if(amount<1)return;

		int batcount=0;
		int totalchargeable=0;
		int biggestindex=-1;
		int biggestamt=0;
		int smallestindex=-1;
		int smallestamt=20;

		//get the smallest and biggest amounts, and number usable for this
		for(int i=0;i<amount;i++){
			int chargeamt=mags[i];
			if(chargeamt>0){
				totalchargeable+=chargeamt;
				batcount++;
				if(
					!usetop
					&&biggestamt<chargeamt
					&&chargeamt<20
				){
					biggestamt=chargeamt;
					biggestindex=i;
				}
				if(
					smallestamt>=chargeamt
				){
					smallestamt=chargeamt;
					smallestindex=i;
				}
			}
		}
		if(usetop){
			int maxindex=amount-1;
			biggestindex=maxindex;
			biggestamt=mags[maxindex];
		}
		if(
			biggestindex<0
			||smallestindex<0
			||smallestamt>=20
			||biggestindex==smallestindex
		)return;
		if(
			batcount<3	//need at least 3 to increase any one
			||totalchargeable-biggestamt-2<biggestamt	//min. chargor value must exceed target amount
		)return;

		//keep going until exactly ONE battery is fully drained or charged
		while(
			chargestodo
			&&mags[smallestindex]>0
			&&mags[biggestindex]<20
		){
			chargestodo--;
			mags[smallestindex]--;
			if(random(0,39))mags[biggestindex]++;
		}
		if(hd_debug)LogAmounts();
	}

	states(actor){
	spawn2:
		CELL CAB -1{
			int amt=mags[0];
			if(amt>13)frame=0;
			else if(amt>6)frame=1;
		}stop;
	spawnempty:
		CELL D -1;
		stop;
	}
}
class HDCellpackEmpty:IdleDummy{
	default{
		//$Category "Ammo/Hideous Destructor/"
		//$Title "Cell Battery (Spent)"
		//$Sprite "CELLD0"
	}
	override void postbeginplay(){
		super.postbeginplay();
		angle=frandom(0,360);
		HDMagAmmo.SpawnMag(self,"HDBattery",0);
		destroy();
	}
}




