//-------------------------------------------------
// heh
//-------------------------------------------------
extend class HDHandlers{
	//drops all ammo for which you do not have a weapon
	void PurgeUselessAmmo(hdplayerpawn ppp){
		if(
			!ppp.countinv("HDPistol")
			&&!ppp.countinv("HDSecondPistol")
			&&!ppp.countinv("HDSMG")
			&&!ppp.countinv("DERPUsable")
		){
			ppp.A_DropInventory("HDPistolAmmo",ppp.countinv("HDPistolAmmo"));
			ppp.A_DropInventory("HD9mMag30",ppp.countinv("HD9mMag30"));
			ppp.A_DropInventory("HD9mMag15",ppp.countinv("HD9mMag15"));
		}
		if(
			!ppp.countinv("Slayer")
			&&!ppp.countinv("Hunter")
		){
			ppp.A_DropInventory("HDShellAmmo",ppp.countinv("HDShellAmmo"));
		}
		if(
			!ppp.countinv("ZM66AssaultRifle")
			&&!ppp.countinv("Vulcanette")
			&&!ppp.countinv("HERPUsable")
		){
			ppp.A_DropInventory("HD4mMag",ppp.countinv("HD4mMag"));
			if(!ppp.countinv("LiberatorRifle")&&!ppp.countinv("BossRifle"))
				ppp.A_DropInventory("FourMilAmmo",ppp.countinv("FourMilAmmo"));
		}
		if(
			!ppp.countinv("HDRL")
		){
			ppp.A_DropInventory("HEATAmmo",ppp.countinv("HEATAmmo"));
			if(
				!ppp.countinv("Blooper")
				&&(
					!ppp.countinv("ZM66AssaultRifle")
					||hdweapon(ppp.findinventory("ZM66AssaultRifle")).weaponstatus[0]
						&ZM66F_NOLAUNCHER
				)
				&&!ppp.countinv("LiberatorRifle")
				&&(
					!ppp.countinv("LiberatorRifle")
					||hdweapon(ppp.findinventory("LiberatorRifle")).weaponstatus[0]
						&LIBF_NOLAUNCHER
				)
			)ppp.A_DropInventory("HDRocketAmmo",ppp.countinv("HDRocketAmmo"));
		}
		if(
			!ppp.countinv("LiberatorRifle")
			&&!ppp.countinv("BossRifle")
		){
			ppp.A_DropInventory("HD7mMag",ppp.countinv("HD7mMag"));
			ppp.A_DropInventory("HD7mClip",ppp.countinv("HD7mClip"));
			ppp.A_DropInventory("SevenMilAmmo",ppp.countinv("SevenMilAmmo"));
		}
		if(
			!ppp.countinv("BFG9k")
		){
			if(
				!ppp.countinv("ThunderBuster")
				&&!ppp.countinv("Vulcanette")
				&&!ppp.countinv("Lumberjack")
				&&!ppp.countinv("HERPUsable")
			)ppp.A_DropInventory("HDBattery",ppp.countinv("HDBattery"));
		}
		if(!ppp.countinv("Brontornis"))
			ppp.A_DropInventory("BrontornisRound",ppp.countinv("BrontornisRound"));
		if(!ppp.countinv("HDIEDKit"))
			ppp.A_DropInventory("DudRocketAmmo",ppp.countinv("DudRocketAmmo"));
		double aaa=ppp.angle;
		ppp.angle+=frandom(-10,10);ppp.A_DropInventory("DERPDEAD");
		ppp.angle+=frandom(-10,10);ppp.A_DropInventory("HERPDEAD");
		ppp.angle=aaa;
	}
	//drops one or more units of your selected weapon's ammo
	void DropOne(hdplayerpawn ppp,playerinfo player,int amt){
		let cw=hdweapon(player.readyweapon);
		if(!cw)return;

		//if unspecified, shotgun should drop in groups of 4
		if(!amt&&(cw is "Hunter"||cw is "Slayer"))amt=4;
		else amt=max(1,amt);

		if(cw is "HDPistol"){
			int ccc=ppp.countinv("HDPistolAmmo");
			if(ccc)ppp.A_DropInventory("HDPistolAmmo",ccc);
			else if(amt>0)ppp.A_DropInventory("HD9mMag15",amt);
		}else if(cw is "HDSMG"){
			int ccc=ppp.countinv("HDPistolAmmo");
			if(ccc)ppp.A_DropInventory("HDPistolAmmo",ccc);
			else if(amt>0)ppp.A_DropInventory("HD9mMag30",amt);
		}else if(cw is "Hunter"||cw is "Slayer"){
			ppp.A_DropInventory("HDShellAmmo",amt);
		}else if(cw is "ZM66AssaultRifle"){
			if(
				!(cw.weaponstatus[0]&ZM66F_NOLAUNCHER)
			)ppp.A_DropInventory("HDRocketAmmo",amt);
			int ccc=ppp.countinv("FourMilAmmo");
			if(ccc)ppp.A_DropInventory("FourMilAmmo",ccc);
			else ppp.A_DropInventory("HD4mMag",amt);
		}else if(cw is "Vulcanette"){
			ppp.A_DropInventory("HDBattery",amt);
			int ccc=ppp.countinv("FourMilAmmo");
			if(ccc)ppp.A_DropInventory("FourMilAmmo",ccc);
			else ppp.A_DropInventory("HD4mMag",amt);
		}else if(cw is "HDRL"){
			if(ppp.countinv("HEATAmmo")){
				ppp.angle+=1;
				ppp.A_DropInventory("HDRocketAmmo",amt);
				ppp.angle-=2;
				ppp.A_DropInventory("HEATAmmo",amt);
				ppp.angle+=1;
			}else ppp.A_DropInventory("HDRocketAmmo",amt);
		}
		else if(cw is "Blooper")ppp.A_DropInventory("HDRocketAmmo",amt);
		else if(cw is "HDFragGrenades")ppp.A_DropInventory("HDFragGrenadeAmmo",amt);
		else if(cw is "LiberatorRifle"){
			if(
				!(cw.weaponstatus[0]&LIBF_NOLAUNCHER)
			)ppp.A_DropInventory("HDRocketAmmo",amt);
			int ccc=ppp.countinv("SevenMilAmmo");
			if(ccc)ppp.A_DropInventory("SevenMilAmmo",ccc);
			else if(amt>0)ppp.A_DropInventory("HD7mMag",amt);
		}
		else if(cw is "Brontornis")ppp.A_DropInventory("BrontornisRound",amt);
		else if(cw is "BossRifle"){
			int ccc=ppp.countinv("SevenMilAmmo");
			if(ccc)ppp.A_DropInventory("SevenMilAmmo",ccc);
			else if(amt>0)ppp.A_DropInventory("HD7mClip",amt);
		}else if(cw is "ThunderBuster"||cw is "Lumberjack"||cw is "BFG9k"){
			ppp.A_DropInventory("HDBattery",amt);
		}else if(cw is "SelfBandage"){
			ppp.A_DropInventory("PortableStimpack",amt);
			ppp.A_DropInventory("PortableMedikit",amt);
			ppp.A_DropInventory("PortableBerserkPack",amt);
			ppp.A_DropInventory("BluePotion",amt);
		}else if(cw is "HDBP"){
			let hdbp=hdbp(cw);
			int which=hdbp.weaponstatus[HDBPS_WHICH];
			int howmany=hdbp.itemamount[which];
			class<inventory> what=hdbp.itemtype[which];
			if(!howmany)return;
			if(what is "HDMagAmmo"){
				HDMagAmmo.SpawnMag(ppp,what,hdbp.BPTakeMag(which));
			}else{
				int unit=1;
				if(what is "HDShellAmmo")unit=4;
				else if(what is "FourMilAmmo")unit=random(1,49);
				else if(what is "HDArmour")unit=howmany;
				howmany=min(unit,howmany);
				hdbp.itemamount[hdbp.weaponstatus[HDBPS_WHICH]]-=howmany;
				inventory ddd=inventory(ppp.spawn(what,ppp.pos));
				if(ddd){
					ddd.addz(ppp.height-14);ddd.angle=ppp.angle;
					ddd.A_ChangeVelocity(4,0,1,CVF_RELATIVE);
					ddd.amount=howmany*amt;
				}
			}
			hdbp.getbulk();
		}else if(cw is "MagManager"){
			let mmm=HDMagAmmo(MagManager(cw).thismag);
			if(!mmm)return;
			let what=mmm.roundtype;
			if(!what)return;
			int howmany=min(max(1,hdmath.maxinv(ppp,what)*0.2),ppp.countinv(what));
			if(!howmany)return;
			ppp.A_DropInventory(what,howmany);
		}else PurgeUselessAmmo(ppp);
	}
}

class DropMeds:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(health<1)return;
			A_DropInventory("PortableStimpack",8);
			A_DropInventory("PortableMedikit",8);
			A_DropInventory("PortableBerserkPack",8);
			A_DropInventory("BluePotion",8);
		}fail;
	}
}
class DropMisc:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(health<1)return;
			A_DropInventory("PortableLiteAmp",countinv("PortableLiteAmp"));
			A_DropInventory("PortableLadder",countinv("PortableLadder"));
			A_DropInventory("HERPUsable",countinv("HERPUsable"));
			A_DropInventory("DERPUsable",countinv("DERPUsable"));
			A_DropInventory("HERPDEAD",countinv("HERPDEAD"));
			A_DropInventory("DERPDEAD",countinv("DERPDEAD"));
//			A_DropInventory("HDBlurSphere"); //too precious to drop casually
			A_DropInventory("PortableRadsuit",countinv("PortableRadsuit"));
			A_DropInventory("SquadSummoner",countinv("SquadSummoner"));
			A_DropInventory("HDIEDKit",countinv("HDIEDKit"));
			A_DropInventory("DudRocketAmmo",countinv("DudRocketAmmo"));
			A_DropInventory("HDArmour");
		}fail;
	}
}

