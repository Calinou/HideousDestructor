//-------------------------------------------------
// heh
//-------------------------------------------------
class HDDropOne:ActionItem{
	states{
	use:
		TNT1 A 0{
			if(!player||!player.readyweapon)return;
			name cw=player.readyweapon.getclassname();
			if(cw=="HDPistol")A_GiveInventory("DropOnePistolMag");
			else if(cw=="HDSMG")A_GiveInventory("DropOneSMGMag");
			else if(cw=="Hunter"||cw=="Slayer")A_GiveInventory("DropFourSG");
			else if(cw=="ZM66AssaultRifle"){
				let cww=ZM66AssaultRifle(player.readyweapon);
				if(cww.weaponstatus[0]&ZM66F_NOLAUNCHER)A_GiveInventory("DropOneZMag");
				else A_GiveInventory("DropOneZMagFrag");
			}
			else if(cw=="Vulcanette")A_GiveInventory("DropOneZMagCell");
			else if(cw=="RocquetteLauncher")A_GiveInventory("DropOneRocketHEAT");
			else if(cw=="Blooper")A_GiveInventory("DropOneRocket");
			else if(cw=="HDFragGrenades")A_GiveInventory("DropOneFrag");
			else if(cw=="LiberatorRifle"){
				let cww=LiberatorRifle(player.readyweapon);
				if(cww.weaponstatus[0]&LIBF_NOLAUNCHER)A_GiveInventory("DropOneLib");
				else A_GiveInventory("DropOneLibFrag");
			}
			else if(cw=="Brontornis")A_GiveInventory("DropOneBronto");
			else if(cw=="BossRifle")A_GiveInventory("DropOneBoss");
			else if(cw=="ThunderBuster"||cw=="BFG9k"||cw=="Lumberjack")
				A_GiveInventory("DropOneCell",1);
			else if(cw=="SelfBandage")A_GiveInventory("DropOneMedStimZerk");
			else A_GiveInventory("DropExtra");
		}fail;
	}
}
class DropActionItem:ActionItem{
	action void A_MassDrop(
		class<inventory> item,
		class<actor> pickup,
		double throwdistmin=3,
		double throwdistmax=5,
		int amtperunit=1
	){
		double dheight=height-12;
		while(countinv(item)>=amtperunit){  
			A_TakeInventory(item,amtperunit);
			double tdh=throwdistmin*0.5;
			A_SpawnItemEx(pickup,
				random(8,12),random(0,12),dheight,
				frandom(throwdistmin,throwdistmax),0,0,
				frandom(tdh,tdh),SXF_NOCHECKPOSITION|SXF_NOPOINTERS
			);
		}
	}
}

class DropFrag:DropActionItem{
	states{
	pickup:
		TNT1 A 0 A_MassDrop("HDFragGrenadeAmmo","HDFragGrenadeAmmo",3,5);
		fail;
	}
}
class DropMisc:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			A_DropInventory("PortableLiteAmp");
			A_DropInventory("HERPUsable");
			A_DropInventory("DERPUsable");
			A_MassDrop("PortableRadsuit","PortableRadsuit",1,2);
			A_MassDrop("SquadSummoner","SquadSummoner",1,2);
			A_MassDrop("HDIEDKit","HDIEDKit",3,5);
			A_MassDrop("DudRocquetteAmmo","DudRocquetteAmmo",3,4);
			if(countinv("GreenArmourStored"))A_DropInventory("GreenArmourStored");
			if(countinv("BlueArmourStored"))A_DropInventory("BlueArmourStored");
			if(countinv("BlackArmourStored"))A_DropInventory("BlackArmourStored");
		}fail;
	}
}
class DropMeds:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			A_DropInventory("PortableBerserkPack",8);
			A_DropInventory("PortableMedikit",8);
			A_DropInventory("PortableStimpack",8);
			A_DropInventory("BluePotion",8);
		}fail;
	}
}


class DropExtra:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(
				!countinv("HDPistol")
				&&!countinv("HDSecondPistol")
				&&!countinv("HDSMG")
				&&!countinv("DERPUsable")
			){
				A_MassDrop("HDPistolMag","HD9mPMag",3,5);
				A_MassDrop("HDSMGMag","HD9mSMag",3,4);
				A_MassDrop("HDPistolAmmo","HDPistolAmmo",4,6);
			}
			if(
				!countinv("Slayer")
				&&!countinv("Hunter")
			){
				A_MassDrop("HDShellAmmo","ShellPickup",3,5,4);
				A_MassDrop("HDShellAmmo","HDShellAmmo",3,5);
			}
			if(!countinv("Hunter")){
				A_MassDrop("ShotgunQuickAmmo","ShellPickup",3,5,4);
				A_MassDrop("ShotgunQuickAmmo","HDShellAmmo",3,5);
			}
			if(!countinv("Slayer")){
				A_MassDrop("ShotgunQuickAmmo2","ShellPickup",3,5,4);
				A_MassDrop("ShotgunQuickAmmo2","HDShellAmmo",3,5);
			}
			if(
				!countinv("ZM66AssaultRifle")
				&&!countinv("Vulcanette")
				&&!countinv("HERPUsable")
			){
				A_MassDrop("ZM66RifleMags","ZM66RifleMags",3,5);
				A_DropInventory("FourMilAmmo",countinv("FourMilAmmo"));
			}
			if(
				!countinv("RocquetteLauncher")
			){
				A_MassDrop("HEATAmmo","HEATAmmo",2,2.5);
				if(
					!countinv("Blooper")
					&&!countinv("ZM66AssaultRifle")
					&&!countinv("LiberatorRifle")
				){
					A_MassDrop("HDRocketAmmo","HDRocketAmmo",2,3);
				}
			}
			if(
				!countinv("LiberatorRifle")
				&&!countinv("BossRifle")
			){
				A_MassDrop("LiberatorMag","LiberatorMag",2,2.5);
				A_MassDrop("BossClip","BossClip",4,5);
				A_MassDrop("SevenMilAmmo","SevenMilAmmo",5,6);
			}
			if(
				!countinv("BFG9k")
			){
				A_MassDrop("HDCellPackEmpty","HDCellPackEmpty",3,4);
				if(
					!countinv("ThunderBuster")
					&&!countinv("Vulcanette")
					&&!countinv("Lumberjack")
					&&!countinv("HERPUsable")
				){
					A_MassDrop("HDCellPacks","HDCellPacks",3,4);
					A_GiveInventory("HDSpareCellDropper");
				}
			}
			if(!countinv("Brontornis"))
				A_MassDrop("BrontornisRound","BrontornisRound",4,5);
			if(!countinv("HDIEDKit"))
				A_MassDrop("DudRocquetteAmmo","DudRocquetteAmmo",4,5);
			double aaa=angle;
			angle+=frandom(-10,10);A_DropInventory("DERPDEAD");
			angle+=frandom(-10,10);A_DropInventory("HERPDEAD");
			angle=aaa;
		}fail;
	}
}



class DropOnePistolMag:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(countinv("HDPistolAmmo")){
				A_MassDrop("HDPistolAmmo","HDPistolAmmo",4,6);
			}
			else A_DropInventory("HDPistolMag",1);
		}fail;
	}
}
class DropOneSMGMag:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(countinv("HDPistolAmmo")){
				A_MassDrop("HDPistolAmmo","HDPistolAmmo",4,6);
			}else A_DropInventory("HDSMGMag",1);
		}fail;
	}
}
class DropFourSG:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(countinv("HDShellAmmo")>=4){  
				A_TakeInventory("HDShellAmmo",4);
				A_SpawnItemEx("ShellPickup",0,0,height-12,
					cos(pitch)*cos(angle)*5,cos(pitch)*sin(angle)*5,sin(pitch)*-5,
					0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM
				);
			}else A_MassDrop("HDShellAmmo","HDShellAmmo",3,5);
		}
		fail;
	}
}
class DropOneZMag:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(countinv("FourMilAmmo"))A_DropInventory("FourMilAmmo",countinv("FourMilAmmo"));
			else A_DropInventory("ZM66RifleMags",1);
		}fail;
	}
}
class DropOneZMagFrag:DropOneZMag{
	states{
	pickup:
		TNT1 A 0 A_DropInventory("HDRocketAmmo",1);
		goto super::pickup;
	}
}
class DropOneZMagCell:DropOneZMag{
	states{
	pickup:
		TNT1 A 0{
			if(countinv("HDCellAmmo"))A_GiveInventory("HDSpareCellDropper");
			else A_DropInventory("HDCellPacks",1);
		}goto super::pickup;
	}
}
class DropOneRocketHEAT:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			angle+=1;
			A_DropInventory("HDRocketAmmo",1);
			A_DropInventory("HEATAmmo",1);
			angle-=1;
		}fail;
	}
}
class DropOneRocket:DropActionItem{
	states{
	pickup:
		TNT1 A 0 A_DropInventory("HDRocketAmmo",1);
		fail;
	}
}
class DropOneFrag:DropActionItem{
	states{
	pickup:
		TNT1 A 0 A_DropInventory("HDFragGrenadeAmmo",1);
		fail;
	}
}
class DropOneLib:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(countinv("SevenMilAmmo"))A_MassDrop("SevenMilAmmo","SevenMilAmmo",5,6);
			else A_DropInventory("LiberatorMag",1);
		}fail;
	}
}
class DropOneLibFrag:DropOneLib{
	states{
	pickup:
		TNT1 A 0 A_DropInventory("HDRocketAmmo",1);
		goto super::pickup;
	}
}
class DropOneBronto:DropActionItem{
	states{
	pickup:
		TNT1 A 0 A_DropInventory("BrontornisRound",1);
		fail;
	}
}
class DropOneBoss:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(countinv("SevenMilAmmo"))A_MassDrop("SevenMilAmmo","SevenMilAmmo",5,6);
			else A_DropInventory("BossClip",1);
		}fail;
	}
}
class DropOneCell:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(countinv("HDCellAmmo"))A_GiveInventory("HDSpareCellDropper");
			if(countinv("HDCellPackEmpty"))A_DropInventory("HDCellPackEmpty",1);
			else A_DropInventory("HDCellPacks",1);
		}fail;
	}
}
class DropOneMedStimZerk:DropActionItem{
	states{
	pickup:
		TNT1 A 0{
			A_DropInventory("PortableStimpack",8);
			A_DropInventory("PortableMedikit",8);
			A_DropInventory("PortableBerserkPack",8);
		}fail;
	}
}


