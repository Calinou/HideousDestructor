//-------------------------------------------------
// heh
//-------------------------------------------------
class HDDropOne:ActionItem{
	states{
	use:
		TNT1 A 0{
			if(!player||!player.readyweapon)return;
			name cw=player.readyweapon.getclassname();
			if(cw=="HDPistol")A_GiveInventory("DropOnePistolMag");
			else if(cw=="HDSMG")A_GiveInventory("DropOneSMGMag");
			else if(cw=="Hunter"||cw=="Slayer")A_GiveInventory("DropFourSG");
			else if(cw=="ZM66Rifle"||cw=="ZM66SemiRifle")A_GiveInventory("DropOneZMag");
			else if(cw=="ZM66GrenadeRifle")A_GiveInventory("DropOneZMagFrag");
			else if(cw=="Vulcanette")A_GiveInventory("DropOneZMagCell");
			else if(cw=="RocquetteLauncher")A_GiveInventory("DropOneRocketHEAT");
			else if(cw=="Blooper")A_GiveInventory("DropOneRocket");
			else if(cw=="HDFragGrenades")A_GiveInventory("DropOneFrag");
			else if(cw=="LiberatorRifle")A_GiveInventory("DropOneLib");
			else if(cw=="LiberatorGrenadeRifle")A_GiveInventory("DropOneLibFrag");
			else if(cw=="Brontornis")A_GiveInventory("DropOneBronto");
			else if(cw=="BossRifle")A_GiveInventory("DropOneBoss");
			else if(cw=="ThunderBuster"||cw=="BFG9k"||cw=="Lumberjack")
				A_GiveInventory("DropOneCell",1);
			else if(cw=="SelfBandage")A_GiveInventory("DropOneMedStimZerk");
			else A_GiveInventory("DropExtra");
		}fail;
	}
}


//experimental, ignore
class HDBlursphere:Inventory{
	override void DoEffect(){
		owner.bshadow=true;
		hdplayerpawn(owner).nocrosshair=10;
		if(
			owner.vel==(0,0,0)&&
			owner.player&&
			!owner.player.cmd.pitch&&
			!owner.player.cmd.yaw&&
			(!owner.player.cmd.buttons||owner.player.cmd.buttons==BT_CROUCH)
		){
			if(!bjustattacked){
				owner.A_SetRenderStyle(0,Style_Translucent);
				owner.binvisible=true;
				actor aaa=spawn("idledummy",pos);
				aaa.health=10;
				aaa.bshootable=true;
				aaa.A_AlertMonsters(0,AMF_TARGETEMITTER);
				aaa.stamina=10;
				bjustattacked=true;
			}
		}else{
			bjustattacked=false;
			owner.A_SetRenderStyle(1,STYLE_Fuzzy);
			owner.binvisible=false;
		}
		stamina--;
		if(stamina<1){
			EndEffect();
			destroy();
		}
	}
	override void OwnerDied(){
		EndEffect();
	}
	void EndEffect(){
		if(!owner)return;
		owner.binvisible=false;
		owner.bshadow=false;
		owner.A_SetRenderStyle(1,STYLE_Normal);
	}
	default{
		+countitem +visibilitypulse +floatbob +nogravity
		+inventory.alwayspickup +inventory.bigpowerup
		inventory.maxamount 1;
		stamina 35*60;
		renderstyle "translucent";alpha 0.4;
		inventory.pickupmessage "$GOTINVIS";
	}
	states{
	spawn:
		PINS ABCD 6 bright;
		loop;
	}
}


