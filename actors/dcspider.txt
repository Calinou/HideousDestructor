

//  Spider Mastermind

ACTOR Spiderdemon : SpiderMastermind replaces SpiderMastermind
{
	+noblooddecals bloodtype "NotQuiteBloodSplat"
	+missileevenmore +nodropoff +nofear +doharmspecies
	maxstepheight 72
	maxdropoffheight 72
	speed 10
	decal "BulletScratch"
	attacksound "weapons/rifle"
	painchance 80
	damagefactor "Thermal", 0.9
	damagefactor "SmallArms0", 0.6
	damagefactor "SmallArms1", 0.7
	damagefactor "SmallArms2", 0.8
	damagefactor "SmallArms3", 0.9
	obituary "%o was operated upon by the spider mastermind."
	maxtargetrange 2048
	health 3000
	radius 76
	-noradiusdmg
	states
	{
	spawn:
		TNT1 A 0 A_Look
		TNT1 A 0 A_PlaySound("spider/walk")
		SPID AA 4 A_Wander
		TNT1 A 0 A_Look
		SPID BB 4 A_Wander
		TNT1 A 0 A_Recoil(-1)
		SPID C 4 A_Wander
		TNT1 A 0 A_Recoil(1)
	spawn2:
		SPID C 4 A_Look
		TNT1 A 0 A_SetAngle(angle+random(-1,1)*4)
		TNT1 A 0 A_Jump(28,1)
		loop
	spawn3:
		TNT1 A 0 A_PlaySound ("spider/walk")
		SPID DD 4 A_Wander
		TNT1 A 0 A_Look
		SPID EE 4 A_Wander
		TNT1 A 0 A_Recoil(-1)
		SPID F 4 A_Wander
		TNT1 A 0 A_Recoil(1)
	spawn4:
		SPID F 4 A_Look
		TNT1 A 0 A_SetAngle(angle+random(-1,1)*4)
		TNT1 A 0 A_Jump(28,"spawn")
		loop
	see:
		TNT1 A 0 A_PlaySound ("spider/walk")
		TNT1 A 0 A_Recoil(-3)
		SPID AAABBB 2 A_Chase
		TNT1 A 0 A_PlaySound ("spider/walk")
		TNT1 A 0 A_Recoil(-3)
		SPID CCCDDD 2 A_Chase
		TNT1 A 0 A_PlaySound ("spider/walk")
		TNT1 A 0 A_Recoil(-3)
		SPID EEEFFF 2 A_Chase
		TNT1 A 0 A_JumpIfTargetInLOS("see")
	roam:
		TNT1 A 0 A_PlaySound ("spider/walk")
		SPID AABB 3 A_Wander
		TNT1 A 0 A_PlaySound ("spider/walk")
		SPID CC 3 A_Wander
		TNT1 A 0 A_Jump(48,"roamc")
	roam2:
		SPID DD 3 A_Wander
		TNT1 A 0 A_PlaySound ("spider/walk")
		SPID EEFF 3 A_Wander
		TNT1 A 0 A_Jump(48,"roamf")
		TNT1 A 0 A_JumpIfTargetInLOS("see")
		goto roam
	roamc:
		TNT1 A 0 A_Recoil(-1)
		SPID CC 2 A_Chase("missile","missile",CHF_DONTMOVE)
		TNT1 A 0 A_Recoil(1)
	roamc2:
		SPID CCCCCC 1 A_Chase("missile","missile",CHF_DONTMOVE)
		TNT1 A 0 A_Jump(48,1)
		loop
		TNT1 A 0 A_PlaySound ("spider/walk")
		goto roam2
	roamf:
		TNT1 A 0 A_Recoil(-1)
		SPID FF 2 A_Chase("missile","missile",CHF_DONTMOVE)
		TNT1 A 0 A_Recoil(1)
	roamf2:
		SPID FFFFFF 1 A_Chase("missile","missile",CHF_DONTMOVE)
		TNT1 A 0 A_Jump(48,"roam")
		loop
	missile:
		TNT1 A 0 A_PlaySound ("spider/walk")
		SPID G 0 A_TakeInventory("Clip",999)
		TNT1 A 0 A_JumpIfTargetInLOS("shoot",10)
		TNT1 A 0 A_Recoil(-1)
		SPID AA 2 A_FaceTarget(18,40)
		TNT1 A 0 A_Recoil(-1)
		SPID BB 2 A_FaceTarget(18,40)
		TNT1 A 0 A_PlaySound ("spider/walk")
		TNT1 A 0 A_Recoil(-1)
		SPID CC 2 A_FaceTarget(18,40)
		TNT1 A 0 A_JumpIfTargetInLOS("shoot",10)
		TNT1 A 0 A_Recoil(-1)
		SPID DD 2 A_FaceTarget(18,40)
		TNT1 A 0 A_JumpIfTargetInLOS("shoot",10)
		TNT1 A 0 A_PlaySound ("spider/walk")
		TNT1 A 0 A_Recoil(-1)
		SPID EE 2 A_FaceTarget(18,40)
		TNT1 A 0 A_JumpIfTargetInLOS("shoot",10)
		TNT1 A 0 A_Recoil(-1)
		SPID FF 2 A_FaceTarget(18,40)
		TNT1 A 0 A_JumpIfTargetInLOS("missile",10)
		goto see
	shoot:
		TNT1 A 0 A_Jump(256,1,4)
		SPID F 4 A_Recoil(-1)
		TNT1 A 0 A_FaceTarget(8,8)
		SPID F 4 A_Recoil(2)
		goto shoot2
		SPID C 4 A_Recoil(-1)
		TNT1 A 0 A_FaceTarget(8,8)
		SPID C 4 A_Recoil(2)
	shoot2:
		SPID GHGHGH 2 bright{
			if (countinv("NotShot")>4){A_TakeInventory("NotShot",4);}
			A_GiveInventory("NotShot",1);
			A_GiveInventory("Clip",1);
			A_PlaySound("weapons/bigrifle",countinv("NotShot")+3);
			A_SetPitch(pitch+frandom(-4,4));
			A_SpawnItemEx("nb776",24,0,48,velx,vely,velz,frandom(-4,4),SXF_TRANSFERPITCH);
		}
		SPID G 0 A_FaceTarget(4,4)
		SPID G 0 A_JumpIfInventory("Clip",50,"see")
		TNT1 A 0 A_JumpIfTargetInLOS(1,20)
		goto guard
		SPID G 0 A_Jump(220,"shoot2")
		goto see
	guard:
		SPID C 1 A_JumpIfTargetInLOS("shoot2",20)
		SPID C 1 A_JumpIfTargetInLOS("missile")
		SPID C 1 A_Jump(12,"see")
		SPID C 0 A_JumpIfTargetInLOS("shoot2",20)
		SPID C 1 A_SetAngle(angle+random(-4,4))
		SPID C 1 A_Jump(28,"shoot2")
		SPID C 0 A_JumpIfTargetInLOS("shoot2",20)
		loop
	Pain:
		SPID I 4
		SPID I 4 A_Pain
		goto missile
	Death:
		SPID J 1 A_ChangeFlag ("nodropoff", 0)
		SPID J 2 A_Scream
		SPID JJ 4 A_SpawnItemEx("HDSmokeChunk", random(-10,10), random(-10,10), random(38,50), velx+random(-6,6),vely+random(-6,6),velz+random(3,12),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)

		TNT1 AA 0 A_SpawnItemEx("HDSmokeChunk", random(-10,10), random(-10,10), random(38,50), velx+random(-6,6),vely+random(-6,6),velz+random(3,12),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)

		SPID JJJJJJJ 2 A_SpawnItemEx ("HDSmoke", random(-36,36), random(-36,36), random(24,80), random(-1,1),random(-1,1), random(1,3),0,160)
		SPID K 0 bright A_SpawnItemEx ("HDExplosion", random(-34,34), random(-34,34), random(12,40), random(-1,1),random(-1,1), random(1,3))

		SPID KKKK 2 A_SpawnItemEx ("HDSmoke", random(-36,36), random(-36,36), random(24,80), random(-1,1),random(-1,1), random(1,3),0,160)
		SPID K 0 bright A_SpawnItemEx ("HDExplosion", random(-34,34), random(-34,34), random(12,40), random(-1,1),random(-1,1), random(1,3))
		SPID K 2 A_SpawnItemEx ("HDSmoke", random(-36,36), random(-36,36), random(24,80), random(-1,1),random(-1,1), random(1,3),0,160)

		SPID L 0 A_NoBlocking
		SPID LLLL 2 A_SpawnItemEx ("HDSmoke", random(-36,36), random(-36,36), random(24,80), random(-1,1),random(-1,1), random(1,3),0,160)
		SPID L 0 bright A_SpawnItemEx ("HDExplosionBoss", random(-24,24), random(-24,24), random(24,40), random(-1,1),random(-1,1), random(1,3))
		SPID L 2 A_SpawnItemEx ("HDExplosion", random(-36,36), random(-36,36), random(12,24), random(-1,1),random(-1,1), random(1,3))

		SPID M 3 bright A_SpawnItemEx ("HDExplosionBoss", random(-46,46), random(-46,46), random(24,40), random(-1,1),random(-1,1), random(1,3))

		SPID M 2 bright A_SpawnItemEx ("HDExplosion", random(-36,36), random(-36,36), random(12,24), random(-1,1),random(-1,1), random(1,3))

		SPID M 5 bright A_SpawnItemEx ("HDExplosionBoss", random(-46,46), random(-46,46), random(24,40), random(-1,1),random(-1,1), random(1,3))

		TNT1 AAA 0 A_SpawnItemEx("HDSmokeChunk", random(-10,10), random(-10,10), random(38,50), velx+random(-6,6),vely+random(-6,6),velz+random(3,12),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)

		SPID M 5 bright A_SpawnItemEx ("HDExplosion", random(-46,46), random(-46,46), random(24,40), random(-1,1),random(-1,1), random(1,3))
		SPID M 2 A_Explode (512, 16)
		SPID M 0 A_Quake(5,42,0,500)

		TNT1 AAA 0 A_SpawnItemEx("CyberGibs", random(-10,10), random(-10,10), random(38,50), velx+random(-6,6),vely+random(-6,6),velz+random(3,12),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)

		SPID N 3 bright A_SpawnItemEx ("HDExplosion", random(-46,46), random(-46,46), random(24,40), random(-1,1),random(-1,1), random(1,3))
		SPID N 4 bright A_SpawnItemEx ("HDExplosionBoss", random(-46,46), random(-46,46), random(24,40), random(-1,1),random(-1,1), random(1,3))
		SPID N 3 bright A_SpawnItemEx ("HDExplosion", random(-46,46), random(-46,46), random(24,40), random(-1,1),random(-1,1), random(1,3))

		TNT1 A 0 A_QuakeEx(2,random(-2,2),3,72,0,2048,"",QF_RELATIVE|QF_SCALEDOWN|QF_WAVE,9,9,9)

		TNT1 AA 0 A_SpawnItemEx("CyberGibs", random(-10,10), random(-10,10), random(38,50), velx+random(-6,6),vely+random(-6,6),velz+random(3,12),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)

		TNT1 AAA 0 A_SpawnItemEx("HDSmokeChunk", random(-10,10), random(-10,10), random(38,50), velx+random(-6,6),vely+random(-6,6),velz+random(3,12),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)

		SPID O 3 bright A_SpawnItemEx ("HDExplosionBoss", random(-46,46), random(-46,46), random(20,36), random(-1,1),random(-1,1), random(1,3))

		TNT1 AAAA 0 A_SpawnItemEx("HDSmokeChunk", random(-10,10), random(-10,10), random(38,50), velx+random(-6,6),vely+random(-6,6),velz+random(3,12),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)

		SPID O 3 bright A_SpawnItemEx ("HDExplosion", random(-56,56), random(-56,56), random(20,36), random(-1,1),random(-1,1), random(1,3))

		SPID O 4 bright A_SpawnItemEx ("HDExplosionBoss", random(-56,56), random(-56,56), random(20,36), random(-1,1),random(-1,1), random(1,3))

		SPID PPQQR 4 bright A_SpawnItemEx ("HDSmoke", random(-56,56), random(-56,56), random(12,14), random(-1,1),random(-1,1), random(1,3),0,160)
		SPID R 0 A_SpawnItemEx ("SpiderRemains")
		TNT1 A 0 A_GivetoTarget("HDKillCount",1)
		SPID SSSSSSSSSSSSSSSSSSSSSSSSSSSSSSSS 5 A_SpawnItemEx ("HDSmoke", random(-66,66), random(-56,56), random(12,14), random(-1,1),random(-1,1), random(1,3),random(0,255),160)
		SPID S 200
		SPID S -1 A_BossDeath
		Stop
	}
}
actor SpiderRemains : CyberRemains
{
	states
	{
	spawn:
		SPID R 1
		goto spawn2
	spawn3:
		"####" S 0
		goto spawn2
	}
}

