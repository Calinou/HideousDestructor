
// ------------------------------------------------------------
//   BFG 9000
// ------------------------------------------------------------

actor BFG9K : HDWeapon
{
	game Doom
	weapon.selectionorder 700
	weapon.slotnumber 7
	weapon.kickback 200
	weapon.ammotype "BFGChamber"
	weapon.ammotype2 "HDCellPacks"
	weapon.bobrangex 0.3
	weapon.bobrangey 1.0
	weapon.bobspeed 1.2
	weapon.bobstyle normal
	scale 0.7
	inventory.pickupmessage "You got the BFG 9000! Oh yes."
	states
	{
	Ready:
		BFGG A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		BFGG A 0 A_JumpIfInventory("PressingUnload",1,"unload")
		BFGG A 0 A_JumpIfInventory("pressingaltreload",1,"altreload")
		BFGG A 0 A_GiveInventory("IsWeaponBFG",1)
		goto ReadyEnd

	select0:
		BFGG A 0 {if(countinv("BFGLoaded")<1){A_TakeInventory("BFGChamber");}}
		goto select0bfg
	deselect0:
		BFGG A 0
		goto deselect0bfg

	altfire:
	althold:
		stop
	altreload:
	Unload:
		BFGG A 3
		BFGG A 0 A_JumpIfInventory("BFGLoaded",1,1)
		BFGG C 2 offset(0,36)
		BFGG C 2 offset(0,38)
		BFGG C 4 offset(0,40) A_PlaySound("weapons/rifleclick2")
		BFGG C 8 offset(0,42) A_PlaySound("weapons/huntopen")
		BFGG C 8 offset(0,43) A_PlaySound("weapons/rifleload")

		BFGG C 0 A_GiveInventory("BFGUnloader",1)

		BFGG C 4 offset(0,37) A_PlaySound("weapons/rifleclick2")
		BFGG C 2 offset(0,38)
		BFGG C 2 offset(0,36)
		BFGG A 2 offset(0,34)
		BFGG A 6
		BFGG A 1 A_Refire
		goto Ready
	flash:
		BFGF B 3 bright{
			A_Light1();
			HDFlashAlpha(0,true);
		}
		BFGF A 2 bright{
			A_Light2();
			HDFlashAlpha(200);
		}
		BFGF A 2 bright HDFlashAlpha(128)
		goto lightdone

	Fire:
		BFGG C 0 A_TakeInventory("Timer")
	Hold:
		BFGG C 0
		{
			if ((countinv("BFGInternalCharge")==20)&&(countinv("BFGChamber")==20))
			{return state("chargeend");}
			else if (
				(countinv("BFGLoaded")>0)&&  
				(
					(countinv("BFGInternalCharge")>6)||
					(countinv("BFGChamber")>6)  
				)
			)
			{return state("charge");}
			return state("nope");
		}
	charge:
		BFGG B 0
		{
			if (((countinv("BFGInternalCharge")==20) && (countinv("BFGChamber")==20))
			||(countinv("PressingReload")>0)) {return state("nope");}
			return state("");
		}
		BFGG B 5
		{
			A_GiveInventory("Timer",1);
			if (countinv("Timer")>3) {
				A_TakeInventory("Timer",999);
				if (countinv("BFGChamber")<20) {
					A_GiveInventory("BFGChamber",1);
					if (countinv("BFGChamber")==20) {A_GiveInventory("BFGDemonSummoned",1);}
				}
				else {A_GiveInventory("BFGInternalCharge",1);}
			}
			if (countinv("BFGChamber")==20) {A_SetTics(4);}
			if (health<40) {
				A_SetTics(3);
				if(health>16){A_GiveInventory("BleedOutAI");}
			}
			A_TakeInventory("WeaponBusy",1);
			A_PlaySoundEx("weapons/plasidle",5);
			A_FireCustomMissile("BFGSparkSpawnerSpawner",random(-40,40),0,0,-4);
			A_WeaponReady(WRF_NOFIRE);
		}
		BFGG B 0
		{
			if ((countinv("BFGInternalCharge")==20)&&(countinv("BFGChamber")==20))
			{A_Refire("shoot");}
			else {A_Refire;}
		}
		loop
	chargeend:
		BFGG B 1
		{
			A_FireCustomMissile("BFGSparkSpawnerSpawner",random(-40,40),0,0,-4);
			A_PlaySoundEx("weapons/plasidle",5);
			A_WeaponReady(WRF_ALLOWRELOAD|WRF_NOFIRE);
			A_SetTics(6-countinv("Timer")/3);
			A_GiveInventory("Timer",1);
		}
		BFGG B 0 A_JumpIfInventory("Timer",18,"shoot")
		BFGG B 0 A_Refire("chargeend")
		goto ready
	shoot:
		BFGG B 0 A_TakeInventory("Timer",999)
		BFGG B 0 A_GiveInventory("BFGCycled",1)
		BFGG B 0 A_PlaySound("weapons/bfgf")
		BFGG B 0 A_GiveInventory("PowerFrightener",1)
		BFGG BBBBBBBBBBBBBB 3
		{
			A_GiveInventory("Timer",1);
			A_PlaySoundEx("weapons/plasidle",5);
			A_FireCustomMissile("BFGSparkSpawnerSpawner",random(-40,40),0,0,-4);
			if (countinv("Timer")>6) {A_SetTics(2);}
		}
		BFGG B 0 A_TakeInventory("Timer",999)
		BFGG B 0 A_PlaySoundEx("weapons/plasidle",6)
		BFGG BBBB 0 A_FireCustomMissile("BFGSparkSpawnerSpawner",random(-40,40),0,0,-4)
		BFGG B 1 A_PlaySoundEx("weapons/plasidle",5)
		BFGG BBBB 0 A_FireCustomMissile("BFGSparkSpawnerSpawner",random(-40,40),0,0,-4)
		BFGG B 1 A_PlaySoundEx("weapons/plasidle",6)
		BFGG BBBB 0 A_FireCustomMissile("BFGSparkSpawnerSpawner",random(-40,40),0,0,-4)
		BFGG B 1 A_PlaySoundEx("weapons/plasidle",5)
		BFGG BBBB 0 A_FireCustomMissile("BFGSparkSpawnerSpawner",random(-40,40),0,0,-4)
		BFGG B 1 A_PlaySoundEx("weapons/plasidle",6)

		BFGG A 4 A_AlertMonsters
		BFGG A 4 A_GiveInventory("PowerFrightener",1)
		BFGG B 2
		{
			A_PlaySoundEx("weapons/plasmas","soundslot5");
			A_GiveInventory("PowerFrightener",1);

			A_TakeInventory("BFGInternalCharge",20);
			A_TakeInventory("BFGChamber",20);
			A_TakeInventory("BFGCycled",999);
			A_GunFlash;
			if(random(1,8)>1){A_TakeInventory("BFGDemonSummoned",999);}
			A_SpawnItemEx("BFGBallTail",0,0,height-12,cos(pitch)*cos(angle)*4+velx,cos(pitch)*sin(angle)*4+vely,-sin(pitch)*4+velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			A_SpawnItemEx("BFGBalle",0,0,height-12,cos(pitch)*cos(angle)*13+velx,cos(pitch)*sin(angle)*13+vely,-sin(pitch)*13+velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH|SXF_SETMASTER);
		}
		BFGG B 3
		{
			A_GiveInventory("PowerFrightener",1);
			ThrustThingZ(0,62,1,0);
		}
		BFGG C 0 A_GiveInventory("PowerFrightener",1)
		BFGG C 3 A_Recoil(random(4,5))
		BFGG C 6 ACS_NamedExecuteAlways("MuzzleClimb",0,random(64,96),random(12,18),0)
		BFGG C 0 A_GiveInventory("PowerFrightener",1)
		BFGG C 0 A_Refire("nope")
		goto Ready
	Zoom:
		BFGG A 0 A_JumpIfInventory("HDCellPackEmpty",1,1)
		goto nope
		BFGG A 0 A_GiveInventory("BFGLoadingEmpty",1)
		goto Reload
	Reload:
		BFGG A 4{
			if(!countinv("HDCellPacks")&&!countinv("HDCellAmmo")&&countinv("HDCellPackEmpty"))
			{A_GiveInventory("BFGLoadingEmpty");}
		}
		BFGG A 0 A_JumpIfInventory("BFGLoadingEmpty",1,4)
		BFGG A 0 A_JumpIfInventory("BFGChamber",0,"nope")
		BFGG A 0 A_JumpIfInventory("HDCellPacks",1,2)
		BFGG A 0 A_JumpIfInventory("HDCellAmmo",6,1)
		goto Ready
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-12,-24),random(12,24),0)
		BFGG C 2 offset(0,36)
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-12,-24),random(12,24),0)
		BFGG C 2 offset(0,38)
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-12,-24),random(12,24),0)
		BFGG C 4 offset(0,40) A_PlaySound("weapons/rifleclick2")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-12,-24),random(12,24),0)
		TNT1 A 0 A_GiveInventory("BFGUnloader",1)
		BFGG C 4 offset(0,42) A_PlaySound("weapons/huntopen")

		BFGG C 4 offset(0,42) A_PlaySound("weapons/pocket")
		BFGG C 12

	Reload2:
		BFGG C 12 offset(0,42) A_PlayWeaponSound("weapons/rifleload")
		BFGG C 10 offset(0,36) A_PlayWeaponSound("weapons/grenopen")
		BFGG C 0{
			A_TakeInventory("ShittyReloadDummy");
			if(countinv("BFGChamber")||countinv("BFGLoaded")){A_GiveInventory("BFGUnloader",1);}
			if(countinv("BFGLoadingEmpty")){
				A_TakeInventory("BFGLoadingEmpty");
				A_TakeInventory("HDCellPackEmpty",1,TIF_NOTAKEINFINITE);
				A_GiveInventory("BFGLoaded");
			}else if(countinv("HDCellPacks")){
				A_TakeInventory("HDCellPacks",1,TIF_NOTAKEINFINITE);
				A_GiveInventory("BFGChamber",20);
			}else if(countinv("HDCellAmmo")){
				A_SetInventory("ShittyReloadDummy",
					min(countinv("HDCellAmmo"),random(1,20))
				);
				A_TakeInventory("HDCellAmmo",
					countinv("ShittyReloadDummy"),TIF_NOTAKEINFINITE
				);
				A_GiveInventory("BFGChamber",countinv("ShittyReloadDummy"));
			}
		}
	Reload3:
		BFGG C 0{if(countinv("BFGChamber")){A_GiveInventory("BFGLoaded");}}
		BFGG C 12 offset(0,38) A_PlaySound("weapons/huntopen")
		BFGG C 16 offset(0,37) A_PlaySound("weapons/rifleclick2")
		BFGG C 2 offset(0,38)
		BFGG C 2 offset(0,36)
		BFGG A 2 offset(0,34)
		BFGG A 12
		goto Ready

	spawn:
		TNT1 A 1
		TNT1 A 0 A_CheckProximity("spawndropped","PlayerPawn",20,1, CPXF_COUNTDEAD|CPXF_SETTARGET|CPXF_ANCESTOR|CPXF_CLOSEST|CPXF_NOZ)
	spawnfresh:
		TNT1 A 0 A_SpawnItemEx("BFGPickup",0,0,0,velx,vely,velz,0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_NOPOINTERS)
		stop
	spawndropped:
//		TNT1 A 0 A_JumpIfInTargetInventory("BFG9k",1,"spawnfresh")
		TNT1 A 0 A_SpawnItemEx("BFGPickup",0,0,0,velx,vely,velz,0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS)
		stop
	}
}
actor BFGCycled : InventoryFlag {}
actor BFGLoadingEmpty : InventoryFlag {}
actor BFGLoaded : Ammo
{
	+inventory.untossable
	inventory.maxamount 1
	ammo.backpackmaxamount 1
	ammo.backpackamount 0
	states{spawn:TNT1 A 0 nodelay{return state("null");}stop}
}
ACTOR BFGChamber : Ammo
{
	+inventory.untossable
	inventory.maxamount 20
	ammo.backpackmaxamount 20
	ammo.backpackamount 0
	states{spawn:TNT1 A 0 nodelay{return state("null");}stop}
}
ACTOR BFGInternalCharge : Ammo
{
	+inventory.untossable
	inventory.maxamount 20
	ammo.backpackmaxamount 20
	ammo.backpackamount 0
	states{spawn:TNT1 A 0 nodelay{return state("null");}stop}
}
ACTOR BFGDemonSummoned : InventoryFlag{}

actor BFGSParkSpawnerSpawner
{
	projectile
	+nointeraction
	damage 0
	height 0
	radius 0
	speed 40
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SpawnItemEx("BFGSparkSpawner",0,0,0,velx,vely,(velz+random(-30,30)),0,168)
		stop
	}
}
actor BFGSParkSpawner
{
	+nointeraction
	height 0
	radius 0
	states
	{
	spawn:
		TNT1 A 1
		TNT1 A 0 A_SpawnItemEx("BFGSpark",0,0,0,(-velx/10),(-vely/10),(-velz/10),0,168)
		stop
	}
}
actor BFGSpark
{
	scale 0.16
	+nointeraction
	+forcexybillboard
	radius 0
	height 0
	renderstyle add
	alpha 0.1
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("Timer",10)
	spawn2:
		TNT1 A 0 A_JumpIfInventory("Timer",1,1)
		goto death
		TNT1 A 0 A_TakeInventory("Timer",1)
		TNT1 A 0 A_JumpIfCloser(1,"death")
		BFE2 D 1 bright A_FadeIn(0.1)
		loop
	death:
		BFE2 D 1 A_FadeOut(0.3)
		loop
	}
}
actor BFGVileShard
{
	+ismonster
	+float
	+nogravity
	+noclip
	+lookallaround
	+nofear
	radius 0
	height 0
	renderstyle add
	speed 24
	scale 0.16
	states
	{
	spawn:
		BFE2 A 0
		BFE2 A 0 A_GiveInventory("HDFireEnder")
		BFE2 A 0 A_SetGravity(0.1)
		BFE2 AB 1 bright{
			A_Look;
			A_Wander;
		}loop
	see:
		BFE2 D 0 A_Jump(64,2)
		BFE2 D 0 ThrustThingZ(0,random(-4,8),0,0)
		BFE2 D 1 bright A_JumpIf(alpha < 0.2,"see2")
		BFE2 D 1 A_Wander
		BFE2 A 0 A_CustomMissile("BFGSpark",0,random(-24,24),random(-24,24),2,random(-14,14))
		BFE2 A 1 bright A_Wander
		BFE2 B 1 bright A_FadeOut(0.1)
		BFE2 A 0 A_Wander
		loop
	see2:
		TNT1 AAA 0 A_Wander
		TNT1 A 5 A_VileChase
		BFE2 A 0 A_SpawnItemEx("BFGSpark",random(-4,4),random(-4,4),random(28,36),random(4,6),random(-1,1),random(-6,6),random(0,360),SXF_NOCHECKPOSITION,200)
		loop
	heal:
		BFE2 AAAAAAA 0 A_SpawnItemEx("BFGSpark",random(-4,4),random(-4,4),random(28,36),random(4,6),random(-1,1),random(-6,6),random(0,360),SXF_NOCHECKPOSITION)
		BFE2 AAAA 1 A_SpawnItemEx("BFGSpark",random(-4,4),random(-4,4),random(28,36),random(4,6),random(-1,1),random(-6,6),random(0,360),SXF_NOCHECKPOSITION)
		stop
	}
}
actor BFGShard : BFGVileShard
{
	states
	{
	see2:
		TNT1 A 0
		stop
	}
}
actor SkillSharder:ActionItem{
	states{
	pickup:
		TNT1 A 0 A_SpawnItemEx("BFGVileShard",-2048,0,0,0,0,0,0,SXF_NOPOINTERS|SXF_NOCHECKPOSITION|SXF_SETTARGET)
		fail
	}
}

actor BFGBalle : FastProjectile
{
	decal HDBFGLightning
	renderstyle add
	var int user_bfgdamage;
	damage(user_bfgdamage)
	projectile
	deathsound "weapons/bfgx"
	obituary "$OB_MPBFG_BOOM"
	alpha 0.9
	height 6
	radius 6
	+skyexplode
	+forceradiusdmg
	+ripper
	-noteleport
	+telestomp
	+notarget
	speed 10
	obituary "%o was smacked by %k's big green gob."
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_BFGSpray

		TNT1 A 0{
			user_bfgdamage=1;
			if(countinv("IsMoving",AAPTR_TARGET)>=6){
				A_SetPitch(pitch+random(-3,3));
				A_SetAngle(angle+random(-1,1));
			}
		}
		BFS1 AB 2 bright A_SpawnItemEx("BFGBallTail",0,0,0,(velx*0.2),(vely*0.2),(velz*0.2),0,168,0)
		BFS1 A 0{
			user_bfgdamage=random(10,20)*50;
			A_ChangeFlag("ripper",0);
		}
		goto spawn2
	spawn2:
		TNT1 A 0 A_BishopMissileWeave
		TNT1 A 0 A_SpawnItemEx("BFGBoll",0,0,4,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS)
		BFS1 AB 2 bright A_SpawnItemEx("BFGBallTail",0,0,0,(velx*0.2),(vely*0.2),(velz*0.2),0,168,0)
		TNT1 A 0 A_JumpIf(z-floorz>10,2)
		TNT1 A 0 A_ChangeVelocity(0,0,0.4)
		TNT1 A 0 A_JumpIf(ceilingz-z>18,"spawn2")
		TNT1 A 0 A_ChangeVelocity(0,0,-0.4)
		loop
	death:
		BFE1 A 2
		BFE1 B 2 bright A_Explode(160,512,0)
		TNT1 A 0 A_SpawnItemEx("DistantBFG",0,0,0,0,0,0,0,160)
		BFE1 B 4 bright A_Quake(6,21,0,256)
		TNT1 AAAAA 0 A_SpawnItemEx("HDSmokeChunk",random(-2,0),random(-3,3),random(-2,2),random(-5,0),random(-5,5),random(0,5),random(100,260),SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,16)
		TNT1 AAAAA 0 A_SpawnItemEx("BFGBallRemains",-1,0,-12,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,16)
		BFE1 C 8 bright A_QuakeEx(1,random(-1,1),2,24,0,4096,"",QF_RELATIVE|QF_SCALEDOWN|QF_WAVE,6,6,6)
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-4,0),random(-3,3),random(0,4),random(-1,1),random(-1,1),random(1,3),0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,16)
		BFE1 DEF 6 bright
	fade:
		BFE1 F 3 bright A_FadeOut(0.1)
		loop
	}
}
actor BFGBallRemains : GreenParticleFountain
{
	states
	{
	spawn:
		TNT1 AAAAAAAAA 4 A_SpawnItemEx("HDSmoke",random(-3,3),random(-3,3),random(-3,3),random(-1,1),random(-1,1),random(1,3),0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		TNT1 A 8
		stop
	}
}
actor BFGBallTail : GreenParticleFountain
{
	-invisible
	+forcexybillboard
	scale 0.8
	renderstyle add
	states
	{
	spawn:
		BFS1 AB 2 bright A_FadeOut(0.2)
		loop
	}
}
actor BFGBoll
{
	-noteleport
	+ismonster
	+telestomp
	+shootable
	+invulnerable
	+notarget
	projectile
	damage 10
	speed 20
	radius 1
	height 1
	damagetype "BFGBallAttack"
	+friendly
	+missilemore
	+missileevenmore
	+forcexybillboard
	minmissilechance 256
	+bloodlessimpact
	+seeinvisible
	+ambush
	maxtargetrange 65536
	decal BFGLightningSmall
	renderstyle none//add
	speed 20
	obituary "%o couldn't be bothered to get away from %k's BFG."
	states
	{
	spawn:
		TNT1 A 1 nodelay A_SetAngle(random(0,360))
		TNT1 A 0{
			A_AlertMonsters;
			A_ClearTarget;
		}
		TNT1 AAA 0 A_LookEx(0,0,4096.0,65536.0,330.0,"see")
		TNT1 A 1
		stop
	see:
		TNT1 A 0 A_JumpIf(IsPointerEqual(AAPTR_MASTER,AAPTR_TARGET),"nope")
		TNT1 A 0 A_FaceTarget(0,0)
		TNT1 A 2 A_CustomRailgun((0),0,none,"55 ff 88",RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,50.0,"BFGPuff",0,0,65536,18,0.2,1.0)
		stop
	nope:
		TNT1 A 1
		stop
	}
}
ACTOR BFGPuff : GreenParticleFountain
{
	-invisible
	+nointeraction
	+forcexybillboard
	+bloodlessimpact
	+noblood
	+alwayspuff
	-allowparticles
	+puffonactors
	+puffgetsowner
	+forceradiusdmg
	renderstyle add
	damagetype "BFGBallAttack"
	obituary "%o couldn't be bothered to get away from %k's BFG."
	scale 0.8
	states
	{
	spawn:
		BFE2 A 0
		BFE2 A 0 A_TransferPointer(AAPTR_TARGET,AAPTR_DEFAULT,AAPTR_MASTER,AAPTR_TARGET)
		BFE2 A 1 bright A_PlaySound("misc/bfgrail",5)
		BFE2 A 3 bright A_Explode(random(196,320),320)
		BFE2 ABCDE 2 bright A_FadeOut(0.1)
		TNT1 A 0 A_Jump(2,1)
		stop
		TNT1 A 0 A_SpawnItemEx("BFGVileShard",0,0,10,10,0,0,random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		stop
	}
}


actor BFGUnloader : ActionItem
{
	states
	{
	pickup:
		TNT1 A 0 A_JumpIfInventory("BFGLoaded",1,1)
		fail
		TNT1 A 0 A_JumpIfInventory("BFGChamber",1,1)
		goto puempty


		TNT1 A 0 A_JumpIfInventory("BFGDemonSummoned",1,1)
		goto pu0
		TNT1 A 0 A_TakeInventory("BFGDemonSummoned",1)
		TNT1 A 0 A_Jump(128,"harmless")
		TNT1 A 0 A_Jump(128,"vile","harmless")
		TNT1 A 0 A_Jump(256,"yok","vile","harmless")
	vile:
		TNT1 AAAAA 0 A_FireCustomMissile("BFGVileShard",random(170,190),-10,random(-20,20),5,0,0)
		goto pu0
	yok:
		TNT1 A 0 A_FireCustomMissile("YokaiSpawner",random(170,190),-10,random(-20,20),5,0,0)
	harmless:
		TNT1 AAAAA 0 A_FireCustomMissile("BFGShard",random(170,190),-10,random(-20,20),5,0,0)
		goto pu0

	pu0:
		TNT1 A 0 A_TakeInventory("BFGLoaded",1)
		TNT1 A 0 A_JumpIfInventory("BFGChamber",0,"pu2")
	pu1:
		TNT1 A 0 A_SpawnItemEx("HDCelXB",cos(pitch)*10,0,height-10-10*sin(pitch),velx,vely,velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		fail
	pu2:
		TNT1 A 0 A_TakeInventory("BFGChamber",60)
		TNT1 A 0 A_SpawnItemEx("HDCellpacks",4,0,40,velx,vely,velz,0,SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION,0)
		fail
	puempty:
		TNT1 A 0 A_TakeInventory("BFGLoaded",1)
		TNT1 A 0 A_SpawnItemEx("HDCellpackEmpty",4,0,40,velx,vely,velz,0,SXF_ABSOLUTEANGLE|SXF_NOCHECKPOSITION,0)
		fail
	}
}



actor BFGPickup : HDUPK replaces BFG9000
{
	scale 0.7 height 24
	obituary "%o won the game."
	states
	{
	spawn:
		BFUG A 0 A_Stop
		"----" A 0 A_JumpIfInTargetInventory("BFGCycled",1,"bwahaha")
		"----" A 0 A_JumpIfInventory("Clip",1,"spawn2")
		"----" A 0 A_GiveInventory("Clip",1)
		"----" A 0 A_JumpIf(CheckClass("PlayerPawn",AAPTR_TARGET,true),"takeloop")
		"----" A 0 A_GiveInventory("BFGInternalCharge",20)
		"----" A 0 A_GiveInventory("BFGChamber",20)
		"----" A 0 A_GiveInventory("BFGLoaded",1)
		"----" A 0 A_Jump(256,"spawn2")
	bwahaha:
		"----" A 4 A_PlaySound("weapons/bfgf",CHAN_VOICE)
		"----" A 0 A_PlaySoundEx("weapons/plasidle","soundslot5")
		"----" A 0 A_TakeFromTarget("BFGCycled",999)
		"----" A 0 A_TakeFromTarget("BFGInternalCharge",999)
		"----" A 0 A_TakeFromTarget("BFGChamber",999)
		"----" A 0 A_TakeFromTarget("BFGInternalCharge",999)
		"----" A 0 A_TakeFromTarget("BFGLoaded",999)
		"----" A 0 A_GiveInventory("BFGLoaded",1)
		"----" A 0 A_GiveToTarget("PowerFrightener",1)
		"----" A 0 A_GiveInventory("PowerFrightener",1)
		"----" AAAAAA 2 A_CustomMissile("BFGSparkSpawnerSpawner",14,0,random(-40,40),CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)
		"----" AAAAAA 1 A_CustomMissile("BFGSparkSpawnerSpawner",14,0,random(-40,40),CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)
		"----" AAA 0 A_CustomMissile("BFGSparkSpawnerSpawner",14,0,random(-40,40),CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)
		"----" A 1 A_CustomMissile("BFGSparkSpawnerSpawner",14,0,random(-40,40),CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)
		"----" AAA 0 A_CustomMissile("BFGSparkSpawnerSpawner",14,0,random(-40,40),CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)
		"----" A 1 A_CustomMissile("BFGSparkSpawnerSpawner",14,0,random(-40,40),CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)
		"----" AAA 0 A_CustomMissile("BFGSparkSpawnerSpawner",14,0,random(-40,40),CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)
		"----" A 1 A_CustomMissile("BFGSparkSpawnerSpawner",14,0,random(-40,40),CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)
		"----" AAA 0 A_CustomMissile("BFGSparkSpawnerSpawner",14,0,random(-40,40),CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)
		"----" A 1 A_CustomMissile("BFGSparkSpawnerSpawner",14,0,random(-40,40),CMF_AIMDIRECTION|CMF_ABSOLUTEPITCH)

		"----" A 1 A_AlertMonsters
		"----" A 0 A_GiveInventory("PowerFrightener",1)
		"----" AAAA 1 A_ChangeVelocity(0,0,random(-1,1))
		"----" AAAA 1 A_ChangeVelocity(0,0,0.4)

		"----" A 0 A_PlaySoundEx("weapons/plasmas","soundslot5")
		"----" A 0 A_GiveToTarget("PowerFrightener",1)

		"----" A 0 A_SetPitch(random(-3,3))
		"----" A 0 A_SetAngle(angle+random(-3,3))
		"----" A 0 A_SpawnItemEx("BFGBallTail",0,0,height-12,cos(pitch)*cos(angle)*4+velx,cos(pitch)*sin(angle)*4+vely,sin(pitch)*4+velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		"----" A 1 A_SpawnItemEx("BFGBalle",0,0,height-12,cos(pitch)*cos(angle)*13+velx,cos(pitch)*sin(angle)*13+vely,sin(pitch)*13+velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)

		"----" A 0 A_ChangeVelocity(-4,0,4,CVF_RELATIVE)

		"----" A 0 A_JumpIfInventory("BFGDemonSummoned",1,1)
		goto spawn2
		"----" A 0 A_TakeFromTarget("BFGDemonSummoned",999)
		"----" A 0 A_SpawnItemEx("BFGVileShard",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		goto spawn2
	takeloop:
		"----" A 0 A_JumpIfInTargetInventory("BFGInternalCharge",1,1)
		goto take2
		"----" A 0 A_TakeFromTarget("BFGInternalCharge",1)
		"----" A 0 A_GiveInventory("BFGInternalCharge",1)
		loop
	take2:
		"----" A 0 A_JumpIfInTargetInventory("BFGLoaded",1,2)
		"----" A 0 A_TakeFromTarget("BFGChamber",999)
		goto take4
		"----" A 0 A_TakeFromTarget("BFGLoaded",1)
		"----" A 0 A_GiveInventory("BFGLoaded",1)
	takeloop3:
		"----" A 0 A_JumpIfInTargetInventory("BFGChamber",1,1)
		goto take4
		"----" A 0 A_TakeFromTarget("BFGChamber",1)
		"----" A 0 A_GiveInventory("BFGChamber",1)
		loop
	take4:
		"----" A 0 A_JumpIfInTargetInventory("BFGDemonSummoned",1,1)
		goto spawn2
		"----" A 0 A_TakeFromTarget("BFGDemonSummoned",1)
		"----" A 0 A_GiveInventory("BFGDemonSummoned",1)
		goto spawn2
	give:
		"----" A 0 A_JumpIfInTargetInventory("BFG9k",1,"spawn")
	givegun:
		"----" A 0 A_PlaySound("misc/w_pkup")
		"----" A 0 A_GiveToTarget("BFG9k",1)
	givegunloop:
		"----" A 0 A_JumpIfInventory("BFGInternalCharge",1,1)
		goto givegun2
		"----" A 0 A_GiveToTarget("BFGInternalCharge",1)
		"----" A 0 A_TakeInventory("BFGInternalCharge",1)
		loop
	givegun2:
		"----" A 0 A_JumpIfInventory("BFGLoaded",1,3)
		"----" A 0 A_TakeFromTarget("BFGChamber",999)
		"----" A 0 A_TakeFromTarget("BFGLoaded",999)
		goto givegun4
		"----" A 0 A_GiveToTarget("BFGLoaded",1)
	givegunloop3:
		"----" A 0 A_JumpIfInventory("BFGChamber",1,1)
		goto givegun4
		"----" A 0 A_GiveToTarget("BFGChamber",1)
		"----" A 0 A_TakeInventory("BFGChamber",1)
		loop
	givegun4:
		"----" A 0 A_JumpIfInventory("BFGDemonSummoned",1,1)
		goto givegunloopend
		"----" A 0 A_GiveToTarget("BFGDemonSummoned",1)
		"----" A 0 A_TakeInventory("BFGDemonSummoned",1)
		goto givegunloopend
	}
}
actor BFGPickupDropped : BFGPickup
{
	states
	{
	spawn:
		BFUG A 0 A_Stop
		"----" A 0 A_JumpIfInventory("Clip",1,"spawn2")
		"----" A 0 A_GiveInventory("Clip",1)
		"----" A 0 A_GiveInventory("BFGInternalCharge",random(1,20))
		"----" A 0 A_Jump(24,3)
		"----" A 0 A_GiveInventory("BFGLoaded",1)
		"----" A 0 A_GiveInventory("BFGChamber",random(1,20))
		"----" A 0 A_Jump(200,2)
		"----" A 0 A_GiveInventory("BFGDemonSummoned",1)
		"----" A 0 A_Jump(256,"spawn2")
	}
}
