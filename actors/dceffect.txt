// ------------------------------------------------------------
//   Misc. special effects
// ------------------------------------------------------------

actor ParticleWhiteSmall:WhiteParticleFountain{
	height 10 radius 10
	+nointeraction
	states{
	spawn:
		TNT1 A 2
		stop
	}
}
actor ParticleBlackSmall:BlackParticleFountain{
	height 10 radius 10
	+nointeraction
	states{
	spawn:
		TNT1 A 3
		stop
	}
}



actor HDEffectSprite{
	-solid +noblockmap +noteleport +cannotpush +randomize +forcexybillboard
	radius 1 height 1
}
actor HDSmokeChunk:HDDebris{
	scale 0.2
	damagetype "Thermal"
	obituary "%o was smoked and roasted."
	bouncefactor 0.2 bouncesound ""
	states
	{
	spawn:
		TNT1 A 0 nodelay A_PlaySound("misc/firecrkl",0,0.4,0,3)
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_ChangeFlag("rockettrail",1)
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_ChangeFlag("grenadetrail",1)
	spawn2:
		TNT1 A 0 A_Explode(8,16)
	spawn3:
	death:
		TNT1 AA 6 A_SpawnItemEx ("HDFlameRed", random(-3,3),random(-3,3),random(1,3), velx+random(-1,1),vely+random(-1,1),velz+random(1,2), 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION)
		TNT1 A 0 A_PlaySound("misc/firecrkl",0,0.4,0,3)
		TNT1 A 0 A_GiveInventory("NotShot",1)
		TNT1 A 0 A_JumpIfInventory("NotShot",9,1)
		loop
		TNT1 A 0 A_Jump(256,1,2,3)
		PUFF CC 3 A_SpawnItemEx("HDSmoke",0,0,random(1,3), velx,vely,velz+random(1,3), 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION,96)
		PUFF CCCC random(4,8) A_SpawnItemEx("HDSmoke",0,0,random(1,3), velx,vely,velz+random(1,3), 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION,96)
		PUFF CDD 2 A_FadeOut(0.3)
		stop
	}
}
actor TinyWallChunk:WallChunk{scale 0.12}
actor BigWallChunk:WallChunk{scale 0.25}
actor HugeWallChunk:WallChunk{scale 0.35}
actor WallChunker{
	projectile height 8 radius 8 speed 1 +ripper damage(0)
	var int user_p;
	states{
	spawn:
		TNT1 A 1
		stop
	death:
		TNT1 A 0{
			if((ceilingz-z<12)&&(z-floorz>12)){user_p=-2;}
			else{user_p=5;}
		}
		TNT1 AAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("HugeWallChunk", 0,0,4, random(-9,9),0,random(-3,12)*user_p, random(0,360),SXF_NOCHECKPOSITION)
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("BigWallChunk", 0,0,4, random(-12,12),0,random(-3,14)*user_p, random(0,360),SXF_NOCHECKPOSITION)
		TNT1 AA 0 A_SpawnItemEx ("HDSmoke", -1,0,1, random(-2,2),random(-2,2),0, random(0,360),SXF_NOCHECKPOSITION)
		stop
	}
}





actor HDExplosion:IdleDummy{
	+forcexybillboard alpha 0.9 renderstyle "add"
	states{
	spawn:
	death:
		MISL B 0 nodelay{
			A_ChangeVelocity(0,0,4);
			A_PlaySound("world/explode");
			A_Countdown();
		}
		MISL BB 0 A_SpawnItemEx("ParticleWhiteSmall", 0,0,0, velx+random(-2,2),vely+random(-2,2),velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		MISL BBBB 0 A_SpawnItemEx("HDSmoke", 0,0,0, velx+frandom(-2,2),vely+frandom(-2,2),velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		MISL B 0 A_Jump(256,"fade")
	fade:
		MISL B 1 light("EX1") bright A_FadeOut(0.1)
		MISL C 1 light("EX2") bright A_FadeOut(0.2)
		MISL DD 1 light("EX3") bright A_FadeOut(0.2)
		TNT1 A 20
		stop
	}
}

actor HDExplosionBoss:HDExplosion{
	states{
	spawn:
	death:
		MISL B 0 nodelay{
			A_ChangeVelocity(0,0,4);
			A_PlaySound("world/explode");
			A_Countdown();
			A_Explode(140,random(112,256),0);
		}
		MISL BBBBBBBBBBBBBBB 0 A_SpawnItemEx ("HugeWallChunk", 0,0,0, random(-12,12),random(-12,12),random(8,20),random(0,360),160)
		MISL BBBBB 0 A_SpawnItemEx ("HDSmoke", -1,0,random(-4,4), velx+random(-2,2),vely+random(-2,2),velz+random(-1,1), 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION)
		MISL BBBBBBBBBBBBBBBBBBBBBBBBB 0 A_SpawnItemEx ("BigWallChunk", 0,0,0, random(-6,6),random(-6,6),random(4,18),random(0,360),160)
		goto fade
	}
}



// ------------------------------------------------------------
//   Potentially hellish objects
// ------------------------------------------------------------

actor IsInHell:InventoryFlag{inventory.maxamount 10}
actor HDShortRedColumn:ShortRedColumn replaces ShortRedColumn{
	+forceybillboard +shootable +nodamage +noblood +dontthrust
}
actor HDShortGreenColumn:ShortGreenColumn replaces ShortGreenColumn{
	+forceybillboard +shootable +nodamage +noblood +dontthrust
}
actor HDTallRedColumn:TallRedColumn replaces TallRedColumn{
	+forceybillboard +shootable +nodamage +noblood +dontthrust
}
actor HDTallGreenColumn:TallGreenColumn replaces TallGreenColumn{
	+forceybillboard +shootable +nodamage +noblood +dontthrust
}
actor HDHeartColumn:HeartColumn replaces HeartColumn{
	+forceybillboard +shootable +nodamage +noblood +dontthrust
}
actor HDSkullColumn:SkullColumn replaces SkullColumn{
	+forceybillboard +shootable +nodamage +noblood +dontthrust
}
actor HDEvilEye:SwitchableDecoration replaces EvilEye{
	+forceybillboard +dontthrust +solid height 56 radius 20
	activation thingspec_switch +usespecial
	states
	{
	active:
	inactive:
		CEYE A 0 A_SetScale(-scalex,1)
	spawn:
		CEYE A 2 bright
	spawn2:
		CEYE A 0 A_Jump(256,1,2,3)
		CEYE A 1 bright A_SetTics(random(2,50)) loop
		CEYE B 1 bright A_SetTics(random(2,20)) loop
		CEYE C 1 bright A_SetTics(random(2,20)) loop
	}
}
actor HDStalagmite:Stalagmite replaces Stalagmite{
	+shootable +nodamage +noblood
	+forceybillboard
	+dontthrust
	mass MAXINT
	states{
	spawn:
		SMT2 A -1 nodelay{
			A_SetScale((random(0,1)*2-1)*frandom(0.6,1.6),frandom(0.6,1.6));
			A_SetSize(radius*scalex,height*scaley);
		}
		stop
	}
}
actor HDBigTree:BigTree replaces BigTree{
	+shootable +nodamage +dontthrust
	+forceybillboard +rollsprite
	+dontthrust
	mass MAXINT
	painchance 48
	radius 32 height 100
	states
	{
	quiet:
		"----" A 0 A_ChangeFlag("noblood",1)
		"----" A 0 A_ChangeFlag("nopain",1)
		"----" A -1
		stop
	spawn:
		TRE2 A 0 nodelay{
			A_SetScale((random(0,1)*2-1)*frandom(0.8,1.2),frandom(0.8,1.2));
			A_SetSize(radius*scalex,height*scaley);
		}
	spawn2:
		"----" A 1 A_CheckFloor(1)
		wait
		"----" A 2 ACS_NamedExecuteAlways("CheckHellFloor")
		"----" A 1 A_JumpIfInventory("IsInHell",1,"spawn3")
		"----" A 0 A_JumpIfInventory("IsInHell",random(1,6),"spawn3",AAPTR_PLAYER1)
		goto quiet
		"----" A 0 A_SetAngle(random(1,360))
	spawn3:
		"----" A 1 A_SetTics(random(1,60))
		"----" A 0 A_Jump(32,2)
		"----" A 0 A_PlaySound("grunt/active",random(0,7),frandom(0.1,0.4),0,1)
		goto spawn3
		"----" A 0 A_PlaySound("tree/pain",random(0,7),frandom(0.2,1.0),0,1)
		goto spawn3
	pain:
		"----" A 0 A_StopSound(1)
		"----" A 0 A_StopSound(2)
		"----" A 0 A_StopSound(3)
		"----" A 0 A_StopSound(4)
		"----" A 0 A_StopSound(5)
		"----" A 0 A_StopSound(6)
		"----" A 0 A_StopSound(7)
		"----" A 0 A_StopSound(0)
		"----" A 0 A_PlaySound("tree/pain",0,1.0,0,1)
		"----" A 0 A_GiveInventory("HDFireStarter",1)
		"----" A 0 A_ChangeFlag("nopain",1)
		"----" A 1 A_SetTics(random(35,350))
		"----" A 0 A_ChangeFlag("nopain",0)
		goto spawn2
	}
}
actor HDTorchTree:HDBigTree replaces TorchTree{
	radius 20
	height 54
	states
	{
	spawn:
		TRE1 A 0 nodelay{
			A_SetScale((random(0,1)*2-1)*frandom(0.9,1.6),frandom(0.6,1.6));
			A_SetSize(radius*scalex,height*scaley);
		}
		goto spawn2
	}
}
actor HDStalagtite:HDBigTree replaces Stalagtite{
	radius 20
	height 36
	states{
	spawn:
		SMIT A 0 nodelay{
			A_SetScale((random(0,1)*2-1)*frandom(0.6,1.6),frandom(0.6,1.6));
			A_SetSize(radius*scalex,height*scaley);
		}
	spawn2:
		"----" A 0 A_SetAngle(random(1,360))
		"----" A 0 A_Jump(32,1)
		goto quiet
	spawn3:
		"----" A 1 A_SetTics(random(40,120))
		"----" A 0 A_Jump(32,2)
		"----" A 0 A_PlaySound("grunt/active",random(0,7),frandom(0.05,0.1),0,1)
		goto spawn3
		"----" A 0 A_PlaySound("tree/pain",random(0,7),frandom(0.05,0.2),0,1)
		goto spawn3
	pain:
		"----" A 0 A_StopSound(1)
		"----" A 0 A_StopSound(2)
		"----" A 0 A_StopSound(3)
		"----" A 0 A_StopSound(4)
		"----" A 0 A_StopSound(5)
		"----" A 0 A_StopSound(6)
		"----" A 0 A_StopSound(7)
		"----" A 0 A_StopSound(0)
		"----" A 0 A_PlaySound("tree/pain",0,1.0,0,1)
		"----" A 0 A_GiveInventory("HDFireStarter",1)
		"----" A 0 A_ChangeFlag("nopain",1)
		"----" A 1 A_SetTics(random(35,350))
		"----" A 0 A_ChangeFlag("nopain",0)
		goto spawn2
	}
}

