// ------------------------------------------------------------
//   Mancubus
// ------------------------------------------------------------
ACTOR dManJuice:FatShot replaces FatShot{
	DamageType "Thermal"
	Decal "Scorch"
	+forcexybillboard
	gravity 0.1
	speed 26
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_Explode (18, 72)
		MANF AB 4 BRIGHT A_SpawnItemEx ("HDSmoke", -6, 0, 0, 10, 0, 0)
		TNT1 A 0 A_ChangeFlag("nogravity",0)
		Loop
	Death:
		TNT1 A 0 A_ChangeVelocity(1,0,1,CVF_RELATIVE)
		MISL BBBB 1 BRIGHT A_SpawnItemEx ("HDSmoke", -1, 0, 0, random(2,-4), random(-2,2), 0, 0,160)
		TNT1 A 0 A_RadiusGive ("HDFireStarter", 64, RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS|RGF_CORPSES, random(1,3))
		MISL B 2 BRIGHT A_Explode (22, 96)
		MISL CCDD 2 BRIGHT A_FadeOut (0.2)
		TNT1 A 0 A_ChangeVelocity(-1,0,-1)
		TNT1 A 0 A_SpawnItemEx ("HDFlameRed", 0,0,0, random(2,-4),0,random(1,2), 0,160)
		TNT1 AAAAAAAAAAAAA 4 A_SpawnItemEx ("HDSmoke", random(-2,2),random(-2,2),random(-2,2), random(2,-4), random(-2,2),random(1,4), 0,160)
		stop
	}
}

ACTOR Mancubus:Fatso replaces Fatso{
	deathheight 20
	radius 38
	damagefactor "Thermal", 0.7
	+MISSILEMORE
	+DONTHURTSPECIES
	obituary "%o was smoked by a mancubus."
	painchance 16
	states{
	See:
		FATT AABBCCDDEEFF 4 A_Chase
		loop
	Missile:
		FATT G 4 A_FatRaise
		FATT G 4 A_FaceTarget(20,20)
		FATT GGGGGG 1 BRIGHT A_SpawnItemEx ("HDSmoke", 24, ((random(0,1)*2-1)*24), 18, random(2,4), 0,0,0, 0,160)	
		FATT H 0 A_Jump (256, "Missile1", "Missile2", "Missile3")
	Missile1:
		FATT H 0{
			A_SpawnProjectile("ManJuice",32,-24,random(0,12),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
			A_SpawnProjectile("ManJuice",32,24,random(0,-12),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
		}goto MissilePost
	Missile2:
		FATT H 0{
			A_SpawnProjectile("ManJuice",32,-24,random(8,24),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
			A_SpawnProjectile("ManJuice",32,24,random(0,-2),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
		}goto MissilePost
	Missile3:
		FATT H 0{
			A_SpawnProjectile("ManJuice",32,-24,random(0,2),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
			A_SpawnProjectile("ManJuice",32,24,random(-8,-24),
				CMF_AIMDIRECTION,pitch-frandom(1,4));
		}goto MissilePost
	MissilePost:
		FATT H 10 bright A_GiveInventory("Clip",1)
		FATT H 0 A_JumpIfInventory("Clip",3,"MissileStop")
		FATT IG 5 A_FaceTarget(20,20)
		FATT H 0 A_Jump(256,"Missile2","Missile3")
	MissileStop:
		FATT GGGG 1 BRIGHT A_SpawnItemEx ("HDSmoke", 24, ((random(0,1)*2-1)*24), 18, random(2,4), 0, 0)	
		FATT I 5 A_FaceTarget(20,20)
		FATT G 5 A_TakeInventory ("Clip")
		goto See
	Death:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_SpawnItemEx ("tempshield", 0,0,0, velx,vely,velz, 0,40)
		FATT K 6 A_SpawnItemEx ("HDExplosion", 0, 0, 36)
		FATT L 6 A_Scream
		FATT M 6 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx ("tempshield2", 0,0,0, velx,vely,velz, 0,40)
		FATT NOPQRS 6 A_SpawnItemEx ("HDSmoke", ((random(0,1)*2-1)*random(1,3)), random(-4,4), 30, 0,0,random(1,4), 0,160)	
		FATT TTT 8 A_SpawnItemEx ("HDSmoke", ((random(0,1)*2-1)*random(1,3)), random(-4,4), 12, 0,0,random(1,2), 0,160)	
		TNT1 A 0 A_ChangeFlag("alwaystelefrag",1)
		FATT T -1 A_BossDeath
		Stop
	Raise:
		TNT1 A 0 A_JumpIfInventory("VileCount",2,2)
		TNT1 A 0 A_Die("NotVileEnough")

		FATT T 0 A_GiveInventory("HDCorpseFlags")
		FATT ST 14 A_SpawnItemEx("MegaBloodSplatter",random(-1,1),random(-1,1),random(10,16),velx,vely,velz,0,SXF_NOCHECKPOSITION)
		FATT T 0 A_TakeInventory("VileCount")
		FATT TTSSRRQPONMLK 5
		goto see
	Death.NotVileEnough:
		"####" A 0 A_GiveInventory("HDCorpseFlags")
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_GiveInventory("VileCount")
		FATT STST 14 A_SpawnItemEx("MegaBloodSplatter",random(-1,1),random(-1,1),random(10,16),velx,vely,velz,0,SXF_NOCHECKPOSITION)
		FATT T -1
		stop
	}
}
