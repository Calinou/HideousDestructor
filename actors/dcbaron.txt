
// ------------------------------------------------------------
//   Baron Of Hell
// ------------------------------------------------------------

ACTOR HellPrince:BaronOfHell replaces BaronofHell{
	maxtargetrange 65536
	+missilemore +seeinvisible +dontharmspecies
	BloodColor "44 99 22"
	damagefactor "Thermal",0.8
	damagefactor "SmallArms0",0.86
	damagefactor "SmallArms1",0.95
	damagefactor "Balefire",0.3
	meleedamage 12
	meleerange 58
	health 1600
	meleesound "baron/melee"
	speed 6
	painchance 4
	Obituary "%o was sauteed by a baron of hell."
	HitObituary "%o was shredded by a baron of hell."
	States
	{
	spawn:
		BOSS A 0 nodelay{
			if(countinv("Clip")<1){
				A_GiveInventory("Clip",1);
				A_SetScale(scalex*frandom(0.95,1.05));
				A_SetSize(radius*scalex,height*scaley);
				mass*=scalex;
				if(scalex>1.0){
					A_SetHealth(health*scalex);
				}
			}
		}
		BOSS AA 8 A_Look
		BOSS A 1 A_SetTics(random(1,16))
		BOSS BB 8 A_Look
		BOSS B 1 A_SetTics(random(1,16))
		BOSS CC 8 A_Look
		BOSS C 1 A_SetTics(random(1,16))
		BOSS DD 8 A_Look
		BOSS D 1 A_SetTics(random(1,16))
		TNT1 A 0 A_Jump(216,"spawn")
		TNT1 A 0 A_PlaySound("baron/active")
		loop
	See:
		TNT1 A 0 A_JumpIfInventory("VileCount",1,"NotVileEnough")
		TNT1 A 0 A_AlertMonsters
		BOSS AAABBBCCCDDD 3 A_Chase
		TNT1 A 0 A_JumpIfTargetInLOS("see")
		goto Roam
	Roam:
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 HealThing(random(2,4))
		TNT1 A 0 A_JumpIfTargetInLOS("missile")
		TNT1 A 0 A_Jump(192,2)
		BOSS AAABBBCCCDDD 4 A_Wander
		loop
	missile:
		BOSS ABCD 3{
			A_FaceTarget(30);
			if(A_JumpIfTargetInLOS("null",10)){return state("shoot");}
			return state("");
		}
		BOSS E 0 A_JumpIfTargetInLOS("shoot",10)
		BOSS E 0 A_JumpIfTargetInLOS("missile")
		goto see
	shoot:
		BOSS E 0 A_AlertMonsters(0,AMF_TARGETNONPLAYER)
		BOSS E 0 A_Jump(64,2)
		BOSS E 0 A_JumpIfCloser(420,"MissileSweep")
		BOSS E 0 A_JumpIfHealthLower(1200,1)
		goto MissileFuckYou
		BOSS E 0 A_JumpIfHealthLower(250,"MissileFuckYou")
		BOSS E 0 A_Jump(16,"MissileFuckYou")
		BOSS E 0 A_Jump(256,"MissileSkull","MissileMissile")
	MissileSkull:
		BOSS H 12 A_FaceTarget(0,0)
		BOSS H 12 BRIGHT{
			if(random(1,3)<2){A_SpawnProjectile("BelphBall",28,0,0,2,pitch);}
			else{A_SpawnItemEx("BaronDancer",24,0,28,2,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);}
		}
		BOSS H 18
		Goto MissileSweep
	MissileMissile:
		BOSS H 16 A_FaceTarget(20,20)
		BOSS H 0 BRIGHT A_SpawnProjectile("BaleBall",38,0,2,0,0)
		BOSS H 6 BRIGHT A_SpawnProjectile("BaleBall",38,0,-2,0,0)
		BOSS H 0 BRIGHT A_SpawnProjectile("MiniBBall",46,0,9,2,0)
		BOSS H 6 BRIGHT A_SpawnProjectile("MiniBBall",46,0,-9,2,0)
		BOSS H 0 BRIGHT A_SpawnProjectile("MiniBBall",56,0,17,2,4)
		BOSS H 6 BRIGHT A_SpawnProjectile("MiniBBall",56,0,-17,2,4)
		BOSS H 0 BRIGHT A_SpawnProjectile("MiniBBall",66,0,24,2,7)
		BOSS H 6 BRIGHT A_SpawnProjectile("MiniBBall",66,0,-24,2,7)
		BOSS H 12
		Goto See
	MissileFuckYou:
		BOSS H 18 A_FaceTarget(20,20)
		BOSS H 0 BRIGHT A_SpawnProjectile("BaleBall",38,0,2,0,0)
		BOSS H 0 BRIGHT A_SpawnProjectile("BaleBall",38,0,-2,0,0)
		BOSS H 6 BRIGHT{
			if(random(1,4)>1){A_SpawnProjectile("BelphBall",28,0,0,2,pitch);}
			else{A_SpawnItemEx("BaronDancer",24,0,28,2,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER);}
		}
		BOSS H 0 BRIGHT A_SpawnProjectile("MiniBBall",46,0,5,2,0)
		BOSS H 6 BRIGHT A_SpawnProjectile("MiniBBall",46,0,-5,2,0)
		BOSS H 0 BRIGHT A_SpawnProjectile("MiniBBall",56,0,7,2,4)
		BOSS H 6 BRIGHT A_SpawnProjectile("MiniBBall",56,0,-7,2,4)
		BOSS H 0 BRIGHT A_SpawnProjectile("MiniBBall",66,0,12,2,7)
		BOSS H 6 BRIGHT A_SpawnProjectile("MiniBBall",66,0,-12,2,7)
		BOSS H 12
		Goto See
	Pain:
		BOSS H 6 A_Pain
		BOSS H 3 A_Jump(116,"See","MissileSkull")
	MissileSweep:
		BOSS F 4 A_FaceTarget(20,20)
		BOSS E 6
		BOSS E 2 A_SpawnProjectile("MiniBBall",56,6,-6,CMF_AIMDIRECTION,pitch)
		BOSS F 2 A_SpawnProjectile("MiniBBall",46,4,-3,CMF_AIMDIRECTION,pitch)
		BOSS F 2 A_SpawnProjectile("MiniBBall",38,0,-1,CMF_AIMDIRECTION,pitch)
		BOSS G 2 A_SpawnProjectile("MiniBBall",32,0,1,CMF_AIMDIRECTION,pitch)
		BOSS G 2 A_SpawnProjectile("MiniBBall",32,0,3,CMF_AIMDIRECTION,pitch)
		BOSS G 2 A_SpawnProjectile("MiniBBall",32,0,6,CMF_AIMDIRECTION,pitch)
		BOSS G 6
		BOSS E 2 A_Jump(194,"see")
		loop
	Melee:
		BOSS E 6 A_FaceTarget
		BOSS F 2
		BOSS G 6 A_MeleeAttack
		BOSS F 5 A_JumpIf(getdistance(true,AAPTR_TARGET)>84,"missilesweep")
		goto See
	Death:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_Quake(2,64,0,600)
		BOSS I 0 A_SpawnItemEx("tempshieldgreen",0,0,0,velx,vely,velz,0,40)
		BOSS I 2 A_SpawnItemEx("BFGVileShard",0,0,20,10,0,8,45,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		BOSS I 2 A_SpawnItemEx("BFGVileShard",0,0,35,10,0,8,135,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		BOSS I 2 A_SpawnItemEx("BFGVileShard",0,0,50,10,0,8,225,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		BOSS I 2 A_SpawnItemEx("BFGVileShard",0,0,65,10,0,8,315,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		BOSS J 8 A_Scream
		BOSS K 8 A_SpawnItemEx("tempshield2green",0,0,0,velx,vely,velz,0,40)
		BOSS L 8 A_NoBlocking
		BOSS M 8
		BOSS N 8
		BOSS O 24 A_Quake(2,28,0,600)
		BOSS O 0 A_Quake(1,12,0,600)
		BOSS O -1 A_BossDeath
		stop
	Raise:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_GiveInventory("VileCount",1)
		BOSS ONMLKJI 5
		TNT1 A 0 A_JumpIfInventory("VileCount",5,"ReallyRaise")
		BOSS H 8
		BOSS H 0 A_PlaySound("baron/sight")
		BOSS AAABB 3 A_Chase("NotVileEnough","NotVileEnough")
	NotVileEnough:
		BOSS H 0 A_StopSound(CHAN_BODY)
		BOSS H 0 A_Die("NeedMoreVile")
	Death.NeedMoreVile:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		BOSS H 5 A_PlaySound("misc/gibbed",CHAN_BODY)
		BOSS I 5 A_SpawnItemEx("tempshieldgreen",0,0,0,velx,vely,velz,0,40)
		BOSS J 5
		BOSS K 5 A_SpawnItemEx("tempshield2green",0,0,0,velx,vely,velz,0,40)
		BOSS L 5 A_NoBlocking
		BOSS MN 5
		BOSS O -1
	ReallyRaise:
		BOSS H 5 A_TakeInventory("VileCount")
		BOSS H 0 A_PlaySound("baron/sight")
		goto see
	}
}

actor BaronDancer:DisappearingFireDancer{
	-solid alpha 0.05 scale 0.6 species "BaronofHell" states{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIf(countinv("Clip")<1,1)
		goto super::spawn
		SKUL JIHGF 3 bright A_FadeIn(0.2)
		SKUL B 2 bright A_FaceTarget
		SKUL C 2 bright A_SkullAttack(60)
		SKUL C 0 A_SetSolid
	spawn2:
		SKUL CD 2 bright A_SpawnItemEx("DrunkImpBallTail",-4,0,8,velx*0.7,vely*0.7,velz*0.7+1,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM,128)
		loop
	}
}

ACTOR BelphBall : FastProjectile
{
	damagetype "Thermal"
	Decal "BigScorch"
	+forcexybillboard +seekermissile +hittracer
	Renderstyle soultrans
	Alpha 0.05
	Scale 0.6
	Radius 12
	Height 13
	Speed 2
	Damage 8
	SeeSound "skull/melee"
	DeathSound "bakaslave/death"
	States
	{
	Spawn:
		SKUL JIHGF 3 BRIGHT A_FadeIn(0.2)
		SKUL F 0 BRIGHT A_ScaleVelocity(32)
	See:
		SKUL C 2 BRIGHT
		SKUL C 1 BRIGHT A_SpawnItemEx("DrunkImpBallTail",-6,0,12,velx*0.8,vely*0.8,velz*0.8,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SKUL D 2 BRIGHT
		SKUL D 1 BRIGHT A_SpawnItemEx("DrunkImpBallTail",-6,0,12,velx*0.8,vely*0.8,velz*0.8,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		Loop
	death:
		SKUL FFFFFF 0 A_SpawnItemEx("HDSmoke",0,0,random(-2,4),frandom(-2,2),frandom(-2,2),random(3,5),0,SXF_NOCHECKPOSITION)
		SKUL F 1 BRIGHT A_Quake(3,28,0,128)
		SKUL G 1 BRIGHT A_Explode(56,96,1)
		SKUL GGGGGGGGGGG 0 A_SpawnItemEx("BigWallChunk",0,0,random(-4,4),random(-10,10),random(-10,10),random(-2,10),random(0,360),SXF_NOCHECKPOSITION)
		SKUL HIJ 1 BRIGHT A_FadeOut(0.2)
		TNT1 AAAAA random(2,3) A_SpawnItemEx("HDSmoke",0,0,random(-2,4),frandom(-2,2),frandom(-2,2),random(3,5),0,SXF_NOCHECKPOSITION)
		Stop
	}
}

ACTOR MiniBBallTail
{
	+nointeraction
	+forcexybillboard
	renderstyle add
	alpha 0.6
	scale 0.7
	radius 4
	height 6
	States
	{
	Spawn:
		BAL7 E 2 bright A_FadeOut(0.2)
		TNT1 A 0 A_PlaySound("weapons/plasidle",0,0.4,0,6)
		loop
	}
}

ACTOR MiniBBall
{
	damagetype "Balefire"
	PROJECTILE
	+SEEKERMISSILE
	+forcexybillboard
	Renderstyle add
	Decal "GooScorch"
	Alpha 0.8
	Scale 0.6
	Radius 4
	Height 6
	Speed 16
	Damage 6
	SeeSound "prince/attack"
	DeathSound "prince/shotx"
	var int user_counter;
	States
	{
	Spawn:
		BAL7 EDC 1 BRIGHT
		BAL7 ABABA 2 BRIGHT
		BAL7 BAB 3 BRIGHT
	Spawn2:
		BAL7 A 2 BRIGHT A_SeekerMissile(5,10)
		BAL7 B 2 BRIGHT A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0)
		BAL7 A 2 BRIGHT A_SeekerMissile(5,9)
		BAL7 B 2 BRIGHT A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0)
		BAL7 A 2 BRIGHT A_SeekerMissile(4,8)
		BAL7 B 2 BRIGHT A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0)
		BAL7 A 2 BRIGHT A_SeekerMissile(3,6)
		BAL7 B 2 BRIGHT A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0)
	Spawn3:
		TNT1 A 0 A_JumpIf(user_counter>4,"spawn4")
		TNT1 A 0 A_SetUserVar(user_counter,user_counter+1)
		BAL7 A 3 BRIGHT A_SeekerMissile(1,1)
		BAL7 B 3 BRIGHT A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0)
		Loop
	spawn4:
		BAL7 A 3 BRIGHT A_SpawnItemEx("MiniBBallTail",-3,0,3,3,0,random(1,2),0,161,0)
		TNT1 A 0 A_JumpIf(z-floorz<10,2)
		BAL7 B 3 BRIGHT A_ChangeVelocity(frandom(-0.2,1),frandom(-1,1),frandom(-1,0.9),CVF_RELATIVE)
		loop
		BAL7 B 3 BRIGHT A_ChangeVelocity(frandom(-0.2,1),frandom(-1,1),frandom(-0.6,1.9),CVF_RELATIVE)
		loop
	Death:
		BAL7 CDE 4 BRIGHT A_FadeOut(0.2)
		stop
	}
}

ACTOR BaleBall : MiniBBall
{
	Decal "GooScorch"
	Speed 18
	Damage 8
	Scale 1.0
	Radius 6
	Height 8
	States
	{
	Spawn:
		BAL7 AA 1 BRIGHT A_SeekerMissile(4,6)
		BAL7 A 1 BRIGHT A_SpawnItemEx("MiniBBallTail",-3,0,3,3,random(-1,1), random(1,2),0,161)
		BAL7 BB 1 BRIGHT A_SeekerMissile(4,6)
		BAL7 B 1 BRIGHT A_SpawnItemEx("MiniBBallTail",-3,0,3,3,random(-1,1), random(1,2),0,161)
		BAL7 ABABABAB 3 BRIGHT A_SpawnItemEx("MiniBBallTail",-3,0,3,3,random(-1,1), random(1,2),0,161)
		loop
	}
}


// ------------------------------------------------------------
//   Hell Knight
// ------------------------------------------------------------

ACTOR HellKnave : HellKnight replaces HellKnight
{
	+missilemore
	+MISSILEEVENMORE
	damagefactor "Balefire",0.3
	damagefactor "Thermal",0.8
	damagefactor "SmallArms0",0.9
	BloodColor "44 99 22"
	Height 64
	Scale 0.9
	speed 6
	meleedamage 10
	meleerange 56
	meleesound "baron/melee"
	Obituary "%o didn't get quite medieval enough on a hell knight's ass."
	HitObituary "%o had %p loins tendered by sir knight."
	States
	{
	spawn2:
		BOS2 A 4 A_Wander
		TNT1 A 0 A_Look
		BOS2 B 4 A_Wander
		TNT1 A 0 A_Look
		BOS2 C 4 A_Wander
		TNT1 A 0 A_Look
		BOS2 D 4 A_Wander
		BOS2 A 0 A_Jump(198,"spawn2")
		BOS2 A 0 A_Recoil(-0.4)
	spawn:
		BOS2 A 0 nodelay{
			if(countinv("Clip")<1){
				A_GiveInventory("Clip",1);
				A_SetScale(scalex*frandom(0.8,1.1));
				A_SetSize(radius*scalex,height*scaley);
				mass*=scalex;
				if(scalex>1.0){
					A_SetHealth(health*scalex);
				}
			}
		}
		BOS2 AAABBCCCDD 8 A_Look
		BOS2 A 0 A_SetAngle(angle+random(-4,4))
		BOS2 A 1 A_SetTics(random(1,3))
		TNT1 A 0 A_Jump(216,2)
		TNT1 A 0 A_PlaySound("knight/active")
		BOS2 A 0 A_CheckFlag("ambush","spawn")
		BOS2 A 0 A_Jump(32,"spawn2")
		loop
	See:
		TNT1 A 0 A_JumpIfInventory("VileCount",1,"NotVileEnough")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 HealThing(random(1,3))
		BOS2 AAABBBCCCDDD 2 A_Chase
		loop
	pain.balefire:
		BOS2 H 3{
			A_Recoil(0.4);
			HealThing(20);
			if(random(1,4==1)){A_KillChildren;}
		}
		goto pain
	missile:
		BOS2 ABCD 3{
			A_FaceTarget(30);
			if(A_JumpIfTargetInLOS("null",10)){return state("shoot");}
			return state("");
		}
		BOS2 E 0 A_JumpIfTargetInLOS("shoot",10)
		BOS2 E 0 A_JumpIfTargetInLOS("missile")
		goto see
	shoot:
		BOS2 E 0 A_JumpIfCloser(512,"missile2")
		BOS2 E 0 A_Jump(208,"missile2")
		BOS2 E 0 A_CheckProximity("missile2","Putto",256,4,CPXF_CHECKSIGHT)
		goto missile3
	missile3:
		BOS2 E 6 A_PlaySound("knight/sight")
		BOS2 E 4 A_FaceTarget(60,60)
		BOS2 E 2
		BOS2 F 5
		BOS2 G 3
		BOS2 G 6 A_SpawnItemEx("Putto",24,0,0,28*cos(pitch),0,28*sin(-pitch),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_SETMASTER)
		BOS2 F 6
		Goto See
	missile2:
		BOS2 E 6 A_FaceTarget(60,60)
		BOS2 E 3
		BOS2 F 2
		BOS2 F 1 {if(random(1,3)<3){A_SpawnProjectile("MiniBBall",36,4,-4,CMF_AIMDIRECTION,pitch);}}
		BOS2 G 1 A_SpawnProjectile("BaleBall",28,0,0,CMF_AIMDIRECTION,pitch)
		BOS2 G 2 {if(random(1,3)<3){A_SpawnProjectile("MiniBBall",32,2,4,CMF_AIMDIRECTION,pitch);}}
		BOS2 GF 5
		Goto See
	Melee:
		BOS2 E 6 A_FaceTarget
		BOS2 F 2
		BOS2 G 6 A_MeleeAttack
		BOS2 F 5
		goto See
	Death:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		BOS2 I 8 A_SpawnItemEx("tempshieldgreen",0,0,0,velx,vely,velz,0,40)
		BOS2 J 8 A_Scream
		BOS2 K 8 A_SpawnItemEx("tempshield2green",0,0,0,velx,vely,velz,0,40)
		BOS2 L 8 A_NoBlocking
		BOS2 M 8
		BOS2 N 8
		BOS2 O -1 A_BossDeath

	Raise:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_GiveInventory("VileCount",1)
		BOS2 ONMLKJI 5
		TNT1 A 0 A_JumpIfInventory("VileCount",2,"ReallyRaise")
		BOS2 H 8
		BOS2 H 0 A_PlaySound("knight/sight")
		BOS2 H 0 A_FaceTarget
		BOS2 AAABB 3 A_Chase("NotVileEnough","NotVileEnough")
	NotVileEnough:
		BOS2 H 0 A_StopSound(CHAN_BODY)
		BOS2 H 0 A_Die("NeedMoreVile")
	Death.NeedMoreVile:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		BOS2 H 5 A_PlaySound("misc/gibbed",CHAN_BODY)
		BOS2 I 5 A_SpawnItemEx("tempshieldgreen",0,0,0,velx,vely,velz,0,40)
		BOS2 J 5
		BOS2 K 5 A_SpawnItemEx("tempshield2green",0,0,0,velx,vely,velz,0,40)
		BOS2 L 5 A_NoBlocking
		BOS2 MN 5
		BOS2 O -1
	ReallyRaise:
		BOS2 H 5 A_TakeInventory("VileCount")
		BOS2 H 0 A_PlaySound("knight/sight")
		goto see
	}
}




