actor NoBlockWepDrop:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			A_NoBlocking;
			if(CheckClass("BotBot",AAPTR_DEFAULT,true)){return state("");}
			if(CheckClass("ZombieSMGStormtrooper",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("HDSMGDropped");
					A_DropItem("HDFragGrenadeAmmo",0,8);
					A_DropItem("HDFragGrenadeAmmo",0,4);
					A_DropItem("HDSMGMag",8);
				}else{
					A_DropItem("HDSMGMag",96);
					A_DropItem("HDFragGrenadeAmmo",0,4);
					A_DropItem("HDFragGrenadeAmmo",0,4);
				}
			}
			else if(CheckClass("ZombieSemiStormtrooper",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("ZM66SemiRiflePickupDropped");
				}else{
					A_DropItem("ZM66RifleMags",0,96);
				}
			}
			else if(CheckClass("ZombieStormtrooper",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("ZM66RiflePickupDropped");
				}else{
					A_DropItem("ZM66RifleMags",0,96);
				}
			}
			else if(CheckClass("UndeadJackbootman",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("ZM66RiflePickupDropped");
				}else{
					A_DropItem("ZM66RifleMags",0,96);
				}
			}
			else if(CheckClass("JackAndJillBoot",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("SuperShotgunPickupDropped");
				}else{
					A_DropItem("ShellPickup",0,200);
				}
			}
			else if(CheckClass("Jackboot",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("ShotgunPickupDropped");
				}else{
					A_DropItem("ShellPickup",0,200);
				}
			}
			else if(CheckClass("DragonGuard",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("VulcPickupDropped");
					A_DropItem("HDCellPacks",16);
				}else{
					A_DropItem("ZM66RifleMags",0,96);
					A_DropItem("ZM66RifleMags",0,96);
					A_DropItem("HDCellPacks",0,8);
				}
			}
			else if(CheckClass("HoopBubble",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("HDSMGDropped");
				}else{
					A_DropItem("HDSMGMag",0,96);
				}
			}
			else if(CheckClass("SWAT2Base",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("HDSMGDropped");
				}else{
					A_DropItem("HDSMGMag",0,96);
				}
			}
			else if(CheckClass("SWATBase",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("ShotgunPickupDropped");
				}else{
					A_DropItem("ShellPickup",0,200);
				}
			}
			else if(CheckClass("RocketeerBase",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("RocquetteLauncherPickup");
				}else{
					A_DropItem("HDRocketAmmo",0,96);
					A_DropItem("HDRocketAmmo",0,96);
					A_DropItem("HDRocketAmmo",0,96);
					A_DropItem("HDRocketAmmo",0,96);
				}
			}
			else if(CheckClass("RiflemanBase",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("ZM66GrenadeRiflePickupDropped");
				}else{
					A_DropItem("ZM66RifleMags",96);
				}
			}

/*
			else if(CheckClass("",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_DropItem("");
				}else{
					A_DropItem("");
				}
			}
*/

				//marines drop armour!
			if(CheckClass("RiflemanBase",AAPTR_DEFAULT,true)){
				if(countinv("HasDroppedGun")<1){
					A_GiveInventory("GreenArmour",random(1,HDCONST_GREENARMOUR));
					if(countinv("GreenArmour")>60){
						A_GiveInventory("HDDropArmour");
					}
				}
			}

				//everybody can have a pistol!
			if(countinv("HasDroppedGun")<1){
				A_DropItem("HDPistolDropped",0,16);
			}
			A_GiveInventory("HasDroppedGun");
			return state("");
		}fail
	}
}



//
//   Former Human
//

actor ZombieHideousTrooper:RandomSpawner replaces ZombieMan
{
	dropitem "ZombieSemiStormtrooper" 256 24
	dropitem "ZombieStormtrooper" 256 48
	dropitem "ZombieSMGStormtrooper" 256 12
	dropitem "EnemyHERP" 256 1
}
actor ZombieStormtrooper:ZombieMan //replaces ZombieMan
{
	Translation "58:66=128:136","214:223=141:148","176:191=24:47","16:34=68:79"
	Health 70
	gibhealth 100
	Speed 8
	Height 52
	radius 12
	+MISSILEEVENMORE
	+doharmspecies
	Decal "BulletScratch"
	AttackSound ""
	MeleeSound "weapons/smack"
	MeleeDamage 3
	DropItem ""
	PainChance 250
	Obituary "%o was gunned down by a zombieman."
	HitObituary "%o was beaten up by a zombieman."
	var int user_spread;
	var int user_turn;
	States
	{
	spawn:
		POSS A 0
	spawn2:
		"####" A 0 A_Look
		"####" A 0 A_Recoil(random(-1,1)*0.1)
		"####" E 1 A_SetTics(random(5,20))
		"####" A 0 A_Look
		"####" E 1 A_SetTics(random(5,20))
		"####" A 0 A_Look
		"####" E 1 A_SetTics(random(5,20))
		"####" A 0 A_Look
		"####" E 1 A_SetTics(random(5,10))
		"####" A 0 A_Look
		"####" A 0 A_Recoil(random(-1,1)*0.1)
		"####" E 1 A_SetTics(random(10,40))
		"####" B 0 A_Jump(132,2,5,5,5,5)
		"####" B 8 A_Recoil(random(-1,1)*0.2)
		loop
		"####" A 0 A_Recoil(random(-1,1)*0.4)
		"####" G 1 A_SetTics(random(30,80))
		"####" A 0 A_PlaySound("grunt/active")
		loop
		"####" A 0 A_CheckFlag("ambush","spawnstill")
		goto spawnwander
	spawnstill:
		"####" A 0 A_Look
		"####" A 0 A_Recoil(random(-1,1)*0.4)
		"####" CD 5 A_SetAngle(angle+random(-4,4))
		"####" A 0 A_Look
		"####" A 0 A_Jump(216,2)
		"####" A 0 A_PlaySound("grunt/active")
		"####" AB 5 A_SetAngle(angle+random(-4,4))
		"####" B 1 A_SetTics(random(10,40))
		TNT1 A 0 A_Jump(256,"spawn")
	spawnwander:
		"####" A 0 A_Look
		"####" CD 5 A_Wander
		"####" A 0 A_Look
		"####" A 0 A_Jump(216,2)
		"####" A 0 A_PlaySound("grunt/active")
		"####" AB 5 A_Wander
		"####" A 0 A_Jump(64,"spawn")
		loop
	missile:
		"####" A 0 A_JumpIfTargetInLOS(3,120)
		"####" CD 2 A_FaceTarget(90)
		"####" E 1 A_SetTics(random(4,10)) //when they just start to aim,not for followup shots!
		"####" A 0 A_JumpIfTargetInLOS("missile2")
		"####" A 0 A_CheckLOF("see",CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|CLOFF_JUMPOBJECT|CLOFF_MUSTBESOLID|CLOFF_SKIPENEMY,0,0,0,0,44,0)
	missile2:
		"####" A 0 A_JumpIfCloser(200,4)
		"####" A 0 A_JumpIfCloser(600,2)
		"####" A 0 A_SetUserVar("user_turn",10)
		goto turntoaim
		"####" A 0 A_SetUserVar("user_turn",30)
		goto turntoaim
		"####" A 0 A_SetUserVar("user_turn",50)
		goto turntoaim
	turntoaim:
		"####" E 2 A_FaceTarget(user_turn,user_turn)
		"####" A 0 A_JumpIfTargetInLOS(1)
		goto see
		"####" A 0 A_JumpIfTargetInLOS(1,10)
		loop
		"####" A 0 A_FaceTarget(user_turn,user_turn)
		"####" E 1 A_SetTics(random(0,100/clamp(user_turn,1,user_turn+1)+4))
		"####" A 0 A_SetUserVar("user_spread",random(user_turn*0.1,user_turn*0.2))
		"####" A 0 A_GiveInventory("ZM66FullAuto",2)
		"####" A 0 A_TakeInventory("ZM66FullAuto",random(1,2))
		"####" A 0 A_Jump(256,"shoot")
	shoot:
		"####" A 0 A_JumpIfInventory("ZM66Loaded",0,"ohforfuckssake")
		"####" F 0 A_PlayWeaponSound("weapons/rifle")
		"####" F 1 bright light("SHOT"){
			pitch+=frandom(-user_spread,user_spread);
			angle+=frandom(-user_spread,user_spread);
			A_GiveInventory("DecoBulleter426");
		}
		"####" A 0 A_GiveInventory("ZM66Loaded",1)
		"####" A 0 A_SetUserVar("user_spread",user_spread+1)
		"####" A 0 A_JumpIfInventory("ZM66Ratchet",0,2)
		"####" A 0 A_JumpIfInventory("ZM66FullAuto",0,"burst")
		"####" E 2 A_SetUserVar("user_spread",user_spread+1)
		"####" A 0 A_JumpIfInventory("ZM66Ratchet",1,2)
		"####" A 0 A_Jump(256,3)
		"####" A 0 A_TakeInventory("ZM66Ratchet",999)
		"####" A 0 A_Jump(256,"postshot")
		"####" A 0 A_Jump(120,"shoot")
	postshot:
		"####" A 0 A_JumpIfInventory("ZM66Loaded",0,"reload")
		"####" A 0 A_JumpIf(user_spread<1,2)
		"####" A 0 A_SetUserVar("user_spread",user_spread-1)
		"####" E 5 A_SetTics(random(2,6))
		"####" E 5
		"####" E 0 A_JumpIfTargetInLOS(1)
		goto coverfire  //even if not in los,occasionally keep shooting anyway
		"####" E 0 A_Jump(30,"see")  //even if in los,occasionally stop shooting anyway
		"####" E 0
		goto coverfire

	coverfire:
		"####" E 1 A_SetTics(random(2,12))
		"####" E 0 A_SetUserVar("user_spread",2)
		"####" A 0 A_Jump(90,"roam")
		"####" E 0 A_JumpIfTargetInLOS("missile2")
		"####" A 0 A_Jump(216,"shoot")
		loop

	burst:
		"####" A 0 A_GiveInventory("ZM66Ratchet",1)
		goto shoot

	ohforfuckssake:
		"####" E 8
	reload:
		"####" A 0 A_ChangeFlag("frightened",1)
		"####" A 0 A_PlaySound("weapons/rifleclick2")
		"####" AA 1 A_Chase("melee","")
		"####" A 0 A_PlaySound("weapons/rifleload")
		"####" A 0 A_JumpIfInventory("HDSMG",1,3)
		"####" A 0 A_SpawnProjectile("ZM66RifleEmptyMag",38,0,random(90,120))
		"####" A 0 A_Jump(256,2)
		"####" A 0 A_SpawnProjectile("HDSMGEmptyMag",38,0,random(90,120))
		"####" BBCCDD 1 A_Chase("melee","")
		"####" E 12 A_PlaySound("weapons/pocket")
		"####" E 8 A_PlaySound("weapons/rifleload")
		"####" E 2 A_PlaySound("weapons/rifleclick2")
		"####" A 0 A_TakeInventory("ZM66Loaded",50)
		"####" A 0 A_ChangeFlag("frightened",0)
		"####" CCBB 2 A_Wander

	see_evade:
		"####" A 0 A_Jump(64,"Missile")
		"####" A 0 A_Chase("missile","melee",CHF_DONTMOVE)
		"####" A 0 A_FaceTarget
		"####" A 0 A_SetAngle(angle+random(60,100)*(random(0,1)*2-1))
		"####" A 0 A_ChangeFlag("nodropoff",1)
	see_evade2:
		"####" A 0 A_SetAngle(angle+random(-30,30))
		"####" ABCD 4 A_Recoil(-3)
		"####" A 0 A_SetUserVar("user_spread",4)
		"####" A 0 A_Stop
		"####" A 0 A_JumpIf(velx*velx+vely*vely>25,2)
		"####" A 0 A_SetAngle(angle+random(-160,200))
		"####" A 0 A_ChangeFlag("nodropoff",0)
		"####" A 0 A_Jump(16,2)
		"####" A 0 A_TakeInventory("EvadeNow",999)
		"####" A 0 A_JumpIfTargetInLOS("See")
		goto roam

	BleedOut:
		"####" A 0 A_ChangeFlag("nopain",1)
		"####" AAAA 0 A_SpawnItemEx("BleedOut",0,0,32,1,0,-1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		"####" A 1
		"####" A 0 A_ChangeFlag("nopain",0)
		"####" A 0 A_Jump(256,"see2")
	See:
		"####" A 0 A_JumpIfHealthLower(20,"BleedOut")
	see2:
		"####" A 0 A_JumpIf(floorz<z-4,2)
		"####" A 0 A_JumpIfInventory("EvadeNow",1,"see_evade")
		"####" A 0 A_JumpIfInventory("ZM66Loaded",0,"reload")
		"####" AABBCCDD 2 A_Chase
		"####" A 0 A_SetUserVar("user_spread",2)
		"####" A 0 A_JumpIfTargetInLOS("See")
		"####" A 0 A_Jump(24,"roam")
		loop
	roam:
		"####" A 0 A_TakeInventory("EvadeNow",1)
		"####" A 0 A_Jump(60,"roam2")
	roam1:
		"####" E 3
		"####" A 0 A_ChangeFlag("missilemore",1)
		"####" A 0 A_SetUserVar("user_spread",1)
		"####" EEEE 1 A_Chase("melee","turnaround",CHF_DONTMOVE)
		"####" A 0 A_SetUserVar("user_spread",0)
		"####" EEEEEEEEEEEEE 1 A_Chase("melee","turnaround",CHF_DONTMOVE)
		"####" A 0 A_ChangeFlag("missilemore",0)
		"####" A 0 A_Jump(60,"roam")
	roam2:
		"####" A 0 A_Jump(8,"see")
		"####" A 0 A_Jump(200,2)
		"####" A 0 HealThing(random(12,32))
		"####" AA 3 A_Chase
		"####" BBCC 3 A_Wander
		"####" DD 3 A_Chase
		"####" A 0 A_Jump(140,"Roam")
		"####" A 0 A_AlertMonsters
		"####" A 0 A_JumpIfTargetInLOS("see")
		Loop
	turnaround:
		"####" A 0 A_FaceTarget(15,0)
		"####" E 2 A_JumpIfTargetInLOS("missile2",40)
		"####" A 0 A_SetUserVar("user_spread",3)
		"####" A 0 A_FaceTarget(15,0)
		"####" A 0 A_SetUserVar("user_spread",6)
		"####" E 2 A_JumpIfTargetInLOS("missile2",40)
		"####" A 0 A_SetUserVar("user_spread",4)
		"####" AABBCCDD 2 A_Chase
		goto see
	Melee:
		"####" C 8 A_FaceTarget
		"####" D 4
		"####" E 4 A_MeleeAttack
		"####" E 3 A_JumpIfCloser(64,2)
		"####" E 4 A_FaceTarget
		Goto Missile+1
		"####" A 4
		Goto See
	Pain:
		"####" G 2
		"####" G 3 A_Pain
		"####" A 0 A_Jump(200,2)
		"####" A 0 A_AlertMonsters
		"####" G 0 A_JumpIfCloser(100,"See")
		"####" G 0 A_ChangeFlag("frightened",1)
		"####" ABCD 2 A_Chase
		"####" D 0 A_ChangeFlag("frightened",0)
		Goto See
	Death.BleedOut:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" A 0 A_GivetoTarget("HDKillCount",1)
		"####" H 5 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		"####" A 0 A_Jump(96,2)
		"####" A 0 A_PlaySound("grunt/bleedout",CHAN_VOICE)
		"####" I 5
		goto DeathPostScream
	Death:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" H 5 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		"####" I 5 A_Scream
	DeathPostScream:
		"####" A 0 A_SpawnItemEx("tempshield2",0,0,0,velx,vely,velz,0,40)
		"####" I 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" J 5 A_GiveInventory("NoBlockWepDrop")
		"####" K 5
	Dead:
		"####" K 3 canraise A_JumpIf(abs(velz)<2,1)
		loop
		"####" L 5 canraise A_JumpIf(abs(velz)>=2,"Dead")
		loop
	XDeathBrewtleLulz:
		"####" A 0 A_ChangeFlag("shootable",0)
		"####" A 0 A_GiveInventory("IsGibbed",1)
		"####" M 5
		"####" M 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" N 5 A_XScream
		"####" N 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" O 5
		"####" O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" PQRS 5
		"####" T 5
		goto XDead
	XDeath:
		"####" A 0 A_ChangeFlag("shootable",0)
		"####" A 0 A_GiveInventory("IsGibbed",1)
		"####" A 0 A_GivetoTarget("HDKillCount",1)
		"####" M 5
		"####" M 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" N 5 A_XScream
		"####" N 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" O 5 A_GiveInventory("NoBlockWepDrop")
		"####" O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" PQRS 5
		"####" T 5
	XDead:
		"####" T 3 canraise A_JumpIf(abs(velz)<2,1)
		wait
		"####" U 5 canraise A_JumpIf(abs(velz)>=2,"XDead")
		wait
	Raise:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" A 0 A_JumpIfInventory("IsGibbed",1,"RaiseGibbed")
		"####" L 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" LK 6
		"####" JIH 4
		goto see
	RaiseGibbed:
		"####" A 0 A_TakeInventory("IsGibbed",999)
		"####" U 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" UT 8
		"####" SRQ 6
		"####" PONM 4
		"####" L 0 A_Die("Ungibbed")
	Death.Ungibbed:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" H 5 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		"####" I 5
		"####" A 0 A_SpawnItemEx("tempshield2",0,0,0,velx,vely,velz,0,40)
		"####" I 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" J 5 A_NoBlocking
		"####" K 5
		goto Dead
	}
}
actor ZombieSemiStormtrooper:ZombieStormTrooper //replaces Zombieman
{
	states{
	shoot:
		"####" A 0 A_JumpIfInventory("ZM66Loaded",0,"ohforfuckssake")
		"####" F 0 A_PlayWeaponSound("weapons/rifle")
		"####" F 1 bright light("SHOT"){
			pitch+=frandom(-user_spread,user_spread);
			A_GiveInventory("DecoBulleter426");
		}
		"####" A 0 A_GiveInventory("ZM66Loaded",1)
		"####" E 1 A_SetTics(random(2,6))
		goto postshot
	}
}

actor ZombieSMGStormtrooper:ZombieStormTrooper //replaces Zombieman
{
	MaxTargetRange 12000
	Translation "58:66=128:136","214:223=141:148","176:191=24:47","16:34=68:79","92:111=5:8"
	states{
	missile:
		"----" A 0{
			if((random(1,40)==1)&&(200<getdistance(true,AAPTR_TARGET)<900)){return state("frag");}
			return state("");
		}goto super::missile
	shoot:
		"####" A 0 A_JumpIfInventory("ZM66Loaded",30,"ohforfuckssake")
		"####" F 0 A_PlaySound("weapons/pistol",CHAN_WEAPON,0.7)
		"####" F 1 bright light("SHOT"){
			pitch+=frandom(-user_spread,user_spread);
			angle+=frandom(-user_spread,user_spread);
			A_GiveInventory("DecoBulleter9f");
		}
		"####" A 0 A_GiveInventory("ZM66Loaded",1)
		"####" E 2
		"####" A 0 A_SpawnProjectile("HDSpent9mm",40)
		"####" A 0 A_Jump(192,"shoot")
		goto postshot

	ohforfuckssake:
		"####" E 6
		"----" A 0{
			if((random(1,10)==1)&&(200<getdistance(true,AAPTR_TARGET)<900)){return state("frag");}
			return state("reload");
		}
	frag:
		"----" A 10 A_PlaySound("grunt/sight",CHAN_VOICE)
		"----" A 20{
			A_PlaySound("weapons/pocket",CHAN_WEAPON);
			A_FaceTarget(0,0);
			A_SetPitch(-pitch-random(10,50));
		}
		"----" A 10{
			A_SpawnItemEx("HDFragSpoon",cos(pitch)*4,-1,height-6-sin(pitch)*4,cos(pitch)*cos(angle)*4+velx,cos(pitch)*sin(angle)*4+vely,sin(-pitch)*4+velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			A_GiveInventory("GrenadeForce",random(10,30));
			A_GiveInventory("HDGrenadeToss");
		}
		"----" A 0 A_JumpIfInventory("ZM66Loaded",30,"reload")
		goto see
	}
}

actor DeadZombieHideousTrooper:RandomSpawner replaces DeadZombieMan
{
	dropitem "DeadZombieSemiStormtrooper" 256 2
	dropitem "DeadZombieStormtrooper" 256 4
	dropitem "DeadZombieSMGStormtrooper" 256 1
}
actor DeadZombieSMGStormtrooper:ZombieSMGStormtrooper
{
	states{
	spawn:
		POSS A 0 nodelay
		POSS A 0 A_JumpIfInventory("ShellChamber1",1,3)
		POSS A 0 A_GiveInventory("ShellChamber1",1)
		POSS A 0 A_Die("spawndead")
		POSS A 0
		goto super::spawn
	death.spawndead:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_GiveInventory("NoBlockWepDrop")
		POSS A 0 A_ChangeFlag("nodropoff",0)
		goto super::dead
	}
}
actor DeadZombieStormtrooper:ZombieStormtrooper
{
	states{
	spawn:
		POSS A 0 nodelay
		POSS A 0 A_JumpIfInventory("ShellChamber1",1,3)
		POSS A 0 A_GiveInventory("ShellChamber1",1)
		POSS A 0 A_Die("spawndead")
		POSS A 0
		goto super::spawn
	death.spawndead:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_GiveInventory("NoBlockWepDrop")
		POSS A 0 A_ChangeFlag("nodropoff",0)
		goto super::dead
	}
}
actor DeadZombieSemiStormtrooper:ZombieSemiStormtrooper
{
	states{
	spawn:
		POSS A 0 nodelay
		POSS A 0 A_JumpIfInventory("ShellChamber1",1,3)
		POSS A 0 A_GiveInventory("ShellChamber1",1)
		POSS A 0 A_Die("spawndead")
		POSS A 0
		goto super::spawn
	death.spawndead:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_GiveInventory("NoBlockWepDrop")
		POSS A 0 A_ChangeFlag("nodropoff",0)
		goto super::dead
	}
}



// ------------------------------------------------------------
//   Former Human Sergeant
// ------------------------------------------------------------

actor HideousShotgunGuy:RandomSpawner replaces ShotgunGuy
{
	dropitem "Jackboot" 256 5
	dropitem "JackAndJillBoot" 256 1
	dropitem "UndeadJackbootman" 256 2
}
actor Jackboot:ShotgunGuy //replaces ShotgunGuy
{
	Translation "58:66=128:136","214:223=141:148","176:191=24:47","16:34=68:79"
	dropitem ""
	Health 80
	gibhealth 100
	Speed 10
	Height 52
	radius 12
	Decal "BulletScratch"
	AttackSound ""
	MeleeSound "weapons/smack"
	MeleeDamage 4
	MaxTargetRange 4000
	+MISSILEMORE
	+FASTER
	+doharmspecies
	Obituary "%o was brutally pumped full of a shotgun sergeant's hot,manly lead."
	HitObituary "%o was whipped and dominated by fifty shades of shotgun guy."
	PainChance 200
	var int user_spread;
	var int user_turn;
	States
	{
	spawn:
		SPOS A 0
	spawn2:
		"####" A 0 A_Look
		"####" A 0 A_Recoil(random(-1,1)*0.1)
		"####" E 1 A_SetTics(random(1,10))
		"####" A 0 A_Look
		"####" E 1 A_SetTics(random(1,10))
		"####" A 0 A_Look
		"####" E 1 A_SetTics(random(1,10))
		"####" A 0 A_Look
		"####" E 1 A_SetTics(random(1,10))
		"####" A 0 A_Look
		"####" E 1 A_SetTics(random(1,10))
		"####" A 0 A_Look
		"####" A 0 A_Recoil(random(-1,1)*0.1)
		"####" E 1 A_SetTics(random(10,40))
		"####" B 0 A_Jump(132,2,5,5,5,5)
		"####" B 8 A_Recoil(random(-1,1)*0.2)
		loop
		"####" A 0 A_Recoil(random(-1,1)*0.4)
		"####" G 1 A_SetTics(random(30,80))
		"####" A 0 A_PlaySound("grunt/active")
		loop
		"####" A 0 A_CheckFlag("ambush","spawnstill")
		goto spawnwander
	spawnstill:
		"####" A 0 A_Look
		"####" A 0 A_Recoil(random(-1,1)*0.4)
		"####" CD 5 A_SetAngle(angle+random(-4,4))
		"####" A 0 A_Look
		"####" A 0 A_Jump(216,2)
		"####" A 0 A_PlaySound("grunt/active")
		"####" AB 5 A_SetAngle(angle+random(-4,4))
		"####" B 1 A_SetTics(random(10,40))
		goto spawn2
	spawnwander:
		"####" A 0 A_Look
		"####" CD 5 A_Wander
		"####" A 0 A_Look
		"####" A 0 A_Jump(216,2)
		"####" A 0 A_PlaySound("grunt/active")
		"####" AB 5 A_Wander
		"####" A 0 A_Jump(64,"spawn2")
		loop
	missile:
		"####" A 0 A_JumpIfTargetInLOS(3,120)
		"####" CD 2 A_FaceTarget(90)
		"####" E 1 A_SetTics(random(4,10)) //when they just start to aim,not for followup shots!
		"####" A 0 A_CheckLOF("see",CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|CLOFF_JUMPOBJECT|CLOFF_MUSTBESOLID|CLOFF_SKIPENEMY,0,0,0,0,44,0)
	missile2:
		"####" A 0 A_JumpIfCloser(300,4)
		"####" A 0 A_JumpIfCloser(800,2)
		"####" A 0 A_SetUserVar("user_turn",20)
		goto turntoaim
		"####" A 0 A_SetUserVar("user_turn",30)
		goto turntoaim
		"####" A 0 A_SetUserVar("user_turn",40)
		goto turntoaim
	turntoaim:
		"####" E 2 A_FaceTarget(user_turn,user_turn)
		"####" A 0 A_JumpIfTargetInLOS(1)
		goto see
		"####" A 0 A_JumpIfTargetInLOS(1,10)
		loop
		"####" A 0 A_FaceTarget(user_turn,user_turn)
		"####" E 1 A_SetTics(random(0,100/user_turn+4))
		"####" A 0 A_SetUserVar("user_spread",random(user_turn/14,user_turn/5))
		"####" A 0 A_Jump(256,"shoot")
	shoot:
		"####" E 0 A_JumpIf(velx*velx+vely*vely>64,"see")
		"####" E 0 A_JumpIfInventory("HunterSpentShell",1,"Chamber")
		"####" A 0 A_JumpIfInventory("ShotgunTube",0,"ohforfuckssake")
		"####" A 0 A_PlayWeaponSound("weapons/hunter")

		"####" F 1 bright light("SHOT"){
			angle+=frandom(-user_spread,user_spread);
			pitch+=frandom(-user_spread,user_spread);
			A_GiveInventory("DecoBulleter00");
		}
//		"####" F 1 bright light("SHOT") A_SpawnProjectile("ShotgunBlast",44,0,frandom(-user_spread,user_spread),CMF_AIMDIRECTION|CMF_SAVEPITCH,pitch+frandom(-user_spread,user_spread))

		"####" E 0 A_GiveInventory("ShotgunTube",1)
		"####" E 2 A_GiveInventory("HunterSpentShell",1)
		"####" A 0 A_JumpIfInventory("EvadeNow",1,"see_evade")
		"####" E 1 A_SetTics(random(4,12))
		Goto See
	Chamber:
		"####" E 0 A_SpawnItemEx("HDSpentShell",cos(pitch)*8,0,height-7-sin(pitch)*8,velx+cos(pitch)*cos(angle-random(86,90))*6,vely+cos(pitch)*sin(angle-random(86,90))*6,velz+sin(pitch)*random(5,7),0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		"####" E 0 A_TakeInventory("HunterSpentShell",999)
		"####" E 0 A_Jump(128,2)
		"####" E 8 A_PlaySoundEx("weapons/shotgr","soundslot5")
		goto see
		"####" E 1 A_SpidRefire
		goto turntoaim

	ohforfuckssake:
		"####" E 6
	reload:
		"####" A 0 A_ChangeFlag("frightened",1)
		"####" AA 1 A_Chase("melee","")
		"####" A 0 A_PlaySound("weapons/huntopen")
		"####" BBCC 1 A_Chase("melee","")
		"####" DDAA 1 A_Chase("melee","")
		"####" A 0 A_TakeInventory("ShotgunTube",1)
		"####" A 0 A_PlaySound("weapons/huntreload")
		"####" A 0 A_JumpIfInventory("ShotgunTube",1,"reload2")
		goto reloadend
	reload2:
		"####" BB 3 A_Chase("melee","")
		"####" A 0 A_TakeInventory("ShotgunTube",1)
		"####" A 0 A_PlaySound("weapons/huntreload")
		"####" A 0 A_JumpIfInventory("ShotgunTube",1,1)
		goto reloadend
		"####" CC 3 A_Chase("melee","")
		"####" A 0 A_TakeInventory("ShotgunTube",1)
		"####" A 0 A_PlaySound("weapons/huntreload")
		"####" A 0 A_JumpIfInventory("ShotgunTube",1,1)
		goto reloadend
		"####" DD 3 A_Chase("melee","")
		"####" A 0 A_TakeInventory("ShotgunTube",1)
		"####" A 0 A_PlaySound("weapons/huntreload")
		"####" A 0 A_JumpIfInventory("ShotgunTube",1,1)
		goto reloadend
		"####" A 0 A_PlaySound("weapons/pocket")
		"####" AABBCCDDAA 2 A_Chase("melee","")
		loop
	reloadend:
		"####" A 0 A_ChangeFlag("frightened",0)
		"####" BBCCDD 2 A_Chase("melee","")
		"####" A 0 A_PlaySound("weapons/huntopen")
		"####" EEE 1 A_Chase("melee","missile",CHF_DONTMOVE)
		goto see

	see_evade:
		"####" A 0 A_Chase("missile","melee",CHF_DONTMOVE)
		"####" A 0 A_FaceTarget
		"####" A 0 A_SetAngle(angle+random(40,120)*(random(0,1)*2-1))
		"####" A 0 A_ChangeFlag("nodropoff",1)
	see_evade2:
		"####" A 0 A_SetAngle(angle+random(-30,30))
		"####" ABCD 3 A_Recoil(-3)
		"####" A 0 A_Stop
		"####" A 0 A_JumpIf(velx*velx+vely*vely>25,2)
		"####" A 0 A_SetAngle(angle+random(-160,200))
		"####" A 0 A_ChangeFlag("nodropoff",0)
		"####" A 0 A_TakeInventory("EvadeNow",1)
		"####" A 0 A_JumpIfTargetInLOS("Missile")
		goto roam
	See:
		"####" E 0 A_JumpIfInventory("HunterSpentShell",1,"Chamber")
		"####" A 0 A_JumpIfHealthLower(25,"BleedOut")
	see2:
		"####" A 0 A_JumpIf(floorz<z-4,2)
		"####" A 0 A_JumpIfInventory("EvadeNow",1,"see_evade")
		"####" AABBCCDD 2 A_Chase
		"####" A 0 A_JumpIfTargetInLOS("See")
		"####" A 0 A_Jump(16,"roam")
		goto see
	roam:
		"####" A 0 A_TakeInventory("EvadeNow",1)
		"####" A 0 A_Jump(60,"roam2")
	roam1:
		"####" E 4
		"####" A 0 A_ChangeFlag("missileevenmore",1)
		"####" EEEEEEEEEEEEEEEE 1 A_Chase("melee","turnaround",CHF_DONTMOVE)
		"####" A 0 A_ChangeFlag("missileevenmore",0)
		"####" A 0 A_Jump(60,"roam")
	roam2:
		"####" A 0 A_Jump(8,"see")
		"####" A 0 A_Jump(200,2)
		"####" A 0 HealThing(random(12,32))
		"####" AA 3 A_Chase
		"####" A 0 A_Chase("melee","turnaround",CHF_DONTMOVE)
		"####" BBCC 3 A_Wander
		"####" A 0 A_Chase("melee","turnaround",CHF_DONTMOVE)
		"####" DD 3 A_Chase
		"####" A 0 A_Jump(140,"Roam")
		"####" A 0 A_AlertMonsters
		"####" A 0 A_JumpIfTargetInLOS("see")
		loop
	turnaround:
		"####" A 0 A_FaceTarget(15,0)
		"####" E 2 A_JumpIfTargetInLOS("missile2",40)
		"####" A 0 A_FaceTarget(15,0)
		"####" E 2 A_JumpIfTargetInLOS("missile2",40)
		"####" AABBCCDD 2 A_Chase
		goto see
	BleedOut:
		"####" A 0 A_ChangeFlag("nopain",1)
		"####" AAAA 0 A_SpawnItemEx("BleedOut",0,0,32,1,0,-1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		"####" A 1
		"####" A 0 A_ChangeFlag("nopain",0)
		goto See2
	Melee:
		"####" D 6 A_FaceTarget
		"####" D 4
		"####" E 4 A_MeleeAttack
		"####" E 3 A_JumpIfCloser(56,2)
		"####" E 3 A_FaceTarget
		Goto Missile+1
		"####" A 3
		Goto See
	Pain:
		"####" G 3 A_Jump(12,1)
		"####" G 3 A_Pain
		"####" A 0 A_ChangeFlag("frightened",0)
		"####" A 0 A_Jump(200,2)
		"####" G 0 A_AlertMonsters
		"####" G 0 A_Jump(182,"See")
		goto see_evade
	Death.BleedOut:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" A 0 A_GivetoTarget("HDKillCount",1)
		"####" A 0 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		"####" H 5 A_ChangeFlag("NODROPOFF",0)
		"####" A 0 A_Jump(196,2)
		"####" A 0 A_Scream
		"####" I 5
		goto DeathPostScream
	Death:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" A 0 A_GivetoTarget("HDKillCount",1)
		"####" A 0 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		"####" H 5 A_ChangeFlag("NODROPOFF",0)
		"####" I 5 A_Scream
		"####" A 0 A_Jump(256,"DeathPostScream")
	DeathPostScream:
		"####" A 0 A_SpawnItemEx("tempshield2",0,0,0,velx,vely,velz,0,40)
		"####" J 5 A_GiveInventory("NoBlockWepDrop")
		"####" K 5
	Dead:
		"####" K 3 canraise A_JumpIf(abs(velz)<2,1)
		loop
		"####" L 5 canraise A_JumpIf(abs(velz)>=2,"Dead")
		loop
	XDeathBrewtleLulz:
		"####" A 0 A_ChangeFlag("shootable",0)
		"####" A 0 A_GiveInventory("IsGibbed",1)
		"####" M 5
		"####" M 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" N 5 A_XScream
		"####" N 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" O 5
		"####" O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" PQRS 5
		"####" T 5
		goto XDead
	XDeath:
		"####" A 0 A_ChangeFlag("shootable",0)
		"####" A 0 A_GiveInventory("IsGibbed",1)
		"####" A 0 A_GivetoTarget("HDKillCount",1)
		"####" M 5 A_ChangeFlag("NODROPOFF",0)
		"####" M 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" N 5 A_XScream
		"####" N 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" O 5 A_GiveInventory("NoBlockWepDrop")
		"####" O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" PQRS 5
		"####" T 5
	XDead:
		"####" T 3 canraise A_JumpIf(abs(velz)<2,1)
		wait
		"####" U 5 canraise A_JumpIf(abs(velz)>=2,"XDead")
		wait
	Raise:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" A 0 A_JumpIfInventory("IsGibbed",1,"RaiseGibbed")
		"####" L 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" LK 6
		"####" JIH 4
		goto see
	RaiseGibbed:
		"####" A 0 A_TakeInventory("IsGibbed",999)
		"####" U 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" UT 8
		"####" SRQ 6
		"####" PONM 4
		"####" H 4 A_Die("Ungibbed")
	Death.Ungibbed:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" H 5 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		"####" I 5
		"####" A 0 A_SpawnItemEx("tempshield2",0,0,0,velx,vely,velz,0,40)
		"####" I 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" J 5 A_NoBlocking
		"####" K 5
		goto Dead
	}
}
actor JackAndJillBoot:Jackboot
{
	states{
	shoot:
		"####" E 0 A_JumpIf(velx*velx+vely*vely>64,"see")
		"####" A 0 A_JumpIfInventory("ZedChamber",0,"reload")

		"####" E 0 A_SetAngle(angle+frandom(-user_spread,user_spread))
		"####" E 0 A_SetPitch(pitch+frandom(-user_spread,user_spread))

		"####" E 0 A_JumpIfInventory("ZedChamber",1,5)
		"####" E 2 A_Jump(212,4)

		"####" E 0 A_GiveInventory("ZedChamber",1)
		"####" A 0 A_PlaySound("weapons/slayersingle",5)
		"####" F 1 bright light("SHOT"){
			angle+=frandom(-user_spread,user_spread);
			pitch+=frandom(-user_spread,user_spread);
			A_GiveInventory("DecoBulleter00f");
		}
//		"####" F 1 bright light("SHOT") A_SpawnProjectile("SuperShotgunBlast",44,-1,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,pitch)

		"####" E 0 A_GiveInventory("ZedChamber",1)
		"####" A 0 A_PlaySound("weapons/slayersingle",6)
		"####" F 1 bright light("SHOT"){
			angle+=frandom(-user_spread,user_spread);
			pitch+=frandom(-user_spread,user_spread);
			A_GiveInventory("DecoBulleter00f");
		}
//		"####" F 1 bright light("SHOT") A_SpawnProjectile("SuperShotgunBlast",44,0,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,pitch)

		"####" E 0 A_JumpIfInventory("ZedChamber",1,2)
		"####" E 2 A_Jump(32,"missile")
		"####" A 0 A_JumpIfInventory("EvadeNow",1,"see_evade")
		"####" E 1 A_SetTics(random(4,12))
		Goto See
	see:
		"####" A 0 A_JumpIfInventory("ZedChamber",0,"reload")
		goto super::see
	ohforfuckssake:
	reload:
		"####" E 4
		"####" E 0 A_PlaySound("weapons/sshoto")
		"####" EE 0 A_SpawnItemEx("HDSpentShell",cos(pitch)*5,-1,height-7-sin(pitch)*5,cos(pitch-45)*cos(angle)*random(1,4)+velx,cos(pitch-45)*sin(angle)*random(1,4)+vely,-sin(pitch-45)*random(1,4)+velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)

		"####" EDDAAB 4 A_Wander
		"####" E 0 A_PlaySound("weapons/sshotl")
		"####" CCD 4
		"####" E 0 A_PlaySound("weapons/sshotc")
		"####" E 8 A_TakeInventory("ZedChamber",2)
		goto see
	}
}
actor ZedChamber:InventoryFlag
{
	inventory.maxamount 2
}

actor UndeadJackbootman:Jackboot
{
	translation "112:113=30:33","114:120=101:111","121:127=5:8","48:71=128:151"
	meleedamage 5
	+faster +missileevenmore
	Obituary "%o was hunted down by the Tyrant's jack-booted thugs."
	HitObituary "%o imagines a boot stamping on a human face,for ever and ever."
	speed 10
	health 90
	mass 100
	states{
	spawn:
		PLAY A 0
		goto spawn2
	shoot:
		"----" A 0 A_JumpIfInventory("ZM66BrokenRound",1,"ohforfuckssake")
		"----" A 0 A_JumpIfInventory("ZM66Loaded",0,"ohforfuckssake")
		"----" A 0{
			A_SetAngle(angle+frandom(-user_spread,user_spread));
			A_SetPitch(pitch+frandom(-user_spread,user_spread));
		}
		"####" FFF 1 bright light("SHOT"){
			if(countinv("ZM66Loaded")>49){
				A_SetTics(0);
				return state("");
			}
			A_PlayWeaponSound("weapons/rifle");
			pitch+=frandom(-0.05,0.15);
			A_GiveInventory("DecoBulleter426");
			A_GiveInventory("ZM66Loaded",1);
			return state("");
		}
	postshot:
		"####" A 0{
			A_TakeInventory("ZM66Ratchet");
			if(random(1,55)==1){A_GiveInventory("ZM66BrokenRound");}
		}
		"####" A 0 A_JumpIfInventory("ZM66Loaded",0,"reload")
		"####" A 0 A_SetUserVar("user_spread",user_spread+1)
		"####" E 5 A_SetTics(random(2,6))
		"####" E 5
		"####" E 0 A_JumpIfTargetInLOS(1)
		goto coverfire  //even if not in los,occasionally keep shooting anyway
		"####" E 0 A_Jump(30,"see")  //even if in los,occasionally stop shooting anyway
		"####" E 0
		goto coverfire
	coverfire:
		"####" E 1 A_SetTics(random(0,12))
		"####" E 0 A_SetUserVar("user_spread",2)
		"####" A 0 A_Jump(70,"see")
		"####" E 0 A_JumpIfTargetInLOS("missile2")
		"####" A 0 A_Jump(216,"shoot")
		loop
	burst:
		"####" A 0 A_GiveInventory("ZM66Ratchet",1)
		goto shoot
	ohforfuckssake:
		"####" E 8
	reload:
		"####" A 0 A_ChangeFlag("frightened",1)
		"####" A 0 A_PlaySound("weapons/rifleclick2")
		"####" AA 1 A_Chase("melee","")
		"####" A 0{
			if(countinv("ZM66NoMag")<1){
				A_PlaySound("weapons/rifleload");
				A_SpawnProjectile("ZM66RifleEmptyMag",38,0,random(90,120));
				A_GiveInventory("ZM66NoMag");
			}
		}
		"####" BBCCDD 1 A_Chase("melee","")
		"####" E 8{
			if(countinv("ZM66BrokenRound")<1){
				A_SetTics(0);
				return state("");
			}
			else if(random(1,5)==1){
				A_TakeInventory("ZM66BrokenRound",1);
				A_PlayWeaponSound("weapons/rifleclick");
				A_SpawnItemEx("TinyWallChunk",0,0,20,random(4,7),random(-2,2),random(-2,1),0,SXF_NOCHECKPOSITION);
				A_SpawnItemEx("TinyWallChunk",0,0,20,random(4,7),random(-2,2),random(-2,1),0,SXF_NOCHECKPOSITION);
				A_SpawnItemEx("TinyWallChunk",0,0,20,random(4,7),random(-2,2),random(-2,1),0,SXF_NOCHECKPOSITION);
				A_SpawnItemEx("TinyWallChunk",0,0,20,random(4,7),random(-2,2),random(-2,1),0,SXF_NOCHECKPOSITION);
				return state("");
			}
			return state("ohforfuckssake");
		}
		"####" E 12 A_PlaySound("weapons/pocket")
		"####" E 8 A_PlaySound("weapons/rifleload")
		"####" E 2{
			A_PlaySound("weapons/rifleclick2");
			A_TakeInventory("ZM66Loaded",50);
			A_TakeInventory("ZM66NoMag");
			A_ChangeFlag("frightened",0);
		}
		"####" CCBB 2 A_Wander
		goto see
	death.bleedOut:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" A 0 A_GivetoTarget("HDKillCount",1)
		"####" H 5 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		"####" I 5
	deathpostscream:
		"####" A 0 A_GiveInventory("NoBlockWepDrop")
		goto super::deathpostscream
	see_evade:
		"####" A 0 A_Chase("missile","melee",CHF_DONTMOVE)
		"####" A 0 A_FaceTarget
		"####" A 0 A_SetAngle(angle+random(40,120)*(random(0,1)*2-1))
		"####" A 0 A_ChangeFlag("nodropoff",1)
	see_evade2:
		"####" A 0 A_SetAngle(angle+random(-30,30))
		"####" ABCD 3 A_Recoil(-3)
		"####" A 0 A_Stop
		"####" A 0 A_JumpIf(velx*velx+vely*vely>25,2)
		"####" A 0 A_SetAngle(angle+random(-160,200))
		"####" A 0 A_ChangeFlag("nodropoff",0)
		"####" A 0 A_TakeInventory("EvadeNow",1)
		"####" A 0 A_JumpIfTargetInLOS("Missile")
		goto roam
	pain:
		"####" G 3 A_Jump(12,1)
		"####" G 3 A_Pain
		"####" A 0 A_ChangeFlag("frightened",0)
		"####" A 0 A_Jump(200,2)
		"####" G 0 A_AlertMonsters
		"####" G 0 A_Jump(182,"see")
		goto see_evade
	death.raisebotch:
	xdeath:
		"####" A 0 A_ChangeFlag("shootable",0)
		"####" O 0 A_GiveInventory("IsGibbed",1)
		"####" O 5 A_ChangeFlag("shootable",0)
		"####" O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" P 5 A_XScream
		"####" P 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" Q 5 A_GiveInventory("NoBlockWepDrop")
		"####" Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		"####" RSTUV 5
		goto XDead
	XDeathBrewtleLulz:
		"####" A 0 A_ChangeFlag("shootable",0)
		"####" A 0 A_GiveInventory("IsGibbed",1)
		"####" P 5
		"####" P 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" Q 5 A_XScream
		"####" Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" R 5
		"####" R 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		"####" STUV 5
		goto XDead
	XDead:
		"####" V 3 canraise A_JumpIf(abs(velz)<2,1)
		wait
		"####" W 5 canraise A_JumpIf(abs(velz)>=2,"XDead")
		wait
	raise:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" A 0 A_JumpIfInventory("Shell",1,"raisezombie")
		"####" A 0 A_JumpIfInventory("IsGibbed",1,"raisegibbed")
		"####" L 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" LK 6
		"####" JIH 4
		"####" A 0 A_Jump(32,1)
		goto see
		"####" A 0 A_Die("raisebotch")
		stop
	raisegibbed:
		"####" A 0 A_TakeInventory("IsGibbed",999)
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		"####" A 0 A_SetSolid
		"####" A 0 A_Jump(172,"raisegibbedzombie")
		"####" WW 8 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" VUT 8
		"####" SRQ 6
		"####" PO 4
		"####" A 0 A_Die("bleedout")
		"####" A 0 A_UnsetSolid
		goto see
	raisegibbedzombie:
		POSS U 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" UT 8
		"####" SRQ 6
		"####" PONMH 4
		"----" A 0 A_GiveInventory("Shell",1)
		"----" A 0 A_Die("bleedout")
		stop
	raisezombie:
		POSS L 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" LK 6
		"####" JIH 4
		"####" A 0 A_UnsetSolid
		"####" H 0 A_SpawnItemEx("ZombieStormtrooper",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_ISTRACER)
		"----" A 0 A_GiveInventory("HasDroppedGun",1,AAPTR_TRACER)
		stop

	}
}

actor DeadHideousShotgunGuy:RandomSpawner replaces DeadShotgunGuy
{
	dropitem "DeadJackboot" 256 5
	dropitem "DeadJackAndJillBoot" 256 1
	dropitem "DeadUndeadJackbootman" 256 2
}
actor DeadJackboot:Jackboot
{
	states{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_JumpIfInventory("ShellChamber1",1,3)
		TNT1 A 0 A_GiveInventory("ShellChamber1",1)
		TNT1 A 0 A_Die("spawndead")
		SPOS A 0
		goto super::spawn
	death.spawndead:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_GiveInventory("NoBlockWepDrop")
		SPOS A 0 A_ChangeFlag("nodropoff",0)
		goto super::dead
	}
}
actor DeadJackandjillboot:Jackandjillboot
{
	states{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_JumpIfInventory("ShellChamber1",1,3)
		TNT1 A 0 A_GiveInventory("ShellChamber1",1)
		TNT1 A 0 A_Die("spawndead")
		SPOS A 0
		goto super::spawn
	death.spawndead:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_NoBlocking
		SPOS A 0 A_ChangeFlag("nodropoff",0)
		goto super::dead
	}
}
actor DeadUndeadJackbootman:UndeadJackbootman
{
	states{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_JumpIfInventory("ShellChamber1",1,3)
		TNT1 A 0 A_GiveInventory("ShellChamber1",1)
		TNT1 A 0 A_Die("spawndead")
		PLAY A 0
		goto super::spawn
	death.spawndead:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_NoBlocking
		PLAY A 0 A_ChangeFlag("nodropoff",0)
		goto super::dead
	}
}



// ------------------------------------------------------------
//   Former Human Commando
// ------------------------------------------------------------

actor HDChainReplacer:RandomSpawner replaces ChaingunGuy
{
	dropitem "DragonGuard" 256 6
	dropitem "UndeadRifleman" 256 2
	dropitem "EnemyHERP" 256 1
	dropitem "EnemyDERP" 256 1
}
actor chaingunbox:inventoryflag{inventory.maxamount 253}
actor dgburst:inventoryflag{inventory.maxamount 50}
actor DragonGuard:ChaingunGuy
{
	dropitem ""
	Translation "60:69=128:135","70:79=144:151","176:191=32:47"
	Health 75
	gibhealth 100
	Height 54
	Radius 16
	Decal "BulletScratch"
	AttackSound "weapons/rifle"
	Obituary "%o met the budda-budda-budda on the street,and it killed %h."
	+missileevenmore
	+doharmspecies
	States
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_Look
		TNT1 A 0 A_Recoil(random(-1,1)*0.1)
		CPOS B 1 A_SetTics(random(10,40))
		TNT1 A 0 A_Look
		CPOS B 1 A_SetTics(random(10,40))
		TNT1 A 0 A_Look
		TNT1 A 0 A_Recoil(random(-1,1)*0.1)
		CPOS B 1 A_SetTics(random(10,40))
		CPOS A 0 A_Jump(132,2,5,5,5,5)
		CPOS A 8 A_Recoil(random(-1,1)*0.2)
		loop
		TNT1 A 0 A_Recoil(random(-1,1)*0.4)
		CPOS G 1 A_SetTics(random(30,80))
		TNT1 A 0 A_PlaySound("grunt/active")
		loop
		TNT1 A 0 A_CheckFlag("ambush","spawnstill")
		goto spawnwander
	spawnstill:
		TNT1 A 0 A_Look
		TNT1 A 0 A_Recoil(random(-1,1)*0.4)
		CPOS CD 5 A_SetAngle(angle+random(-4,4))
		TNT1 A 0 A_Look
		TNT1 A 0 A_Jump(216,2)
		TNT1 A 0 A_PlaySound("grunt/active")
		CPOS AB 5 A_SetAngle(angle+random(-4,4))
		CPOS B 1 A_SetTics(random(10,40))
		goto spawn
	spawnwander:
		TNT1 A 0 A_Look
		CPOS CD 5 A_Wander
		TNT1 A 0 A_Look
		TNT1 A 0 A_Jump(192,2)
		TNT1 A 0 A_PlaySound("grunt/active")
		CPOS AB 5 A_Wander
		TNT1 A 0 A_Jump(196,"spawn")
		loop
	Reload:
		TNT1 A 0 A_ChangeFlag("frightened",1)
		CPOS AABBCCDD 3 A_Chase("","")
		CPOS E 9 A_PlaySound("weapons/pocket",CHAN_WEAPON)
		CPOS E 7 A_PlaySound("weapons/rifleload")
		CPOS E 0 A_PlaySound("weapons/rifleclick2")
		TNT1 A 0 A_TakeInventory("ChaingunBox",50)
		CPOS E 0 A_JumpIfInventory("ChaingunBox",1,1)
		goto reloadend
		CPOS E 9 A_PlaySound("weapons/pocket")
		CPOS E 7 A_PlaySound("weapons/rifleload")
		CPOS E 0 A_PlaySound("weapons/rifleclick2")
		TNT1 A 0 A_TakeInventory("ChaingunBox",50)
		CPOS E 0 A_JumpIfInventory("ChaingunBox",1,1)
		goto reloadend
		CPOS E 9 A_PlaySound("weapons/pocket")
		CPOS E 7 A_PlaySound("weapons/rifleload")
		CPOS E 0 A_PlaySound("weapons/rifleclick2")
		TNT1 A 0 A_TakeInventory("ChaingunBox",50)
		CPOS E 0 A_JumpIfInventory("ChaingunBox",1,1)
		goto reloadend
		CPOS E 9 A_PlaySound("weapons/pocket")
		CPOS E 7 A_PlaySound("weapons/rifleload")
		CPOS E 0 A_PlaySound("weapons/rifleclick2")
		TNT1 A 0 A_TakeInventory("ChaingunBox",50)
		CPOS E 0 A_JumpIfInventory("ChaingunBox",1,1)
		goto reloadend
		CPOS E 9 A_PlaySound("weapons/pocket")
		CPOS E 7 A_PlaySound("weapons/rifleload")
		CPOS E 0 A_PlaySound("weapons/rifleclick2")
		TNT1 A 0 A_TakeInventory("ChaingunBox",50)
	reloadend:
		TNT1 A 0 A_ChangeFlag("frightened",0)
	see:
		CPOS AABBCCDD 4 A_Chase
		TNT1 A 0 A_JumpIfTargetInLOS(3)
		TNT1 A 0 A_Jump(210,2)
		TNT1 A 0 A_JumpIfInventory("ChaingunBox",200,"Reload")
		TNT1 A 0 HealThing(random(2,6))
		TNT1 A 0 A_ChangeFlag("frightened",0)
		loop
	see2:
		CPOS E 8 A_FaceTarget(8,0)
		CPOS E 0 A_CheckLOF(1,CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|CLOFF_JUMPOBJECT|CLOFF_SKIPENEMY|CLOFF_MUSTBESOLID,0,0,0,0,32,9)
		goto ready
		CPOS E 0 A_Jump(64,"WindDown")
		loop
	Pain:
		CPOS G 3
		CPOS G 3 A_Pain
		TNT1 A 0 A_AlertMonsters
		CPOS ABCD 4 A_Wander
		goto see
	Missile:
		CPOS E 0 A_JumpIfInventory("ChaingunBox",0,"Reload")
		CPOS E 0 A_ChangeFlag("frightened",0)
		CPOS E 0 A_CheckLOF("see",CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|CLOFF_JUMPOBJECT|CLOFF_SKIPENEMY|CLOFF_MUSTBESOLID,0,0,0,0,32,9)
	ready:
		CPOS ABCD 4{
			if(A_JumpIfTargetInLOS("aim",6)){return state("aim");}
			A_FaceTarget(16,16);
			return state("");
		}loop
	aim:
		CPOS E 0 A_JumpIfTargetInLOS(2)
		CPOS E 0 A_Jump(256,"see","waitforlos","waitforlos","waitforlos","waitforlos")
		CPOS E 0 A_JumpIfTargetInLOS(1,20)
		goto ready
		CPOS EE 3 A_FaceTarget(4,4)
		CPOS E 2 A_JumpIfTargetInLOS(1,12)
		loop

	fire:
		CPOS E 3{
			A_TakeInventory("ChaingunFast");if(random(1,5)<2){A_GiveInventory("ChaingunFast");}
			A_SetPitch(pitch+frandom(-2,2));A_SetAngle(angle+frandom(-2,2));
			A_TakeInventory("dgburst");A_GiveInventory("dgburst",random(3,12)+random(1,4)*countinv("chaingunfast")*6);
			if(countinv("ChaingunBox")>2){return state("shoot");}
			A_PlaySound("weapons/rifleclick2",1);
			return state("postshot");
		}
	shoot:
		CPOS F 1 bright light("SHOT"){
			A_SpawnItemEx("ZedNoisemaker",0,0,32,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
			A_GiveInventory("ChaingunBox",1);
			A_TakeInventory("dgburst",1);
			pitch+=frandom(-0.5,0.5);
			angle+=frandom(-0.5,0.5);
			A_GiveInventory("DecoBulleter426");
		}
		CPOS E 2 A_JumpIfInventory("ChaingunFast",1,1)
		CPOS E 0{
			if(
				(countinv("ChaingunBox")==53)||
				(countinv("ChaingunBox")==103)||
				(countinv("ChaingunBox")==153)||
				(countinv("ChaingunBox")==203)||
				(countinv("ChaingunBox")==253)
			){A_SpawnItemEx("ZM66RifleEmptyMag",0,4,32,3,random(-3,2),random(0,-2),0,SXF_NOCHECKPOSITION);}
			if(countinv("ChaingunBox")>=253){return state("WindDown");}
			return state("");
		}
	postshot:
		CPOS E 0{
			if(countinv("dgburst")>0){
				if((A_JumpIfTargetInLOS("null"))&&(!A_JumpIfTargetInLOS("null",2))){A_FaceTarget(1,1);A_SetPitch(pitch+frandom(-1,1));A_SetAngle(angle+frandom(-1,1));}
				return state("shoot");
			}
			return state("waitforlos");
		}
	waitforlos:
		CPOS EE 4 A_Jump(2,"fire")
		CPOS E 0{
			if(A_JumpIfHealthLower(1,"null",AAPTR_TARGET)){return state("winddown");}
			if(A_JumpIfTargetInLOS("null")){A_FaceTarget(1,1);}
			if(A_CheckLOF("null",CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|CLOFF_JUMPOBJECT|CLOFF_SKIPENEMY|CLOFF_MUSTBESOLID,0,0,0,0,32,9)){return state("waitforlos");}
			if(A_JumpIfTargetInLOS("null",1)){return state("fire");}
			else if(A_JumpIfTargetInLOS("null",90)){return state("aim");}
			else if(random(1,50)<2){return state("winddown");}
			else if(A_JumpIfTargetInLOS("null")){return state("ready");}
			return state("waitforlos");
		}
	WindDown:
		TNT1 A 0 A_TakeInventory("ChaingunOn")
		CPOS E 8
		CPOS E 0 A_JumpIfInventory("ChaingunBox",0,"Reload")
		goto See
	Death:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_GivetoTarget("HDKillCount",1)
		CPOS H 5 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		CPOS H 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS I 5 A_Scream
		TNT1 A 0 A_SpawnItemEx("tempshield2",0,0,0,velx,vely,velz,0,40)
		CPOS I 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)

		TNT1 A 0 A_JumpIfInventory("ChaingunOn",1,2)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_TakeInventory("ChaingunOn")
		TNT1 A 0 A_PlaySoundEx("weapons/vulcandown","soundslot5")

		CPOS J 5 A_GiveInventory("NoBlockWepDrop")
		CPOS KL 5
		CPOS M 5
	Dead:
		CPOS M 3 canraise A_JumpIf(abs(velz)<2,1)
		loop
		CPOS N 5 canraise A_JumpIf(abs(velz)>=2,"Dead")
		loop
	XDeathBrewtleLulz:
		"####" A 0 A_ChangeFlag("shootable",0)
		TNT1 A 0 A_GiveInventory("IsGibbed",1)
		CPOS LKO 3
		CPOS O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS P 3 A_XScream
		CPOS P 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS R 2 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)

		TNT1 A 0 A_JumpIfInventory("ChaingunOn",1,2)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_TakeInventory("ChaingunOn")
		TNT1 A 0 A_PlaySoundEx("weapons/vulcandown","soundslot5")

		CPOS Q 5
		CPOS Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS R 5 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS R 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS S 5 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		goto XDead
	XDeath:
		"####" A 0 A_ChangeFlag("shootable",0)
		TNT1 A 0 A_GiveInventory("IsGibbed",1)
		TNT1 A 0 A_GivetoTarget("HDKillCount",1)
		CPOS O 5
		CPOS O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS P 3 A_XScream
		CPOS P 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS R 2 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)

		TNT1 A 0 A_JumpIfInventory("ChaingunOn",1,2)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_TakeInventory("ChaingunOn",999)
		TNT1 A 0 A_PlaySoundEx("weapons/vulcandown","soundslot5")

		CPOS Q 5 A_GiveInventory("NoBlockWepDrop")
		CPOS Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS R 5 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS R 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS S 5 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
	XDead:
		CPOS S 3 canraise A_JumpIf(abs(velz)<2,1)
		wait
		CPOS T 5 canraise A_JumpIf(abs(velz)>=2,"XDead")
		wait
	Raise:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_JumpIfInventory("IsGibbed",1,"RaiseGibbed")
		CPOS N 2 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		CPOS NML 6
		CPOS KJIH 4
		goto see
	RaiseGibbed:
		TNT1 A 0 A_TakeInventory("IsGibbed")
		CPOS T 6 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		CPOS TS 12 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		CPOS RQ 7
		CPOS POH 5
		CPOS H 5 A_Die("Ungibbed")
	Death.Ungibbed:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		CPOS H 5 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		CPOS H 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		CPOS I 5
		TNT1 A 0 A_SpawnItemEx("tempshield2",0,0,0,velx,vely,velz,0,40)
		CPOS I 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)

		TNT1 A 0 A_JumpIfInventory("ChaingunOn",1,2)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_TakeInventory("ChaingunOn")
		TNT1 A 0 A_PlaySoundEx("weapons/vulcandown","soundslot5")

		CPOS J 5 A_NoBlocking
		CPOS KLM 5
		goto Dead
	}
}
actor ZedNoisemaker:IdleDummy
{
	states{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_PlaySound("weapons/rifle")
		stop
	}
}


// ------------------------------------------------------------
//   Wolfenstein SS
// ------------------------------------------------------------

actor HoopBubble:WolfensteinSS replaces WolfensteinSS
{
	Health 80
	Height 52
	radius 12
	Speed 10
	Decal "BulletScratch"
	AttackSound ""
	Obituary "%o invoked Godwin's Law."
	DropItem ""
	Translation "192:207=94:111","240:247=9:12"
	+doharmspecies
	States
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_Look
		TNT1 A 0 A_Recoil(random(-1,1)*0.1)
		SSWV E 1 A_SetTics(random(10,40))
		TNT1 A 0 A_Look
		SSWV E 1 A_SetTics(random(10,40))
		TNT1 A 0 A_Look
		TNT1 A 0 A_Recoil(random(-1,1)*0.1)
		SSWV E 1 A_SetTics(random(10,40))
		SSWV B 0 A_Jump(132,2,5,5,5,5)
		SSWV B 8 A_Recoil(random(-1,1)*0.2)
		loop
		TNT1 A 0 A_Recoil(random(-1,1)*0.4)
		SSWV H 1 A_SetTics(random(30,80))
		TNT1 A 0 A_PlaySound("grunt/active")
		loop
		TNT1 A 0 A_CheckFlag("ambush","spawnstill")
		goto spawnwander
	spawnstill:
		TNT1 A 0 A_Look
		TNT1 A 0 A_Recoil(random(-1,1)*0.4)
		SSWV CD 5 A_SetAngle(angle+random(-4,4))
		TNT1 A 0 A_Look
		TNT1 A 0 A_Jump(192,2)
		TNT1 A 0 A_PlaySound("grunt/active")
		SSWV AB 5 A_SetAngle(angle+random(-4,4))
		SSWV B 1 A_SetTics(random(10,40))
		goto spawn
	spawnwander:
		TNT1 A 0 A_Look
		SSWV CD 5 A_Wander
		TNT1 A 0 A_Look
		TNT1 A 0 A_Jump(192,2)
		TNT1 A 0 A_PlaySound("grunt/active")
		SSWV AB 5 A_Wander
		goto spawn
	see_evade:
		TNT1 A 0 A_Jump(64,"Missile")
		TNT1 A 0 A_Chase("missile","",CHF_DONTMOVE)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_SetAngle(angle+random(60,100)*(random(0,1)*2-1))
		TNT1 A 0 A_ChangeFlag("nodropoff",1)
	see_evade2:
		TNT1 A 0 A_SetAngle(angle+random(-40,40))
		SSWV ABCD 4 A_Recoil(-4)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_JumpIf(velx*velx+vely*vely>25,2)
		TNT1 A 0 A_SetAngle(angle+random(-160,200))
		TNT1 A 0 A_ChangeFlag("nodropoff",0)
		TNT1 A 0 A_Jump(16,2)
		TNT1 A 0 A_TakeInventory("EvadeNow",999)
		TNT1 A 0 A_JumpIfTargetInLOS("See")
		goto roam
	See:
		TNT1 A 0 A_ChangeFlag("ismonster",1)
		TNT1 A 0 A_JumpIfHealthLower(20,"BleedOut")
		TNT1 A 0 A_JumpIf(floorz<z-4,2)
		TNT1 A 0 A_JumpIfInventory("EvadeNow",1,"see_evade")
		SSWV AABBCCDD 2 A_Chase
		TNT1 A 0 A_JumpIfTargetInLOS("See")
		goto roam
	Roam:
		TNT1 A 0 A_TakeInventory("EvadeNow",1)
		TNT1 A 0 A_Jump(160,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Jump(216,1)
		goto See
		TNT1 A 0 A_JumpIfTargetInLOS("See")
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 HealThing(random(1,4))
		SSWV AA 4 A_Wander
		SSWV BB 4 A_Chase("","turnaround")
		SSWV CC 4 A_Wander
		SSWV DD 4 A_Chase("","turnaround")
		Loop
	turnaround:
		TNT1 A 0 A_JumpIfTargetInLOS("missile",20)
		SSWV A 2 A_FaceTarget(30,0)
		TNT1 A 0 A_JumpIfTargetInLOS("missile",30)
		SSWV A 2 A_FaceTarget(30,0)
		TNT1 A 0 A_JumpIfTargetInLOS("missile",50)
		SSWV B 3 A_Wander
		SSWV BCCDD 3 A_Chase
		goto see
	BleedOut:
		TNT1 A 0 A_ChangeFlag("nopain",1)
		"####" AAAA 0 A_SpawnItemEx("BleedOut",0,0,32,1,0,-1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		SSWV A 1
		TNT1 A 0 A_ChangeFlag("nopain",0)
		goto see+1
	Pain:
		SSWV H 3
		SSWV H 3 A_Pain
		TNT1 A 0 A_Jump(192,"See")
		TNT1 A 0 A_AlertMonsters
		goto See
	Missile:
		SSWV EF 6 A_FaceTarget(90,90)
		TNT1 A 0 A_CheckLOF("see",CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|CLOFF_JUMPOBJECT|CLOFF_MUSTBESOLID|CLOFF_SKIPENEMY,0,0,0,0,32,0)
		SSWV F 3 A_FaceTarget(0,0)
	Shoot:
		SSWV G 1 bright light("SHOT"){
			A_PlayWeaponSound("weapons/pistol");
			A_GiveInventory("DecoBulleter9f");
		}
		SSWV F 2 A_SpawnProjectile("HDSpent9mm",40)
		SSWV F 0 A_Jump(64,"shoot")
	Shootend:
		SSWV F 1 A_FaceTarget(0,0)
		SSWV F 4 A_Jump(132,"See")
		SSWV F 6 A_CPosRefire
		loop
	Death:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_GivetoTarget("HDKillCount",1)
		SSWV I 5 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		SSWV J 5 A_Scream
		TNT1 A 0 A_SpawnItemEx("tempshield2",0,0,0,velx,vely,velz,0,40)
		TNT1 A 0 A_GiveInventory("NoBlockWepDrop")
		SSWV KL 5
	Dead:
		SSWV L 3 canraise A_JumpIf(abs(velz)<2,1)
		loop
		SSWV M 5 A_JumpIf(abs(velz)>=2,"Dead")
		loop
	XDeathBrewtleLulz:
		"####" A 0 A_ChangeFlag("shootable",0)
		TNT1 A 0 A_GiveInventory("IsGibbed",1)
		SSWV N 5 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		SSWV O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		SSWV O 5 A_XScream
		SSWV P 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		SSWV P 5
		SSWV Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		SSWV QRSTU 5
		goto XDead
	XDeath:
		"####" A 0 A_ChangeFlag("shootable",0)
		TNT1 A 0 A_GiveInventory("IsGibbed",1)
		TNT1 A 0 A_GivetoTarget("HDKillCount",1)
		SSWV N 5 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		SSWV O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		SSWV O 5 A_XScream
		SSWV P 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		SSWV P 5 A_GiveInventory("NoBlockWepDrop")
		SSWV Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,160)
		SSWV QRSTU 5
	XDead:
		SSWV U 3 canraise A_JumpIf(abs(velz)<2,1)
		wait
		SSWV V 5 canraise A_JumpIf(abs(velz)>=2,"XDead")
		wait
	Raise:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_JumpIfInventory("IsGibbed",1,"RaiseGibbed")
		SSWV M 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SSWV MLK 6
		SSWV JIH 4
		goto see
	RaiseGibbed:
		TNT1 A 0 A_TakeInventory("IsGibbed",999)
		SSWV V 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SSWV VUT 8
		SSWV SRQ 6
		SSWV PON 4
		SSWV H 4 A_Die("Ungibbed")
	Death.Ungibbed:
		"####" A 0 A_GiveInventory("HDCorpseFlags",1)
		SSWV I 5 A_SpawnItemEx("tempshield",0,0,0,velx,vely,velz,0,40)
		SSWV J 5
		TNT1 A 0 A_SpawnItemEx("tempshield2",0,0,0,velx,vely,velz,0,40)
		SSWV K 5 A_NoBlocking
		goto Dead
	}
}





