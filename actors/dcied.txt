actor HDIED : DudRocket
{
	-missile +friendly +lookallaround +nosplashalert
	-pushable +shootable +noblood +nodamage
	height 7 radius 4
	painchance 256 maxtargetrange 96
	obituary "%o was blown up by an anonymous %k."
	states{
	spawn:
		IEDS C 35 nodelay A_ChangeVelocity(4*cos(pitch),0,4*sin(-pitch),CVF_RELATIVE)
		IEDS C 0 A_PlaySound("misc/i_pkup")
		IEDS CBCBC 7
		IEDS ABABABABABABAB 5
		IEDS ABABAB 1
		IEDS A 0 A_TakeInventory("HDIEDDetNow")
		goto idle
	passive:
		IEDS A 10 A_JumpIfInventory("HDIEDDetNow",1,"explode") 
		IEDS A 0 A_JumpIfInventory("IEDPassive",1,"passive",AAPTR_MASTER) 
		goto idle
	idle:
		IEDS A 0 A_JumpIfInventory("IEDPassive",1,"passive",AAPTR_MASTER) 
		IEDS A 0 A_JumpIfInventory("HDIEDDetNow",1,"explode")
		IEDS A 10 A_LookEx(LOF_NOSOUNDCHECK,0,96.0,0,360,"see")
		loop
	see:
		IEDS B 2 A_JumpIfCloser(96,"melee")
		IEDS C 2 A_ClearTarget
		goto idle
	melee:
		IEDS A 0{
			bshootable=false;
			bfriendly=false;
			A_RearrangePointers(AAPTR_MASTER,AAPTR_DEFAULT);
		}
		goto explode
	pain:
		IEDS A 0 A_Jump(8,"destroy")
		goto idle
	pain.smallarms1:
	pain.smallarms2:
	pain.smallarms3:
		IEDS A 0 A_Jump(200,"destroy")
		goto idle
	destroy:
		IEDS A 0 A_Jump(32,"explode")
		IEDS A 0 A_ChangeFlag("friendly",0)
		IEDS A 0 A_RearrangePointers(AAPTR_MASTER,AAPTR_DEFAULT)
	grab:
		IEDS A 0{
			A_SpawnItemEx("HDIEDKit",0,0,height-12,velx,vely,velz+4,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
			A_SpawnItemEx("DudRocket",0,0,height-12,velx,vely,velz+2,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
		}
		stop
	explode:
		IEDS A 0 A_UnsetShootable
		goto super::explode
	}
}
actor HDIEDKits:HDIED
{
	height 4 radius 4 scale 0.5
	states
	{
	spawn:
		TNT1 A 0 nodelay A_SpawnItemEx("HDIEDKit",-2,0,0)
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",-2,-2,0)
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",-2,-4,0)
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",0,-2,0)
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",0,-4,0)
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",0,0,0)
		stop
	pickup:
		TNT1 A 0 A_GiveInventory("HDIEDKit",1)
		stop
	}
}
actor HDIEDDet : ActionItem
{
	states
	{
	use:
		TNT1 A 0 A_Print("All online IEDs \cgDETONATED")
		TNT1 A 0 A_GiveToChildren("HDIEDDetNow",1)
		fail
	}
}
actor HDIEDDetNow : InventoryFlag {}
actor IEDPassive : InventoryFlag{} 

actor IEDSetPassive : ActionItem
{
	states
	{
	use:
		TNT1 A 0 A_JumpIfInventory("IEDPassive",1,3)
		TNT1 A 0 A_Print("All online IEDs \cjPASSIVE")
		TNT1 A 0 A_GiveInventory("IEDPassive")
		fail
		TNT1 A 0 A_Print("All online IEDs \cdACTIVE")
		TNT1 A 0 A_TakeInventory("IEDPassive")
		fail
	}
}





