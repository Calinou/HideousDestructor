actor HDIED : DudRocket
{
	-missile +friendly +lookallaround +nosplashalert
	-pushable +shootable +noblood +nodamage
	height 7 radius 4
	painchance 256 maxtargetrange 96
	obituary "%o was blown up by an anonymous %k."
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_ChangeVelocity(4*cos(pitch),0,4*sin(-pitch),CVF_RELATIVE)
		IEDS C 35
		TNT1 A 0 A_PlaySound("misc/i_pkup")
		IEDS CBCBC 7
		IEDS ABABABABABABAB 5
		IEDS ABABAB 1
		TNT1 A 0 A_TakeInventory("HDIEDDetNow")
		goto idle
	passive:
		IEDS A 10 A_JumpIfInventory("HDIEDDetNow",1,"explode") 
		TNT1 A 0 A_JumpIfInventory("IEDPassive",1,"passive",AAPTR_MASTER) 
		goto idle
	idle:
		TNT1 A 0 A_JumpIfInventory("IEDPassive",1,"passive",AAPTR_MASTER) 
		IEDS A 0 A_JumpIfInventory("HDIEDDetNow",1,"explode")
		IEDS A 10 A_LookEx(LOF_NOSOUNDCHECK,0,96.0,0,360,"see")
		loop
	see:
		IEDS B 2 A_JumpIfCloser(96,"melee")
		IEDS C 2 A_ClearTarget
		goto idle
	melee:
		TNT1 A 0 A_ChangeFlag("friendly",0)
		TNT1 A 0 A_ChangeFlag("shootable",0)
		TNT1 A 0 A_RearrangePointers(AAPTR_MASTER,AAPTR_DEFAULT)
		goto explode
	pain:
		TNT1 A 0 A_Jump(8,"destroy")
		goto idle
	pain.smallarms1:
	pain.smallarms2:
	pain.smallarms3:
		TNT1 A 0 A_Jump(200,"destroy")
		goto idle
	destroy:
		TNT1 A 0 A_Jump(32,"explode")
		TNT1 A 0 A_ChangeFlag("friendly",0)
		TNT1 A 0 A_RearrangePointers(AAPTR_MASTER,AAPTR_DEFAULT)
	grab:
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",0,0,height-12,velx,vely,velz+4,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		TNT1 A 0 A_SpawnItemEx("DudRocket",0,0,height-12,velx,vely,velz+2,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		stop
	explode:
		TNT1 A 0 A_UnsetShootable
		goto super::explode
	}
}
actor HDIEDKit:HDPickup{
	inventory.amount 1
	inventory.maxamount 24
	inventory.interhubamount 24
	inventory.icon IEDI
	inventory.pickupmessage "Picked up an IED kit."
	height 4 radius 4 scale 0.5
	states
	{
	spawn2:
		IEDK A -1
	use:
		TNT1 A 0 A_JumpIfInventory("DudRocketAmmo",1,"usedud")
		TNT1 A 0 A_JumpIfInventory("HDRocketAmmo",1,"userocket")
		TNT1 A 0 A_Print("You need at least one live or dud rocket grenade.")
		fail
	usedud:
		TNT1 A 0 A_TakeInventory("DudRocketAmmo",1,TIF_NOTAKEINFINITE)
		goto dropied
	userocket:
		TNT1 A 0 A_TakeInventory("HDRocketAmmo",1,TIF_NOTAKEINFINITE)
	dropied:
		TNT1 A 0 A_GiveInventory("IEDSetPassive",1)
		TNT1 A 0 A_TakeInventory("HDIEDDetNow")
		TNT1 A 0 A_GiveInventory("HDIEDDet")
		TNT1 A 0 A_SpawnItemEx("HDIED",0,0,height-12,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_SETMASTER|SXF_TRANSFERPITCH)
		stop
	}
}
actor HDIEDKits:HDIED
{
	height 4 radius 4 scale 0.5
	states
	{
	spawn:
		TNT1 A 0 nodelay A_SpawnItemEx("HDIEDKit",-2,0,0)
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",-2,-2,0)
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",-2,-4,0)
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",0,-2,0)
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",0,-4,0)
		TNT1 A 0 A_SpawnItemEx("HDIEDKit",0,0,0)
		stop
	pickup:
		TNT1 A 0 A_GiveInventory("HDIEDKit",1)
		stop
	}
}
actor HDIEDDet : ActionItem
{
	states
	{
	use:
		TNT1 A 0 A_Print("All online IEDs \cgDETONATED")
		TNT1 A 0 A_GiveToChildren("HDIEDDetNow",1)
		fail
	}
}
actor HDIEDDetNow : InventoryFlag {}
actor IEDPassive : InventoryFlag{} 

actor IEDSetPassive : ActionItem
{
	states
	{
	use:
		TNT1 A 0 A_JumpIfInventory("IEDPassive",1,3)
		TNT1 A 0 A_Print("All online IEDs \cjPASSIVE")
		TNT1 A 0 A_GiveInventory("IEDPassive",1)
		fail
		TNT1 A 0 A_Print("All online IEDs \cdACTIVE")
		TNT1 A 0 A_TakeInventory("IEDPassive",999)
		fail
	}
}





