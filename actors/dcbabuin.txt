// ------------------------------------------------------------
//   Babuin
// ------------------------------------------------------------
actor Babuin:ZBabuin{
	Health 90
	Radius 16
	Height 44
	DeathHeight 10
	Scale 0.6
	Speed 12
	Mass 70
	Damage 3
	MeleeDamage 1
	MeleeRange 40
	PainChance 90
	MONSTER
	+FLOORCLIP +FASTER
	+missilemore +missileevenmore
	+cannotpush +pushable pushfactor 0.2
	maxtargetrange 128
	SeeSound "demon/sight"
	PainSound "demon/pain"
	DeathSound "demon/death"
	ActiveSound "demon/active"
	Obituary "%o was mauled by a babuin."
	damagefactor "Thermal", 0.76
	damagefactor "SmallArms0", 0.9
	damagetype "Teeth"
	var int user_angle;
	States
	{
	spawn:
		SRG2 A 0 nodelay A_CheckFlag("ambush",1)
		goto spawnwander
	spawnstill:
		SRG2 AABB 4 A_Look
		Loop
	spawnwander:
		TNT1 A 0 A_Look
		SRG2 AA 4 A_BabuMove(true)
		TNT1 A 0 A_Look
		SRG2 BB 4 A_BabuMove(true)
		TNT1 A 0 A_Look
		TNT1 A 0 A_Jump(220,2)
		TNT1 A 0 A_PlaySound("demon/active",1)
		SRG2 CC 4 A_BabuMove(true)
		TNT1 A 0 A_Look
		SRG2 DD 4 A_BabuMove(true)
		TNT1 A 0 A_Jump(42,1)
		loop
	spawnsniff:
		TNT1 A 0 A_ChangeFlag("lookallaround",1)
		SRG2 EEEEEEEE 2 A_Look
		TNT1 A 0 A_SetAngle(angle+random(-20,20))
		TNT1 A 0 A_Jump(220,2)
		TNT1 A 0 A_PlaySound("demon/active",1)
		SRG2 FFF 2 A_Look
		TNT1 A 0 A_ChangeFlag("lookallaround",0)
		TNT1 A 0 A_Jump(36,"spawnwander")
		loop
	See:
		TNT1 A 0 A_ChangeFlag("lookallaround",0)
		SRG2 AABBCCDD 2 A_BabuMove
		TNT1 A 0 A_Jump(240,2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 HealThing(random(2,12))
		TNT1 A 0 A_JumpIfTargetInLOS("See")
		goto Roam
	Roam:
		TNT1 A 0 A_Jump(160,"See")
		TNT1 A 0 A_JumpIfTargetInLOS("See")
		Goto See
		SRG2 AABBCCDD 2 A_BabuMove(true)
		TNT1 A 0 A_Jump (120, 2)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 HealThing (random(2,8))
		Loop
	Melee:
		SRG2 E 7{
			A_FaceTarget(0,0);
			A_PlaySound("demon/melee");
			A_ChangeVelocity(cos(pitch)*4,0,sin(-pitch)*4,CVF_RELATIVE);
		}
		SRG2 F 6
		SRG2 G 2{
			TryLatch;
			A_CustomMeleeAttack(random(5,15),null,null,"Teeth",true);
		}
	PostMelee:
		SRG2 G 6
		goto see
	Missile:
		SRG2 D 0{
			A_TakeInventory("NoBabusForYou");
			A_FaceTarget(0,0);
		}
		SRG2 DE 3 A_ChangeVelocity(cos(pitch)*3,0,sin(-pitch)*3,CVF_RELATIVE)
		SRG2 E 3 A_SpawnProjectile("BabuDummy",42,0,0,CMF_AIMDIRECTION,-pitch)

		SRG2 A 0 A_JumpIfInventory("NoBabusForYou",1,"see")
		SRG2 E 2 A_PlaySound("babuin/sight")
		SRG2 E 0 A_ChangeVelocity(cos(pitch)*16,0,sin(-pitch)*16+random(3,6),CVF_RELATIVE)
		goto fly
	Fly:
		SRG2 F 0 TryLatch
		SRG2 F 1 A_JumpIf(velz==0,1)
		loop
		SRG2 FEH 4 A_ScaleVelocity(0.8)
		SRG2 D 6 A_Stop
		Goto See
	Pain:
		SRG2 H 2 A_SetSolid
		SRG2 H 6 A_Pain
		SRG2 H 0 A_CheckFloor("Missile")
		Goto See
	Death:
		TNT1 A 0 A_ChangeFlag("pushable",0)
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_Scream
		"----" A 0 A_SpawnItemEx("BFGVileShard",0,0,0,0,0,5,0,SXF_TRANSFERPOINTERS|SXF_SETMASTER,240)
		SRG2 I 5 A_SpawnItemEx ("tempshield2", 0,0,0, velx,vely,velz, 0,40)
		SRG2 J 5 A_NoBlocking
		SRG2 K 5
		SRG2 L 5
		SRG2 M 5
	Dead:
		SRG2 M 3 canraise A_JumpIf(abs(velz)<2,1)
		loop
		SRG2 N 5 canraise A_JumpIf(abs(velz)>=2,"Dead")
		loop
	Raise:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_JumpIfInventory("IsGibbed",1,"RaiseGibbed")
		SRG2 NMLKJI 5
		Goto See
	RaiseGibbed:
		TNT1 A 0 A_TakeInventory("IsGibbed",999)
		TROO U 6 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		TROO UT 8
		TROO SRQ 6
		TROO PO 4
		SRG2 H 4 A_Die("Ungibbed")
	Death.Ungibbed:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_SpawnItemEx ("tempshield", 0,0,0, velx,vely,velz, 0,40)
		SRG2 I 5 A_ChangeFlag("NODROPOFF", 0)
		SRG2 J 5
		SRG2 K 5 A_SpawnItemEx ("tempshield2", 0,0,0, velx,vely,velz, 0,40)
		SRG2 LM 5 //A_NoBlocking
		goto Dead
	XDeathBrewtleLulz:
		TNT1 A 0 A_GiveInventory("IsGibbed",1)
		TNT1 A 0 A_ChangeFlag("shootable",0)
		TROO N 0 A_ChangeFlag("NODROPOFF", 0)
		TROO N 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		TROO O 4 A_XScream
		TROO O 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		TROO P 4
		TROO P 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		TROO QRS 4
		TROO T 4
		goto XDead
	XDeath:
		TNT1 A 0 A_GiveInventory("IsGibbed",1)
		TNT1 A 0 A_ChangeFlag("shootable",0)
		TROO N 0 A_ChangeFlag("NODROPOFF", 0)
		TROO N 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		TROO O 4 A_XScream
		TROO O 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		TROO P 4 A_NoBlocking
		TROO P 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		TROO QRS 4
		TROO T 4
	XDead:
		TROO T 5 canraise A_JumpIf(abs(velz)<2,1)
		wait
		TROO U 5 canraise A_JumpIf(abs(velz)>=2,"XDead")
		wait
	}
}
actor NoBabusForYou : InventoryFlag {}
actor BabuDummy
{
	speed 32
	height 20
	radius 20
	projectile
	+floorhugger
	+noexplodefloor
	states
	{
	spawn:
		TNT1 A 3
		stop
	death:
		TNT1 A 1 A_GiveInventory("NoBabusForYou",1,AAPTR_TARGET)
		stop
	}
}



actor DeadDemonSpawner : RandomSpawner replaces DeadDemon
{
	+ismonster
	dropitem "DeadBabuin" 256 5
	dropitem "DeadSpecBabuin" 256 2
	dropitem "DeadSpectre" 256 1
}

actor DeadBabuin : Babuin
{
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("ShellChamber1",1,3)
		TNT1 A 0 A_GiveInventory("ShellChamber1",1)
		TNT1 A 0 A_Die("spawndead")
		TNT1 A 0
		goto super::spawn
	death.spawndead:
		TNT1 A 0 A_ChangeFlag("pushable",0)
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_ChangeFlag("nodropoff", 0)
		TNT1 A 0 A_NoBlocking
		goto super::dead
	}
}



actor SpecBabuin : Babuin
{
	renderstyle fuzzy
	dropitem "HDBlurSphere",8
	states
	{
	see:
		TNT1 A 0 A_SetTranslucent(1,2)
		TNT1 A 0 A_JumpIfCloser(128,2)
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0
		goto super::see
		TNT1 A 4 A_Chase
		loop
	death:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(2,14),velx,vely,velz+random(1,3),0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SetTranslucent(1,0)
		goto super::death
	xdeath:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(2,14),velx,vely,velz+random(1,3),0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION,0)
		TNT1 A 0 A_SetTranslucent(1,0)
		goto super::xdeath
	raise:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_JumpIfInventory("IsGibbed",1,"RaiseGibbed")
		TNT1 AA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(4,24),velx,vely,velz+random(1,3),0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION,0)
		SARG NMLKJI 5 A_FadeOut(0.15)
		Goto See
	}
}
actor DeadSpecBabuin : SpecBabuin
{
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("ShellChamber1",1,3)
		TNT1 A 0 A_GiveInventory("ShellChamber1",1)
		TNT1 A 0 A_Die("spawndead")
		TNT1 A 0
		goto super::spawn
	death:spawndead:
		TNT1 A 0 A_ChangeFlag("pushable",0)
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_ChangeFlag("nodropoff", 0)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SetTranslucent(1,0)
		goto super::dead
	}
}

