// ------------------------------------------------------------
//   Revenant
// ------------------------------------------------------------

ACTOR BonerBall : RevenantTracer
{
	+rockettrail
	+shootable +thruspecies +noblood -noblockmap +thruspecies +notargetswitch health 40 mass 80
	+forcexybillboard
	+seekermissile +randomize
	deathsound "skeleton/tracex"
	seesound "skeleton/attack"
	renderstyle add
	Radius 6
	Height 6
	Scale 0.6
	Decal "RevenantScorch"
	seesound none
	Speed 2
	var int user_speed;
	var int user_velx;
	var int user_vely;
	var int user_velz;
	damage (random(30,60)+user_speed)
	damagetype "SmallArms2"
	States
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("skeleton/attack",CHAN_VOICE)
	spawn2:
		FATB AB 2 bright
		TNT1 A 0{
			A_PlaySound("world/rocketfar",CHAN_BODY,0.4);
			A_SetUserVar("user_speed",sqrt(abs(velx)*abs(vely)*abs(velz)));
			A_SetUserVar("user_velx",velx);
			A_SetUserVar("user_vely",vely);
			A_SetUserVar("user_velz",velz);
			if (A_JumpIfTargetInLOS("spawn",0,JLOSF_CHECKTRACER)){
				A_ScaleVelocity(0.9);
				A_FaceTracer(45,45,0,0,FAF_TOP,-height);
				A_SetPitch(pitch+frandom(-1,1));
				A_SetAngle(angle+frandom(-1,1));
			}
			else {
				A_SetPitch(pitch+frandom(-10,10));
				A_SetAngle(angle+frandom(-20,20));
			}
			if ((A_JumpIfTargetInLOS("spawn",1,JLOSF_CHECKTRACER))
				&&(GetDistance(1,AAPTR_TRACER)<96))
				{A_ChangeVelocity(Cos(Pitch)*5,0,Sin(-Pitch)*5,CVF_RELATIVE);}
			else {A_ChangeVelocity(Cos(Pitch)*2,0,Sin(-Pitch)*2,CVF_RELATIVE);}
			if(z-floorz<14){A_ChangeVelocity(0,0,2);}
		}
		loop
	Death:
		TNT1 AAAAAAAAAAA 0 A_SpawnItemEx ("BigWallChunk", random(-4,4),random(-4,4),random(2,14), user_velx+random(-6,6),user_vely+random(-6,6),user_velz+random(-4,18), random(0,360),SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 AA 0 A_SpawnItemEx ("HDSmokeChunk", random(-4,4),random(-4,4),random(2,14), user_velx+random(-6,6),user_vely+random(-6,6),user_velz+random(-4,18), random(0,360),SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,160)
		TNT1 A 0 A_ChangeVelocity(0,0,2)
		FATB A 0 A_SetDamageType("SmallArms0")
		MISX A 4 BRIGHT A_Explode (random(10,30),random(50,70),0)
		TNT1 A 0 A_Quake(2,48,0, 24,"") 
		TNT1 AA 0 A_SpawnItemEx ("HDSmoke", 0,0,-2, user_velx>>2-1,user_vely>>2,user_velz>>2+1, 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		MISL CCCC 1 A_FadeOut (0.1)
		goto Fade
	Fade:
		MISL D 1 A_FadeOut (0.1)
		loop
	}
}

ACTOR BonerBallTail  //keeping this because I use it elsewhere
{
	+nointeraction
	+forcexybillboard
	renderstyle add
	scale 0.8
	alpha 0.8
	translation 2
	states
	{
	spawn:
		BAL7 AB 4 A_FadeOut (0.05)
		Loop
	}
}

ACTOR Boner : Revenant replaces Revenant
{
	Scale 0.7
	+DONTHURTSPECIES
	+noblooddecals bloodtype "NotQuiteBloodSplat"
	+FASTER
	Speed 10
	Mass 60
	health 250
	Obituary "%o was bagged by a boner."
	HitObituary "%o was slapped by a boner."
	damagefactor "Thermal", 1.1
	damagefactor "SmallArms0", 0.9
	damagefactor "SmallArms1", 0.9
	damagefactor "SmallArms2", 0.8
	damagefactor "SmallArms3", 0.7
	States
	{
	See:
		SKEL AABBCCDDEEFF 2 A_Chase
		loop
	Melee:
		SKEL G 6 A_FaceTarget
		SKEL G 2 A_SkelWhoosh
		SKEL H 3 A_Recoil(-3)
		SKEL I 6 A_SkelFist
		SKEL H 4
		goto See
	Missile:
		SKEL I 0 A_FaceTarget
	missile2:
		SKEL I 8
		SKEL J 0 BRIGHT A_CustomMissile("BonerBall",42,4,-24,2,4)
		SKEL J 10 BRIGHT A_CustomMissile("BonerBall",42,-4,24,2,4)
		SKEL K 10
		SKEL K 0 A_Jump(32,"missile2")
		goto see
	Death:

		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_SpawnItemEx ("tempshieldpuff", 0,0,0, velx,vely,velz, 0,40)
		SKEL LM 7
		TNT1 A 0 A_SpawnItemEx ("tempshield2puff", 0,0,0, velx,vely,velz, 0,40)
		SKEL N 7 A_Scream
		SKEL O 7 A_NoBlocking
	Dead:
		SKEL P 5 canraise A_JumpIf(floorz>z-6,1)
		wait
		SKEL Q 5 canraise A_JumpIf(floorz<=z-6,"Dead")
		wait
	Raise:

		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		goto super::raise
	}
}

