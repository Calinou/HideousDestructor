// ------------------------------------------------------------
//   Kick
// ------------------------------------------------------------
actor KickProjectile : FastProjectile
{
	projectile
	height 16
	radius 2
	damage 2
	speed 40
	+thrughost
	+hittracer
	states{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_ChangeFlag("forcepain",1)
		TNT1 A 1 A_ChangeVelocity(40,0,0,CVF_RELATIVE)
		stop
	death:
		TNT1 A 0
		stop
	xdeath:
		TNT1 A 0 A_PlaySound("weapons/smack")
		TNT1 A 0 A_RadiusThrust(80,8,RTF_NOIMPACTDAMAGE|RTF_AFFECTSOURCE)
		TNT1 A 0 A_JumpIfInTargetInventory("PowerStrength",1,"zerkspawn")
		goto xdeathfinal
	zerkspawn:
  		TNT1 A 0 A_SpawnItemEx("KickProjectile2",0,0,0,1,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_RadiusThrust(80,10,RTF_NOIMPACTDAMAGE|RTF_AFFECTSOURCE)
	xdeathfinal:
		TNT1 A 1 A_ChangeVelocity(8,0,-2,CVF_RELATIVE)
		TNT1 A 0 A_RadiusThrust(80,5,RTF_NOIMPACTDAMAGE|RTF_AFFECTSOURCE)
		stop
	}
}
actor KickProjectile2:KickProjectile{
	deathsound "*fist"
	damage 10
	radius 6 height 4
	states{
	spawn:
	death:
	xdeath:
		TNT1 A 0 nodelay A_RadiusThrust(120,5,RTF_NOIMPACTDAMAGE)
		stop
	}
}





// ------------------------------------------------------------
//   Fist
// ------------------------------------------------------------

actor FistTurner:ActionItem{
	states{
	pickup:
		TNT1 A 0{
			if(radius<50){
				A_SetAngle(angle+random(-4,4)*30);
				A_SetPitch(randompick(-90,90));
				if(mass<50){A_ChangeVelocity(0,0,0.1);}
			}
		}fail
	}
}
actor HDNullPuff:HDFistPuff{
	+puffonactors stamina 0 alpha 0
	states{
	spawn:
		TNT1 A 0 nodelay
		stop
	}
}
actor HDZerkGibber : HDSawGibber
{
	states
	{
	xdeath:
		TNT1 A 1 A_GiveInventory("ZerkGib",1,AAPTR_TARGET)
		stop
	}
}
//test actor for perspective
actor HDScalerPuff : FastProjectile
{
	height 1
	radius 1
	+nointeraction
	speed 14
	scale 0.3
	states
	{
	spawn:
		PUFF A 2
		stop
	}
}
actor HDScalerPuff2 : HDScalerPuff
{
	speed 8
}
actor Ring : HDWeapon replaces Fist
{
	obituary "%o made %k take the kid gloves off."
	attacksound "*fist"
	weapon.selectionorder 1
	weapon.kickback 120
	weapon.bobstyle Alpha
	weapon.bobspeed 2.6
	weapon.bobrangex 0.1
	weapon.bobrangey 0.5
	weapon.slotnumber 1
	+WEAPON.WIMPY_WEAPON
	+WEAPON.MELEEWEAPON
	+WEAPON.NOALERT
	+WEAPON.NO_AUTO_SWITCH
	+forcepain
	states{
	Ready:
		TNT1 A 1{
			A_WeaponReady(WRF_ALLOWRELOAD);
			A_TakeInventory("IsKicking");
		}goto ReadyEnd
	Reload:
		PUNF A 0 A_JumpIfInventory("IsFemale",1,3)
		PUNG A 0 A_JumpIfInventory("IsMale",1,2)
		TNT1 A 0
		"####" A 0 A_CheckFloor(2)
		"####" A 0 A_Jump(256,2)
		"####" A 0 A_Recoil(-3)
		"####" A 1 offset(0,50)
		"####" A 1 offset(0,36)
		"####" A 0 A_JumpIfInventory("PowerStrength",1,"ZerkFlick")
		"####" AAAAAAA 0 A_CustomPunch((1),1,CPF_PULLIN,"HDNullPuff",36)
		goto FlickEnd
	ZerkFlick:
		"####" AAAAAAA 0 A_CustomPunch((random(1,3)),1,CPF_PULLIN,"HDNullPuff",36)
	FlickEnd:
		"####" AA 1 offset(0,38) A_FaceTarget
		"####" A 1 offset(0,42) A_FaceTarget
		"####" A 1 offset(0,50) A_FaceTarget
		goto fire
	Fire:
	Hold:
	AltHold:
		PUNF A 0 A_JumpIfInventory("IsFemale",1,3)
		PUNG A 0 A_JumpIfInventory("IsMale",1,2)
		TNT1 A 0
		"####" A 0 A_GiveInventory("IsKicking",1)
		"####" A 0 A_JumpIfInventory("PowerStrength",1,"ZerkPunch")
		goto Punch
	Punch:
		"####" B 1 offset(0,32)
		"####" DDDDD 0 A_CustomPunch(1,0,0,"HDNullPuff",46)
		"####" D 0 A_CustomPunch(1,0,0,"HDFistPuff",46)
		"####" D 6
		"####" CB 3
		TNT1 A 3
		TNT1 A 1 A_ReFire
		goto Ready
	ZerkPunch:
		"####" D 0 A_Recoil(-1)
		"####" D 0 A_CustomPunch(36,0,0,"HDFistPuff",46)
		"####" DDDDDDD 0 A_CustomPunch(1,0,0,"HDNullPuff",46)
		"####" D 0 A_CustomPunch(1,0,1,"HDSawPuff",36)
		"####" D 0 A_CustomPunch(1,0,0,"HDFistPuff",46)
		"####" D 0 A_FireCustomMissile("HDSawGibber")
		"####" D 3
		"####" CB 1
		TNT1 A 5
		TNT1 A 2 A_ReFire
		goto Ready
	AltFire:
		"####" A 1 offset(0,36)
		"####" A 1 offset(0,50)
		TNT1 A 0 A_GiveInventory("IsKicking",1)
		TNT1 A 2 A_CheckFloor("Lunge")
		goto Kick
	Lunge:
		TNT1 A 0 A_JumpIfInventory("Fatigue",30,"Hold")
		TNT1 A 0 A_GiveInventory("Fatigue",3)
		TNT1 A 0 A_JumpIfInventory("PowerStrength",1,2)
		TNT1 A 0 A_Jump(256,3)
		TNT1 AA 1 A_Recoil(-random(4,8)*(countinv("PowerStrength")*2+1))
		TNT1 A 0 offset(0,50) A_Recoil(-4)
		TNT1 A 1 offset(0,70) A_Recoil(-4)
	Kick:
		TNT1 A 0 A_GiveInventory("Fatigue",2)
		TNT1 A 8 A_SpawnItemEx("KickProjectile",0,0,4,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		TNT1 A 5 A_JumpIfInventory("PowerStrength",1,1)
		PUNF A 0 A_Refire
		goto Ready
	spawn:
		TNT1 A 1
		TNT1 A 0 A_CheckProximity("spawndropped","PlayerPawn",20,1, CPXF_COUNTDEAD|CPXF_SETTARGET|CPXF_ANCESTOR|CPXF_CLOSEST|CPXF_NOZ)
	spawnfresh:
		TNT1 A 0
		stop
	spawndropped:
//		TNT1 A 0 A_JumpIfInTargetInventory("Ring",1,"spawnfresh")
		TNT1 A 0 A_GiveToTarget("DropMisc")
		stop
	}
}
actor IsKicking : InventoryFlag {}


