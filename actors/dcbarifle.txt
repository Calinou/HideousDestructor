// ------------------------------------------------------------
//   Bolt-Action Rifle
// ------------------------------------------------------------

actor BossMOA:InventoryFlag{inventory.maxamount 36}
actor BossZoomDegree:InventoryFlag{inventory.maxamount 2}
actor BossChamber : Ammo
{
	+inventory.ignoreskill
	+inventory.untossable
	inventory.maxamount 1
	ammo.backpackmaxamount 1
	ammo.backpackamount 0
	states{spawn:TNT1 A 0 nodelay{return state("null");}stop}
}
actor BossSpentChamber : Ammo
{
	+inventory.ignoreskill
	+inventory.untossable
	inventory.maxamount 1
	ammo.backpackmaxamount 1
	ammo.backpackamount 0
	states{spawn:TNT1 A 0 nodelay{return state("null");}stop}
}
actor BossMag : Ammo
{
	+inventory.ignoreskill
	+inventory.untossable
	inventory.maxamount 10
	ammo.backpackmaxamount 10
	ammo.backpackamount 0
	states{spawn:TNT1 A 0 nodelay{return state("null");}stop}
}
actor BossHand : InventoryFlag {inventory.maxamount 3}
actor BossClip : Ammo
{
	+inventory.ignoreskill
	inventory.maxamount 12
	ammo.backpackmaxamount 30
	ammo.backpackamount 0
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("BossClipPickup",0,0,0,velx,vely,velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		stop
	}
}
actor BossClipPickup : HDUPK replaces BossClip
{
	scale 0.5
	states
	{
	spawn:
		RCLP A 0 A_Stop
		goto spawn2
	give:
		"----" A 0 A_JumpIfInTargetInventory("BossClip",0,"spawn")
		"----" A 0 A_GiveToTarget("BossClip",1)
		"----" A 0 A_PlaySound("weapons/pocket")
		stop
	}
}
ACTOR BossRifleSpawner
{
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEx("BossClipPickup",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("BossClipPickup",3,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("BossClipPickup",1,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SpawnItemEx("BossRiflePickup",-3,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		stop
	}
}
actor BossRiflePickup : HDUPK
{
	scale 0.7
	states
	{
	spawn:
		BORF A 0 A_Stop
		"----" A 0 A_JumpIfInventory("Clip",1,"spawn2")
		"----" A 0 A_GiveInventory("Clip",1)
		"----" A 0 A_JumpIf(CheckClass("PlayerPawn",AAPTR_TARGET,true),"takeloop")
		"----" A 0 A_GiveInventory("BossMag",12)
		"----" A 0 A_Jump(256,"spawn2")
	takeloop:
		"----" A 0 A_JumpIfInTargetInventory("BossMag",1,1)
		goto take2
		"----" A 0 A_TakeFromTarget("BossMag",1)
		"----" A 0 A_GiveInventory("BossMag",1)
		loop
	take2:
		"----" A 0 A_JumpIfInTargetInventory("BossChamber",1,1)
		goto take3
		"----" A 0 A_TakeFromTarget("BossChamber",1)
		"----" A 0 A_GiveInventory("BossChamber",1)
	take3:
		"----" A 0 A_JumpIfInTargetInventory("BossSpentChamber",1,1)
		goto take4
		"----" A 0 A_TakeFromTarget("BossSpentChamber",1)
		"----" A 0 A_GiveInventory("BossSpentChamber",1)
	take4:
		"----" A 0 A_JumpIfInTargetInventory("BossMOA",1,1)
		goto take5
		"----" A 0 A_TakeFromTarget("BossMOA",1)
		"----" A 0 A_GiveInventory("BossMOA",1)
		loop
	take5:
		"----" A 0 A_JumpIfInTargetInventory("BossZoomDegree",1,1)
		goto take6
		"----" A 0 A_TakeFromTarget("BossZoomDegree",1)
		"----" A 0 A_GiveInventory("BossZoomDegree",1)
		goto take6
	take6:
		"----" A 0 A_JumpIfInTargetInventory("BossJam",1,1)
		goto spawn2
		"----" A 0 A_TakeFromTarget("BossJam",1)
		"----" A 0 A_GiveInventory("BossJam",1)
		goto spawn2
	give:
		"----" A 0 A_JumpIfInTargetInventory("BossRifle",1,"spawn")
	givegun:
		"----" A 0 A_PlaySound("misc/w_pkup")
		"----" A 0 A_GiveToTarget("BossRifle",1)
	givegunloop:
		"----" A 0 A_JumpIfInventory("BossMag",1,1)
		goto givegun2
		"----" A 0 A_GiveToTarget("BossMag",1)
		"----" A 0 A_TakeInventory("BossMag",1)
		loop
	givegun2:
		"----" A 0 A_JumpIfInventory("BossChamber",1,1)
		goto give3
		"----" A 0 A_GiveToTarget("BossChamber",1)
		"----" A 0 A_TakeInventory("BossChamber",1)
	give3:
		"----" A 0 A_JumpIfInventory("BossSpentChamber",1,1)
		goto give4
		"----" A 0 A_GiveToTarget("BossSpentChamber",1)
		"----" A 0 A_TakeInventory("BossSpentChamber",1)
		goto give4
	give4:
		"----" A 0 A_JumpIfInventory("BossMOA",1,1)
		goto give5
		"----" A 0 A_GiveToTarget("BossMOA",1)
		"----" A 0 A_TakeInventory("BossMOA",1)
		loop
	give5:
		"----" A 0 A_JumpIfInventory("BossZoomDegree",1,1)
		goto give6
		"----" A 0 A_GiveToTarget("BossZoomDegree",1)
		"----" A 0 A_TakeInventory("BossZoomDegree",1)
		goto give6
	give6:
		"----" A 0 A_JumpIfInventory("BossJam",1,1)
		goto givegunloopend
		"----" A 0 A_GiveToTarget("BossJam",1)
		"----" A 0 A_TakeInventory("BossJam",1)
		goto givegunloopend
	}
}
actor BossRifleEmptyPickup : BossRiflePickup
{
	states
	{
	spawn:
		BORF A -1 A_Stop
	}
}
actor BossClipPickupRounds : InventoryFlag {inventory.maxamount 30}
actor BossZoom : InventoryFlag {}
actor BossJam : InventoryFlag {}
actor BossRifle : HDWeapon
{
	game Doom
	weapon.slotnumber 8
	weapon.ammotype1 "BossMag"
	weapon.ammotype2 "BossClip"
	weapon.kickback 15
	weapon.SelectionOrder 200
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "You got the bolt-action rifle!"
	weapon.bobrangex 0.28
	weapon.bobrangey 1.1
	scale 0.7
	Obituary "%o sure showed %k who was the boss!"
	states
	{
	select0:
		BARG A 0
		goto select0bfg
	deselect0:
		BARG A 0
		goto deselect0big

	ready:
		BARG A 1 A_WeaponReady(WRF_ALLOWZOOM|WRF_ALLOWRELOAD)
		TNT1 A 0 A_TakeInventory("WeaponBusy")
		TNT1 A 0 A_TakeInventory("PressingZoom")
		TNT1 A 0 A_GiveInventory("IsWeaponReady",1)
		TNT1 A 0 A_GiveInventory("IsWeaponLong",1)
		TNT1 A 0 A_GiveInventory("NotShot",1)
		TNT1 A 0 A_JumpIfInventory("NotShot",200,"fillmag")
		TNT1 A 0 A_JumpIfInventory("PressingUnload",1,"Unload")
		TNT1 A 0 A_JumpIfInventory("PressingAltReload",1,"altreload")
		BARG A 0 A_JumpIfInventory("PressingFireMode",1,"moaadjust")
		goto ReadyEnd
	fillmag:
		BARG A 0 A_TakeInventory("NotShot")
		BARG A 0 A_JumpIfInventory("BossClip",0,"ready")
		BARG A 0 A_JumpIfInventory("SevenMilAmmo",10,1)
		goto ready
		BARG A 0 A_GiveInventory("BossClip",1)
		BARG A 0 A_TakeInventory("SevenMilAmmo",10)
		goto ready
	altreload:
		TNT1 A 0 A_JumpIfInventory("SevenMilAmmo",0,"altreloadend")
		TNT1 A 0 A_JumpIfInventory("BossClip",1,"altreloadgetclip")
		TNT1 A 0 A_JumpIfInventory("LiberatorMag",1,"altreloadgetmag")
		goto altreloadend
	altreloadgetmag:
		TNT1 A 0 A_TakeInventory("LiberatorMag",1)
		TNT1 A 0 A_GiveInventory("BossClipPickupRounds",30)
		goto altreloadgenerate
	altreloadgetclip:
		TNT1 A 0 A_TakeInventory("BossClip",1)
		TNT1 A 0 A_GiveInventory("BossClipPickupRounds",10)
	altreloadgenerate:
		BARG A 2 offset(4,36) A_TakeInventory("BossClipPickupRounds",1)
		TNT1 A 0 A_PlaySound("weapons/rifleclick2")
		TNT1 A 0 A_JumpIfInventory("SevenMilAmmo",0,3)
		TNT1 A 0 A_GiveInventory("SevenMilAmmo",1)
		TNT1 A 0 A_JumpIfInventory("BossClipPickupRounds",1,"altreloadgenerate")
		goto altreloadend
		TNT1 A 0 A_SpawnItemEx("HDLoose7mm",random(4,8),0,32,frandom(2,3),frandom(1,2),frandom(1,3),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_JumpIfInventory("BossClipPickupRounds",1,"altreloadgenerate")
		goto altreloadend
	altreloadend:
		TNT1 A 0 A_TakeInventory("NotShot")
		goto ready
	fire:
	firegun:
	shoot:
		BARG A 0 A_JumpIfInventory("BossChamber",1,1)
		goto noshoot
		BARG A 1 A_Gunflash
		BARG F 2 A_JumpIfInventory("IsSupported",1,"noshoot")
		goto noshoot
	noshoot:
	hold:
		BARG A 1 A_WeaponReady(WRF_NOFIRE)
		BARG A 1 A_Refire
		goto ready
	althold:
		BARG E 1 A_WeaponReady(WRF_NOFIRE)
		BARG E 1 A_Refire
		BARG DCB 3 A_WeaponReady(WRF_NOFIRE)
		goto ready
	flash:
		BARF A 0 A_TakeInventory("BossChamber",1)
		TNT1 A 0 A_GiveInventory("BossSpentChamber",1)
		BARF A 0 A_ZoomFactor(0.95,ZOOM_INSTANT|ZOOM_NOSCALETURNING)
		BARF A 0 A_PlaySound("weapons/bigrifle",1)
		BARF A 0 A_AlertMonsters
		BARF A 0 A_GiveInventory("DecoBulleter776f")

		BARF A 1 bright{
			A_Light1();
			HDFlashAlpha(-96);
		}
		BARF A 0 A_Light0
		BARF A 0 A_ZoomFactor(1.0,ZOOM_INSTANT|ZOOM_NOSCALETURNING)
		TNT1 A 1 ACS_NamedExecuteAlways("MuzzleClimb",0,random(6,10),random(2,4),0)
		TNT1 AA 1 ACS_NamedExecuteAlways("MuzzleClimb",0,random(12,21),random(4,7),0)
		stop
	altfire:
		TNT1 A 0 A_TakeInventory("IsWeaponReady")
		BARG A 2 offset(0,34)
		BARG B 1 offset(2,36)
		TNT1 A 0 A_JumpIfInventory("BossJam",1,"jamderp")
		BARG B 1 offset(4,38) A_Refire("chamber")
		TNT1 A 0 A_JumpIfInventory("BossChamber",1,3)
		TNT1 A 0 A_JumpIfInventory("BossSpentChamber",1,2)
		BARG A 0 A_Jump(256,3)
		BARG A 0 A_Jump(96,2)
		BARG A 0 A_GiveInventory("BossJam")
		BARG B 1 offset(0,34)
		goto ready
	jam:
		TNT1 A 0 A_JumpIfInventory("BossChamber",1,2)
		TNT1 A 0 A_JumpIfInventory("BossSpentChamber",1,1)
		goto chamber
		BARG A 0 A_GiveInventory("BossJam")
	jamderp:
		BARG A 0 A_PlaySound("weapons/rifleclick")
		BARG D 1 offset(4,38)
		BARG D 2 offset(2,36)
		TNT1 A 0 A_SetPitch(pitch-frandom(-0.3,0.6),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+frandom(-0.5,0.6),SPF_INTERPOLATE)
		BARG D 2 offset(4,38)
		TNT1 A 0 A_SetPitch(pitch-frandom(-0.3,0.6),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+frandom(-0.5,0.6),SPF_INTERPOLATE)
		TNT1 A 0 A_Jump(64,"unjam")
		BARG D 3 offset(2,36)
		BARG D 2 offset(4,38)
		BARG D 3 offset(2,36)
		BARG A 0 A_Refire("jamderp")
		goto ready
	unjam:
		TNT1 A 0 A_TakeInventory("BossJam")
	chamber:
		BARF A 0 A_PlaySound("weapons/boltback")
		BARG C 2 offset(4,38)
		BARG D 2 offset(6,42)
		TNT1 A 0 A_JumpIfInventory("IsSupported",1,3)
		TNT1 A 0 A_SetPitch(pitch-frandom(-0.2,0.8),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+frandom(-0.4,0.8),SPF_INTERPOLATE)
		TNT1 A 0 A_SetPitch(pitch-frandom(-0.1,0.3),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+frandom(-0.1,0.3),SPF_INTERPOLATE)
		TNT1 A 0 A_Jump(8,"jam")
		BARG D 1 offset(6,42)
		TNT1 A 0 A_JumpIfInventory("BossChamber",1,"chamberderp")
		TNT1 A 0 A_JumpIfInventory("BossSpentChamber",1,1)
		goto chamber2
		TNT1 A 0 A_SpawnItemEx("HDSpent7mm",cos(pitch)*8,1,height-7-sin(pitch)*8,cos(pitch)*cos(angle-80)*6+velx,cos(pitch)*sin(angle-80)*6+vely,-sin(pitch)*6+velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		TNT1 A 0 A_TakeInventory("BossSpentChamber")
		goto chamber2
	chamberderp:
		TNT1 A 0 A_JumpIfInventory("BossChamber",1,1)
		goto chamber2
		TNT1 A 0 A_SpawnItemEx("HDLoose7mm",cos(pitch)*8,1,height-7-sin(pitch)*8,cos(pitch)*cos(angle-80)*4+velx,cos(pitch)*sin(angle-80)*4+vely,-sin(pitch)*4+velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		TNT1 A 0 A_TakeInventory("BossChamber",1)
	chamber2:
		BARG A 0 A_JumpIfInventory("BossMag",1,1)
		goto chamber3
		BARG A 0 A_GiveInventory("BossChamber",1)
		BARG A 0 A_TakeInventory("BossMag",1)
	chamber3:
		BARG D 2 offset(6,42)
	chamber4:
		BARG E 1 offset(6,42) A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_Refire("chamber4")
	chamberend:
		BARG E 1 offset(3,34) A_WeaponReady(WRF_NOFIRE)
		BARG E 1 offset(3,34) A_PlaySound("weapons/boltfwd")
		BARG E 3 offset(4,38)
		TNT1 A 0 A_JumpIfInventory("IsSupported",1,3)
		TNT1 A 0 A_SetPitch(pitch+frandom(-0.2,0.8),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle-frandom(-0.4,0.8),SPF_INTERPOLATE)
		BARG D 2 offset(3,37)
		BARG C 3 offset(2,36)
		BARG B 3 offset(0,34)
		goto ready
	zoom:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_TakeInventory("IsWeaponReady")
		TNT1 A 0 A_JumpIfInventory("BossZoom",1,3)
		TNT1 A 0 A_GiveInventory("BossZoom",1)
		TNT1 A 0 A_Jump(256,2)
		BARG A 1 A_TakeInventory("BossZoom")
/*
		BARG A 1 offset (0,34)
		BARG A 1 offset (0,38)
		BARG A 1 offset (0,42)
		BARG A 1 offset (0,38)
		BARG A 1 offset (0,34)
*/
		BARG A 0 A_JumpIfInventory("PressingZoom",1,"zoomadjust")
		goto nope
	zoomadjust:
		BARG A 1 A_WeaponReady(WRF_NOFIRE)
		BARG A 0{
			A_GiveInventory("WeaponBusy",1);
			if (countinv("PressingFire")+countinv("PressingAltfire")>0){
				return state("zoomclick");
			} else if(countinv("PressingFireMode")>0){return state("zoomadjust");}
			if(countinv("PressingZoom")>0){return state("zoomadjust");}
			return state("nope");
		}
	zoomclick:
		BARG A 0{
			A_GiveInventory("BossZoom",1);
			if(countinv("PressingFire")>0){A_GiveInventory("BossZoomDegree",1);}
			else if (countinv("PressingAltFire")>0){A_TakeInventory("BossZoomDegree",1);}
		}
	zoomclicked:
		BARG A 1{
			A_WeaponReady(WRF_NOFIRE);
			if(countinv("PressingFire")+countinv("PressingAltfire")>0){return state("");}
			return state("zoomadjust");
		}
		goto zoomclicked
	reload:
		TNT1 A 0 A_TakeInventory("IsWeaponReady")
		BARG A 1 offset (0,34)
		BARG A 1 offset (2,36)
		BARG A 1 offset (4,40)
		TNT1 A 0 A_TakeInventory("IsWeaponLong")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-4,-14),random(4,8),0)
		BARG A 2 offset (8,42) A_PlayWeaponSound ("weapons/rifleclick2")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-4,-14),random(4,8),0)
		BARG A 4 offset (14,46) A_PlayWeaponSound ("weapons/rifleload")
		TNT1 A 0 A_JumpIfInventory("BossMag",0,"reloaddone")
		TNT1 A 0 A_JumpIfInventory("BossMag",1,"loadhand")
		TNT1 A 0 A_JumpIfInventory("BossClip",1,"loadclip")
	loadhand:
		TNT1 A 0 A_JumpIfInventory("SevenMilAmmo",1,"loadhandloop")
		TNT1 A 0 A_JumpIfInventory("BossClip",1,"stripbossclip")
		TNT1 A 0 A_JumpIfInventory("LiberatorMag",1,"striplibmag")
		goto reloaddone
	stripbossclip:
		BARG AAA 3 A_PlayWeaponSound("weapons/rifleclick2")
		TNT1 A 0 A_TakeInventory("BossClip",1)
		TNT1 A 0 A_GiveInventory("SevenMilAmmo",10)
		goto loadhandloop
	striplibmag:
		BARG AA 4 A_PlayWeaponSound("weapons/rifleclick2")
		BARG AA 3 A_PlayWeaponSound("weapons/rifleclick2")
		BARG AAAAAA 2 A_PlayWeaponSound("weapons/rifleclick2")
		TNT1 A 0 A_TakeInventory("LiberatorMag",1)
		TNT1 A 0 A_GiveInventory("SevenMilAmmo",30)
		goto loadhandloop
	loadhandloop:
		TNT1 A 0 A_JumpIfInventory("SevenMilAmmo",1,1)
		goto loadone
		TNT1 A 0 A_JumpIfInventory("BossHand",0,"loadone")
		TNT1 A 0 A_TakeInventory("SevenMilAmmo",1)
		TNT1 A 0 A_GiveInventory("BossHand",1)
		BARG A 4 A_PlayWeaponSound("weapons/pocket")
		loop
	loadone:
		BARG A 0 A_JumpIfInventory("BossHand",1,1)
		goto reloadcontinue
		BARG A 0 A_JumpIfInventory("BossMag",0,"reloaddone")
		TNT1 A 0 A_TakeInventory("BossHand",1)
		TNT1 A 0 A_GiveInventory("BossMag",1)
		BARG A 2 offset(16,50) A_PlayWeaponSound("weapons/rifleclick2")
		BARG A 4 offset(14,46)
		loop
	loadclip:
		BARG AA 4 offset(16,50) A_PlayWeaponSound("weapons/rifleclick2")
		BARG AA 3 offset(17,52) A_PlayWeaponSound("weapons/rifleclick2")
		BARG AAAAAA 2 offset(16,50) A_PlayWeaponSound("weapons/rifleclick2")
		TNT1 A 0 A_PlayWeaponSound("weapons/rifleclick")
		TNT1 A 0 A_TakeInventory("BossClip",1)
		TNT1 A 0 A_GiveInventory("BossMag",10)
		BARG A 4 offset(14,46)
		goto reloaddone
	reloaddone:
		BARG A 1 offset (4,40)
		BARG A 1 offset (2,36)
		BARG A 1 offset (0,34)
	unloadhand:
		BARG A 0 A_JumpIfInventory("BossHand",1,1)
		goto nope
		BARG A 0 A_TakeInventory("BossHand",1)
		BARG A 0 A_GiveInventory("SevenMilAmmo",1)
		loop
	unload:
		TNT1 A 0 A_TakeInventory("IsWeaponReady")
		BARG A 1 offset (0,34)
		BARG A 1 offset (2,36)
		BARG A 1 offset (4,40)
		TNT1 A 0 A_TakeInventory("IsWeaponLong")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-4,-14),random(4,8),0)
		BARG A 2 offset (8,42) A_PlayWeaponSound ("weapons/rifleclick2")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-4,-14),random(4,8),0)
		BARG A 4 offset (14,46) A_PlayWeaponSound ("weapons/rifleload")
		BARG A 0 A_JumpIfInventory("BossMag",1,"unload1")
		goto unloaddone
	unload1:
		BARG A 0 A_TakeInventory("BossMag",1)
		BARG A 0 A_JumpIfInventory("SevenMilAmmo",0,"unloaddrop")
		BARG A 0 A_GiveInventory("SevenMilAmmo",1)
		goto unloadloop
	unloaddrop:
		BARG A 0 A_SpawnItemEx("HDLoose7mm",cos(pitch)*8,0,height-7-sin(pitch)*8,cos(pitch)*cos(angle-40)*1+velx,cos(pitch)*sin(angle-40)*1+vely,-sin(pitch)*1+velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		goto unloadloop
	unloadloop:
		BARG A 4 offset (3,41) A_PlaySound("weapons/rifleclick2")
		BARG A 2 offset (2,42)
		BARG A 0 A_JumpIfInventory("BossMag",1,1)
		goto unloaddone
		BARG A 0{
			if(countinv("PressingUnload")+countinv("PressingReload")+countinv("PressingFire")+countinv("PressingAltfire")>0){return state("unloaddone");}return state("unload1");
		}
	unloaddone:
		BARG A 2 offset (2,42)
		BARG A 3 offset (3,41)
		BARG A 1 offset (4,40) A_PlayWeaponSound("weapons/rifleclick")
		BARG A 1 offset (2,36)
		BARG A 1 offset (0,34)
		goto ready
	reloadcontinue:
		BARG A 0{
			if(countinv("PressingUnload")+countinv("PressingReload")+countinv("PressingFire")+countinv("PressingAltfire")>0){return state("reloaddone");}return state("");
		}
		BARG A 0 A_JumpIfInventory("SevenMilAmmo",1,1)	//don't strip clips automatically
		goto reloaddone
		BARG A 8 offset(16,48) A_PlaySound("weapons/pocket")
		goto loadhandloop
	xloadabort:
		"----" A 1 A_WeaponReady(WRF_NOFIRE)
		"----" A 0 A_JumpIfInventory("PressingReload",1,"xloadabort")
		"----" A 0 A_JumpIfInventory("PressingUnload",1,"xloadabort")
		goto reloaddone
	moaadjust:
		BARG A 3 A_WeaponReady(WRF_NOFIRE)
		BARG A 0{
			A_GiveInventory("WeaponBusy",1);
			if (countinv("PressingFire")+countinv("PressingAltfire")>0){
				return state("moaclick");
			} else if(countinv("PressingFireMode")>0){return state("moaadjust");}
			return state("nope");
		}
	moaclick:
		BARG A 0{
			if (countinv("PressingFire")>0){A_GiveInventory("BossMOA",1);}
			else if (countinv("PressingAltFire")>0){A_TakeInventory("BossMOA",1);}
			return state("moaadjust");
		}

	spawn:
		TNT1 A 1
		TNT1 A 0 A_CheckProximity("spawndropped","PlayerPawn",20,1, CPXF_COUNTDEAD|CPXF_SETTARGET|CPXF_ANCESTOR|CPXF_CLOSEST|CPXF_NOZ)
	spawnfresh:
		TNT1 A 0 A_SpawnItemEx("BossRiflePickup",0,0,0,velx,vely,velz,0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_NOPOINTERS)
		stop
	spawndropped:
		TNT1 A 0 A_JumpIfInTargetInventory("BossRifle",1,"spawnfresh")
		TNT1 A 0 A_SpawnItemEx("BossRiflePickup",0,0,0,velx,vely,velz,0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS)
		stop
	}
}
actor BossEmptyHand : ActionItem
{
	states
	{
	pickup:
		TNT1 A 0 A_JumpIfInventory("BossHand",1,1)
		fail
		TNT1 A 0 A_TakeInventory("BossHand",1)
		TNT1 A 0 A_JumpIfInventory("SevenMilAmmo",0,2)
		TNT1 A 0 A_GiveInventory("SevenMilAmmo",1)
		loop
		TNT1 A 0 A_SpawnItemEx("HDLoose7mm",cos(pitch)*5,1,height-7-sin(pitch)*5,cos(pitch)*cos(angle)*random(1,4)+velx,cos(pitch)*sin(angle)*random(1,4)+vely,-sin(pitch)*random(1,4)+velz,0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		loop
	}
}



