// ------------------------------------------------------------
// Player classes
// ------------------------------------------------------------
actor HDP_Custom:HDPlayerPawn{
	player.displayname "Custom"
	player.startitem CustomLoadout
}
actor HDP_AR:HDPlayerPawn{
	player.displayname "Rifleman"
	player.startitem ZM66AssaultRifle
	player.startitem SoldierExtras
	player.startitem ZM66RifleMags 5
	player.startitem HDRocketAmmo 8
	player.startitem HDFragGrenadeAmmo 8
}
actor HDP_BR:HDPlayerPawn{
	player.displayname "Classic Rifleman"
	player.startitem LiberatorRifle
	player.startitem SoldierExtras
	player.startitem SevenMilAmmo 20
	player.startitem LiberatorMag 4
	player.startitem HDRocketAmmo 8
	player.startitem HDFragGrenadeAmmo 8
}
actor HDP_SG:HDPlayerPawn{
	player.displayname "Enforcer"
	player.startitem Hunter
	player.startitem SoldierExtras
	player.startitem HDShellAmmo 24
	player.startitem Blooper
	player.startitem HDRocketAmmo 4
	player.startitem HDFragGrenadeAmmo 12
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY ABCDABCDABCDABCDABCD 4
		PLAY E 25
		PLAY F 2
		PLAY E 15
		PLAY F 2
		PLAY E 18
		PLAY F 2
		PLAY E 35
		loop
	}
}
actor HDP_SM:HDPlayerPawn{
	player.displayname "Infiltrator"
	player.startitem HDSMG
	player.startitem SoldierExtras
	player.startitem HDSMGMag 5
	player.startitem Blooper
	player.startitem HDRocketAmmo 4
	player.startitem HDFragGrenadeAmmo 6
	player.startitem HDPistolAmmo 20
	player.startitem PortableLiteAmp
}
actor HDP_MG:HDPlayerPawn{
	player.displayname "Support Gunner"
	player.startitem Vulcanette
	player.startitem SoldierExtras
	player.startitem HDCellPacks 2
	player.startitem ZM66RifleMags 6
	player.startitem HDFragGrenadeAmmo 8
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY AAABBCCCDDAAABBCCCDDAAABBCCCDDAAABBCCCDD 2
		PLAY E 25
		PLAY FEEEEFEEFEEFEEFEEF 1
		PLAY E 16
		PLAY FEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEF 1
		PLAY E 10
		PLAY FEEFEEFEEFEEFEEFEEF 1
		PLAY E 12
		PLAY FEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEFEEF 1
		PLAY E 35
		loop
	}
}
actor HDP_GL:HDPlayerPawn{
	player.displayname "Grenadier"
	player.startitem HDRL
	player.startitem SoldierExtras
	player.startitem Blooper
	player.startitem HDRocketAmmo 16
	player.startitem HEATAmmo 3
	player.startitem HDFragGrenadeAmmo 12
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY ABCDABCDABCDABCDABCD 4
		PLAY E 25
		PLAY F 6
		PLAY E 12
		PLAY F 6
		PLAY E 12
		PLAY F 6
		PLAY E 35
		loop
	}
}
actor HDP_BO:HDPlayerPawn{
	player.displayname "Bombardier"
	player.startitem Brontornis
	player.startitem SoldierExtras
	player.startitem BrontornisRound 20
	player.startitem HDFragGrenadeAmmo 6
	player.startitem HDSMG
	player.startitem HDSMGMag 1
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY ABCDABCDABCDABCDABCD 4
		PLAY E 25
		PLAY F 2
		PLAY EGAD 5
		PLAY E 35
		loop
	}
}
actor HDP_PR:HDPlayerPawn{
	player.displayname "Specialist"
	player.startitem ThunderBuster
	player.startitem SoldierExtras
	player.startitem HDCellPacks 6
	player.startitem HDFragGrenadeAmmo 6
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY ABCDABCDABCDABCDABCD 4
		PLAY E 15
		PLAY FEEEEEFEEEEFEEEFEEFEFEFEFEFEFEFEFEFEFFEFFEFFFEE 1
		PLAY EGAD 5
		PLAY E 35
		loop
	}
}
actor HDP_BF:HDPlayerPawn{
	player.displayname "Hideous Destructor"
	player.startitem BFG9K
	player.startitem SoldierExtras
	player.startitem HDCellPacks 6
	player.startitem HDFragGrenadeAmmo 6
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY AAABBCCCDDAAABBCCCDDAAABBCCCDDAAABBCCCDD 2
		PLAY E 8
		PLAY FEEEEFEEEFEEFEFEFEFEFEFEFEFEFEFFEFFFEFFFFEE 1
		PLAY G 10
		PLAY AD 4
		PLAY E 35
		loop
	}
}
actor HDP_SS:HDPlayerPawn{
	player.displayname "Contractor"
	player.startitem SlayerOrBoss
	player.startitem HDPistol
	player.startitem SoldierExtras
	player.startitem BlackArmour 999
	player.startitem HDSecondPistol
	player.startitem HDBackpack 1
	player.startitem HDPistolMag 2
	player.startitem HDFragGrenadeAmmo 8
	player.startitem Blooper
	player.startitem HDRocketAmmo 4
	player.startitem PortableMedikit 4
	player.startitem PortableStimpack 8
	player.startitem PortableLiteAmp
	player.startitem HDCellPacks 2
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY ABCDABCDABCDABCDABCD 4
		PLAY E 8
		PLAY F 3
		PLAY E 1
		PLAY F 3
		PLAY E 35
		loop
	}
}
actor HDP_BA:HDPlayerPawn{
	player.displayname "Marksman"
	player.startitem BossRifle
	player.startitem HDPistolFullAuto
	player.startitem SoldierExtras
	player.startitem BossClip 10
	player.startitem HDFragGrenadeAmmo 6
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY ABCDABCDABCDABCDABCD 4
		PLAY E 8
		PLAY F 2
		PLAY E 24
		PLAY F 2
		PLAY E 24
		PLAY F 2
		PLAY E 35
		loop
	}
}
actor HDP_BS:HDPlayerPawn{
	player.displayname "Medic"
	player.startitem HDPistolFullAuto
	player.startitem SoldierExtras
	player.startitem Lumberjack
	player.startitem PortableBerserkPack 8
	player.startitem PortableStimpack 8
	player.startitem PortableMedikit 8
	player.startitem PortableLiteAmp
	player.startitem PortableRadsuit
	player.startitem HDCellPacks 3
	player.startitem HDBackpack 1
	player.startitem HDFragGrenadeAmmo 1
}
actor HDP_BT:HDPlayerPawn{
	player.displayname "Roboticist"
	player.startitem HDPistol
	player.startitem SoldierExtras
	player.startitem HDFragGrenadeAmmo 4
	player.startitem HDPistolMag 5
	player.startitem ZM66RifleMags 6
	player.startitem PortableMedikit 2
	player.startitem PortableStimpack 3
	player.startitem HDCellPacks 6
	player.startitem DudRocketAmmo 6
	player.startitem HERPUsable 1
	player.startitem HDIEDKit 12
	player.startitem HDBackpack 1
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY AAABBCCCDDAAABBCCCDDAAABBCCCDDAAABBCCCDD 2
		PLAY E 24
		PLAY F 1
		PLAY E 16
		PLAY F 1
		PLAY E 8
		PLAY F 1
		PLAY E 12
		PLAY F 1
		PLAY E 35
		loop
	}
}
actor HDP_DG:HDPlayerPawn{
	player.displayname "Doomed Guy"
	player.startitem DoomguyLoadout
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY ABCDABCDABCDABCDABCD 3
		PLAY E 10
		PLAY F 1
		PLAY E 7
		PLAY F 1
		PLAY E 12
		PLAY F 1
		PLAY E 6
		PLAY F 1
		PLAY E 6
		PLAY F 1
		PLAY E 4
		PLAY F 1
		PLAY E 35
		loop
	}
}
actor HDP_SC:HDPlayerPawn{
	player.displayname "Security Guard"
	player.startitem GreenArmour 144
	player.startitem PortableMedikit 2
	player.startitem HDPistol
	player.startitem HDPistolMag 1
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY ABCDABCDABCDABCDABCD 4
		PLAY E 24
		PLAY F 1
		PLAY E 16
		PLAY F 1
		PLAY E 8
		PLAY F 1
		PLAY E 12
		PLAY F 1
		PLAY E 35
		loop
	}
}
actor HDP_RB:HDPlayerPawn{
	player.displayname "Insurgent"
	player.startitem RebelLoadout
}
actor HDP_NK:HDPlayerPawn{
	player.displayname "Naturist"
	states{
	see:
		"----" A 0 A_Jump(256,"see0")
	seepreview:
		PLAY ABCDABCDABCDABCDABCD 3
		PLAY DGDGDGD 6
		loop
	}
}


actor RebelLoadout:ActionItem{
	states{
	pickup:
		TNT1 A 0 A_Jump(64,"weapon0","weapon1","weapon3","weapon4a","weapon10")
		TNT1 A 0 A_Jump(220,"weapon0","weapon1","weapon2","weapon1a","weapon5a","weapon3","weapon4a","weapon6a")
		TNT1 A 0 A_Jump(256,"weapon4","weapon5","weapon6","weapon7","weapon8","weapon9","weapon6b","weapon11")
	weapon0:
		TNT1 A 0{
			A_GiveInventory("Lumberjack",1);
			A_GiveInventory("LumberCell",1200);
			A_GiveInventory("HDCellPacks",random(1,4));
		}goto otherstuff
	weapon1a:
		TNT1 A 0 A_GiveInventory("HDSMG",1)
		TNT1 A 0 A_GiveInventory("HDSMGMag",random(1,2))
		TNT1 A 0 A_Jump(16,1)
		goto otherstuff
	weapon1:
		TNT1 A 0 A_GiveInventory("HDPistol",1)
		TNT1 A 0 A_GiveInventory("HDPistolMag",random(1,5))
		goto otherstuff
	weapon2:
		TNT1 A 0 A_GiveInventory("Hunter")
		TNT1 A 0 A_GiveInventory("HDShellAmmo",random(1,24))
		goto otherstuff
	weapon3:
		TNT1 A 0 A_GiveInventory("Slayer",1)
		TNT1 A 0 A_GiveInventory("HDShellAmmo",random(1,24))
		goto otherstuff
	weapon4a:
		TNT1 A 0 A_GiveInventory("ZM66AltStarter",2)
		TNT1 A 0 A_GiveInventory("ZM66RifleMags",random(1,5))
		goto otherstuff
	weapon4:
		TNT1 A 0 A_GiveInventory("ZM66AltStarter",1)
		TNT1 A 0 A_GiveInventory("ZM66RifleMags",random(1,5))
		goto otherstuff
	weapon5:
		TNT1 A 0 A_GiveInventory("HDRL",1)
		TNT1 A 0 A_GiveInventory("HDRocketAmmo",random(1,10))
		goto otherstuff
	weapon5a:
		TNT1 A 0 A_GiveInventory("Blooper",1)
		TNT1 A 0 A_GiveInventory("HDRocketAmmo",random(1,12))
		goto otherstuff
	weapon6b:
		TNT1 A 0 A_GiveInventory("LiberatorRifle",1)
		TNT1 A 0 A_GiveInventory("HDRocketAmmo",random(1,6))
		goto weapon6a+1
	weapon6a:
		TNT1 A 0 A_GiveInventory("LiberatorNoGLItem",1)
		TNT1 A 0 A_GiveInventory("LiberatorMag",random(1,6))
		TNT1 A 0 A_GiveInventory("SevenMilAmmo",random(1,30))
		goto otherstuff
	weapon6:
		TNT1 A 0 A_GiveInventory("ThunderBuster",1)
		TNT1 A 0 A_GiveInventory("HDCellPacks",random(1,6))
		goto otherstuff
	weapon7:
		TNT1 A 0 A_GiveInventory("BFG9k",1)
		TNT1 A 0 A_GiveInventory("HDCellPacks",random(1,6))
		goto otherstuff
	weapon8:
		TNT1 A 0 A_GiveInventory("ZM66AssaultRifle")
		TNT1 A 0 A_GiveInventory("ZM66RifleMags",random(1,5))
		TNT1 A 0 A_GiveInventory("HDRocketAmmo",random(1,6))
		goto otherstuff
	weapon9:
		TNT1 A 0 A_GiveInventory("Brontornis",1)
		TNT1 A 0 A_GiveInventory("BrontornisRound",random(1,12))
		goto otherstuff
	weapon10:
		TNT1 A 0 A_GiveInventory("BossRifle",1)
		TNT1 A 0 A_GiveInventory("BossClip",random(1,12))
		TNT1 A 0 A_GiveInventory("SevenMilAmmo",random(1,50))
		goto otherstuff
	weapon11:
		TNT1 A 0 A_GiveInventory("Vulcanette")
		TNT1 A 0 A_GiveInventory("ZM66RifleMags",random(1,5))
		TNT1 A 0 A_GiveInventory("HDCellPacks",random(1,4))
		goto otherstuff
	otherstuff:
		TNT1 A 0 A_Jump(164,1)
		goto weapon1
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_GiveInventory("HDBackpack",1)
		TNT1 A 0 A_Jump(164,2)
		TNT1 A 0 A_GiveInventory("MapRevealer",1)
		TNT1 A 0 A_Jump(164,2)
		TNT1 A 0 A_GiveInventory("PortableRadSuit",random(1,2))
		TNT1 A 0 A_Jump(164,2)
		TNT1 A 0 A_GiveInventory("HDSecondPistol")
		TNT1 A 0 A_Jump(254,2)
		TNT1 A 0 A_GiveInventory("HDPistolFullAuto")
		TNT1 A 0 A_Jump(164,2)
		TNT1 A 0 A_GiveInventory("PortableLiteAmp",random(1,2))
		TNT1 A 0 A_Jump(194,2)
		TNT1 A 0 A_GiveInventory("PortableBerserkPack",random(0,2))
		TNT1 A 0 A_Jump(64,2)
		TNT1 A 0 A_GiveInventory("PortableStimpack",random(0,6))
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_GiveInventory("HDIEDKit",random(0,12))
		TNT1 A 0 A_Jump(196,2)
		TNT1 A 0 A_GiveInventory("DudRocketAmmo",random(0,6))
		TNT1 A 0 A_Jump(64,2)
		TNT1 A 0 A_GiveInventory("PortableMedikit",random(0,6))
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_GiveInventory("DERPUsable")
		TNT1 A 0 A_Jump(250,3)
		TNT1 A 0 A_GiveInventory("HERPUsable",1)
		TNT1 A 0 A_GiveInventory("ZM66RifleMags",random(1,4))
		TNT1 A 0 A_Jump(220,2)
		TNT1 A 0 A_GiveInventory("HDCellPacks",random(1,6))
		TNT1 A 0 A_Jump(220,2)
		TNT1 A 0 A_GiveInventory("HDPistolAmmo",random(1,6))
		TNT1 A 0 A_Jump(240,2)
		TNT1 A 0 A_GiveInventory("HDFragGrenadeAmmo",random(0,5))
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_GiveInventory("SquadSummoner",random(0,3))
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_GiveInventory("SquadSummoner",random(0,3))
		TNT1 A 0 A_Jump(240,2)
		TNT1 A 0 A_GiveInventory("GreenArmour",144)
		TNT1 A 0
		fail
	}
}


actor Taunter:ActionItem{
	states{
	use:
		TNT1 A 0{
			A_PlaySound("*taunt",CHAN_VOICE);
			A_ChangeFlag("shadow",0);
			A_TakeInventory("powerfrightener");
			A_FireCustommissile("DelayedTaunter",0,0);
		}fail
	}
}
actor DelayedTaunter{
	+missile -solid +noblockmap height 0 radius 0 speed 2
	states{
	spawn:
	death:
		TNT1 A 18
		TNT1 A 0 A_AlertMonsters
		stop
	}
}


// ------------------------------------------------------------
// Null weapon
// ------------------------------------------------------------
actor NulledWeapon:InventoryFlag{}
actor NullWeapon:Weapon{
	+weapon.wimpy_weapon
	+weapon.cheatnotweapon
	+nointeraction
	+weapon.noalert
	+inventory.untossable
	height 0
	radius 0
	states{
	spawn:
		TNT1 A 0
		stop
	select:
		TNT1 A 0{
			A_TakeInventory("PowerFrightener",1);
			A_GiveInventory("NulledWeapon",1);
			A_SetCrosshair(21);
		}
		TNT1 A 0 A_Raise
		wait
	deselect:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_Lower
		wait
	ready:
		TNT1 A 1{
			A_SetCrosshair(21);
			A_WeaponReady(WRF_NOFIRE);
			A_TakeInventory("WeaponBusy");
		}loop
	fire:
		TNT1 A 1
		goto ready
	}
}
