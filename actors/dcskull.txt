
// ------------------------------------------------------------
//   Lost Soul
// ------------------------------------------------------------

ACTOR FireDancer : LostSoul replaces LostSoul
{
	+NOBLOOD +PUSHABLE -FLOORCLIP +AVOIDMELEE
//	+FLOATBOB
	Obituary "%o found a skull."
	ActiveSound ""
	Scale 0.6
	Radius 12
	Height 26
	Speed 4
	Damage 3
	Health 70
	Alpha 1.0 renderstyle normal
	States
	{
	Spawn:
		SKUL A 0 nodelay{
			if(countinv("Clip")<1){
				A_GiveInventory("Clip",1);
				A_SetScale(scalex*frandom(0.8,1.3));
				A_SetSize(radius*scalex,height*scaley);
				mass*=scalex;
				if(scalex>1.0){
					A_SetHealth(health*scalex);
				}
			}
		}
		SKUL A 0 A_LowGravity
		TNT1 A 0 A_ChangeFlag("floatbob",0)
		SKUL AAABBB 4 BRIGHT A_Look
		SKUL A 0 A_Jump(12,1)
		Loop
		SKUL A 1 BRIGHT A_PlaySound("skull/melee")
		Loop
	See:
		TNT1 A 0 A_GiveInventory("Shell", 1)
		SKUL A 0 A_NoGravity
//		TNT1 A 0 A_ChangeFlag("floatbob",1)
	see2:
		TNT1 A 0 A_Chase
		TNT1 A 0 A_ScaleVelocity(0.8)
		SKUL A 3 bright A_ChangeVelocity(0.4,0,frandom(-0.6,0.6),CVF_RELATIVE)
		TNT1 A 0 A_Chase
		TNT1 A 0 A_ScaleVelocity(0.8)
		SKUL B 3 bright A_ChangeVelocity(0.4,0,frandom(-0.6,0.6),CVF_RELATIVE)
	roam:
		TNT1 A 0 A_JumpIfTargetInLOS("see2")
		TNT1 A 0 A_Wander
		SKUL B 3 bright A_ChangeVelocity(frandom(-0.3,1),random(-2,2),frandom(-0.6,0.6),CVF_RELATIVE)
		SKUL A 0 A_Jump(4,1)
		loop
		SKUL A 0 A_PlaySound("skull/melee")
		loop
	Missile:
		SKUL A 2 bright A_SpawnItemEx("ImpBallTail", random(-4,-6),random(-1,1),random(4,6), velx*0.7,vely*0.7,velz*0.7+1, 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SKUL B 2 bright A_FaceTarget
		SKUL A 2 bright A_SpawnItemEx("ImpBallTail", random(-4,-6),random(-1,1),random(4,6), velx*0.7,vely*0.7,velz*0.7+1, 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SKUL B 2 bright A_FaceTarget
		SKUL A 2 bright A_SpawnItemEx("ImpBallTail", random(-4,-6),random(-1,1),random(4,6), velx*0.7,vely*0.7,velz*0.7+1, 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SKUL B 2 bright A_FaceTarget
		SKUL C 2 BRIGHT A_SkullAttack
			//intentional fall-through
	Fly:
		SKUL C 1 BRIGHT A_SpawnItemEx("ImpBallTail", -4,0,8, velx*0.7,vely*0.7,velz*0.7+1, 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SKUL C 0 BRIGHT ThrustThingZ(0,random(1,4),random(0,1),1)
		SKUL D 2 BRIGHT
		SKUL D 1 BRIGHT A_SpawnItemEx("ImpBallTail", -4,0,8, velx*0.7,vely*0.7,velz*0.7+1, 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SKUL C 2 BRIGHT
		SKUL C 0 BRIGHT ThrustThingZ(0, random(1,4), random(0,1), 1)
		SKUL C 0 BRIGHT A_GiveInventory("Clip",1)
		SKUL C 0 BRIGHT A_JumpIfInventory("Clip",4,1)
		loop
		SKUL C 2 BRIGHT A_TakeInventory("Clip")
		SKUL D 2 BRIGHT A_SpawnItemEx("ImpBallTail", -4,0,8, velx*0.7,vely*0.7,velz*0.7+1, 0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SKUL CD 2 BRIGHT A_FastChase
		SKUL C 4 BRIGHT
		goto see
	Pain:
		TNT1 A 0 A_GiveInventory("HDFireEnder",5)
		SKUL E 3 bright
		SKUL E 3 bright A_Pain
		goto See
	Death:
		SKUL E 2 bright{
			A_SetTranslucent(1,1);
		}
		SKUL F 2 bright A_ChangeVelocity(0,0,1)
		TNT1 AAA 0 A_SpawnItemEx("HDSmokeChunk",0,0,3,velx+random(-4,4),vely+random(-4,4),velz+random(1,6),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		SKUL G 2 BRIGHT A_NoBlocking
		SKUL H 2 BRIGHT A_Scream
		SKUL HHH 0 A_SpawnItemEx("HDSmoke", random(-2,2),random(-2,2),random(4,8), velx,vely,velz+random(1,2), 0,168)
		SKUL HHHHHHHHH 0 A_SpawnItemEx("BigWallChunk", random(-4,4),random(-4,4),random(2,14), velx+random(-4,4),vely+random(-4,4),velz+random(-4,10), random(0,360),168)
		SKUL I 1 BRIGHT A_Explode(8, 96, 1)
		SKUL JJJJ 1 BRIGHT A_FadeOut(0.2)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_Jump(256,"dead")
	dead:
		TNT1 A -1 A_Gravity
	Raise:
		TNT1 AA 6 A_SpawnItemEx("HDFlameRed",random(-1,1),random(-1,1),random(1,3),0,0,velz+random(1,3),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 ThrustThingZ(0,2,0,1)
		TNT1 AAAA 2 A_SpawnItemEx("HDFlameRed",random(-1,1),random(-1,1),random(1,3),0,0,velz+random(1,3),0,SXF_NOCHECKPOSITION)
		SKUL JJJJ 1 bright A_FadeIn(0.2)
		SKUL IHGFE 2
		SKUL A 0 A_SetRenderStyle(1,STYLE_NORMAL)
		goto See
	}
}

actor DisappearingFireDancer:FireDancer{
	states{
	dead:
		TNT1 A 0
		stop
	}
}


// ------------------------------------------------------------
//   Pain Elemental
// ------------------------------------------------------------

ACTOR AngstElemental : PainElemental replaces PainElemental
{
	obituary "%o braved the elements."
	hitobituary "%o thought it was elementary that certain monsters had no melee attack."
	damagefactor "SmallArms0", 0.8
	damagefactor "SmallArms1", 0.9
	MeleeDamage 8
	speed 3
	+pushable
	minmissilechance 256
	pushfactor 0.3
	States
	{
	spawn:
		PAIN A 0 nodelay{
			if(countinv("Clip")<1){
				A_GiveInventory("Clip",1);
				A_SetScale(scalex*frandom(0.9,1.1));
				A_SetSize(radius*scalex,height*scaley);
				mass*=scalex;
				if(scalex>1.0){
					A_SetHealth(health*scalex);
				}
			}
		}
		goto super::spawn
	see:
		TNT1 AAAAAA 0 A_Chase("melee","missile",CHF_DONTMOVE)
		TNT1 A 0 A_ScaleVelocity(0.6)
		TNT1 A 0 A_JumpIfTargetInLOS(2)
		TNT1 A 0 A_Wander
		PAIN A 24 A_ChangeVelocity(0.2,0,frandom(-0.3,0.1),CVF_RELATIVE)
		TNT1 A 0 A_Chase("melee","missile",CHF_DONTMOVE)
		TNT1 A 0 A_ScaleVelocity(0.6)
		TNT1 A 0 A_JumpIfTargetInLOS(2)
		TNT1 A 0 A_Wander
		PAIN B 8 A_ChangeVelocity(0.36,0,frandom(-0.1,0.3),CVF_RELATIVE)
		TNT1 AAAAAA 0 A_Chase("melee","missile",CHF_DONTMOVE)
		TNT1 A 0 A_ScaleVelocity(0.6)
		TNT1 A 0 A_JumpIfTargetInLOS(2)
		TNT1 A 0 A_Wander
		PAIN C 24 A_ChangeVelocity(0.2,0,frandom(-0.3,0.1),CVF_RELATIVE)
		TNT1 A 0 A_Chase("melee","missile",CHF_DONTMOVE)
		TNT1 A 0 A_ScaleVelocity(0.6)
		TNT1 A 0 A_JumpIfTargetInLOS(2)
		TNT1 A 0 A_Wander
		PAIN B 8 A_ChangeVelocity(0.36,0,frandom(-0.1,0.3),CVF_RELATIVE)
		loop
	missile:
		PAIN ABCB 3 A_FaceTarget(10,10)
		TNT1 A 0 A_JumpIfTargetInLOS("missile2",20)
		TNT1 A 0 A_ChangeVelocity(0.8,0,frandom(-0.4,0.4),CVF_RELATIVE)
		TNT1 A 0 A_JumpIfTargetInLOS("missile")
		TNT1 A 0 A_Jump(40,"missile2")
		goto see
	missile2:
		TNT1 A 0 A_JumpIfInventory("VileCount",1,"NotVileEnough")
		PAIN DE 4 A_FaceTarget(5,5)
		PAIN F 6 A_FaceTarget(10,10)
		PAIN C 12 A_PainAttack("DisappearingFireDancer")
		Goto See
	melee:
		TNT1 A 0 A_JumpIfInventory("VileCount",1,"NotVileEnough")
		PAIN DE 4 A_FaceTarget(10,10)
		PAIN F 6 A_FaceTarget(10,10)
		PAIN C 12 A_MeleeAttack
		Goto See
	pain:
		PAIN G 6 A_ScaleVelocity(0.6)
		PAIN G 6 A_Pain
		goto missile
	Death:
		PAIN H 2 bright A_SpawnItemEx("tempshield", 0,0,0, velx,vely,velz, 0,40)
		PAIN I 3 bright A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 AAAA 0 A_SpawnItemEx("HDSmokeChunk", random(-7,7),random(-7,7),random(2,6), velx+random(-7,7),vely+random(-7,7),velz+random(4,8),0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		PAIN J 1 bright
		TNT1 A 0 A_Explode(60,200)
		TNT1 A 0 A_RadiusGive("HDFireStarter", 56, RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS|RGF_CORPSES,random(1,4))
		TNT1 AAAAA 0 A_SpawnItemEx("HDSmokeChunk", random(-7,7),random(-7,7),random(2,6), velx+random(-7,7),vely+random(-7,7),velz+random(4,8),0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		PAIN JJJKKK 1 bright A_SpawnItemEx("HDSmoke", random(-4,4), random(-4,4), random(-2,4), velx,vely,velz+random(1,4), random(0,360), 168, 16)
		PAIN L 0 bright A_PainDie
		PAIN LL 1 bright A_SpawnItemEx("HDSmoke", random(-4,4), random(-4,4), random(-2,4), velx,vely,velz+random(1,4), random(0,360), 168, 16)
		PAIN LLLMMMM 1 bright A_SpawnItemEx("HDSmoke", random(-4,4), random(-4,4), random(-2,4), velx,vely,velz+random(1,4), random(0,360), 168, 72)
		TNT1 AAAAAAA 3 bright A_SpawnItemEx("HDSmoke", random(-4,4), random(-4,4), random(-2,4), 0, 0, 0, random(0,360), 160, 92)
		TNT1 A -1
		stop
	Raise:
		TNT1 A 0 A_GiveInventory("VileCount",1)
		TNT1 AA 16 A_SpawnItemEx("HDFlameRed",random(-1,1),random(-1,1),random(1,3),0,0,velz+random(1,2),0,SXF_NOCHECKPOSITION)
		TNT1 AAA 6 A_SpawnItemEx("HDFlameRed",random(-2,2),random(-2,2),random(1,3),0,0,velz+random(1,2),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 ThrustThingZ(0,2,0,1)
		TNT1 AAAAA 2 A_SpawnItemEx("HDFlameRed",random(-4,4),random(-4,4),random(1,5),0,0,velz+random(1,3),0,SXF_NOCHECKPOSITION)
		TNT1 AAAAA 1 A_SpawnItemEx("HDFlameRed",random(-7,7),random(-7,7),random(1,6),0,0,velz+random(1,3),0,SXF_NOCHECKPOSITION)
		PAIN MLKJIH 4
		TNT1 A 0 A_JumpIfInventory("VileCount",3,"ReallyRaise")
		PAIN G 8
		PAIN H 0 A_PlaySound("pain/sight")
		PAIN H 0 A_FaceTarget

		PAIN AABBAA 3 A_Chase("NotVileEnough","NotVileEnough")
	NotVileEnough:
		PAIN H 0 A_StopSound(CHAN_BODY)
		PAIN H 0 A_Die("NeedMoreVile")
	Death.NeedMoreVile:
		PAIN H 2 bright A_SpawnItemEx("tempshield", 0,0,0, velx,vely,velz, 0,40)
		PAIN I 3 bright A_Scream
		TNT1 A 0 A_NoBlocking
		TNT1 AAAA 0 A_SpawnItemEx("HDSmokeChunk", random(-7,7),random(-7,7),random(2,6), velx+random(-7,7),vely+random(-7,7),velz+random(4,8),0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		PAIN J 1 bright
		TNT1 A 0 A_Explode(60,200)
		TNT1 A 0 A_RadiusGive("HDFireStarter", 56, RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS|RGF_CORPSES,1)
		TNT1 AAAAA 0 A_SpawnItemEx("HDSmokeChunk", random(-7,7),random(-7,7),random(2,6), velx+random(-7,7),vely+random(-7,7),velz+random(4,8),0, SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		PAIN JJJKKK 1 bright A_SpawnItemEx("HDSmoke", random(-4,4), random(-4,4), random(-2,4), velx,vely,velz+random(1,4), random(0,360), 168, 16)
		PAIN LL 1 bright A_SpawnItemEx("HDSmoke", random(-4,4), random(-4,4), random(-2,4), velx,vely,velz+random(1,4), random(0,360), 168, 16)
		PAIN LLLMMMM 1 bright A_SpawnItemEx("HDSmoke", random(-4,4), random(-4,4), random(-2,4), velx,vely,velz+random(1,4), random(0,360), 168, 72)
		TNT1 AAAAAAA 3 bright A_SpawnItemEx("HDSmoke", random(-4,4), random(-4,4), random(-2,4), 0, 0, 0, random(0,360), 160, 92)
		TNT1 A -1
	ReallyRaise:
		PAIN G 5 A_TakeInventory("VileCount",999)
		PAIN G 0 A_PlaySound("pain/sight")
		goto see
	}
}
