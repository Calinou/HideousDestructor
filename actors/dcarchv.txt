
// ------------------------------------------------------------
//   Archvile
// ------------------------------------------------------------

ACTOR BloodyHellFire
{
	+ISMONSTER
	+LOOKALLAROUND
	-SHOOTABLE +DONTHARMCLASS +MISSILEMORE
	+ALWAYSPUFF +PUFFONACTORS +FIXMAPTHINGPOS +PUFFGETSOWNER
	+nofear
	health 60
	damagefactor "Thermal", 0
	bloodcolor "44 3c 22"
	Damagetype "Thermal"
	Renderstyle Add
	Alpha 0.8
	Radius 16
	Height 44
	damage 24
	Speed 3
	maxstepheight 1024
	maxdropoffheight 1024
	SeeSound "misc/firecrkl"
	DeathSound "weapons/plasmas"
	Obituary "%o was roasted by an archvile."
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("Timer",1,"spawn2")
		TNT1 A 0 A_RearrangePointers(AAPTR_NULL,AAPTR_TARGET)
		TNT1 A 0 A_TransferPointer(AAPTR_MASTER,AAPTR_DEFAULT,AAPTR_TARGET,AAPTR_TARGET)
		TNT1 A 0 A_JumpIfInTargetInventory("IsImmuneToFire",1,"yokai")
		TNT1 A 0 A_JumpIf(z-floorz>40,"yokai")
		TNT1 AAAAAAA random(4,6) A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),velx+random(-1,1),vely+random(-1,1),velz+random(1,3),0,SXF_NOCHECKPOSITION)
		TNT1 AAAAAAA random(1,4) A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),velx+random(-1,1),vely+random(-1,1),velz+random(1,3),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_PlaySound("vile/firestrt",6)
		TNT1 A 0 A_SetShootable
		TNT1 AAAAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),velx+random(-1,1),vely+random(-1,1),velz+random(1,3),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_GiveInventory("Timer",random(120,240))
	goto see
	spawn2:
		FIRE AB 2 bright light("HELL") A_Look
		loop
	See:
		TNT1 A 0 A_GiveInventory("HDFireEnder",999)
		TNT1 A 0 A_GiveInventory("PowerFrightener",999)
		TNT1 A 0 A_PlaySound("misc/firecrkl")
		TNT1 A 0 A_JumpIfInventory("Timer",1,1)
		goto Detonate
		TNT1 A 0 A_TakeInventory("Timer",1)
		TNT1 A 0 A_SetScale((random(0,1)*2-1)*frandom(0.8,1.2),frandom(0.8,1.2))
		FIRE D 0 A_SpawnItemEx("BloodyHellFireTail",0,0,0,-1,0,1,0,SXF_NOCHECKPOSITION)
		FIRE D 0 A_VileChase
		FIRE CC random(1,2) bright light("HELL") A_Chase
		FIRE D 0 A_CustomMissile("BloodyHellFireTail", 0, 0, 0, 2, 0)
		FIRE DD random(1,2) bright light("HELL") A_Wander
		loop
	Death:
		FIRE CD 2 bright light("HELL")
		TNT1 A 0 A_NoBlocking
		TNT1 E 2 bright A_Scream
		FIRE FFGGHH 2 bright light("HELL") A_FadeOut(0.1)
		TNT1 A 6 A_ChangeFlag("nointeraction",1)
		TNT1 A 0 A_QuakeEx(2,2,5,20,0,256,"",QF_SCALEDOWN|QF_WAVE,10,10,10)
	Death2:
		TNT1 A 0 A_RadiusThrust(1024,128,RTF_NOIMPACTDAMAGE|RTF_NOTMISSILE,64)
		TNT1 A 0 A_JumpIfCloser(64,2)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_ChangeVelocity(0,0,12,CVF_RELATIVE,AAPTR_TARGET)
		TNT1 A 1 A_GiveToTarget("HDFireStarter",1)
		TNT1 AAA 0 A_SpawnItemEx("HDExplosion",random(-1,1),random(-1,1),24,0,0,random(4,9),0,SXF_NOCHECKPOSITION)
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("HugeWallChunk",random(-1,1),random(-1,1),random(1,2), random(4,12),0,random(8,16),random(1,360),SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		TNT1 AAAA 1 A_SpawnItemEx("HDSmokeChunk",random(-1,1),random(-1,1),random(1,2), random(4,8),0,random(8,12),random(1,360),SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,64)
		TNT1 A 20 A_AlertMonsters
		stop
	Missile:
		TNT1 A 0 A_CustomMissile("HeckFire", 0, 0, 0)
		FIRE D 0 A_CustomMissile("BloodyHellFireTail", 0, 0, 0, 2, 0)
		FIRE D random(1,2) bright light("HELL") A_Wander
		goto See
	yokai:
		TNT1 A 0 A_TransferPointer(AAPTR_MASTER,AAPTR_DEFAULT,AAPTR_MASTER,AAPTR_MASTER)
		TNT1 A 0 A_PlaySound("vile/firestrt")
		TNT1 AAAAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),velx+random(-1,1),vely+random(-1,1),velz+random(1,3),0,SXF_NOCHECKPOSITION)
		TNT1 AA 0 A_SpawnItemEx("Yokai",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		stop
	Melee:
	Detonate:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),velx+random(-1,1),vely+random(-1,1),velz+random(1,3),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Die
	Heal:
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),random(1,6),velx+random(-1,1),vely+random(-1,1),velz+random(1,3),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Die("Heal")
	Death.Heal:
		TNT1 A 0 A_FaceTarget
		FIRE CD 2 bright light("HELL")
		TNT1 A 0 A_NoBlocking
		TNT1 E 2 bright A_Scream
		FIRE FFGGHH 2 bright light("HELL") A_FadeOut(0.1)
		TNT1 A 6 A_ChangeFlag("nointeraction",1)
		stop
	}
}
ACTOR HeavenlyCarnageFlame : BloodyHellFire
{
	+friendly
	Species "Archangel"
}
ACTOR BloodyHellFireTail
{
	+nointeraction height 0 radius 0
	RenderStyle Add
	Damagetype "Thermal"
	Alpha 0.6
	speed 2
	States
	{
	Spawn:
		TNT1 A 0 A_SetScale((random(0,1)*2-1)*frandom(0.7,1.3),frandom(0.7,1.3))
		FIRE CDCDC random(1,2) bright
		FIRE BABA random(1,2) bright A_FadeOut(0.2)
		Stop
	}
}


ACTOR HeckFire
{
	projectile
	+ripper
	-nogravity
	gravity 0.6
	+slidesonwalls
	+noexplodefloor
	+seekermissile
	+nodamagethrust
	+bloodlessimpact
	-noblockmap
	damagetype "Thermal"
	renderstyle add
	alpha 0.7
	Speed 6
	radius 8
	height 16
	Damage(random(0,2))
	States
	{
	Spawn:
		FIRE A 0
		FIRE ABAB random(1,2) bright
		TNT1 A 0 A_Gravity
		FIRE ABAB random(1,2) bright A_SeekerMissile(2, 4)
	See:
		FIRE A 0 A_SetScale((random(0,1)*2-1)*frandom(0.8,1.2),frandom(0.8,1.2))
		FIRE A random(1,2) bright A_PlaySound("misc/firecrkl")
		FIRE B random(1,2) bright A_NoGravity
		FIRE A 0 A_SetScale((random(0,1)*2-1)*frandom(0.8,1.2),frandom(0.8,1.2))
		FIRE ABAB random(1,2) bright A_SeekerMissile(4, 8)
		FIRE A 0 A_SetScale((random(0,1)*2-1)*frandom(0.8,1.2),frandom(0.8,1.2))
		FIRE A random(1,2) bright A_Jump(24, 2)
		FIRE B random(1,2) bright A_Gravity
		Loop
	Death:
		FIRE CDCDEFEFGH 2 bright
		Stop
	}
}

actor IsImmuneToFire : InventoryFlag {}
ACTOR ArchFiend : Archvile replaces ArchVile
{
	Radius 16
	Height 56
	Scale 0.8
	RenderStyle Add
	BloodColor "ff ff 44"
	damagetype "Thermal"
	Speed 14
	PainChance 72
	Health 1000000
	+SHADOW
	+NOFEAR
	-NOTARGET
	-QUICKTORETALIATE
	+SEEINVISIBLE
	var int user_oldx;
	var int user_oldy;
	States
	{
	Spawn:
		VILE A 0 nodelay{
			if(countinv("Clip")<1){
				A_GiveInventory("Clip",1);
				A_SetScale(scalex*frandom(0.8,1.3));
				A_SetSize(radius*scalex,height*scaley);
				mass*=scalex;
				if(scalex>1.0){
					A_SetHealth(health*scalex);
				}
			}
			A_GiveInventory("IsImmuneToFire",1);
		}
		goto super::spawn
	See:
		VILE A 0 A_CheckFlag("friendly", "SeeFriendly")
		VILE A 0 A_AlertMonsters(0,AMF_TARGETNONPLAYER)
		VILE H 0 A_GiveInventory("PowerFrightener",1)
		VILE A 0 A_SetUserVar(user_oldx,x)
		VILE A 0 A_SetUserVar(user_oldx,y)
		VILE AABBCCDDEEFF 3 A_VileChase
		VILE A 0 A_JumpIf(user_oldx==x,1)
		loop
		VILE A 0 A_JumpIf(user_oldx==y,1)
		loop
		VILE A 0 A_ChangeFlag("NOCLIP",1)
		loop
	SeeFriendly:
		VILE H 0 A_GiveInventory("PowerFrightener",1)
		VILE H 0 A_ClearTarget
		VILE H 0 A_AlertMonsters(0,AMF_TARGETEMITTER)
		VILE AABBCCDDEEFF 3 A_VileChase
		loop
	Missile:
		VILE H 0 A_GiveInventory("PowerFrightener",1)
		TNT1 A 0 A_Jump(8,"Heal")
		VILE G 10 bright A_FaceTarget
		VILE H 0 A_ChangeFlag("SHOOTABLE", 1)
		VILE H 0 A_ChangeFlag("NOFEAR", 1)
		VILE H 2 bright light("CANF") A_ChangeFlag("FRIGHTENED", 0)
		VILE HHH 3 bright light("CANF") A_CustomMissile("HeckFire", 16, 0, random(-4,4))
		TNT1 A 0 A_FaceTarget
		VILE IJ 4 bright light("CANF")
		VILE K 4 bright light("HECK")
		VILE KLL 2 bright light("HECK") A_CustomMissile("HeckFire", 16, 0, 8)
		TNT1 A 0 A_FaceTarget
		VILE L 8 bright light("HELL")
		VILE MN 2 bright light("HELL")
		TNT1 A 0 A_FaceTarget
		VILE NOO 2 bright A_CustomMissile("HeckFire", 16, 0, -8)
		TNT1 A 0 A_FaceTarget
		TNT1 A 0 A_JumpIfTargetInLOS("MissileLong")
		TNT1 A 0 bright A_SpawnItemEx("Yokai", 4,0,30, 2,0,2, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
	MissileLong:
		TNT1 A 0 A_CheckFlag("friendly", "MissileLongFriendly")
		TNT1 A 0 A_CustomBulletAttack(0,0, 1, 1, "BloodyHellFire")
		goto MissileEnd
	MissileLongFriendly:
		TNT1 A 0 A_CustomBulletAttack(0,0, 1, 1, "HeavenlyCarnageFlame")
		goto MissileEnd
	MissileEnd:
		VILE PO 4 bright light("HELL")
		VILE N 8 bright light("HELL")
		VILE A 0 A_ChangeFlag("nopain",0)
		Goto See
	Heal:
		TNT1 A 0 A_RadiusGive("VileCount",64,RGF_CORPSES|RGF_MONSTERS,random(1,3))
		TNT1 A 0 A_CheckFlag("friendly","HealFriendly")
		VILE A 0 A_ChangeFlag("SHOOTABLE", 1)
		VILE H 0 A_ChangeFlag("NOFEAR", 1)
		VILE H 0 A_ChangeFlag("FRIGHTENED", 0)
		VILE "[\]" 8 bright light("HEAL")
		VILE "[\]" 3 bright light("HEAL") A_RadiusGive("HDRaiser", 256, RGF_CORPSES|RGF_MONSTERS,random(-1,1))
		VILE "[\]" 16 bright light("HEAL")
		Goto See
	HealFriendly:
		TNT1 A 0 A_RadiusGive("VileCount",64,RGF_CORPSES|RGF_MONSTERS,7)
		VILE A 0 A_ChangeFlag("SHOOTABLE", 1)
		VILE H 0 A_ChangeFlag("NOFEAR", 1)
		VILE H 0 A_ChangeFlag("FRIGHTENED", 0)
		VILE "[\]" 8 bright light("HEAL")
//		VILE H 0 A_RadiusGive("HDRaiserFriendly",256,RGF_CORPSES|RGF_MONSTERS,2)
		VILE "[\]" 16 bright light("HEAL")
		TNT1 A 0 A_ChangeFlag("nopain",0)
		TNT1 A 0 A_TakeInventory("Clip",1)
		Goto See
	Pain.Thermal:
		TNT1 A 0 A_GiveInventory("HDFireEnder",999)
		goto See
	Pain:
		VILE H 0 A_ChangeFlag("NOPAIN",1)
		TNT1 A 0 A_CheckFlag("friendly", "PainFriendly")
		TNT1 A 0 A_JumpIfInventory("Clip",5, "Dying")
		TNT1 A 0 A_SpawnItem("DistantRocket", 1, 1, 0, 0)
		VILE Q 0 A_SpawnItem("SpawnFire", 0.1, 28, 0, 0)
		VILE Q 0 A_Explode(10,96,XF_NOTMISSILE,0,64)
		VILE Q 0 bright A_Quake(3,36,0,360)
		VILE Q 0 A_ChangeFlag("SHOOTABLE", 0)
		VILE H 0 A_ChangeFlag("NOFEAR", 0)
		VILE Q 0 A_ChangeFlag("FRIGHTENED", 1)
		VILE Q 0 A_ChangeFlag("NOCLIP", 1)
		VILE Q 2 light("HELL") A_Pain
		VILE Q 20 light("HELL") A_GiveInventory("Clip",1)
		VILE H 0 A_ChangeFlag("NOPAIN",0)
		goto Pained
	Pained:
		VILE A 1 A_Chase("","")
		TNT1 AAA 0 A_Chase("","")
		VILE B 1 A_Chase("","")
		TNT1 AAA 0 A_Chase("","")
		VILE C 1 A_Chase("","")
		TNT1 AAA 0 A_Chase("","")
		VILE D 1 A_Chase("","")
		TNT1 AAA 0 A_Chase("","")
		VILE E 1 A_Chase("","",CHF_RESURRECT)
		TNT1 AAA 0 A_Chase("","",CHF_RESURRECT)
		VILE F 1 A_VileChase
		TNT1 AAA 0 A_Chase("","",CHF_RESURRECT)
		VILE F 0 A_GiveInventory("NotShot",1)
		VILE F 0 A_JumpIfInventory("NotShot",600,2)
		VILE F 0 A_Jump(2,1)
		loop
		VILE F 0 A_TakeInventory("NotShot")
		VILE F 0 A_ChangeFlag("FRIGHTENED",0)
		VILE F 0 A_ChangeFlag("SHOOTABLE",1)
		goto See
	PainFriendly:
		TNT1 A 0 A_JumpIfInventory("Clip",7, "Dying")
		TNT1 A 0 A_SpawnItem("DistantRocket", 1, 1, 0, 0)
		VILE Q 0 A_SpawnItem("SpawnFire", 0.1, 28, 0, 0)
		VILE Q 0 A_Explode(46,196)
		VILE Q 0 bright A_Quake(3,36,0,360)
		VILE Q 0 A_ChangeFlag("SHOOTABLE", 0)
		VILE Q 2 light("HELL") A_Pain
		VILE Q 20 light("HELL") A_GiveInventory("Clip",1)
		VILE Q 0 A_ChangeFlag("SHOOTABLE", 1)
		VILE H 0 A_ChangeFlag("NOPAIN",0)
		goto See
	Dying:
		VILE Q 0 bright A_ChangeFlag("nodamage", 0)
		VILE Q 0 bright A_SetTranslucent(0.6,1)
		VILE Q 0 bright A_Die
		goto Death
	Death:
		TNT1 A 0 A_SpawnItemEx("tempshieldyellow", 0,0,0, 0,0,0, 0,32)
		VILE Q 42 bright A_Pain
		VILE Q 0 A_FaceTarget
		VILE Q 0 A_Quake(2,40,0,768,0)
		VILE G 6 bright light("HELL") A_SetTranslucent(0.8, 1)
		VILE G 6 A_SetTranslucent(0.4, 1)
		VILE G 6 bright light("HELL") A_SetTranslucent(0.8, 1)
		VILE G 6 A_SetTranslucent(0.4, 1)
		VILE G 6 bright light("HELL") A_Pain
		VILE Q 0 A_Quake(4,40,0,768,0)
		VILE G 4 bright light("HELL") A_SetTranslucent(0.8, 1)
		VILE G 4 A_SetTranslucent(0.4, 1)
		VILE G 4 bright light("HELL") A_SetTranslucent(0.8, 1)
		VILE G 4 A_SetTranslucent(0.4, 1)
		VILE G 4 bright light("HELL") A_Pain
		VILE G 2 bright light("HELL") A_SetTranslucent(1, 1)
		VILE G 2 A_SetTranslucent(0.5, 1)
		VILE G 2 bright light("HELL") A_SetTranslucent(1, 1)
		VILE G 2 A_SetTranslucent(0.5, 1)
		VILE G 0 A_SetTranslucent(1, 1)
		VILE Q 0 A_Quake(6,8,0,768,0)
		VILE GGG 2 bright light("HELL") A_Pain
	XDeath:
		VILE Q 0 A_SpawnItem("SpawnFire", 0.1, 28, 0, 0)
		TNT1 AA 0 A_SpawnItemEx("DistantRocket", 0,0,0, 0,0,0, 0,32,0)
		VILE Q 0 A_PlayWeaponSound("weapons/rocklx")
		VILE Q 0 A_Explode(72, 196)
		VILE Q 6 bright light("HELL") A_Scream
		VILE Q 14 bright light("HELL") A_Quake(8,14,0,768,0)
		VILE Q 0 A_SpawnItem("SpawnFire", 0.1, 30, 0, 0)
		VILE Q 0 A_SpawnItem("SpawnFire", 0.6, 28, 0, 0)
		VILE QQQQQQQQ 0 A_CustomMissile("HeckFire", random(12, 32), 0, random(0, 360), 2, random(-2, 12))
		VILE Q 0 A_Quake(3,26,0,1024,0)
		VILE QQQQQ 8 A_SpawnItemEx("VileShard", random(-8,8),random(-8,8),random(26, 50), random(-1,1),0,0, random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,32)
	Fade:
		VILE QQ 2 A_SpawnItemEx("VileShard", random(-8,8),random(-8,8),random(26, 50), random(-1,1),0,0, random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,16)
		VILE Q 8 A_FadeOut(0.05)
		VILE Q 0 A_JumpIf(alpha < 0.10, 1)
		loop
		VILE Q 8 A_FadeOut(0.04)
		TNT1 A 8 A_SpawnItemEx("VileShard", random(-8,8),random(-8,8),random(26, 50), random(-1,1),0,0, random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,16)
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_CheckFlag("friendly", "FadeFriendly")
		TNT1 A 0 A_GivetoTarget("HDKillCount",1)
		TNT1 A 0 A_DropItem("Megasphere")
	FadeFriendly:
		TNT1 AAAAA 12 A_SpawnItemEx("VileShard", random(-8,8),random(-8,8),random(26, 50), random(-1,1),0,0, random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,16)
		TNT1 A 12 A_SpawnItemEx("VileGhost", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		stop
	}
}
actor ArchAngel : ArchFiend
{
	+friendly
	+noclip
	+noblockmonst
	maxstepheight 4096
	maxdropoffheight 4096
	PainChance 56
	Scale 1.0
	Speed 18
	Species "Archangel"
	states
	{
	see:
		TNT1 A 0 A_ChangeFlag("nopain",0)
		goto super::see
	missile:
		TNT1 A 0 A_ChangeFlag("nopain",1)
		goto super::missile
	heal:
		TNT1 A 0 A_ChangeFlag("nopain",1)
		goto super::heal
	}
}
actor HDRaiser : ActionItem
{
	states
	{
	pickup:
		TNT1 A 0 A_CheckFlag("corpse","raise")
		stop
	raise:
		TNT1 A 0 A_Jump(180,1)
		stop
		TNT1 A 0 A_SpawnItemEx("HDRaiserDummy",0,0,0,velx,vely,velz,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		stop
	}
}
actor HDRaiserFriendly : ActionItem
{
	states
	{
	pickup:
		TNT1 A 0 A_CheckFlag("corpse","raise")
		stop
	raise:
		TNT1 A 0 A_Jump(200,1)
		stop
		TNT1 A 0 A_SpawnItemEx("HDRaiserDummyFriendly",0,0,0,velx,vely,velz,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
		stop
	}
}
actor HDRaiserDummy
{
	-solid -shootable +noblockmap
	height 2 radius 2
	states
	{
	spawn:
		TNT1 AAAAAAAAAAAAAAA 1 A_Chase("","",CHF_RESURRECT|CHF_DONTMOVE)
		stop
	heal:
		TNT1 A 1
		stop
	}
}
actor HDRaiserDummyFriendly : HDRaiserDummy
{
	+friendly
}

actor BloodyHellSpawner : ActionItem
{
	states
	{
	pickup:
		TNT1 A 0 A_CustomMissile("BloodyHellSpawner2",4)
		TNT1 A 0 A_CustomMissile("Rocket",32)
		stop
	}
}
actor BloodyHellSpawner2
{
	projectile
	height 1
	radius 1
	states
	{
	spawn:
		TNT1 A 0 A_SpawnItemEx("BloodyHellFire", 24,0,0, 0,0,0, 0,32)
		BAL1 A -1
		stop
	}
}

actor VileGhost
{
	+ismonster
	-countkill
	+noclip
	+nogravity
	+lookallaround
	+nosector
	+nofear
	radius 1
	height 1
	speed 20
	states
	{
	spawn:
		TNT1 A 10 A_Look
		wait
	see:
		TNT1 A 20
		TNT1 A 0 A_Jump(1,"birth")
		loop
	birth:
		TNT1 A 20 A_Warp(AAPTR_TARGET,0,0,32,0,WARPF_NOCHECKPOSITION)
		TNT1 A 80 A_Quake(1,40,0,512,"vile/active")
		TNT1 AAAAAA 0 A_SpawnItemEx("VileGhostShard",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION,72)
		TNT1 A 40
		TNT1 A 0 A_CheckFlag("friendly","BirthFriendly")
		TNT1 A 40 A_RadiusGive("HDRaiser",1024,RGF_CORPSES|RGF_MONSTERS,1)
		stop
	BirthFriendly:
		TNT1 A 0 A_RadiusGive("HDRaiserFriendly",1024,RGF_CORPSES|RGF_MONSTERS,1)
		stop
	}
}
actor VileGhostShard : VileGhost
{
	height 56
	radius 20
	states
	{
	spawn:
		TNT1 A 2 A_VileChase
		loop
	heal:
		TNT1 A 0 A_CheckFlag("friendly", "HealFriendly")
		TNT1 A 1 A_RadiusGive("HDRaiser", 256, RGF_CORPSES|RGF_MONSTERS,1)
		stop
	HealFriendly:
		TNT1 A 1 A_RadiusGive("HDRaiserFriendly", 256, RGF_CORPSES|RGF_MONSTERS,1)
		stop
	}
}
actor VileShard
{
	+ismonster
	+float
	+nogravity
	+noclip
	+lookallaround
	translation 1
	scale 0.6
	radius 0
	height 0
	renderstyle add
	speed 5
	states
	{
	spawn:
		APLS A 0
		APLS A 0 A_SetGravity(0.1)
		APLS AB 1 bright A_Look
		loop
	see:
		APLS A 0 A_Jump(64, 2)
		APLS A 0 ThrustThingZ(0, random(-4, 8), 0, 0)
		APLS A 2 bright A_JumpIf(alpha < 0.1, "see2")
		APLS B 2 A_Wander
		APLS A 0 A_CustomMissile("VileShardTail", 0, random(-24, 24), random(-24, 24), 2, random(-14, 14))
		APLS A 2 bright
		APLS A 0 A_Wander
		APLS B 2 bright A_FadeOut(0.05)
		APLS A 0 A_Wander
		loop
	see2:
		TNT1 A 0
		stop
		TNT1 A 6 A_VileChase
		TNT1 AAA 0 A_Wander
		loop
	heal:
		TNT1 A 1
		stop
	}
}
actor VileShardTail
{
	projectile
	speed 2
	scale 0.4
	translation 1
	+noclip
	+nogravity
	renderstyle add
	states
	{
	spawn:
		APLS A 0
		APLS AB 2 bright A_FadeOut(0.01)
		APLS A 0 ThrustThingZ(0, random(-4, 8), 0, 0)
		loop
	}
}

