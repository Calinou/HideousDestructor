// ------------------------------------------------------------
// Cacodemon
// ------------------------------------------------------------
actor CacoPuff
{
	+nogravity
	+puffgetsowner
	scale 0.4
	damagetype "Caco"
	+extremedeath
	+forcexybillboard
	+puffonactors
	+forcepain
	+puffgetsowner
	+noblood
	obituary "%o was smashed by a cacodemon."
	decal none
	radius 1
	height 1
	states
	{
	spawn:
		TNT1 A 0 A_PlaySound("weapons/plasidle",0,0.1,0,0.4)
		BAL2 AB 1 A_FadeOut (0.05)
		loop
	}
}
actor BakaBall
{
	projectile
	+extremedeath
	+forcexybillboard
	Decal "DoomImpScorch"
	Radius 5
	Height 6
	Speed 18
	Damage 3
	RenderStyle Add
	Alpha 0.9
	SeeSound "baka/attack"
	DeathSound "baka/shotx"
	obituary "%o was smashed by a cacodemon."
	var int user_velx;
	var int user_vely;
	States
	{
		Spawn:
			BAL2 ABABABAB 1 bright{
				A_SetUserVar("user_velx",velx);
				A_SetUserVar("user_vely",vely);
			}
		See:
			BAL2 A 0{
				if((velx==0)&&(vely==0)&&(velz==0)){A_Countdown;}
				A_PlaySound("weapons/plasidle",0,0.6,0,2);
				A_SpawnItemEx("BakaBallConstantRailer",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
				A_SetUserVar("user_velx",velx);
				A_SetUserVar("user_vely",vely);
			}
			BAL2 A 1 BRIGHT A_BishopMissileWeave
			BAL2 B 1 BRIGHT A_BishopMissileWeave
			loop
		Death:
			TNT1 AAAA 0 A_SpawnItemEx("HDSmoke",0,0,0,2,0,3,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			TNT1 A 1 A_SpawnItemEx("BakaBallRailer",0,0,1,1,0,2,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
			stop
	}
}
actor BakaBallRailer
{
	renderstyle add
	+nointeraction height 1 radius 1
	obituary "%o was smashed by a cacodemon."
	states
	{
	spawn:
		TNT1 A 0
		BAL2 AAAAAA 0 A_CustomRailgun ((random(1,3)),0,none,azure,RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,4000,"BeamSpot",180,180,random(40,160),28,1.4,0.5) 
		BAL2 AAAAAA 0 A_CustomRailgun ((random(1,3)),0,none,blueviolet,RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,4000,"CacoPuff",180,180,random(120,260),38,1.4,0.5)
		BAL2 CCCCDDDDEE 1 bright light("BAKAPOST3") A_CustomRailgun ((random(1,3)),0,none,blueviolet,RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,4000,"CacoPuff",180,180,random(80,190),18,1.4,0.5)
		TNT1 A 0 A_FadeOut(0.1)
		BAL2 EEEEEE 1 bright light("BAKAPOST4"){
			A_CustomRailgun ((random(1,3)),0,none,blueviolet,RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,4000,"CacoPuff",180,180,random(80,190),18,1.4,0.5);
			A_FadeOut(0.2);
		}
		TNT1 AAAAAAAA 2 light("PLAZMABX2") A_GiveInventory("CacoZapper2",1)
		Stop
	}
}
actor BakaBallConstantRailer:BakaBallRailer
{
	states
	{
	spawn:
		TNT1 AAAAA 0 A_CustomRailgun((random(1,2)),0,none,blueviolet,RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,4000,"CacoPuff",180,180,random(40,120),8,1.4,1.5)
		stop
	}
}

ACTOR FastBakaBall:BakaBall
{
	Decal "DoomImpScorch"
	Speed 32
	States
	{
		Spawn:
			TNT1 A 0 nodelay A_PlaySound ("baka/attack")
			goto see
	}
}

ACTOR BakaSlave:FastProjectile{
	+forcexybillboard
	Decal "BakaScorch"
	radius 4 height 4 speed 1
	damage 6
	scale 0.6 renderstyle add
	translation "112:127=16:47"
	SeeSound "bakaslave/shoot"
	DeathSound "bakaslave/death"
	MissileType "BakaSlaveTail"
	missileheight 12
	obituary "%o was inverted by a cacodemon."
	states{
		spawn:
			TNT1 A 0
			TNT1 A 0{
				A_PlaySound("bakaslave/shoot");
				A_ScaleVelocity(120);
				A_PlaySound("caco/sight",CHAN_VOICE);
			}
		spawn2:
			BAL2 A 0 A_ScaleVelocity(1.2)
			BAL2 AB 1 BRIGHT
			loop
		death:
			TNT1 A 1{
				A_StopSound(CHAN_VOICE);
				A_Scream;
				A_SpawnItemEx("BakaSlaveRailer",0,0,0,-1,0,1,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS);
			}
			stop
	}
}

actor BakaSlaveRailer{
	+nointeraction height 1 radius 1 renderstyle add scale 2.7
	obituary "%o was inverted by a cacodemon."
	states
	{
	spawn:
		TNT1 A 0
		TNT1 AA 0 A_GiveInventory("CacoZapper",1)
		BAL2 C 1 bright light("BAKASLAVE")
		BAL2 C 1 bright light("BAKASLAVE") A_GiveInventory("CacoZapper",1)
		MISL B 1 bright light("BAKAPOST1"){
			A_GiveInventory("CacoZapper",1);
			A_SetScale(1.6);
			A_SpawnItemEx ("DistantRocket", 0,0,4,0,0,0, 0,40);
		}
		MISL B 1 bright light("BAKAPOST1"){
			A_GiveInventory("CacoZapper",1);
			A_Explode(200, 300, 1);
		}
		TNT1 AAAAAAAAA 0 A_SpawnItemEx ("HDSmoke", 0,0,0, random(-6,6), random(-6,6), random(-3,12), 0,160,32)
		TNT1 AAAAAA 0 A_SpawnItemEx ("HDSmokeChunk", 0,0,0, random(-6,6), random(-6,6), random(-3,12), 0,160,32)
		TNT1 AAA 0 A_SpawnItemEx ("BakaBallRailer", random(-4,4),random(-4,4),random(-3,4),0,0,random(1,3),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 AAAAAAA 0 A_SpawnItemEx ("BigWallChunk", random(-2,2),random(-2,2),random(-2,2), random(-4,4), random(-4,4), random(2,6), random(0,360),160)
		TNT1 A 0 A_GiveInventory("CacoZapper",1)
		MISL BBC 1 bright light("BAKAPOST1") A_GiveInventory("CacoZapper",1)
		MISL C 1 bright light("BAKAPOST2"){
			A_Quake(3,35,0,300);
			A_GiveInventory("CacoZapper",1);
		}
		MISL CCCDDD 1 bright light("BAKAPOST2"){
			A_FadeOut(0.1);
			A_GiveInventory("CacoZapper",1);
		}
		TNT1 AAAAAAAAAAAAAAAAAAAA 1 light("PLAZMABX1") A_GiveInventory("CacoZapper",1)
		TNT1 A 35
		stop
	}
}

actor CacoZapper:ActionItem
{
	states
	{
	pickup:
		TNT1 A 0 A_PlaySound("weapons/plasidle",0,0.6,0,2)
		TNT1 A 0 A_CustomRailgun ((random(4,8)),0,none,azure,RGF_SILENT|RGF_FULLBRIGHT,1,4000,"CacoPuff",180,180,random(64,256),4,0.4,0.6)
		stop
	}
}
actor CacoZapper2:ActionItem
{
	states
	{
	pickup:
		TNT1 A 0 A_PlaySound("weapons/plasidle",0,0.3,0,2)
		TNT1 A 0 A_CustomRailgun ((random(1,4)),0,none,blueviolet,RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,4000,"CacoPuff",180,180,random(60,160),18,1.4,1.5)
		stop
	}
}
ACTOR BakaSlaveTail{
	translation "112:127=16:47"
	+nointeraction height 1 radius 1
	+forcexybillboard renderstyle add scale 0.6
	states{
	spawn:
		TNT1 A 0
		TNT1 AA 0 A_CustomRailgun ((random(1,2)),0,none,azure,RGF_SILENT|RGF_NOPIERCING|RGF_FULLBRIGHT,0,4000,"CacoPuff",10,10,random(80,100),2,0.4,0.6)
		TNT1 A 0 A_Warp(AAPTR_DEFAULT,0,random(-1,1)*random(3,6),random(-1,1)*2)
	spawn2:
		BAL7 AB 3 A_FadeOut(0.1)
		loop
	}
}

actor BakaMeleePreshot:FastProjectile{
	+extremedeath
	+nointeraction
	radius 1 height 1
	speed 25 damage 8
	renderStyle add +forcexybillboard
	scale 1.8 alpha 0.6
	states{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("bakaslave/attack",CHAN_AUTO,1,0,1)
		TNT1 A 0 A_Jump(256,"spawn2")
	spawn2:
		TNT1 A 0 A_ScaleVelocity(0.04)
		TNT1 A 2 A_Warp(AAPTR_TARGET,48,0,28,0,WARPF_NOCHECKPOSITION)
		BAL2 A 2 BRIGHT
		TNT1 A 2 A_Warp(AAPTR_TARGET,48,0,28,0,WARPF_NOCHECKPOSITION)
		BAL2 A 2 BRIGHT
		TNT1 A 2 A_Warp(AAPTR_TARGET,48,0,28,0,WARPF_NOCHECKPOSITION)
		BAL2 A 2 BRIGHT
		TNT1 A 2 A_Warp(AAPTR_TARGET,48,0,28,0,WARPF_NOCHECKPOSITION)
		BAL2 B 2 BRIGHT
		TNT1 A 2 A_Warp(AAPTR_TARGET,48,0,28,0,WARPF_NOCHECKPOSITION)
		BAL2 A 2 BRIGHT
		TNT1 A 2 A_Warp(AAPTR_TARGET,48,0,28,0,WARPF_NOCHECKPOSITION)
		BAL2 B 2 BRIGHT
		TNT1 A 2 A_Warp(AAPTR_TARGET,48,0,28,0,WARPF_NOCHECKPOSITION)
		BAL2 A 2 BRIGHT
		TNT1 A 2 A_Warp(AAPTR_TARGET,48,0,28,0,WARPF_NOCHECKPOSITION)
		BAL2 B 2 BRIGHT
		TNT1 A 2 A_Warp(AAPTR_TARGET,48,0,28,0,WARPF_NOCHECKPOSITION)
		BAL2 B 2 BRIGHT
		TNT1 A 2 A_Warp(AAPTR_TARGET,48,0,28,0,WARPF_NOCHECKPOSITION)
		TNT1 A 0 A_MonsterRefire(0,"Abort")
		TNT1 A 0 A_Jump(256,"death")
	death:
	fade:
		BAL2 E 2 bright A_FadeOut (0.1)
		loop
	abort:
		TNT1 A 0
		stop
	}
}
actor BakaSlavePreshot:BakaMeleePreshot
{
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("bakaslave/attack",CHAN_AUTO,1,0,0.32)
		TNT1 A 0 A_Jump(256,"spawn2")
	death:
		TNT1 A 0 A_Scream
		TNT1 A 0 A_Explode(40, 128, 0)
		TNT1 A 0 A_SpawnItemEx ("BakaSlave", 0,0,0,velx,vely,velz, 180,40)
		TNT1 A 0 A_SetScale (2.2)
		BAL2 CDCDCDDEE 1 bright A_SpawnItemEx ("FastBakaBall", 0,0,random(28,0), velx*16+random(-4,4),vely*16+random(-4,4),velz*16+random(-1,1), 0,40,12)
		goto fade
	}
}

ACTOR Bakademon:Cacodemon{//replaces Cacodemon{
	+MISSILEMORE
	+MISSILEEVENMORE
	+DONTHURTSPECIES
	+PUSHABLE pushfactor 0.05
	+NOBLOODDECALS
	bloodtype "CacoShellBlood"
//	+FLOATBOB
	Painchance 90
	deathheight 29
	damagefactor "SmallArms0", 0.8
	damagefactor "SmallArms1", 0.9
	damagefactor "Caco",0
	damagefactor "Beam",0
	Obituary "%o was inverted by a cacodemon."
	speed 4
	var int user_m;var int user_a;var int user_p;
	states{
	Spawn:
		HEAD A 0 nodelay{
			if(countinv("Clip")<1){
				A_GiveInventory("Clip",1);
				A_SetScale(scalex*frandom(0.8,1.2));
				A_SetSize(radius*scalex,height*scaley);
				mass*=scalex;
				if(scalex>1.0){
					A_SetHealth(health*scalex);
				}
			}
		}
		HEAD A 0 A_LowGravity
		HEAD A 2 A_Look
		Goto Spawn+1
	See:
		HEAD A 0 A_NoGravity
		HEAD AA 0 A_Chase
		HEAD A 0 A_ScaleVelocity(0.6)
		HEAD A 4 A_ChangeVelocity(0.2,0,frandom(-0.4,0.4),CVF_RELATIVE)
		HEAD A 0 HealThing(random(0,5))
		TNT1 A 0 A_JumpIfTargetInLOS ("See")
	Roam:
		TNT1 A 0 A_Jump (120, 2)
		TNT1 A 0 A_JumpIfTargetInLOS ("See")
		HEAD A 0 A_Jump(100,3)
		HEAD A 0 A_Wander
		HEAD A 0 A_Jump(256,3)
		HEAD A 0 A_FaceTarget
		HEAD A 0 A_SetAngle(angle+random(5,15)+(random(0,1)*2-1))
		HEAD A 6 A_ChangeVelocity(frandom(-0.1,0.3),frandom(-0.2,0.2),frandom(-0.4,0.4),CVF_RELATIVE)
		HEAD A 0 HealThing(random(2,10))
		loop
	Pain:
		TNT1 A 0 A_JumpIfHealthLower(256,2)
		TNT1 A 0 A_Jump(256,3)
		TNT1 A 0 A_ChangeFlag("floatbob",1)
		TNT1 A 0 A_ChangeVelocity(0,0,frandom(-0.4,-1.4),CVF_RELATIVE)
		HEAD E 3
		HEAD E 3 A_Pain
		HEAD F 6
	missile:
		HEAD A 4{
			A_FaceTarget(40,40);
			if(A_JumpIfTargetInLOS("null",40)){return state("shoot");}
			else if(A_JumpIfTargetInLOS("null")){return state("see");}
			return state("");
		}loop
	shoot:
		HEAD A 0 A_JumpIfCloser(128,"melee")
		HEAD A 0{
			if((getdistance(true,AAPTR_TARGET)>560)&&(random(1,2)==1)){
				return state("baka");
			}
			if(z-getzat(0)<100){A_ChangeVelocity(0,0,0.8,CVF_RELATIVE);}
			if((getdistance(true,AAPTR_TARGET)<128)||(random(1,3)==1)){
				return state("centre");
			}
			return state("sweep");
		}
	sweep:
		HEAD B 2 A_SetUserVar(user_m,randompick(-1,1)*6)
		HEAD C 4
		HEAD D 8 BRIGHT A_SpawnProjectile("BakaBall",32,0,user_m,CMF_AIMDIRECTION,pitch)
		HEAD C 4
		HEAD B 2
		HEAD A 6 A_FaceTarget(10,20)
		HEAD B 2
		HEAD C 4
		HEAD D 8 BRIGHT A_SpawnProjectile("BakaBall",32,0,0,CMF_AIMDIRECTION,pitch)
		HEAD C 4
		HEAD B 2
		HEAD A 6 A_FaceTarget(10,20)
		HEAD B 2
		HEAD C 4
		HEAD D 8 BRIGHT A_SpawnProjectile("BakaBall",32,0,-user_m,CMF_AIMDIRECTION,pitch)
		HEAD C 4
		HEAD B 2
		HEAD A 6 A_FaceTarget(10,20)
		goto see+1
	centre:
		HEAD A 0{
			A_FaceTarget(20,40);
			A_SetUserVar(user_a,angle);
			A_SetUserVar(user_p,pitch);
		}
		HEAD B 2{
			A_FaceTarget(20,40);
			A_SetUserVar(user_a,angle-user_a);
			A_SetUserVar(user_p,pitch-user_p);
			A_SetUserVar(user_m,getdistance(true,AAPTR_TARGET)/random(8,10));
			A_SetAngle(angle+user_a*user_m);
			A_SetPitch(pitch+user_p*user_m);
		}
		HEAD C 4
		HEAD D 8 BRIGHT A_SpawnProjectile("BakaBall",32,0,0,CMF_AIMDIRECTION,pitch)
		HEAD C 4
		HEAD B 2
		HEAD A 6{
			A_SetAngle(angle-user_a*user_m);
			A_SetPitch(pitch-user_p*user_m);
		}
		HEAD B 2
		HEAD C 4
		HEAD D 8 BRIGHT A_SpawnProjectile("BakaBall",32,0,0,CMF_AIMDIRECTION,pitch)
		HEAD C 4
		HEAD B 2
		HEAD A 6{
			A_SetAngle(angle+user_a*user_m/2);
			A_SetPitch(pitch+user_p*user_m/2);
		}
		HEAD B 2
		HEAD C 4
		HEAD D 8 BRIGHT A_SpawnProjectile("BakaBall",32,0,0,CMF_AIMDIRECTION,pitch)
		HEAD C 4
		HEAD B 2
		HEAD A 6
		goto see+1
	baka:
		HEAD B 2
		HEAD C 4 A_ChangeVelocity(0,0,frandom(0.3,1),CVF_RELATIVE)
		HEAD D 6 A_FaceTarget(1,2,0,0,FAF_BOTTOM)
		HEAD D 36 bright{
			A_ChangeFlag("nopain",1);
			A_ChangeFlag("seeinvisible",1);
			A_SpawnProjectile("BakaSlavePreshot",28,0,0,CMF_AIMDIRECTION,pitch);
		}
		HEAD D 32 A_ChangeVelocity(-cos(pitch)*3,0,sin(pitch),CVF_RELATIVE)
		HEAD C 6
		HEAD B 3{
			A_ChangeFlag("nopain",0);
			A_ChangeFlag("seeinvisible",0);
		}
		HEAD A 6
		goto see+1
	Melee:
		HEAD B 4{
			A_FaceTarget(40,40);
			if(A_JumpIfTargetInLOS("null",10)){return state("zap");}
			return state("");
		}wait
	zap:
		HEAD C 4{
			A_SetAngle(angle+random(-10,10));
			A_SetPitch(-pitch+random(-10,10));
			A_PlaySound("caco/sight");
		}
		HEAD D 2 bright A_SpawnProjectile("BakaMeleePreshot", 28, 0, 0)
HEAD DDDDDDDDDDDD 2 bright A_CustomRailgun((random(1,2)),0,none,azure,RGF_SILENT|RGF_FULLBRIGHT,0,4000,"BeamSpot",20,20,random(120,260),28,1.4,1.5,none,-12)
		HEAD C 4
		HEAD B 2
		HEAD A 6
		Goto See+1
	Death:
		HEAD G 3{
			A_ChangeFlag("pushable",0);
			A_ChangeFlag("floatbob",0);
			A_GiveInventory("HDCorpseFlags",1);
			A_SpawnItemEx("tempshieldblue", 0,0,0, velx,vely,velz, 0,40);
			A_Gravity;
			A_NoBlocking;
			A_Scream;
		}
		HEAD HI 3
		HEAD I -1
		wait
	crash:
		HEAD J 4{
			A_ChangeFlag("pushable",0);
			A_ChangeFlag("floatbob",0);
			A_GiveInventory("HDCorpseFlags",1);
			A_PlaySound("misc/gibbed",CHAN_AUTO);
			A_SpawnItemEx ("tempshield2blue", 0,0,0, velx,vely,velz, 0,40);
		}
		HEAD JKKK 1 light("PLAZMABX1") A_GiveInventory("CacoZapper",1)
		HEAD K 1 A_SpawnItemEx ("tempshield2blue", 0,0,0, velx,vely,velz, 0,40)
		HEAD KKKK 1 light("PLAZMABX1") A_GiveInventory("CacoZapper",1)
		HEAD L 1 A_SetTics(random(5,25))
		HEAD LLLLL 2 light("PLAZMABX1") A_GiveInventory("CacoZapper",1)
	crash2:
		HEAD L 1 light("PLAZMABX1") A_SetTics(random(1,4))
		TNT1 A 0 A_PlaySound("weapons/plasidle",0,0.6,0,2)
		TNT1 A 0 A_CustomRailgun ((random(4,8)),random(-12,12),none,azure,RGF_SILENT|RGF_FULLBRIGHT,1,4000,"CacoPuff",180,180,random(32,128),4,0.4,0.6)
		HEAD L 0 A_Jump(128,2)
		HEAD L 1 A_SetTics(random(1,40))
		HEAD L 0 A_Jump(4,1)
		loop
	dead:
		HEAD L -1
		stop
	Raise:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_ChangeFlag("pushable",1)
		goto super::raise
	}
}


actor DeadBakademon:Bakademon{// replaces DeadCacodemon{
	states{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_JumpIfInventory("HDShellAmmo",1,3)
		TNT1 A 0 A_GiveInventory("HDShellAmmo",1)
		TNT1 A 0 A_Die("spawndead")
		TNT1 A 0
		goto super::spawn
	death.spawndead:
		TNT1 A 0 A_NoBlocking
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		TNT1 A 0 A_ChangeFlag("pushable",0)
		TNT1 A 0 A_ChangeFlag("floatbob",0)
		TNT1 A 0 A_NoBlocking
		goto super::dead
	}
}
