
actor Injector : HDWeapon
{
	game Doom
	+weapon.wimpy_weapon
	+weapon.no_auto_switch
	weapon.slotnumber 9
	weapon.selectionorder 999
	states
	{
	select:
		TNT1 A 0 A_JumpIfInventory("IsTextless",1,2)
		TNT1 A 0 A_Print("Inject into a teammate:\n\n\n\n\cjFire = Medikit\n\cu(or bandage if no meds)\n\n\n\cdAltfire = Stims\n\n\n\cgBoth = Berserk",5)
		TNT1 A 0 A_TakeInventory("InjectorCheckerFlag")
		goto super::select
	deselect:
		TNT1 A 0 A_TakeInventory("InjectorCheckerFlag")
		goto super::deselect
	fire:
	hold:
		TNT1 A 0 A_JumpIfInventory("PressingAltFire",1,2)
		TNT1 A 0 A_JumpIfInventory("PortableMedikit",1,1) goto bandage
	altfire:
	althold:
		TNT1 A 4
		TNT1 A 0 A_FireCustomMissile("InjectorChecker")
		TNT1 A 0 A_JumpIfInventory("InjectorCheckerFlag",0,"injectmeds")
		TNT1 A 0 A_Refire
		TNT1 A 0 A_TakeInventory("InjectorCheckerFlag",999)
		goto ready
	inject:
		TNT1 A 10
		//TNT1 A 0 A_FireCustomMissile("Rocket") goto postinject
		goto injectmeds
	injectmeds:
		TNT1 A 0 A_JumpIfInventory("PressingAltFire",1,"injectstims")
		TNT1 A 0 A_JumpIfInventory("PortableMedikit",1,1)
		goto nsmeds
		TNT1 A 0 A_FireCustomMissile("MedInjector")
		TNT1 A 0 A_TakeInventory("PortableMedikit",1)
		goto postinject
	injectstims:
		TNT1 A 0 A_JumpIfInventory("PressingFire",1,"injectbers")
		TNT1 A 0 A_JumpIfInventory("PortableStimpack",1,1)
		goto nsstims
		TNT1 A 0 A_FireCustomMissile("StimInjector")
		TNT1 A 0 A_TakeInventory("PortableStimpack",1)
		goto postinject
	injectbers:
		TNT1 A 0 A_JumpIfInventory("PortableBerserkPack",1,1)
		goto nsbers
		TNT1 A 0 A_FireCustomMissile("BersInjector")
		TNT1 A 0 A_TakeInventory("PortableBerserkPack",1)
		goto postinject
	injectbandage:
		TNT1 A 0 A_FireCustomMissile("BandageInjector")
		goto postinject
	postinject:
		TNT1 A 0 A_TakeInventory("InjectorCheckerFlag",999)
		TNT1 A 20
		TNT1 A 0 A_Refire
		goto ready
	nsmeds:
		TNT1 A 0 A_Print("You need a \camedikit\c-\n\n\nto do this.")
		goto postinject
	nsstims:
		TNT1 A 0 A_Print("You need a \cjstimpack\c-\n\n\nto do this.")
		goto postinject
	nsbers:
		TNT1 A 0 A_Print("You need a \crberserk pack\c-\n\n\nto do this.")
		goto postinject
	bandage:
		TNT1 A random(1,10) A_Jump(128,1,2)
		TNT1 A random(1,10)
		TNT1 A random(1,10) A_PlaySound("weapons/pocket")
		TNT1 A 0 A_FireCustomMissile("InjectorChecker")
		TNT1 A 0 A_JumpIfInventory("InjectorCheckerFlag",0,"injectbandage") //heh
		TNT1 A 0 A_Refire
		TNT1 A 0 A_TakeInventory("InjectorCheckerFlag",999)
		goto ready
	spawn:
		TNT1 A 1
		TNT1 A 0 A_CheckProximity("spawndropped","PlayerPawn",20,1, CPXF_COUNTDEAD|CPXF_SETTARGET|CPXF_ANCESTOR|CPXF_CLOSEST|CPXF_NOZ)
	spawnfresh:
		TNT1 A 0
		stop
	spawndropped:
		TNT1 A 0 A_JumpIfInTargetInventory("Injector",1,"spawnfresh")
		TNT1 A 0{
			A_GiveToTarget("Injector");
			A_GiveToTarget("DropMeds");
		}
		stop
	}
}

actor InjectorChecker
{
	speed 56
	height 2
	radius 2
	projectile
	states
	{
	spawn:
		TNT1 A 1
	death:
		TNT1 A 1 A_TakeInventory("InjectorCheckerFlag",999,AAPTR_TARGET)
		stop
	xdeath:
		TNT1 A 1 A_GiveInventory("InjectorCheckerFlag",1,AAPTR_TARGET)
		stop
	}
}
actor InjectorCheckerFlag : InventoryFlag
{
	inventory.maxamount 10
}



actor MedInjector : FastProjectile
{
	speed 30
	height 2
	radius 2
	+hittracer +bloodsplatter
	states
	{
	spawn:
		TNT1 A 1
		stop
		TNT1 A 0 A_ScaleVelocity(0.001)
		TNT1 A 0 A_Jump(256,"xdeath")
	death:
		TNT1 A 0
		stop
	xdeath:
		TNT1 A 1 A_GiveInventory("MedInstaUser",1,AAPTR_TRACER)
		goto xdeath2
	xdeath2:
		TNT1 AAA 0 A_SpawnItemEx("Blood",8,0,0,8,0,4,random(-4,4),SXF_NOCHECKPOSITION)
		stop
	}
}
actor BandageInjector : MedInjector
{
	states
	{
	xdeath:
		TNT1 A random(1,10) A_PlaySound("weapons/pocket")
		TNT1 A 1 A_GiveInventory("BandageInstaUser",1,AAPTR_TRACER)
		stop
	}
}
actor StimInjector : MedInjector
{
	states
	{
	xdeath:
		TNT1 A 1 A_GiveInventory("StimInstaUser",1,AAPTR_TRACER)
		goto xdeath2
	}
}
actor BersInjector : MedInjector
{
	states
	{
	xdeath:
		TNT1 A 1 A_GiveInventory("BersInstaUser",1,AAPTR_TRACER)
		goto xdeath2
	}
}
actor BandageInstaUser : ActionItem
{
	states
	{
	pickup:
		TNT1 A 0 A_Pain
		TNT1 A 0 A_JumpIf(CheckClass("InnocentBarrel",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("KillerBarrel",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("InnocentFirecan",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("KillerFireCan",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("HERPBot",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("DERPBot",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("Archvile",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("SpiderMastermind",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("Cyberdemon",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("Arachnotron",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("Fatso",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("Yokai",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("LostSoul",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("PainElemental",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("Cacodemon",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("BaronOfHell",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("HellKnight",AAPTR_DEFAULT,true),"nope")
		TNT1 A 0 A_JumpIf(CheckClass("PlayerPawn",AAPTR_DEFAULT,true),"player")
		TNT1 A 0 A_Jump(256,"heal")
	nope:
		TNT1 A 0
		stop
	heal:
		TNT1 A 0 HealThing(20)
		stop
	player:
		TNT1 A 0 A_SetBlend("20 00 00",0.4,60,"00 00 00")
		TNT1 A 0 A_SetPitch(pitch-random(3,4),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+random(-2,2),SPF_INTERPOLATE)
		TNT1 A 0 A_TakeInventory("WoundCount",random(1,2))
		TNT1 A 0 A_GiveInventory("StunCount",100)
		stop
	}
}
actor MedInstaUser : BandageInstaUser
{
	states
	{
	heal:
		TNT1 A 0 HealThing(50)
		stop
	player:
		TNT1 A 0 A_SetBlend("20 00 00",0.8,90,"00 00 00")
		TNT1 A 0 A_SetPitch(pitch-random(5,8),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+random(-4,4),SPF_INTERPOLATE)
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_TakeInventory("LethalDamage",1)
		TNT1 A 0 A_GiveInventory("RegenStims",20)
		TNT1 A 0 A_GiveInventory("BandageCount",25)
		TNT1 A 0 A_GiveInventory("StunCount",100)
		stop
	}
}
actor StimInstaUser : BandageInstaUser
{
	states
	{
	heal:
		TNT1 A 0 HealThing(50)
		stop
	player:
		TNT1 A 0 A_SetBlend("20 00 00",0.8,90,"00 00 00")
		TNT1 A 0 A_SetPitch(pitch-random(5,8),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+random(-4,4),SPF_INTERPOLATE)
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_GiveInventory("WoundCount",1)
		TNT1 A 0 A_Jump(250,2)
		TNT1 A 0 A_GiveInventory("LethalDamage",1)
		TNT1 A 0 A_GiveInventory("RegenStims",30)
		TNT1 A 0 A_GiveInventory("StunCount",35)
		stop
	}
}
actor BersInstaUser : ActionItem
{
	states
	{
	heal:
		TNT1 A 0 A_Scream
		TNT1 A 0 HealThing(200)
		TNT1 A 0 A_ChangeFlag("missilemore",1)
		TNT1 A 0 A_ChangeFlag("missileevenmore",1)
		TNT1 A 0 A_ChangeFlag("nopain",1)
		TNT1 A 0 A_ChangeFlag("nofear",1)
		TNT1 A 0 A_ChangeFlag("frightened",0)
		TNT1 A 0 A_ChangeFlag("avoidmelee",0)
		stop
	player:
		TNT1 A 0 A_SetBlend("20 00 00",0.8,100,"00 00 00")
		TNT1 A 0 A_SetPitch(pitch-random(10,15),SPF_INTERPOLATE)
		TNT1 A 0 A_SetAngle(angle+random(-10,10),SPF_INTERPOLATE)
		TNT1 A 0 A_PlaySound("*xdeath")
		TNT1 A 0 A_GiveInventory("PowerStrength",1)
		TNT1 A 0 A_GiveInventory("HasZerked",1)
		TNT1 A 0 A_GiveInventory("RegenBers",250)
		TNT1 A 0 A_GiveInventory("WoundCount",random(1,3))
		TNT1 A 0 A_GiveInventory("StunCount",35)
		stop
	}
}



actor SelfBandage : HDWeapon
{
	+weapon.wimpy_weapon
	+weapon.no_auto_switch
	weapon.slotnumber 9
	weapon.selectionorder 999
	states
	{
	select:
		TNT1 A 0 A_JumpIfInventory("IsTextless",1,"silentselect")
		TNT1 A 0 A_JumpIfInventory("WoundCount",1,2)
		TNT1 A 0 A_Print("Use this to bandage yourself\n\nwhen you have no meds.\n\n\n\nYou're fine now, so go\n\ndo something else.\n\n\n\n(Altfire can still be used to \n\nbandage someone else, of course.)",5)
		goto super::select
		TNT1 A 0 A_Print("Press and hold Fire\n\nwhile standing still\n\nto try to not die.",5)
	silentselect:
		TNT1 A 0
		goto super::select
	abort:
		TNT1 A 0 A_JumpIfInventory("IsTextless",1,2)
		TNT1 A 0 A_Print("You must stay still\n\nto bandage yourself!",5)
		TNT1 A 0
		goto nope
	fire:
		TNT1 A 0 A_JumpIfInventory("WoundCount",1,"hold")
		TNT1 A 0 A_JumpIfInventory("IsTextless",1,2)
		TNT1 A 0 A_Print("You have no\n\ninjuries to bandage.",5)
	nope:
		TNT1 A 1
		TNT1 A 0 A_Refire("nope")
		goto ready
	hold:
	lower:
		TNT1 A 0 A_JumpIf(pitch>89,2)
		TNT1 A 0 A_JumpIf(pitch<45,1)
		goto try
		TNT1 A 1 A_SetPitch(pitch+4)
		TNT1 A 0 A_JumpIfInventory("IsMoving",4,"abort")
		TNT1 A 0 A_Refire("lower")
		goto ready
	try:
		TNT1 A random(15,25)
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-24,-24),random(-18,15),0)
		TNT1 A 0 A_GiveInventory("Fatigue",4)
		TNT1 A 0 A_Jump(32,2)
		TNT1 A random(5,15) A_GiveInventory("BleedOutAI",1)
		TNT1 A 0 A_JumpIfInventory("IsMoving",4,"abort")
	try2:
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-24,-24),random(-18,15),0)
		TNT1 A 0 A_GiveInventory("Fatigue",4)
		TNT1 A random(1,3) A_Jump(32,2,4)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A random(1,3) A_PlaySound("*usefail",CHAN_VOICE)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A random(1,3) A_PlaySound("*grunt",CHAN_VOICE)
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_PlaySound("imp/melee",CHAN_WEAPON,0.4)
		TNT1 A 0 A_Refire("try4")
		goto ready
	try3:
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-24,-24),random(-18,16),0)
		TNT1 A 0 A_GiveInventory("Fatigue",4)
		TNT1 A 0 A_SetTics(random(20,40))
		TNT1 A 0 A_Jump(200,2)
		TNT1 A 0 A_PlaySound("weapons/pocket")
		TNT1 A random(10,20)
		TNT1 A 0 A_JumpIfInventory("IsMoving",4,"abort")
		TNT1 A 0 A_Refire("try4")
		goto ready
	try4:
		TNT1 A 0 A_CheckFloor(2)
		TNT1 A 0 A_Jump(240,2)
		TNT1 A 0 A_ChangeVelocity(frandom(-0.5,0.5),frandom(-0.5,0.5),frandom(-2,2))
		TNT1 A 0 A_GiveInventory("Fatigue",4)
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-24,-24),random(-18,17),0)
		TNT1 A 0 A_Jump(240,2)
		TNT1 A random(1,3) A_PlaySound("*grunt",CHAN_VOICE)
		TNT1 A 0 A_Jump(140,2)
		TNT1 A 0 A_PlaySound("weapons/pocket")
		TNT1 A random(10,20)
		TNT1 A 0 A_JumpIfInventory("IsMoving",4,"abort")
		TNT1 A 0 A_Refire("try5")
		goto ready
	try5:
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-24,-24),random(-18,18),0)
		TNT1 A 0 A_Jump(8,"try2")
		TNT1 A 0 A_Jump(12,"try3")
		TNT1 A 0 A_Jump(16,"try4")
		TNT1 A 0 A_Jump(80,2)
		TNT1 A 0 A_PlaySound("weapons/pocket")
		TNT1 A random(10,20)
		TNT1 A 0 A_Jump(80,2)
		TNT1 A 0 A_PlaySound("weapons/pocket")
		TNT1 A random(10,20)
		TNT1 A 0 A_JumpIfInventory("IsMoving",4,"abort")
		TNT1 A 0 A_JumpIfInventory("WoundCount",1,3)
		TNT1 A 0 A_JumpIfInventory("IsTextless",1,2)
		TNT1 A 0 A_Print("Despite your best efforts\n\nyou seem to be stable.",5) goto nope
		TNT1 A 4 A_Jump(160,2,3,4)
		TNT1 A 0 A_TakeInventory("WoundCount",random(1,2))
		TNT1 A 0 A_GiveInventory("BleedOutAI",1)
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(-24,-24),random(-24,24),0)
		TNT1 A 0 A_Refire("try2")
		goto ready
	altfire:
	althold:
		TNT1 A random(1,10) A_Jump(128,1,2)
		TNT1 A random(1,10)
		TNT1 A random(1,10) A_PlaySound("weapons/pocket")
		TNT1 A 0 A_FireCustomMissile("InjectorChecker")
		TNT1 A 0 A_JumpIfInventory("InjectorCheckerFlag",0,"injectbandage") //heh
		TNT1 A 0 A_Refire
		TNT1 A 0 A_TakeInventory("InjectorCheckerFlag")
		goto ready
	injectbandage:
		TNT1 A 0 A_FireCustomMissile("BandageInjector")
		TNT1 A 0 A_TakeInventory("InjectorCheckerFlag")
		TNT1 A 20
		TNT1 A 0 A_Refire
		goto ready
	spawn:
		TNT1 A 1
		TNT1 A 0 A_CheckProximity("spawndropped","PlayerPawn",20,1, CPXF_COUNTDEAD|CPXF_SETTARGET|CPXF_ANCESTOR|CPXF_CLOSEST|CPXF_NOZ)
	spawnfresh:
		TNT1 A 0
		stop
	spawndropped:
		TNT1 A 0 A_JumpIfInTargetInventory("SelfBandage",1,"spawnfresh")
		TNT1 A 0{
			A_GiveToTarget("SelfBandage");
			A_GiveToTarget("DropMeds");
		}
		stop
	}
}



