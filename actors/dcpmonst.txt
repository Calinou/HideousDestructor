
// ------------------------------------------------------------
//   Rifleman and derivatives
// ------------------------------------------------------------

ACTOR RiflemanBase
{
	Translation "112:120=152:159","121:127=9:12"
	Height 52
	Radius 12
	Health 130
	Speed 8
	maxdropoffheight 48
	maxstepheight 48
	maxtargetrange 65536
	mass 150
	MONSTER
	+FRIENDLY
	+PUSHABLE
	+MISSILEMORE
	+MISSILEEVENMORE
	+donthurtspecies
	+quicktoretaliate
	Decal "BulletScratch"
	SeeSound "marine/sight"
	PainSound "marine/pain"
	DeathSound "marine/death"
	PainChance 240
	AttackSound ""
	MeleeSound "weapons/smack"
	MeleeDamage 6
	Obituary "%o was shot up by a marine."
	HitObituary "%o violated a marine's personal space."
	var int user_spread;
	var int user_turn;
	States
	{
	spawn:
		PLAY AA 4 A_Wander
		TNT1 A 0 A_Look
		PLAY BB 4 A_Wander
		TNT1 A 0 A_Look
		PLAY CC 4 A_Wander
		TNT1 A 0 A_Look
		PLAY DD 4 A_Wander
		TNT1 A 0 HealThing(1)
		TNT1 A 0 A_Jump(256,"spawn2")
	spawn2:
		TNT1 A 0 A_Look
		TNT1 A 0 A_Jump(60,"spawn")
		TNT1 A 0 A_SetAngle(angle+random(-30,30))
		PLAY EEE 3 A_Look
		TNT1 A 0 A_SetAngle(angle+random(-30,30))
		PLAY EEE 3 A_Look
		TNT1 A 0 A_Jump(60,"spawn")
		loop

	see_evade:
		TNT1 A 0 A_Jump(182,2)
		TNT1 A 0 A_JumpIfTargetInLOS ("Missile")
		TNT1 A 0 A_Chase("missile","melee",CHF_DONTMOVE)
		TNT1 A 0 A_FaceTarget(0,0)
		TNT1 A 0 A_SetAngle(angle+random(60,100)*(random(0,1)*2-1))
		TNT1 A 0 A_ChangeFlag("nodropoff",1)
	see_evade2:
		TNT1 A 0 A_SetUserVar("user_spread",3)
		TNT1 A 0 A_SetAngle(angle+random(-30,30))
		PLAY ABCD 4 A_Recoil(-3)
		TNT1 A 0 A_JumpIf(velx*velx+vely*vely>25,2)
		TNT1 A 0 A_SetAngle(angle+random(-160,200))
		TNT1 A 0 A_ChangeFlag("nodropoff",0)
		TNT1 A 0 A_Jump(72,2)
		TNT1 A 0 A_TakeInventory("EvadeNow",1)
	See:
		TNT1 A 0 A_JumpIfInventory("ZM66Loaded",0,"reload")
	see_all:
		TNT1 A 0 A_JumpIf(floorz<z-4,2)
		TNT1 A 0 A_JumpIfInventory("EvadeNow",1,"see_evade")
		TNT1 A 0 A_SetUserVar("user_spread",2)
		PLAY AABBCCDD 2 A_Chase
		TNT1 A 0{
			if(health<40){A_DamageSelf(random(1,4),"bleedout");}
			else{HealThing(1);}
			A_SetSolid;
		}
		PLAY E 0 A_JumpIfTargetInLOS("See")
		PLAY E 0 A_Jump(256,"spwander")
	spwander:
		PLAY E 0 A_ClearTarget
		PLAY AA 3 A_Wander
		TNT1 A 0 A_Chase("melee","missile",CHF_DONTMOVE)
		PLAY BB 3 A_Wander
		TNT1 A 0 A_Chase("melee","missile",CHF_DONTMOVE)
		PLAY CC 3 A_Wander
		TNT1 A 0 A_Chase("melee","missile",CHF_DONTMOVE)
		PLAY DD 3 A_Wander
		TNT1 A 0 HealThing(1)
		PLAY E 0 A_Jump(128,"spwander")
	spwander2:
		TNT1 A 0 A_Look
		TNT1 A 0 A_Jump(4,"spawn")
		TNT1 A 0 A_SetAngle(angle+random(-30,30))
		PLAY EEE 2 A_Chase("melee","missile",CHF_DONTMOVE)
		TNT1 A 0 A_SetAngle(angle+random(-30,30))
		PLAY EEE 2 A_Chase("melee","missile",CHF_DONTMOVE)
		TNT1 A 0 A_Jump(60,"spwander")
		PLAY E 0 A_JumpIfTargetInLOS("see")
		PLAY E 0 A_Jump(256,"spwander")
	missile:
		TNT1 A 0 A_JumpIfInventory("ZM66Loaded",0,"reload")
		TNT1 A 0 A_JumpIfTargetInLOS(3,120)
		PLAY CD 2 A_FaceTarget(90)
		PLAY E 1 A_SetTics(random(4,10)) //when they just start to aim, not for followup shots!
	missile2:
		TNT1 A 0 A_JumpIfCloser(500,4)
		TNT1 A 0 A_JumpIfCloser(1200,2)
		TNT1 A 0 A_SetUserVar("user_turn",10)
		goto turntoaim
		TNT1 A 0 A_SetUserVar("user_turn",30)
		goto turntoaim
		TNT1 A 0 A_SetUserVar("user_turn",50)
		goto turntoaim
	turntoaim:
		PLAY E 2 A_FaceTarget(user_turn,user_turn)
		TNT1 A 0 A_JumpIfTargetInLOS(1)
		goto see
		TNT1 A 0 A_JumpIfTargetInLOS(1,10)
		loop
		TNT1 A 0 A_FaceTarget(user_turn,user_turn)
		PLAY E 1 A_SetTics(random(0,80/user_turn))
		TNT1 A 0 A_SetUserVar("user_spread",user_turn/10)
		TNT1 A 0 A_Jump(256,"shoot")
	shoot:
		PLAY E 3 A_Jump (128, 1)
		"####" A 0 A_CheckLOF("see",CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|CLOFF_JUMPOBJECT|CLOFF_MUSTBESOLID|CLOFF_SKIPENEMY,0,0, 0,0, 44,0)
	shoot2:
		PLAY E 0 A_JumpIfCloser (600, "rifle")
	Grenade:
		PLAY E 0 A_Jump (160, "rifle")
		PLAY E 0 A_JumpIfCloser (760, 2)
		PLAY E 0 A_Jump (256, "rifle")
		PLAY EE 2 A_FaceTarget(10,10)
		PLAY E 0 A_PlaySound ("weapons/grenadeshot")
		PLAY E 12 A_SpawnProjectile ("GyroGrenade", 44,0,0, CMF_AIMDIRECTION|CMF_SAVEPITCH, pitch+frandom(4,-3))
		TNT1 A 0 A_CheckFlag("friendly",2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_AlertMonsters(0,AMF_TARGETEMITTER)
		TNT1 A 0
		goto See

	rifle:
		TNT1 A 0 A_JumpIfInventory("ZM66Loaded",0,"reload")
		goto gunshot
	gunshot:
		TNT1 A 0 A_JumpIfInventory("ZM66Loaded",0,"ohforfuckssake")
		PLAY F 0 A_PlayWeaponSound("weapons/rifle")
		PLAY F 1 bright light("SHOT"){
			pitch+=frandom(-user_spread,user_spread);
			angle+=frandom(-user_spread,user_spread);
			A_GiveInventory("DecoBulleter426");
		}
		TNT1 A 0 A_GiveInventory("ZM66Loaded",1)
		PLAY E 2 A_SetUserVar("user_spread",user_spread+0.2)
		TNT1 A 0 A_CheckFlag("friendly",2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_AlertMonsters(0,AMF_TARGETEMITTER)
		TNT1 A 0 A_JumpIfCloser(200,"fullauto")
		TNT1 A 0 A_JumpIfCloser(800,1)
		goto postshot
		TNT1 A 0 A_Jump(120,"gunshot")

	postshot:
		TNT1 A 0 A_JumpIfInventory("ZM66Loaded",0,"reload")
		TNT1 A 0 A_JumpIf(user_spread<1,2)
		TNT1 A 0 A_SetUserVar("user_spread",user_spread-1)
		PLAY E 5 A_SetTics(random(1,3))
		PLAY E 1 A_JumpIfTargetInLOS(2)
		PLAY E 0 A_Jump(90,"coverfire")  //even if not in los, occasionally keep shooting anyway
		goto see
		PLAY E 0 A_Jump(8,"see")  //even if in los, occasionally stop shooting anyway
		PLAY E 0 A_JumpIfInTargetInventory("Health",1,2)  //if target is alive, shoot
		PLAY E 0 A_Jump(200,"see")
		PLAY E 0
		goto coverfire
	coverfire:
		PLAY E 1 A_SetTics(random(2,12))
		PLAY E 0 A_SetUserVar("user_spread",2)
		TNT1 A 0 A_Jump(90,"see")
		PLAY E 0 A_JumpIfTargetInLOS("missile2")
		TNT1 A 0 A_Jump(216,"shoot")
		loop

	fullauto:
		TNT1 A 0 A_Jump(128,"shoot")
		TNT1 A 0 A_CPosRefire
		TNT1 A 0 A_Jump(256,"shoot")

	ohforfuckssake:
		PLAY E 4
	reload:
		TNT1 A 0 A_ChangeFlag("frightened",1)
		TNT1 A 0 A_PlaySound ("weapons/rifleclick2")
		PLAY AA 1 A_Chase("melee","")
		TNT1 A 0 A_PlaySound ("weapons/rifleload")
		TNT1 A 0 A_SpawnProjectile("ZM66RifleEmptyMag",38,0,random(90,120))
	reload2:
		PLAY BBCC 1 A_Chase("melee","")
		TNT1 A 0 A_ChangeFlag("frightened",0)
		PLAY DD 1 A_Chase("melee","")
		TNT1 A 0 A_PlaySound ("weapons/pocket")
		PLAY E 3 A_FaceTarget(60,60)
		PLAY E 9
		PLAY E 8 A_PlaySound ("weapons/rifleload")
		PLAY E 2 A_PlaySound ("weapons/rifleclick2")
		TNT1 A 0 A_PlaySound ("weapons/rifleclick")
		TNT1 A 0 A_TakeInventory("ZM66Loaded",999)
		TNT1 A 0 A_ChangeFlag("frightened",0)
		PLAY EEE 1 A_Chase("melee","missile",CHF_DONTMOVE)
		goto see

	Melee:
		PLAY C 8 A_FaceTarget
		PLAY D 4
		PLAY E 4 A_MeleeAttack
		PLAY E 3 A_JumpIfCloser (64, 3)
		PLAY E 4 A_FaceTarget(0,0)
		TNT1 A 0 A_Jump (256, "Missile2")
		PLAY A 4
		Goto See
	Pain:
		PLAY G 4
		PLAY G 4 A_Pain
		PLAY G 0 A_Jump (100,"See")
		PLAY AB 2 A_FaceTarget(50,50)
		PLAY CD 3 A_FastChase
		PLAY G 0 A_CPosRefire
		TNT1 A 0 A_Jump (256,"Missile")
	Death:
		TNT1 A 0 A_ChangeFlag("pushable", 0)
		PLAY H 5 A_SpawnItemEx("tempshield", 0,0,0, velx,vely,velz, 0,40)
		PLAY I 5 A_Scream
	death.bleedout:
		PLAY J 5 A_GiveInventory("NoBlockWepDrop")
		PLAY KLM 5
		TNT1 A 0 A_Jump(256,"Dead")
	XDeath:
		PLAY O 5 A_ChangeFlag ("shootable", 0)
		PLAY O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		PLAY P 5 A_XScream
		PLAY P 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		PLAY Q 5 A_GiveInventory("NoBlockWepDrop")
		PLAY Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		PLAY RSTUV 5
		TNT1 A 0 A_Jump(256,"XDead")
	Dead:
		PLAY N 0 A_SpawnItemEx("DeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE)
		Stop
	XDead:
		PLAY N 0 A_SpawnItemEx("ReallyDeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION|SXF_TRANSFERSCALE)
		Stop
	//Raise:
		PLAY MLKJIH 4
		TNT1 A 0 A_ChangeFlag("pushable",1)
		Goto See
	}
}
actor BlackRifleman : RiflemanBase
{
	Translation "112:120=152:159","121:127=9:12", "48:79=66:79", "18:21=68:68", "213:213=66:66"
}
actor BrownRifleman : RiflemanBase
{
	Translation "112:120=152:159","121:127=9:12", "48:79=61:79", "18:21=63:63", "213:213=63:63"
}
actor WhiteRifleman : RiflemanBase{}
actor Rifleman:RandomSpawner replaces ScriptedMarine
{
	+friendly
	height 56
	radius 12
	DropItem "BlackRifleman"
	DropItem "BrownRifleman"
	DropItem "WhiteRifleman"
}
actor RifleFistman : Rifleman replaces MarineFist {}
actor RifleChaingunman : Rifleman replaces MarineChaingun {}

actor SWAT2Base : RiflemanBase
{
	Translation "112:113=92:98","114:120=99:111","121:127=5:8"
	DropItem ""
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("HDSMG",1)
		goto super::spawn
	see:
		TNT1 A 0 A_JumpIfInventory("ZM66Loaded",30,"reload")
		goto see_all
	missile:
		TNT1 A 0 A_JumpIfInventory("ZM66Loaded",30,"reload")
		goto super::missile+1

	shoot:
	rifle:
		TNT1 A 0 A_JumpIfInventory("ZM66Loaded",30,"reload")
		"####" A 0 A_CheckLOF("see",CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|CLOFF_JUMPOBJECT|CLOFF_MUSTBESOLID|CLOFF_SKIPENEMY,0,0, 0,0, 44,0)
		goto gunshot
	gunshot:
		"----" A 0 A_JumpIfInventory("ZM66Loaded",30,"ohforfuckssake")
		"----" A 0 A_PlaySound("weapons/pistol",CHAN_WEAPON,0.7)
		"####" F 1 bright light("SHOT"){
			pitch+=frandom(-user_spread,user_spread);
			angle+=frandom(-user_spread,user_spread);
			A_GiveInventory("DecoBulleter9f");
		}
		TNT1 A 0 A_GiveInventory("ZM66Loaded",1)
		PLAY E 2 A_SetUserVar("user_spread",user_spread+0.2)
		TNT1 A 0 A_CheckFlag("friendly",2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_AlertMonsters(0,AMF_TARGETEMITTER)
		TNT1 A 0 A_SpawnProjectile("HDSpent9mm",40)
		TNT1 A 0 A_JumpIfCloser(600,"fullauto")
		TNT1 A 0 A_JumpIfCloser(1200,1)
		goto postshot
		TNT1 A 0 A_Jump(120,"gunshot")
		goto postshot
	postshot:
		TNT1 A 0 A_JumpIfInventory("ZM66Loaded",30,"reload")
		goto super::postshot+1
	ohforfuckssake:
		PLAY E 4
	reload:
		TNT1 A 0 A_ChangeFlag("frightened",1)
		TNT1 A 0 A_PlaySound ("weapons/rifleclick2")
		PLAY AA 1 A_Chase("melee","")
		TNT1 A 0 A_PlaySound ("weapons/rifleload")
		TNT1 A 0 A_SpawnProjectile("HDSMGEmptyMag",38,0,random(90,120))
		goto reload2
	}
}
actor BlackSWAT2 : SWAT2Base
{
	Translation "112:113=92:98","114:120=99:111","121:127=5:8", "48:79=66:79", "18:21=68:68", "213:213=66:66"
}
actor BrownSWAT2 : SWAT2Base
{
	Translation "112:113=92:98","114:120=99:111","121:127=5:8", "48:79=61:79", "18:21=63:63", "213:213=63:63"
}
actor WhiteSWAT2 : SWAT2Base{}
actor SWAT2 : RandomSpawner replaces ScriptedMarine
{
	+friendly
	height 56
	radius 12
	DropItem "BlackSWAT2"
	DropItem "BrownSWAT2"
	DropItem "WhiteSWAT2"
}
actor SWATPist : SWAT2 replaces MarinePistol {}
actor SWATChain : SWAT2 replaces MarineChainsaw {}



ACTOR SWATBase : RiflemanBase
{
	Translation "112:113=92:98","114:120=99:111","121:127=5:8"
	Health 120
	gibhealth 180
	Speed 8
	Decal "BulletScratch"
	+doharmspecies
	DropItem ""
	maxtargetrange 1024
	States
	{
	spwander:
		TNT1 A 0 A_JumpIfInventory("ShotgunTube",1,"reload")
		goto super::spwander
	see:
		TNT1 A 0 A_JumpIfInventory("HunterSpentShell",1,"Chamber")
		TNT1 A 0 A_JumpIfInventory("ShotgunTube",0,"reload")
		TNT1 A 0 A_ChangeFlag("frightened",0)
		goto super::see_all
	ohforfuckssake:
		PLAY E 4
	reload:
		TNT1 A 0 A_ChangeFlag("frightened",1)
		PLAY AA 1 A_Chase("melee","")
		TNT1 A 0 A_PlaySound ("weapons/huntopen")
		PLAY BBCC 1 A_Chase("melee","")
		PLAY DDAA 1 A_Chase("melee","missile")
		TNT1 A 0 A_TakeInventory("ShotgunTube",1)
		TNT1 A 0 A_PlaySound ("weapons/huntreload")
		TNT1 A 0 A_JumpIfInventory("ShotgunTube",1,"reload2")
		goto reloadend
	reload2:
		PLAY BB 4 A_Chase("melee","missile")
		TNT1 A 0 A_TakeInventory("ShotgunTube",1)
		TNT1 A 0 A_PlaySound ("weapons/huntreload")
		TNT1 A 0 A_JumpIfInventory("ShotgunTube",1,1)
		goto reloadend
		PLAY CC 4 A_Chase("melee","missile")
		TNT1 A 0 A_TakeInventory("ShotgunTube",1)
		TNT1 A 0 A_PlaySound ("weapons/huntreload")
		TNT1 A 0 A_JumpIfInventory("ShotgunTube",1,1)
		goto reloadend
		PLAY DD 4 A_Chase("melee","missile")
		TNT1 A 0 A_TakeInventory("ShotgunTube",1)
		TNT1 A 0 A_PlaySound ("weapons/huntreload")
		TNT1 A 0 A_JumpIfInventory("ShotgunTube",1,1)
		goto reloadend
		TNT1 A 0 A_PlaySound ("weapons/pocket")
		PLAY AABBCCDDAA 1 A_Chase("melee","missile")
		loop
	reloadend:
		TNT1 A 0 A_ChangeFlag("frightened",0)
		PLAY BBCCDD 2 A_Chase("melee","")
		TNT1 A 0 A_PlaySound ("weapons/huntopen")
		PLAY EEE 1 A_Chase("melee","missile",CHF_DONTMOVE)
		goto see
	missile:
		TNT1 A 0 A_JumpIfInventory("ShotgunTube",0,"reload")
		TNT1 A 0 A_JumpIfTargetInLOS(3,120)
		PLAY CD 2 A_FaceTarget(90,30)
		PLAY E 1 A_SetTics(random(4,10)) //when they just start to aim, not for followup shots!
	missile2:
		TNT1 A 0 A_JumpIfCloser(200,4)
		TNT1 A 0 A_JumpIfCloser(900,2)
		TNT1 A 0 A_SetUserVar("user_turn",10)
		goto turntoaim
		TNT1 A 0 A_SetUserVar("user_turn",20)
		goto turntoaim
		TNT1 A 0 A_SetUserVar("user_turn",30)
		goto turntoaim
	turntoaim:
		PLAY E 2 A_FaceTarget(user_turn,user_turn)
		TNT1 A 0 A_JumpIfTargetInLOS(1)
		goto see
		TNT1 A 0 A_JumpIfTargetInLOS(1,10)
		loop
		TNT1 A 0 A_FaceTarget(user_turn,user_turn)
		PLAY E 1 A_SetTics(random(0,80/user_turn))
		TNT1 A 0 A_SetUserVar("user_spread",user_turn/10)
		"####" A 0 A_CheckLOF("see",CLOFF_JUMPNONHOSTILE|CLOFF_SKIPTARGET|CLOFF_JUMPOBJECT|CLOFF_MUSTBESOLID|CLOFF_SKIPENEMY,0,0, 0,0, 44,0)
		TNT1 A 0 A_Jump(256,"shoot")
	shoot:
		POSS E 0 A_JumpIf(velx*velx+vely*vely>64,"see")
		PLAY E 0 A_JumpIfInventory("HunterSpentShell",1,"Chamber")
		TNT1 A 0 A_JumpIfInventory("ShotgunTube",0,"ohforfuckssake")
		"####" F 1 bright light("SHOT"){
			A_PlayWeaponSound("weapons/hunter");
			angle+=frandom(-user_spread,user_spread);
			pitch+=frandom(-user_spread,user_spread);
			A_GiveInventory("DecoBulleter00");
		}
		PLAY E 0 A_GiveInventory("ShotgunTube",1)
		PLAY E 2 A_GiveInventory("HunterSpentShell",1)
		TNT1 A 0 A_AlertMonsters(0,AMF_TARGETEMITTER)
		TNT1 A 0 A_JumpIfInventory("EvadeNow",1,"see_evade")
		PLAY E 1 A_SetTics(random(4,12))
		Goto See
	Chamber:
		PLAY E 0 A_SpawnItemEx("HDSpentShell",cos(pitch)*8,0,height-7+sin(pitch)*8,velx+cos(pitch)*cos(angle-random(86,90))*6,vely+cos(pitch)*sin(angle-random(86,90))*6,velz-sin(pitch)*random(5,7),0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH)
		PLAY E 0 A_TakeInventory("HunterSpentShell")
		PLAY E 0 A_Jump(48,3)
		PLAY E 0 A_Jump(64,"see")
		PLAY E 0 A_SpidRefire
		goto missile
		PLAY E 8 A_PlaySoundEx("weapons/shotgr","soundslot5")
		goto see
	Melee:
		PLAY D 6 A_FaceTarget
		PLAY D 4
		PLAY E 4 A_MeleeAttack

		PLAY E 3 A_JumpIfCloser (56, 2)

		PLAY E 3 A_FaceTarget(0,0)
		Goto Missile+1

		PLAY A 3
		Goto See
	Pain:
		TNT1 A 0 A_JumpIfHealthLower (40, 3)
		PLAY G 3 A_ChangeFlag("NODROPOFF",1)
		PLAY G 3 A_Pain
		PLAY AB 2 A_FaceTarget(50,50)
		TNT1 A 0 A_ChangeFlag("frightened",0)
		PLAY CDAB 3 A_FastChase
		PLAY G 0 A_CPosRefire
		Goto Missile
	}
}
actor BlackSWAT : SWATBase
{
	Translation "112:113=92:98","114:120=99:111","121:127=5:8", "48:79=66:79", "18:21=68:68", "213:213=66:66"
}
actor BrownSWAT : SWATBase
{
	Translation "112:113=92:98","114:120=99:111","121:127=5:8", "48:79=61:79", "18:21=63:63", "213:213=63:63"
}
actor WhiteSWAT : SWATBase{}
actor SWAT : Rifleman
{
	DropItem "BlackSWAT"
	DropItem "BrownSWAT"
	DropItem "WhiteSWAT"
}
actor SWATShot : SWAT replaces MarineShotgun {}
actor SWATSuperShot : SWAT replaces MarineSSG {}
actor SWATNoShot : SWAT replaces MarineBerserk {}

actor RocketeerBase : RiflemanBase
{
	translation "112:115=128:139","116:119=144:151","120:127=236:239"
	health 120
	speed 8
	dropitem ""
	meleedamage 6
	meleesound "weapons/smack"
	obituary "%o was fragged by a marine."
	States
	{
	see_evade:
		TNT1 A 0 A_Jump(182,2)
		TNT1 A 0 A_JumpIfTargetInLOS ("Missile")
		TNT1 A 0 A_Chase("missile","melee",CHF_DONTMOVE)
		TNT1 A 0 A_FaceTarget(90,90)
		TNT1 A 0 A_SetAngle(angle+random(60,100)*(random(0,1)*2-1))
		TNT1 A 0 A_ChangeFlag("nodropoff",1)
	see_evade2:
		TNT1 A 0 A_SetAngle(angle+random(-30,30))
		PLAY ABCD 4 A_Recoil(-3)
		TNT1 A 0 A_JumpIf(velx*velx+vely*vely>25,2)
		TNT1 A 0 A_SetAngle(angle+random(-160,200))
		TNT1 A 0 A_ChangeFlag("nodropoff",0)
		TNT1 A 0 A_Jump(72,2)
		TNT1 A 0 A_TakeInventory("EvadeNow",1)
	See:
		TNT1 A 0 A_TakeInventory("NoRocketsForYou",1)
		PLAY A 0 A_JumpIfCloser (64, 4)
		PLAY A 0 A_JumpIfCloser (172, 2)
		PLAY A 0 A_Jump (256, 2)
		PLAY A 0 A_ChangeFlag ("frightened", 1)
		goto see_all
		PLAY A 0 A_ChangeFlag ("frightened", 0)
		goto see_all
	Missile:
		PLAY A 0 A_JumpIfCloser (120, 3)
		PLAY EE 4 A_FaceTarget(30,30)
		PLAY E 0 A_FaceTarget(30,30)
		PLAY E 3 A_SpawnProjectile("RocketDummy",42,0,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,pitch)
		TNT1 A 0 A_JumpIfInventory("NoRocketsForYou",1,"see_evade2")
		PLAY F 6 BRIGHT light("SHOT"){
			A_PlayWeaponSound("weapons/rockignite");
			A_SpawnProjectile("GyroGrenade",42,0,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,pitch);
			A_TakeInventory("NoRocketsForYou",1);
		}
		TNT1 A 0 A_CheckFlag("friendly",2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_AlertMonsters(0,AMF_TARGETEMITTER)
		PLAY E 6 A_Recoil(4)
		goto See
	Melee:
		PLAY D 6 A_FaceTarget(0,0)
		PLAY D 4
		PLAY E 4 A_MeleeAttack
		PLAY A 3
		goto See
	Pain:
		TNT1 A 0 A_JumpIfHealthLower (40, 3)
		PLAY G 3
		PLAY G 3 A_Pain
		PLAY AB 2 A_FaceTarget(50,50)
		PLAY CD 3 A_FastChase
		Goto See
	}
}
actor RocketDummy : FastProjectile
{
	speed 1
	height 4
	radius 4
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_ScaleVelocity(80)
		TNT1 A 3
		stop
	death:
		TNT1 A 1 A_GiveInventory("NoRocketsForYou",1,AAPTR_TARGET)
		stop
	}
}
actor NoRocketsForYou : InventoryFlag {}
actor BlackRocketeer : RocketeerBase
{
	Translation "112:115=128:139","116:119=144:151","120:127=236:239", "48:79=66:79", "18:21=68:68", "213:213=66:66"
}
actor BrownRocketeer : RocketeerBase
{
	Translation "112:115=128:139","116:119=144:151","120:127=236:239", "48:79=61:79", "18:21=63:63", "213:213=63:63"
}
actor WhiteRocketeer : RocketeerBase{}
actor Rocketeer : Rifleman
{
	DropItem "BlackRocketeer"
	DropItem "BrownRocketeer"
	DropItem "WhiteRocketeer"
}
actor RRocketeer : Rocketeer replaces MarineRocket {}
actor BFuglyteer : Rocketeer replaces MarineBFG {}
actor Plasmateer : Rocketeer replaces MarinePlasma {}
actor Railgunteer : Rocketeer replaces MarineRailgun {}


ACTOR TangoBase : RiflemanBase
{
	-friendly
	-pushable
	translation "112:116=163:167","117:124=72:79","125:127=5:8"
	obituary "%o lost the war against terrorism."
	hitobituary "%o supports your war of terror."
	decal "BulletScratch"
	states
	{
	Raise:
		PLAY LKJIH 5
		TNT1 A 0 A_ChangeFlag ("pushable", 0)
		Goto See
	Dead:
		PLAY N 0 A_SpawnItemEx ("DeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		stop
	XDead:
		PLAY N 0 A_SpawnItemEx ("ReallyDeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		stop
	}
}
actor BlackTango : TangoBase
{
	Translation "112:116=163:167","117:124=72:79","125:127=5:8", "48:79=66:79", "18:21=68:68", "213:213=66:66"
}
actor BrownTango : TangoBase
{
	Translation "112:116=163:167","117:124=72:79","125:127=5:8", "48:79=61:79", "18:21=63:63", "213:213=63:63"
}
actor WhiteTango : TangoBase{}
actor Tango : RandomSpawner
{
	height 56
	radius 12
	DropItem "BlackTango"
	DropItem "BrownTango"
	DropItem "WhiteTango"
}

ACTOR ShotTangoBase : SWATBase
{
	-friendly
	-pushable
	translation "112:116=163:167","117:124=72:79","125:127=5:8"
	obituary "%o lost the war against terrorism."
	hitobituary "%o supports your war of terror."
	decal "BulletScratch"
	DropItem ""
	states
	{
	Raise:
		PLAY LKJIH 5
		TNT1 A 0 A_ChangeFlag ("pushable", 0)
		Goto See
	Dead:
		PLAY N 0 A_SpawnItemEx ("DeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		stop
	XDead:
		PLAY N 0 A_SpawnItemEx ("ReallyDeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		stop
	}
}
actor BlackShotTango : ShotTangoBase
{
	Translation "112:116=163:167","117:124=72:79","125:127=5:8", "48:79=66:79", "18:21=68:68", "213:213=66:66"
}
actor BrownShotTango : ShotTangoBase
{
	Translation "112:116=163:167","117:124=72:79","125:127=5:8", "48:79=61:79", "18:21=63:63", "213:213=63:63"
}
actor WhiteShotTango : ShotTangoBase{}
actor ShotTango : Tango
{
	DropItem "BlackShotTango"
	DropItem "BrownShotTango"
	DropItem "WhiteShotTango"
}


ACTOR SMGTangoBase : SWAT2Base
{
	-friendly
	-pushable
	translation "112:116=163:167","117:124=72:79","125:127=5:8"
	obituary "%o lost the war against terrorism."
	hitobituary "%o supports your war of terror."
	decal "BulletScratch"
	DropItem ""
	states
	{
	Raise:
		PLAY LKJIH 5
		TNT1 A 0 A_ChangeFlag ("pushable", 0)
		Goto See
	Dead:
		PLAY N 0 A_SpawnItemEx ("DeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		stop
	XDead:
		PLAY N 0 A_SpawnItemEx ("ReallyDeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		stop
	}
}
actor BlackSMGTango : SMGTangoBase
{
	Translation "112:116=163:167","117:124=72:79","125:127=5:8", "48:79=66:79", "18:21=68:68", "213:213=66:66"
}
actor BrownSMGTango : SMGTangoBase
{
	Translation "112:116=163:167","117:124=72:79","125:127=5:8", "48:79=61:79", "18:21=63:63", "213:213=63:63"
}
actor WhiteSMGTango : SMGTangoBase{}
actor SMGTango : Tango
{
	DropItem "BlackSMGTango"
	DropItem "BrownSMGTango"
	DropItem "WhiteSMGTango"
}


ACTOR RocketTangoBase : RocketeerBase
{
	-friendly
	-pushable
	translation "112:116=163:167","117:124=72:79","125:127=5:8"
	obituary "%o lost the war against terrorism."
	hitobituary "%o supports your war of terror."
	dropitem ""
	states
	{
	Raise:
		PLAY LKJIH 5
		TNT1 A 0 A_ChangeFlag ("pushable", 0)
		Goto See
	Dead:
		PLAY N 0 A_SpawnItemEx ("DeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		stop
	XDead:
		PLAY N 0 A_SpawnItemEx ("ReallyDeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		stop
	}
}
actor BlackRocketTango : RocketTangoBase
{
	Translation "112:116=163:167","117:124=72:79","125:127=5:8", "48:79=66:79", "18:21=68:68", "213:213=66:66"
}
actor BrownRocketTango : RocketTangoBase
{
	Translation "112:116=163:167","117:124=72:79","125:127=5:8", "48:79=61:79", "18:21=63:63", "213:213=63:63"
}
actor WhiteRocketTango : RocketTangoBase{}
actor RocketTango : Tango
{
	DropItem "BlackRocketTango"
	DropItem "BrownRocketTango"
	DropItem "WhiteRocketTango"
}



// ------------------------------------------------------------
//   Marine corpse used by player pawn
// ------------------------------------------------------------

actor DeadRifleman replaces DeadMarine
{
	translation "112:120=152:159","121:127=9:12"
	-solid
	+shootable
	+ismonster
	+telestomp
	mass 200
	radius 12
	height 56
	health 1
	States
	{
	Spawn:
		PLAY A 0 nodelay
	Spawn2:
		"####" A 1 A_Die("Spawn")
		wait
	XDeath:
	Death:
		"####" A 0 A_NoBlocking
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
	Dead:
		"####" K 3 canraise A_JumpIf(abs(velz)<2,1)
		loop
		"####" LM 5 canraise A_JumpIf(abs(velz)>=2,"Dead")
	Death.Spawn:
		"####" A 0 A_NoBlocking
		"####" A 0 A_ChangeFlag("shootable",1)
	Dead2:
		"####" N 5 canraise A_JumpIf(abs(velz)>=2,"Dead")
		loop
	Raise:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_UnsetShootable
		"####" A 0 A_SetSolid
		"####" MMK 7 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" JHE 4
		"####" A 0 A_UnsetSolid
		"####" H 0 A_Jump (32,3)
		TNT1 A 1 A_SpawnItemEx ("UndeadRifleman", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION|SXF_ISTRACER)
		TNT1 A 0 A_GiveInventory("HasDroppedGun",1,AAPTR_TRACER)
		stop
		TNT1 A 1 A_SpawnItemEx ("RiflemanBase", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION|SXF_ISTRACER)
		TNT1 A 0 A_GiveInventory("HasDroppedGun",1,AAPTR_TRACER)
		stop
	XXDeath:
		PLAY O 5 A_ChangeFlag("shootable",0)
		PLAY O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		PLAY P 5 A_XScream
		PLAY P 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		PLAY Q 5 A_NoBlocking
		PLAY Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		PLAY RSTUV 5
		PLAY N 0 A_SpawnItemEx("ReallyDeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		stop
	}
}
actor DeadRiflemanCrouched : DeadRifleman
{
	States
	{
	Spawn:
		PLYC A 0 nodelay
		goto super::spawn2
	}
}
actor ReallyDeadRifleman : DeadRifleman replaces GibbedMarine
{
	States
	{
	Spawn:
		PLAY A 0
		"####" A 1 A_Die
		wait
	XDeath:
	Death:
		"####" W 1 A_NoBlocking
	XDead:
		"####" V 3 canraise A_JumpIf(abs(velz)<2,1)
		wait
		"####" W 5 canraise A_JumpIf(floorz<=z-6,"XDead")
		wait
	Raise:
		"----" A 0 A_UnsetShootable
		"####" A 0 A_Jump (172, "RaiseZombie")
		"####" WW 8 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" VUT 7
		"####" SRQ 5
		"####" POH 4
		"####" IJKLM 4
		"----" A 1 A_SpawnItemEx ("DeadRifleman", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION|SXF_ISTRACER)
		"----" A 0 A_GiveInventory("HasDroppedGun",1,AAPTR_TRACER)
		stop
	RaiseZombie:
		PLAY U 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" U 8
		"####" T 4
		"####" T 2 A_PlaySound("weapons/bigcrack",6)
		POSS S 2 A_PlaySound("misc/wallchunks",7)
		POSS AAAAA 0 A_SpawnItemEx("HugeWallChunk",0,0,40,random(4,6),0,random(-2,7),random(1,360))
		"####" SRQ 6
		"####" PONMH 4
		"####" IJKL 4
		"----" A 1 A_SpawnItemEx("DeadZombieStormtrooper",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_ISMASTER)
		"----" A 0 A_GiveInventory("HasDroppedGun",1,AAPTR_MASTER)
		stop
	}
}
actor ReallyDeadRiflemanCrouched : ReallyDeadRifleman replaces GibbedMarineExtra
{
	States
	{
	Spawn:
		PLYC A 0 nodelay
		goto super::spawn2
	}
}


// ------------------------------------------------------------
//   Undead marines
// ------------------------------------------------------------

actor UndeadRifleman : TangoBase
{
	-telestomp
	Translation "112:120=152:159","121:127=9:12"
	Health 100
	gibhealth 100
	SeeSound "grunt/sight"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "%o was shot up by an undead marine."
	HitObituary "%o was beat up by an undead marine."
	States
	{
	Dead:
		PLAY N 0 A_SpawnItemEx ("ReDeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		stop
	XDead:
		PLAY N 0 A_SpawnItemEx ("ReallyReDeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION)
		Stop
	}
}
actor ReDeadRifleman : DeadRifleman
{
	States
	{
	Raise:
		"----" A 0 A_GiveInventory("HasDroppedGun",1)
		"----" A 0 A_UnsetShootable
		PLAY MML 8 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		PLAY KJIH 4
		TNT1 A 0 A_Jump(80,"RaiseBotch")
		TNT1 A 0 A_UnsetSolid
		TNT1 A 1 A_SpawnItemEx ("UndeadRifleman", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION|SXF_ISMASTER)
		TNT1 A 0 A_GiveInventory("HasDroppedGun",1,AAPTR_MASTER)
		stop
	RaiseBotch:
		PLAY O 5 A_ChangeFlag("shootable", 0)
		PLAY O 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		PLAY P 5 A_XScream
		PLAY P 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		PLAY Q 5 A_NoBlocking
		PLAY Q 0 A_SpawnItemEx("MegaBloodSplatter",0,0,34,0,0,0,0,SXF_NOCHECKPOSITION)
		PLAY RSTUV 5
		PLAY N 0 A_SpawnItemEx("ReallyReDeadRifleman",0,0,0,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION|SXF_ISTRACER)
		"----" A 0 A_GiveInventory("HasDroppedGun",1,AAPTR_TRACER)
		Stop
	}
}
actor ReallyReDeadRifleman : ReallyDeadRifleman
{
	States
	{
	Raise:
		"----" A 0 A_GiveInventory("HasDroppedGun",1)
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_UnsetShootable
		PLAY A 0 A_Jump (172, "RaiseZombie")
		"####" WW 8 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" VUT 8
		"####" SRQ 6
		"####" POH 4
		"####" IJKLM 4
		TNT1 A 1 A_SpawnItemEx ("RedeadRifleman", 0,0,0, 0,0,0, 0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS|SXF_TRANSFERTRANSLATION|SXF_ISTRACER)
		TNT1 A 0 A_GiveInventory("HasDroppedGun",1,AAPTR_TRACER)
		stop
	}
}


// ------------------------------------------------------------
//   Summoned marines - no friendly fire
// ------------------------------------------------------------

actor ScaryItem : ActionItem
{
	states
	{
	pickup:
		TNT1 A 0 A_ChangeFlag("frightened",1)
		stop
	}
}
actor SquadRiflemanBase
{
	damagefactor "GhostSquadAttack", 0
	Translation "112:120=152:159","121:127=9:12"
	maxtargetrange 65536
	maxdropoffheight 40
	maxstepheight 40
	health 200000000
	gibhealth 500
	renderstyle add
	+noblooddecals bloodtype "idledummy"
	height 50
	radius 12
	speed 8
	damage(1)
	dropitem SquadSummoner 8
	+shootable +noblockmonst +ghost +shadow -solid -pushable +nopain +nofear +seeinvisible +nodamage +noclip +nonshootable
	States
	{
	Spawn:
		PLAY E 10 A_Look
		loop
	See:
		PLAY E 0 A_GiveInventory("PowerFrightener",1)
		PLAY E 0 A_RadiusGive("ScaryItem",512,RGF_MONSTERS|RGF_CUBE,1)
		PLAY AABBCCDD 2 A_Chase
		TNT1 A 0 A_ClearTarget
		PLAY E 0 HealThing(1)
		PLAY E 0 A_GiveToTarget("ScaryItem",1)
		PLAY E 0 A_GiveInventory("NotShot",1)
		PLAY E 0 A_JumpIfInventory("NotShot",360,"Fade")
		loop
	Fade:
		PLAY A 0 bright A_NoBlocking
	Fade2:
		PLAY A 2 A_Wander
		PLAY E 0 A_FadeOut(0.1)
		PLAY A 2 A_Wander
		PLAY E 0 A_FadeOut(0.1)
		PLAY B 2 A_Wander
		PLAY E 0 A_FadeOut(0.1)
		PLAY B 2 A_Wander
		PLAY E 0 A_FadeOut(0.1)
		PLAY C 2 A_Wander
		PLAY E 0 A_FadeOut(0.1)
		PLAY C 2 A_Wander
		PLAY E 0 A_FadeOut(0.1)
		PLAY D 2 A_Wander
		PLAY E 0 A_FadeOut(0.1)
		PLAY D 2 A_Wander
		PLAY E 0 A_FadeOut(0.1)
		loop
	Missile:
		TNT1 A 0 A_CheckFlag("friendly",2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_AlertMonsters(0,AMF_TARGETEMITTER)
		PLAY E 0 A_GiveToTarget("ScaryItem",1)
		PLAY E 0 A_RadiusGive("ScaryItem",1024,RGF_MONSTERS|RGF_CUBE,1)
		PLAY E 0 A_GiveInventory("PowerFrightener",1)
		PLAY E 0 A_TakeInventory("NotShot")
		PLAY E 3 A_Jump(128,1)
	Missile2:
		PLAY E 7 A_FaceTarget(0,0)
		PLAY E 0 A_JumpIfCloser(500,"Rifle")
	Grenade:
		PLAY E 0 A_Jump(160,"Rifle")
		PLAY E 0 A_JumpIfCloser(760,2)
		PLAY E 0 A_Jump(256,"rifle")
		PLAY E 0 NoiseAlert(0,0)
		PLAY E 0 A_PlaySound ("weapons/grenadeshot")
		PLAY E 12 A_SpawnProjectile ("SquadGyroGrenade",44,0,0,0,14)
		PLAY E 0 A_Jump(256,"see")
	Rifle:
		PLAY E 4 A_JumpIfCloser(600,"RifleCQB")
		PLAY E 0 A_Jump(256,"RifleSnipe")
	RifleSnipe:
		PLAY F 0 A_PlayWeaponSound("weapons/bigrifle")
		PLAY F 1 bright light("SHOT"){
			pitch+=frandom(-1,1);
			A_SpawnItemEx("HDSquadFourMilFast",0,0,44,velx,vely,velz,frandom(-1,1),SXF_TRANSFERPITCH|SXF_SETTARGET|SXF_SETMASTER|SXF_ABSOLUTEMOMENTUM);
		}
		PLAY E 2 A_Jump (24, "PostSnipe")

		PLAY F 0 A_PlayWeaponSound("weapons/bigrifle")
		PLAY F 1 bright light("SHOT"){
			pitch+=frandom(-1,1);
			A_SpawnItemEx("HDSquadFourMilFast",0,0,44,velx,vely,velz,frandom(-1,1),SXF_TRANSFERPITCH|SXF_SETTARGET|SXF_SETMASTER|SXF_ABSOLUTEMOMENTUM);
		}
		PLAY E 2 A_Jump (168, "PostSnipe")

		PLAY F 0 A_PlayWeaponSound("weapons/bigrifle")
		PLAY F 1 bright light("SHOT"){
			pitch+=frandom(-1,1);
			A_SpawnItemEx("HDSquadFourMilFast",0,0,44,velx,vely,velz,frandom(-1,1),SXF_TRANSFERPITCH|SXF_SETTARGET|SXF_SETMASTER|SXF_ABSOLUTEMOMENTUM);
		}
		PLAY E 2 A_Jump (96,"PostSnipe")
		TNT1 A 0 A_Jump (256,"PostSnipe")
	PostSnipe:
		PLAY E 6
		PLAY E 0 A_Jump(256,"see")
	RifleCQB:
		PLAY F 0 A_PlayWeaponSound("weapons/bigrifle")
		PLAY F 1 bright light("SHOT"){
			pitch+=frandom(-1,1);
			A_SpawnItemEx("HDSquadFourMilFast",0,0,44,velx,vely,velz,frandom(-1,1),SXF_TRANSFERPITCH|SXF_SETTARGET|SXF_SETMASTER|SXF_ABSOLUTEMOMENTUM);
		}
		PLAY E 2

		PLAY F 0 A_PlayWeaponSound("weapons/bigrifle")
		PLAY F 1 bright light("SHOT"){
			pitch+=frandom(-1,1);
			A_SpawnItemEx("HDSquadFourMilFast",0,0,44,velx,vely,velz,frandom(-1,1),SXF_TRANSFERPITCH|SXF_SETTARGET|SXF_SETMASTER|SXF_ABSOLUTEMOMENTUM);
		}
		PLAY E 2 A_Jump (32,"PostCQB")

		PLAY F 0 A_PlayWeaponSound("weapons/bigrifle")
		PLAY F 1 bright light("SHOT"){
			pitch+=frandom(-1,1);
			A_SpawnItemEx("HDSquadFourMilFast",0,0,44,velx,vely,velz,frandom(-1,1),SXF_TRANSFERPITCH|SXF_SETTARGET|SXF_SETMASTER|SXF_ABSOLUTEMOMENTUM);
		}
		PLAY E 2 A_Jump (96, "PostCQB")

		PLAY F 0 A_PlayWeaponSound("weapons/bigrifle")
		PLAY F 1 bright light("SHOT"){
			pitch+=frandom(-1,1);
			A_SpawnItemEx("HDSquadFourMilFast",0,0,44,velx,vely,velz,frandom(-1,1),SXF_TRANSFERPITCH|SXF_SETTARGET|SXF_SETMASTER|SXF_ABSOLUTEMOMENTUM);
		}
		PLAY E 2
		TNT1 A 0 A_Jump (256, "PostCQB")
	PostCQB:
		PLAY E 0 A_Jump(96,3)
		PLAY E 9 A_CPosRefire
		PLAY E 0 A_Jump(256,"missile2")
		PLAY E 6
		PLAY E 0 A_Jump(256,"see")
	Melee:
		PLAY E 0 A_TakeInventory("NotShot")
		PLAY C 8 A_FaceTarget(0,0)
		PLAY D 4
		PLAY E 1 A_CustomMeleeAttack((1),"weapons/smack","")
		PLAY E 3 {if(random(1,12)==1){A_GiveInventory("Health",1,AAPTR_TARGET);}}

		PLAY E 3 A_JumpIfCloser(64,3)
		PLAY E 4 A_FaceTarget(0,0)
		PLAY E 0 A_Jump(256,"missile2")

		PLAY A 4
		PLAY E 0 A_Jump(256,"see")

	Pain:
		PLAY G 4
		PLAY G 4 A_Pain
		PLAY G 0 A_Jump(100,"See")
		PLAY AB 2 A_FaceTarget(0,0)
		PLAY CD 3 A_FastChase
		PLAY G 0 A_CPosRefire
		PLAY E 0 A_Jump(256,"missile")
	Death:
	XDeath:
		PLAY H 6
		PLAY I 6 A_Scream
		PLAY J 6 A_NoBlocking
		PLAY KKKLLLMMM 2 A_FadeOut(0.1)
		PLAY N 2 A_FadeOut(0.1)
		wait
	Raise:
		stop
	}
}
actor BlackSquadRifleman : SquadRiflemanBase{
	Translation "112:120=152:159","121:127=9:12", "48:79=66:79", "18:21=68:68", "213:213=66:66"
}
actor BrownSquadRifleman : SquadRiflemanBase{
	Translation "112:120=152:159","121:127=9:12", "48:79=61:79", "18:21=63:63", "213:213=63:63"
}
actor WhiteSquadRifleman:SquadRiflemanBase{}
actor SquadRifleman:RandomSpawner{
	+friendly height 56 radius 12
	DropItem "BlackSquadRifleman"
	DropItem "BrownSquadRifleman"
	DropItem "WhiteSquadRifleman"
}
actor HDSquadFourMilFast:FastProjectile{
	damagetype "GhostSquadAttack" damage(1) +forcepain +hittracer
	height 2 radius 2 speed 1000
	states{
	death:
		TNT1 AA 0 A_SpawnItemEx("BulletPuffBig",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 1 A_AlertMonsters
		stop
	xdeath:
		TNT1 A 1
		TNT1 A 0{if(random(1,3)==1){A_GiveInventory("Health",1,AAPTR_TRACER);}}
		stop
	}
}
ACTOR SquadGyroGrenade:GyroGrenade{
	damagetype "GhostSquadAttack" damage(1) +forcepain renderstyle add
	states{
	Death:
	Explode:
		TNT1 A 0 bright A_NoGravity
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx ("HugeWallChunk", 0, 0, 0, random(-7,7),random(-7,7),random(4,18),random(0,360),160)
		TNT1 A 1 A_Explode(1,512,0,1,512)
		TNT1 A 1{if(random(1,7)==1){A_RadiusGive("Health",512,RGF_PLAYERS|RGF_MONSTERS,1);}}
		goto Death1
	Death1:
		TNT1 AA 0 A_SpawnItemEx ("HDBBWTH", random(-1,1),random(-1,1),2, 0,0,0, 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 2 A_SpawnItemEx ("HDBBWTH", 0,0,0, 0,0,2, 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
	Death2:
		TNT1 AAA 0 A_SpawnItemEx ("HDSmoke", random(-6,6),random(-6,6),1, random(-1,4),random(-1,1),0, random(-15,15),SXF_NOCHECKPOSITION)
		TNT1 A 0 A_SetTranslucent (1, 1)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SpawnItemEx ("DistantRocket", 0,0,0, 0,0,0, 0,32)
		TNT1 A 21 A_Quake(2,21,0,200,none)
		Stop
	}
}

actor SquadSummoner:HDPickup{
	inventory.maxamount 3
	inventory.interhubamount 3
	inventory.icon PLHELMA0
	inventory.pickupsound "misc/p_pkup"
	inventory.pickupmessage "Picked up a summoning talisman."
	+inventory.fancypickupsound
	+forcexybillboard
	states{
	spawn2:
		PRIF A -1
	use:
		TNT1 A 0{
			A_PlaySound("misc/p_pkup",CHAN_AUTO);
			A_AlertMonsters;
			A_SpawnItemEx("SquadRifleman", 0,0,0, 8,0,0, 0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("SquadRifleman", 0,0,0, 0,5,0, 0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("SquadRifleman", 0,0,0, 0,-5,0, 0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("HDSmoke", 0,0,0, 8,0,0, 0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("HDSmoke", 0,0,0, 0,5,0, 0,SXF_NOCHECKPOSITION);
			A_SpawnItemEx("HDSmoke", 0,0,0, 0,-5,0, 0,SXF_NOCHECKPOSITION);
		}
		TNT1 A 0 A_Jump(256,1,2,3,4)
		TNT1 A 0 A_PrintBold("\cj'They shall stand again and hear there\n\cja horn in the hills ringing.\n\n\cjWhose shall the horn be?'",8)
		stop
		TNT1 A 0 A_PrintBold("\cj'For this war will last through years uncounted\n\n\cjand you shall be summoned once again ere the end.'",8)
		stop
		TNT1 A 0 A_PrintBold("\cj'Faint cries I heard, and dim horns blowing,\n\n\cjand a murmur as of countless far voices:\n\n\n\cjit was like the echo of some forgotten battle\n\n\cjin the Dark Years long ago.'",8)
		stop
		TNT1 A 0 A_PrintBold("\cj'Pale swords were drawn; but I know not\n\n\cjwhether their blades would still bite,\n\n\n\cjfor the Dead needed no longer\n\n\cjany weapon but fear.'",8)
	}
}




actor BotBot:RiflemanBase{
	obituary "%o died."
	+noblockmonst
	+nofear
	+donthurtspecies
	species "Player"
	states{
	death:
		"####" A 0{
			A_GiveInventory("BotKill",1,AAPTR_MASTER);
			A_DamageMaster(1000000,"bleedout",DMSS_KILL,"None","None",AAPTR_TARGET,AAPTR_TARGET);
		}goto super::death
	death.bleedout:
		"####" A 0{
			A_GiveInventory("BotKill",1,AAPTR_MASTER);
			A_DamageMaster(1000000,"bleedout",DMSS_KILL,"None","None",AAPTR_TARGET,AAPTR_TARGET);
		}goto super::death.bleedout
	xdeath:
		"####" A 0{
			A_GiveInventory("BotKill",1,AAPTR_MASTER);
			A_DamageMaster(1000000,"bleedout",DMSS_KILL,"None","None",AAPTR_TARGET,AAPTR_TARGET);
		}goto super::xdeath
	see:
		"----" A 0 A_TakeInventory("NotSeen")
		goto super::see
	missile:
		"----" A 0 A_TakeInventory("NotSeen")
		goto super::missile
	melee:
		"----" A 0 A_TakeInventory("NotSeen")
		goto super::melee
	pain:
		"----" A 0 A_TakeInventory("NotSeen")
		goto super::pain
	spawn2:
		"----" A 0 A_JumpIf(CallACS("CheckDM")>0,2)
		"----" A 0 A_CheckSight(3)
		"----" A 0 A_TakeInventory("NotSeen")
		"----" A 0 A_Jump(256,3)
		"----" A 0 A_GiveInventory("NotSeen",1)
		"----" A 0 A_JumpIfInventory("NotSeen",12,"warpandwait")
		TNT1 A 0 A_Look
		TNT1 A 0 A_Jump(60,"spawn")
		TNT1 A 0 A_SetAngle(angle+random(-30,30))
		PLAY EEE 3 A_Look
		TNT1 A 0 A_SetAngle(angle+random(-30,30))
		PLAY EEE 3 A_Look
		TNT1 A 0 HealThing(2)
		TNT1 A 0 A_Jump(60,"spawn")
		loop
	spwander:
		PLAY E 0 A_ClearTarget
		"----" A 0 A_JumpIf(CallACS("CheckDM")>0,2)
		"----" A 0 A_CheckSight(3)
		"----" A 0 A_TakeInventory("NotSeen")
		"----" A 0 A_Jump(256,3)
		"----" A 0 A_GiveInventory("NotSeen",1)
		"----" A 0 A_JumpIfInventory("NotSeen",7,"warpandwait")
		PLAY AA 3 A_Wander
		TNT1 A 0 A_Chase("melee","missile",CHF_DONTMOVE)
		PLAY BB 3 A_Wander
		TNT1 A 0 A_Chase("melee","missile",CHF_DONTMOVE)
		PLAY CC 3 A_Wander
		TNT1 A 0 A_Chase("melee","missile",CHF_DONTMOVE)
		PLAY DD 3 A_Wander
		TNT1 A 0 HealThing(3)
		PLAY E 0 A_Jump(128,"spwander")
		PLAY E 0 A_Jump(256,"spwander2")
	warpandwait:
		TNT1 A 0 A_ChangeFlag("shootable",0)
		TNT1 A 0 A_ChangeFlag("solid",0)
		TNT1 A 0 A_ChangeFlag("canusewalls",0)
		TNT1 A 0 A_ChangeFlag("noblockmap",1)
		TNT1 A 0 A_ChangeFlag("cannotpush",1)
		TNT1 A 0 A_ChangeFlag("noteleport",1)
		TNT1 A 0 A_ChangeFlag("friendly",0)
		TNT1 A 0 A_ChangeFlag("nofear",0)
		TNT1 A 0 A_ChangeFlag("frightened",1)
		TNT1 A 0 A_Warp(AAPTR_PLAYER1,0,0,0,0,WARPF_TOFLOOR|WARPF_NOCHECKPOSITION)
	warpedwait:
		TNT1 AAAAAA 0 A_Chase("","")
		TNT1 A 0 A_Jump(4,"warpandwait")
		TNT1 A 0 A_Jump(4,2)
		TNT1 A 1 A_CheckSight(1)
		loop
		TNT1 A 0 A_ChangeFlag("shootable",1)
		TNT1 A 0 A_ChangeFlag("solid",1)
		TNT1 A 0 A_ChangeFlag("friendly",1)
		TNT1 A 0 A_ChangeFlag("nofear",1)
		TNT1 A 0 A_ChangeFlag("frightened",0)
		TNT1 A 0 A_ChangeFlag("canusewalls",1)
		TNT1 A 0 A_ChangeFlag("noblockmap",0)
		TNT1 A 0 A_ChangeFlag("cannotpush",0)
		TNT1 A 0 A_ChangeFlag("noteleport",0)
		TNT1 A 0 A_TakeInventory("NotSeen",100)
		PLAY A 0 A_Stop
		goto spawn
	}
}
actor NotSeen:InventoryFlag{inventory.maxamount 100}
actor BotBotF:BotBot{
	PainSound "marinef/pain"
	DeathSound "marinef/death"
}



