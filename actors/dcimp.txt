// ------------------------------------------------------------
// Imp
// ------------------------------------------------------------

actor DrunkImpBallTail:BlackParticleFountain{
	+nointeraction +forcexybillboard -invisible
	renderstyle add
	-nogravity
	alpha 0.5 scale 0.6
	radius 12 height 12
	states{
	spawn:
		BAL1 BC 2 bright A_PlaySound("misc/firecrkl",0,0.2,0,4)
	death:
		BAL1 DDEE 2 bright A_FadeOut (0.2)
		stop
	}
}
actor ReverseImpBallTail:RedParticleFountain{
	renderStyle add
	+nointeraction +forcexybillboard -invisible
	-nogravity
	scale 0.6 alpha 0
	radius 16
	height 12
	states{
		spawn:
			BAL1 EEDC 2 bright A_FadeIn (0.2)
		death:
			BAL1 BAB 2 bright A_FadeIn (0.2)
			stop
	}
}
actor DrunkImpBall:DoomImpBall{
	projectile
	+seekermissile +forcexybillboard
	decal "DoomImpScorch"
	radius 4 height 5
	speed 12 scale 0.8
	damage 3 damagetype "Thermal"
	var int user_velx; var int user_vely;
	states{
	spawn:
		BAL1 A 0 nodelay{
			A_PlaySound("imp/attack",CHAN_VOICE);
			user_velx=velx;user_vely=vely;
		}
		BAL1 ABABAB 2 bright
		BAL1 B 2 A_JumpIfTargetInLOS("see")
		BAL1 ABABAB 2 bright A_SeekerMissile(6,9)
	See:
		BAL1 AB 2 bright A_SpawnItemEx ("DrunkImpBallTail", -2,0,0, velx*0.6,vely*0.6,velz*0.6+random(0,1), 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION)
		BAL1 AB 1 bright
		BAL1 A 2 bright A_SpawnItemEx ("DrunkImpBallTail", -2,0,0, velx*0.6,vely*0.6,velz*0.6+random(0,1), 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION)
		BAL1 BA 1 bright
		BAL1 B 2 bright A_SpawnItemEx ("DrunkImpBallTail", -2,0,0, velx*0.7,vely*0.7,velz*0.7+random(0,1), 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION)
		BAL1 AB 1 bright A_BishopMissileWeave
		TNT1 A 0 A_SetUserVar("user_velx",velx)
		TNT1 A 0 A_SetUserVar("user_vely",vely)
		BAL1 A 2 bright A_SpawnItemEx ("DrunkImpBallTail", -2,0,0, velx*0.8,vely*0.8,velz*0.8+random(0,1), 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION)
		BAL1 B 2 A_JumpIfTargetInLOS (2,360)
		BAL1 B 0 A_Jump(256,2)
		BAL1 B 0 A_SeekerMissile(6,9)
		BAL1 B 0{
			user_velx=velx;
			user_vely=vely;
			ThrustThingZ (0, random(-2, 4), random(0,1), 1);
		}loop
	Death:
		BAL1 C 0{
			A_ChangeVelocity(user_velx>>3,user_vely>>3,frandom(1,2));
			A_SpawnItemEx("HDSmoke",0,0,0,user_velx>>2,user_vely>>2,random(1,2),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM);
			A_RadiusGive("HDFireStarter", 32, RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS|RGF_CORPSES, 1);
		}
		BAL1 CDE 4 bright A_FadeOut (0.2)
		Stop
	}
}

actor DrunkImp:DoomImp
{
	damagefactor "Thermal", 0.66
	Health 66
	gibhealth 100
	Radius 12
	Height 54
	Speed 12
	Damage 4
	MeleeDamage 4
	PainChance 80
	+MISSILEMORE
	+FASTER
	translation "64:72=50:71"
	meleesound "imp/melee"
	obituary "%o was marinated by the imps."
	hitobituary "%o was tenderized by the imps."
	var int user_angle;var int user_pitch;var int user_change;
	States
	{
	spawn2:
		"####" A 4 A_Wander
		"----" A 0 A_Look
		"####" B 4 A_Wander
		"----" A 0 A_Look
		"####" C 4 A_Wander
		"----" A 0 A_Look
		"####" D 4 A_Wander
		"####" A 0 A_Jump(198,"spawn2")
		"####" A 0 A_Recoil(-0.4)
		"####" A 0 A_Jump(64,"spawn0")
		loop
	spawn:
		TROO A 0
	spawn0:
		"----" A 0{
			if(countinv("Clip")<1){
				A_GiveInventory("Clip",1);
				A_SetScale(scalex*frandom(0.9,1.1));
				A_SetSize(radius*scalex,height*scaley);
				mass*=scalex;
				if(scalex>1.0){
					A_SetHealth(health*scalex);
				}
			}
		}
		"####" AAABBCCCDD 8 A_Look
		"####" A 0 A_SetAngle(angle+random(-4,4))
		"####" A 1 A_SetTics(random(1,3))
		"----" A 0 A_Jump(216,2)
		"----" A 0 A_PlaySound("imp/active")
		"####" A 0 A_CheckFlag("ambush","spawn0")
		"####" A 0 A_Jump(32,"spawn2")
		loop
	see_evade:
		"----" A 0 A_Chase("missile","melee",CHF_DONTMOVE)
		"----" A 0 A_FaceTarget
		"----" A 0 A_SetAngle(angle+random(40,120)*(random(0,1)*2-1))
		"----" A 0 A_ChangeFlag("nodropoff",1)
	see_evade2:
		"----" A 0 A_SetAngle(angle+random(-30,30))
		"####" ABCD 3 A_Recoil(-3)
		"----" A 0 A_JumpIf(velx*velx+vely*vely>25,2)
		"----" A 0 A_SetAngle(angle+random(-160,200))
		"----" A 0 A_ChangeFlag("nodropoff",0)
		"----" A 0 A_TakeInventory("EvadeNow",1)
		"----" A 0 A_JumpIfTargetInLOS ("Missile")
		goto See
	See:
		"----" A 0 A_JumpIf(floorz<z-4,2)
		"----" A 0 A_JumpIfInventory("EvadeNow",1,"see_evade")
		"####" AABBCCDD 2 A_Chase
		"----" A 0 A_Jump (180, 2)
		"----" A 0 A_AlertMonsters
		"####" A 0 HealThing (random(2,8))
		Loop
	Missile:
		"####" ABCD 4{
			A_FaceTarget(40,40);
			if(A_JumpIfTargetInLOS("null",40)){return state("missile0");}
			else if(!A_JumpIfTargetInLOS("null")){return state("see");}
			return state("");
		}loop
	missile0:
		"----" A 0 A_JumpIfTargetInLOS(1)
		goto spam
		"####" E 0{
			A_ChangeFlag("NODROPOFF",1);
			A_ChangeVelocity(1,random(-1,1)*random(0,3),0,CVF_RELATIVE);
		}
		"####" E 0 A_Jump(16,"hork")
		"####" E 0 A_JumpIfCloser(512,"lead")
		goto hork

	lead:
		"####" E 6 A_FaceTarget(40,40)
		"####" E 3{
			A_FaceTarget(20,40);
			A_SetUserVar(user_angle,angle);
			A_SetUserVar(user_pitch,pitch);
		}
		"####" E 0{
			A_FaceTarget(20,40);
			A_SetUserVar(user_angle,angle-user_angle);
			A_SetUserVar(user_pitch,pitch-user_pitch);
			A_SetUserVar(user_change,getdistance(true,AAPTR_TARGET)/random(28,32)); //projectilespeed/T
			A_SetAngle(angle+user_angle*user_change);
			A_SetPitch(pitch+user_pitch*user_change);
		}
		"####" F 4
		"####" G 8{
			A_SpawnItemEx("DrunkImpBall",0,0,32,velx+12*cos(angle)*cos(pitch), vely+12*sin(angle)*cos(pitch),velz+12*sin(-pitch),0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION|SXF_SETTARGET);
		}
		"####" F 6
		"----" A 0 A_JumpIfTargetInsideMeleeRange("melee")
		"####" E 0 A_Jump(16,"see")
		"####" E 0 A_Jump(40,"spam","hork")
		goto missile

	spam:
		"----" A 0 A_JumpIfTargetInLOS(2)
		"----" A 0 A_Jump(256,3)
		"####" DD 2 A_FaceTarget(12,24)
		"####" E 5 A_SetTics(random(4,6))
		"####" E 0 A_JumpIfTargetInLOS(1)
		goto missile1a
		"####" E 2 A_FaceTarget(12,24)
	missile1a:
		"####" F 2
		"####" G 6 A_SpawnProjectile("DrunkImpBall",32,6,random(-8,8),CMF_AIMDIRECTION,pitch+random(-6,6))
		"####" F 4
		"####" F 0 A_JumpIfTargetInLOS(1)
		goto coverfire
		"----" A 0 A_Jump(120,"missile")
		goto see
	coverfire:
		"----" A 0 A_JumpIfTargetInsideMeleeRange("melee")
		"####" E random(2,7) A_JumpIfTargetInLOS("Missile")
		"----" A 0 A_Jump(180,"missile1a")
		"----" A 0 A_Jump(40,"hork")
		"----" A 0 A_Jump(40,"see")
		"----" A 0 A_Jump(80,1)
		loop
		"####" AABBCCDD 2 A_Chase
		goto missile
	hork:
		"####" E 0 A_Jump (156,"spam")
		"----" A 0 A_JumpIfTargetInLOS(2)
		"----" A 0 A_Jump(256,2)
		"----" A 0 A_FaceTarget(40,80)
		"####" E 2
	Missile2a:
		"####" E 0 A_PlaySound ("imp/sight")
		"####" EEEEE 2 A_SpawnItemEx ("ReverseImpBallTail", 4, 24, random(31,33), 1, 0, 0, 0, 160)
		"####" E 2
		"####" F 2
		"####" G 0 A_SpawnProjectile("DrunkImpBall",(random(34,36)),6,(random(6,14)),CMF_AIMDIRECTION,pitch)
		"####" G 0 A_SpawnProjectile("DrunkImpBall",(random(26,31)),6,(random(-4,4)),CMF_AIMDIRECTION,pitch)
		"####" G 0 A_SpawnProjectile("DrunkImpBall",(random(36,38)),6,(random(-6,-14)),CMF_AIMDIRECTION,pitch)
		"####" GGFE 5 A_SetTics(random(4,6))
		goto spam
	Melee:
		"####" EE 4 A_FaceTarget
		"####" F 2
		"####" G 8 A_MeleeAttack
		"####" F 4
		Goto See
	Pain:
		"----" A 0 A_GiveInventory("HDFireEnder",3)
		"####" H 3 A_ChangeFlag("NODROPOFF",1)
		"####" H 3 A_Pain
		"----" A 0 A_Jump (180, 2)
		"----" A 0 A_AlertMonsters
		"####" A 2 A_FaceTarget
		"####" BCD 2 A_FastChase
		Goto Missile
	Death:
		"----" A 0 A_Gravity
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_SpawnItemEx ("tempshield", 0,0,0, velx,vely,velz, 0,40)
		"####" I 6 A_ChangeFlag("NODROPOFF", 0)
		"####" J 6 A_Scream
		"####" K 5 A_SpawnItemEx ("tempshield2", 0,0,0, velx,vely,velz, 0,40)
		"----" A 0 A_GiveInventory("NoBlockWepDrop")
		"####" L 5
	Dead:
		"####" L 3 canraise A_JumpIf(abs(velz)<2,1)
		loop
		"####" M 5 canraise A_JumpIf(abs(velz)>=2,"Dead")
		loop
	XDeathBrewtleLulz:
		"----" A 0 A_GiveInventory("IsGibbed",1)
		"----" A 0 A_ChangeFlag("shootable",0)
		"####" N 5 A_ChangeFlag("NODROPOFF", 0)
		"####" N 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		"####" O 5 A_XScream
		"####" O 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		"####" P 5
		"####" P 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		"####" QRS 5
		"####" T 5
		goto XDead
	XDeath:
		"----" A 0 A_Gravity
		"----" A 0 A_GiveInventory("IsGibbed",1)
		"----" A 0 A_ChangeFlag("shootable",0)
		"####" N 5 A_ChangeFlag("NODROPOFF", 0)
		"####" N 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		"####" O 5 A_XScream
		"####" O 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		"####" P 5 A_GiveInventory("NoBlockWepDrop")
		"####" P 0 A_SpawnItemEx ("MegaBloodSplatter", 0,0,34, 0,0,0, 0,160)
		"####" QRS 5
		"####" T 5
	XDead:
		"####" T 5 canraise A_JumpIf(abs(velz)<2,1)
		wait
		"####" U 5 canraise A_JumpIf(abs(velz)>=2,"XDead")
		wait
	Raise:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_JumpIfInventory("IsGibbed",1,"RaiseGibbed")
		"####" M 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" ML 6
		"####" KJI 4
		goto see
	RaiseGibbed:
		"----" A 0 A_TakeInventory("IsGibbed",999)
		"####" U 6 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" UT 8
		"####" SRQ 6
		"####" PON 4
		"####" H 4 A_Die("Ungibbed")
	Death.Ungibbed:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_SpawnItemEx ("tempshield", 0,0,0, velx,vely,velz, 0,40)
		"####" I 6 A_ChangeFlag("NODROPOFF", 0)
		"####" J 6
		"####" K 5 A_SpawnItemEx ("tempshield2", 0,0,0, velx,vely,velz, 0,40)
		"####" L 5 A_NoBlocking
		goto Dead
	}
}


// ------------------------------------------------------------
//   Healer Imp
// ------------------------------------------------------------

actor ShieldImpBall : DrunkImpBall
{
	scale 1.2
	height 24
	radius 20
	speed 8
	damage 2
	-noblockmap
	bloodcolor "44 3c 22"
	+shootable
	+nodamage
	+mthruspecies
	painchance 256
	states
	{
	pain:
		TNT1 A 0 A_Die
	death:
		TNT1 A 0 A_ChangeVelocity(user_velx>>3,user_vely>>3,frandom(1,2))
		TNT1 AAA 0 A_SpawnItemEx("HDSmoke",frandom(-1,1),frandom(-1,1),0,user_velx>>2+frandom(-3,3),user_vely>>2+frandom(-3,3),frandom(1,3),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		TNT1 A 0 A_Jump(212,2)
		TNT1 A 0 A_RadiusGive ("HDFireStarter", 32, RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS|RGF_CORPSES, 1)
		TNT1 A 0 A_SpawnItemEx ("tempshield", 0,0,0, velx,vely,velz, 0,40)
		BAL1 A 1 bright A_Scream
		BAL1 A 1 A_NoBlocking
		BAL1 BBCC 1 bright A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),0,user_velx>>2+frandom(-3,3),user_vely>>2+frandom(-3,3),random(1,2),0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		TNT1 A 0 A_FadeOut(0.2)
		BAL1 CCCC 1 bright A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),0,frandom(-3,3),frandom(-3,3),random(1,2),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_FadeOut(0.2)
		BAL1 DDDD 1 bright A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),0,frandom(-3,3),frandom(-3,3),random(1,2),0,SXF_NOCHECKPOSITION)
		BAL1 E 0 bright A_FadeOut (0.2)
		TNT1 A 0 A_Gravity
		BAL1 E 2 bright A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),0,frandom(-3,3),frandom(-3,3),random(1,2),0,SXF_NOCHECKPOSITION)
		BAL1 E 2 bright A_FadeOut (0.2)
		BAL1 E 2 bright A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),0,frandom(-3,3),frandom(-3,3),random(1,2),0,SXF_NOCHECKPOSITION)
		BAL1 E 2 bright A_FadeOut (0.2)
		TNT1 AAAAAAA 4 bright A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),0,frandom(-3,3),frandom(-3,3),random(1,2),0,SXF_NOCHECKPOSITION)
		TNT1 AAAAAAA 6 bright A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),0,frandom(-3,3),frandom(-3,3),random(1,2),0,SXF_NOCHECKPOSITION)
		TNT1 AAAAAAA 14 bright A_SpawnItemEx("HDSmoke",random(-1,1),random(-1,1),0,frandom(-3,3),frandom(-3,3),random(1,2),0,SXF_NOCHECKPOSITION)
		stop
	}
}
actor HealerImp : DrunkImp
{
	scale 0.8
	speed 6
	height 48
	radius 10
	health 70
	gibhealth 120
	-missilemore
	translation "64:77=50:76"
	seesound ""
	activesound ""
	meleedamage 3
	obituary "%o could feel the schadenfreude."
	hitobituary "%o said I want a second opinion, so the imps said okay you're ugly too."
	states
	{
	See:
		"####" AABBCCDD 2{
			if(countinv("Cell")>0){A_Chase;A_Takeinventory("Cell",1);}  
			else{A_VileChase;}
			if(random(1,2)==1){HealThing(random(1,3));}
		}loop
	Missile:
		"####" E 0 A_Jump (128, "Missile2")
		"####" E 4 A_FaceTarget(0,0)
		"----" A 0 A_JumpIfTargetInLOS(2)
		"----" A 0 A_Jump(256,2)
		"----" A 0 A_FaceTarget(0,0)
		"####" F 4
		"####" G 8 A_SpawnProjectile ("DrunkImpBall",(random(24,30)),0,(random(-6,6)),CMF_AIMDIRECTION,pitch)
		"####" F 5
		Goto See
	Missile2:
		"####" E 2 A_FaceTarget(0,0)
		"####" E 0 A_PlaySound ("imp/sight")
		"####" EEEEE 2 A_SpawnItemEx ("ReverseImpBallTail", 3, 19, random(24,30), 1, 0, 0, 0, 160)
		"----" A 0 A_JumpIfTargetInLOS(2)
		"----" A 0 A_Jump(256,2)
		"----" A 0 A_FaceTarget(0,0)
		"####" F 4
		"----" A 0 A_JumpIfTargetInLOS(2)
		"----" A 0 A_Jump(256,2)
		"----" A 0 A_FaceTarget(0,0)
		"####" F 4
		"####" G 0 A_FaceTarget
		"####" G 0 A_SpawnProjectile ("ShieldImpBall",32,8,0,CMF_AIMDIRECTION,pitch)
		"####" GGFE 5
		Goto See
	Pain:
		"----" A 0 A_GiveInventory("HDFireEnder",5)
		"####" H 3
		"####" H 3 A_Pain
		Goto See
	Heal:
		"####" AHAHAHAHAHA 4 light("HEAL")
		"####" A 0{
			A_GiveInventory("Cell",random(24,32));
			HealThing(24);
		}
		goto See
	Death:
		"----" A 0 A_SpawnItemEx("BFGVileShard",0,0,24,0,0,8,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,164)
		goto super::death
	XDeath:
		"----" A 0 A_SpawnItemEx("BFGVileShard",0,0,24,0,0,8,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,188)
		goto super::xdeath
	Raise:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_JumpIfInventory("IsGibbed",1,"RaiseGibbed")
		"####" M 4 A_SpawnItemEx("MegaBloodSplatter",0,0,4,velx,vely,velz+3,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM)
		"####" ML 6
		"####" KJI 4
		goto pain
	}
}


// ------------------------------------------------------------
//  Mage Imp
// ------------------------------------------------------------

actor MageImp : DrunkImp
{
	translation "none"
	-missilemore
	radius 14
	speed 6
	scale 1.1
	obituary "%o experienced the magic."
	hitobituary "%o experienced ALL the magic."
	states
	{
	missile:
		"----" A 0 A_JumpIfTargetInLOS(2)
		"----" A 0 A_Jump(256,"missile1")
		"####" E 0 A_Jump (16, "missile1")
		"####" E 0 A_JumpIfCloser (640, 1)
		goto Missile3
		"####" E 0 A_JumpIfCloser (256, 1)
		goto Missile2
	missile1:
		"####" E 0 A_Jump (32, "missile2")
		"----" A 0 A_JumpIfTargetInLOS(2)
		"----" A 0 A_Jump(256,2)
		"----" A 0 A_FaceTarget(0,0)
		"####" E 6
		"----" A 0 A_JumpIfTargetInLOS(2)
		"----" A 0 A_Jump(256,2)
		"----" A 0 A_FaceTarget(0,0)
		"####" F 6
		"####" G 8 A_SpawnProjectile ("MageImpBall",(random(30,34)),0,(random(-6,6)),CMF_AIMDIRECTION,pitch)
		goto see
	missile2:
		"####" E 0 A_Jump (96, "Missile1")
		"####" E 0 A_Jump (16, "Missile3")
		"####" E 2 A_FaceTarget(0,0)
		"####" E 2 A_PlaySound ("imp/sight")
		"####" EEEEEEE 2 A_SpawnItemEx ("ReverseImpBallTail", random(3,5), random(23,25), random(31,33), 1, 0, 0, 0, 160)
		"----" A 0 A_JumpIfTargetInLOS(2)
		"----" A 0 A_Jump(256,2)
		"----" A 0 A_FaceTarget(0,0)
		"####" F 6

		"####" GGGGGGGG 0 A_SpawnProjectile("MageImpBall2B",random(29,34),6,(random(-18,18)),CMF_AIMDIRECTION,pitch+frandom(-2,4))
		"####" GGFE 5
		Goto See
	missile3:
		"####" E 0 A_Jump (16, "missile1")
		"####" E 0 A_Jump (32, "missile2")
		"####" E 8 A_FaceTarget(0,0)
		"####" H 4 A_NoGravity
		//"####" H 0 A_ChangeFlag ("noblockmap", 1)
		"####" H 16 bright ThrustThingZ (0, 8, 0, 0)
		"####" H 8 bright A_Stop
		"####" H 0 A_SpawnProjectile ("MageImpBall2", 32, 0, 162, 2, 60)
		"####" H 2 bright A_SpawnProjectile ("MageImpBall2", 32, 0, -162, 2, 60)
		"####" H 0 A_SpawnProjectile ("MageImpBall2", 32, 0, 156, 2, 40)
		"####" H 2 bright A_SpawnProjectile ("MageImpBall2", 32, 0, -156, 2, 40)
		"####" H 0 A_SpawnProjectile ("MageImpBall2", 32, 0, 150, 2, 20)
		"####" H 2 bright A_SpawnProjectile ("MageImpBall2", 32, 0, -150, 2, 20)
		"####" H 0 A_SpawnProjectile ("MageImpBall2", 32, 0, 144, 2, 0)
		"####" H 2 bright A_SpawnProjectile ("MageImpBall2", 32, 0, -144, 2, 0)
		"####" H 0 A_SpawnProjectile ("MageImpBall2", 32, 0, 138, 2, -10)
		"####" H 2 bright A_SpawnProjectile ("MageImpBall2", 32, 0, -138, 2, -10)
		"####" H 0 A_SpawnProjectile ("MageImpBall2", 32, 0, 132, 2, -14)
		"####" H 8 bright A_SpawnProjectile ("MageImpBall2", 32, 0, -132, 2, -14)
		"####" H 8 A_LowGravity
		"####" H 0 A_Gravity
		//"####" H 4 A_ChangeFlag ("noblockmap", 0)
		goto missile1
	pain:
		"----" A 0 A_GiveInventory("HDFireEnder",3)
		"####" H 0 A_Gravity
		"####" H 3 A_ChangeFlag("NODROPOFF", 1)
		"####" H 3 A_Pain
		"####" A 2 A_FaceTarget
		"####" BCD 2 A_FastChase
		Goto Missile
	}
}

actor MageImpBall2 : DoomImpBall
{
	damage 2
	speed 2
	height 4
	radius 4
	scale 0.6
	+seekermissile
	+forcexybillboard
	Decal "DoomImpScorch"
	DamageType "Thermal"
	states
	{
	spawn:
		BAL1 A 0 A_JumpIfInventory("Timer",10,2)
		BAL1 A 0 A_Jump(256,2)
		BAL1 A 0 bright A_SpawnItemEx ("MageImpBall2B", -1,0,0,velx*4,vely*4,velz*4,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		stop
		BAL1 A 0 A_GiveInventory("Timer",1)
		BAL1 A 2 bright FAST A_SeekerMissile(8,8)
		BAL1 B 2 bright FAST A_SeekerMissile(8,8)
		BAL1 A 0 bright A_SpawnItemEx("MageImpBall2Tail", -1,0,0, -1,0,1, 0,SXF_ABSOLUTEMOMENTUM|SXF_NOCHECKPOSITION)
		loop
	death:
		TNT1 A 0 A_SpawnItemEx("HDSmoke",0,0,0,0,0,random(1,2),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_Jump(212,2)
		TNT1 A 0 A_RadiusGive ("HDFireStarter", 32, RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS|RGF_CORPSES, 1)
		BAL1 CDE 4 bright A_FadeOut (0.2)
		Stop
	}
}
actor MageImpBall2A : MageImpBall2
{
	decal "DoomImpScorch"
	seesound none
	speed 0
	states
	{
	spawn:
		BAL1 A 0
		BAL1 ABAB 2 bright A_SpawnItemEx ("MageImpBall2Tail", -1,0,0, 8,0,0, 0,160,0)
		BAL1 A 0 ThrustThingZ (0, random(1,3), 1, 0)
	see:
		BAL1 A 0 A_Jump (82, 2)
		BAL1 A 0 A_BishopMissileWeave
		BAL1 AB 2 bright A_SpawnItemEx ("MageImpBall2Tail2", -1,0,0, 8,0,0, 0,160,0)
		BAL1 A 0 A_JumpIf (z-floorz<24, "Tread")
		BAL1 A 0 A_SeekerMissile (random(0,14), random(0,18))
		loop
	tread:
		BAL1 A 0 ThrustThingZ (0, random(2,4), 0, 0)
		goto See
	}
}
actor MageImpBall2B : MageImpBall2A
{
	scale 0.8
	damage 3
	seesound "imp/attack"
	speed 14
	states
	{
	spawn:
		BAL1 A 0 A_Jump (82, 2)
		BAL1 A 0 A_SeekerMissile (4, 8)
		BAL1 A 0 A_BishopMissileWeave
		BAL1 AB 2 bright A_SpawnItemEx ("MageImpBall2Tail2", -6, 0, 0, 8, 0, 0, 0, 160, 0)
		BAL1 A 0 A_JumpIf (z-floorz<24, "Tread")
		loop
	}
}
actor MageImpBall2Tail
{
	+nointeraction
	+forcexybillboard
	scale 0.6
	renderstyle add
	alpha 0.6
	states
	{
	spawn:
		BAL1 A 0
		BAL1 A 0 A_PlaySound("misc/firecrkl",0,0.2,0,6)
		BAL1 A 6 bright A_FadeOut (0.1)
		wait
	}
}
actor MageImpBall2Tail2 : MageImpBall2Tail
{
	states
	{
	spawn:
		BAL1 A 0
		BAL1 A 0 A_PlaySound("misc/firecrkl",0,0.2,0,6)
		BAL1 A 4 bright A_FadeOut (0.2)
		wait
	}
}
actor MageImpBall:DoomImpBall{
	decal "Scorch"
	damageType "Thermal"
	+seekermissile +forcexybillboard
	speed 6
	radius 5
	height 6
	scale 1.2
	damage 4
	states{
	spawn:
		BAL1 AB 4 bright{
			A_SpawnItemEx("MageImpBallTail", 0,0,0, 0,0,0, 0,160,0);
			A_SeekerMissile(6,32);
		}loop
	Death:
		BAL1 C 0{
			A_SpawnItemEx("HDSmoke",0,0,0,0,0,random(1,2),0,SXF_NOCHECKPOSITION);
			if(random(0,1)==1){A_RadiusGive("HDFireStarter", 32, RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS|RGF_CORPSES, 1);}
		}
		BAL1 CDE 4 bright A_FadeOut (0.2)
		stop
	}
}
actor MageImpBallTail : MageImpBall2Tail
{
	scale 1.2
}


// ------------------------------------------------------------
//   Imp Spawner
// ------------------------------------------------------------

actor ImpSpawner : RandomSpawner replaces DoomImp
{
	+ismonster
/*
	dropitem "HDImp" 256 5
	dropitem "HDHImp" 256 2
	dropitem "HDMImp" 256 3
*/
	dropitem "DrunkImp" 256 5
	dropitem "HealerImp" 256 2
	dropitem "MageImp" 256 3
}
actor DeadImpSpawner : RandomSpawner replaces DeadDoomImp
{
	+ismonster
	dropitem "DeadDrunkImp" 256 5
	dropitem "DeadHealerImp" 256 2
	dropitem "DeadMageImp" 256 3
}

actor DeadDrunkImp : DrunkImp
{
	states
	{
	spawn:
		TROO A 0
		"----" A 0 A_JumpIfInventory("HDShellAmmo",1,3)
		"----" A 0 A_GiveInventory("HDShellAmmo",1)
		"----" A 0 A_Die("spawndead")
		"----" A 0
		goto super::spawn+1
	death.spawndead:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_ChangeFlag("nodropoff", 0)
		"----" A 0 A_NoBlocking
		goto super::dead
	}
}
actor DeadHealerImp : HealerImp
{
	states
	{
	spawn:
		TROO A 0
		"----" A 0 A_JumpIfInventory("HDShellAmmo",1,3)
		"----" A 0 A_GiveInventory("HDShellAmmo",1)
		"----" A 0 A_Die("spawndead")
		"----" A 0
		goto super::spawn+1
	death.spawndead:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_ChangeFlag("nodropoff", 0)
		"----" A 0 A_NoBlocking
		goto super::dead
	}
}
actor DeadMageImp : MageImp
{
	dropitem ""
	states
	{
	spawn:
		TROO A 0
		"----" A 0 A_JumpIfInventory("HDShellAmmo",1,3)
		"----" A 0 A_GiveInventory("HDShellAmmo",1)
		"----" A 0 A_Die("spawndead")
		"----" A 0
		goto super::spawn+1
	death.spawndead:
		"----" A 0 A_GiveInventory("HDCorpseFlags",1)
		"----" A 0 A_ChangeFlag("nodropoff", 0)
		"----" A 0 A_NoBlocking
		goto super::dead
	}
}

