//-------------------------------------------------
// Usable items
//-------------------------------------------------
actor PortableHealingItem:RandomSpawner replaces Stimpack{
	dropitem "HD4mMag" 256 7
	dropitem "PortableStimpack" 256 2
	dropitem "PortableMedikit" 256 1
}
actor PortableHealingItemBig:RandomSpawner replaces Medikit{
	dropitem "BluePotion" 128 1
	dropitem "HD4mMag" 256 5
	dropitem "PortableStimpack" 256 10
	dropitem "PortableMedikit" 256 30
}
actor PortableHealingItemBigger:RandomSpawner replaces Berserk{
	dropitem "PortableBerserkPack" 256 5
	dropitem "BlueArmour" 256 2
	dropitem "HEATAmmo" 256 1
	dropitem "HD4mMag" 256 1
}


//-------------------------------------------------
// Other Replacements
//-------------------------------------------------
actor PlasmaReplaces:RandomSpawner replaces PlasmaRifle{
	dropitem "LiberatorNoGL" 256 2
	dropitem "LiberatorRifle" 256 1
	dropitem "ThunderBuster" 256 5
}
actor ChaingunReplaces:RandomSpawner replaces Chaingun{
	dropitem "LiberatorNoGL" 256 1
	dropitem "HDAutoPistol" 256 1
	dropitem "HERPUsable" 256 2
	dropitem "Vulcanette" 256 9
}
actor SSGReplaces:RandomSpawner replaces SuperShotgun{
	dropitem "BloopMapPickup" 256 2
	dropitem "HDAutoPistol" 256 1
	dropitem "BossRifleSpawner" 256 1
	dropitem "LiberatorRifle" 256 3
	dropitem "Hunter" 256 2
	dropitem "Slayer" 256 4
}
actor RLReplaces:RandomSpawner replaces RocketLauncher{
	dropitem "BloopMapPickup" 256 1
	dropitem "HDRL" 256 4
}
actor BloopMapPickup{
	states{
	spawn:
		TNT1 A 0 nodelay{
			A_SpawnItemEx("Blooper");
			A_SpawnItemEx("RocketBigPickup",3);
			A_SpawnItemEx("HDRocketAmmo",5);
		}stop
	}
}


//-------------------------------------------------
// Keys
//-------------------------------------------------
actor HDRedKey:HDUPK replaces RedCard{
	+wallsprite
	scale 0.4
//	radius 20
	states{
	spawn:
		RKEY A 0
	spawn1:
		"####" ABABABAB 2
		"####" ABABABABABABABAB 3
		"####" ABABAB 6
	spawn2:
		"----" A 1 A_SetTics(random(1,1000))
	spawn3:
		"####" AB 1 A_SetTics(random(1,5))
		"----" A 0 A_Jump(16,"spawn2")
		loop
	give:
		"----" A 0{
			A_GiveToTarget("RedCard",1);
			A_GiveInventory("RedCard",1,AAPTR_PLAYER1);
			A_GiveInventory("RedCard",1,AAPTR_PLAYER2);
			A_GiveInventory("RedCard",1,AAPTR_PLAYER3);
			A_GiveInventory("RedCard",1,AAPTR_PLAYER4);
			A_GiveInventory("RedCard",1,AAPTR_PLAYER5);
			A_GiveInventory("RedCard",1,AAPTR_PLAYER6);
			A_GiveInventory("RedCard",1,AAPTR_PLAYER7);
			A_GiveInventory("RedCard",1,AAPTR_PLAYER8);
			A_Log("\crRED\c- local area clearance code downloaded.");
		}
	beep:
		"----" A 0{
			A_PlaySound("misc/i_pkup",0,1,0,0);
		}goto spawn1
	}
}
actor HDBlueKey:HDRedKey replaces BlueCard{
	states{
	spawn:
		BKEY A 0
		goto spawn1
	give:
		"----" A 0{
			A_GiveToTarget("BlueCard",1);
			A_GiveInventory("BlueCard",1,AAPTR_PLAYER1);
			A_GiveInventory("BlueCard",1,AAPTR_PLAYER2);
			A_GiveInventory("BlueCard",1,AAPTR_PLAYER3);
			A_GiveInventory("BlueCard",1,AAPTR_PLAYER4);
			A_GiveInventory("BlueCard",1,AAPTR_PLAYER5);
			A_GiveInventory("BlueCard",1,AAPTR_PLAYER6);
			A_GiveInventory("BlueCard",1,AAPTR_PLAYER7);
			A_GiveInventory("BlueCard",1,AAPTR_PLAYER8);
			A_Log("\cnBLUE\c- local area clearance code downloaded.");
		}goto beep
	}
}
actor HDYellowKey:HDRedKey replaces YellowCard{
	states{
	spawn:
		YKEY A 0
		goto spawn1
	give:
		"----" A 0{
			A_GiveToTarget("YellowCard",1);
			A_GiveInventory("YellowCard",1,AAPTR_PLAYER1);
			A_GiveInventory("YellowCard",1,AAPTR_PLAYER2);
			A_GiveInventory("YellowCard",1,AAPTR_PLAYER3);
			A_GiveInventory("YellowCard",1,AAPTR_PLAYER4);
			A_GiveInventory("YellowCard",1,AAPTR_PLAYER5);
			A_GiveInventory("YellowCard",1,AAPTR_PLAYER6);
			A_GiveInventory("YellowCard",1,AAPTR_PLAYER7);
			A_GiveInventory("YellowCard",1,AAPTR_PLAYER8);
			A_Log("\cfYELLOW\c- local area clearance code downloaded.");
		}goto beep
	}
}
actor HDRedSkull:HDUPK replaces RedSkull{
	+usespecial
	activation THINGSPEC_ClearSpecial
	scale 0.6
	radius 20
	renderstyle translucent
	alpha 0.75
	states{
	spawn:
		RSKU A 0
		"----" A 0 A_Jump(256,"spawn2")
	spawn2:
		RSKU AB 1 light("REDKEY") A_SetTics(random(1,6))
		loop
	give:
		"----" A 0 A_JumpIfInTargetInventory("RedSkull",1,"spawn")
		"----" A 0 A_GiveToTarget("RedSkull",1)
		"----" A 0 A_Log("\crRED\c-, the way of the blood shed for you in vain.")
	effect:
		"----" A 0 A_PlaySound("brain/cube",0,1,0,0)
		"----" A 0 A_DamageTarget(1)
		"####" "#####" 1 A_SpawnItemEx("HDSmoke",0,0,0,random(4,0),random(-2,2),random(1,3),0,SXF_NOCHECKPOSITION)
		"----" A 0 A_GiveToTarget("IsMoving",99)
		"----" A 0 A_Jump(256,"spawn")
	}
}
actor HDBlueSkull:HDRedSkull replaces BlueSkull{
	states{
	give:
		"----" A 0 A_JumpIfInTargetInventory("BlueSkull",1,"spawn")
		"----" A 0 A_GiveToTarget("BlueSkull",1)
		"----" A 0 A_Log("\cnBLUE\c-, the way of the heaven forever lost to you.")
		goto effect
	spawn2:
		BSKU AB 1 light("HEALTHPOTION") A_SetTics(random(1,3))
		loop
	}
}
actor HDYellowSkull:HDRedSkull replaces YellowSkull{
	states{
	give:
		"----" A 0 A_JumpIfInTargetInventory("YellowSkull",1,"spawn")
		"----" A 0 A_GiveToTarget("YellowSkull",1)
		"----" A 0 A_Log("\cfYELLOW\c-, the way of the gold that has lured all mankind to death.")
		goto effect
	spawn2:
		YSKU AB 1 light("YELLOWKEY") A_SetTics(random(1,3))
		loop
	}
}

actor HDMap:HDUPK replaces Allmap{
	scale 0.3
	states{
	give:
		PMAP A 0{
			A_GiveToTarget("MapRevealer",1);
			A_Log("\c-Local area map downloaded.");
			A_PlaySound("misc/i_pkup",0,1,0,0);
		}//fallthrough to spawn
	spawn:
		PMAP A 0 nodelay A_Jump(256,"a","b","c","d")
	a:
		PMAP AE 1 A_SetTics(random(1,3))
		"----" A 0 A_Jump(4,"spawn")
		loop
	b:
		PMAP BF 1 A_SetTics(random(1,3))
		"----" A 0 A_Jump(4,"spawn")
		loop
	c:
		PMAP CG 1 A_SetTics(random(1,3))
		"----" A 0 A_Jump(4,"spawn")
		loop
	d:
		PMAP DH 1 A_SetTics(random(1,3))
		"----" A 0 A_Jump(4,"spawn")
		loop
	}
}


actor HelpScreen:ActionItem{
	states{
	pickup:
		TNT1 A 0 A_Print("Blood on floor means\nyou're \crbleeding\c- to death.\n\nUse \cnmedikits\c- to stop \crbleeding\c-\nand \cnstimpacks\c- to slow it down.\n\nHit the \cdreload\c- key to reload.\n\nTap or hold the \cduse/open door\c- key to pick\nup things and kick corpses.\n\nHold the \cdjump\c- key in front of a \nlow ledge to climb over it.\n\nHold the \cdrun/walk\c- key to run.\n\nStop moving and hold the \cduse/open door\c- \nkey to view all inventory.\n\nDon't forget to bind \cdfire mode\c-, \cdunload\c- and\n\cdsecondary reload\c-! (read the manual too!)")
		fail
	}
}
