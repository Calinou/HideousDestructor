actor SyntheticGaunts : HDWeapon replaces Gauntlets
{
	game Heretic
	weapon.slotnumber 1
	weapon.yadjust 16
	weapon.ammotype "SyntheticGauntZaps"
	inventory.pickupmessage "necromancer's gauntlets"
	obituary "%o was ticked by %k."
	states
	{
	deselect:
		GAUN A 1 A_Lower
		GAUN AAA 0 A_Lower
		loop
	select:
		TNT1 A 0 A_TakeInventory("IsWeaponLong",999)
		TNT1 A 0 A_TakeInventory("IsWeaponShort",999)
		TNT1 A 0 A_TakeInventory("IsWeaponWide",999)
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_JumpIfInventory("IsGauntleted",1,4)
		TNT1 AA 10 A_PlaySound("weapons/pocket")
		TNT1 A 0 A_GiveInventory("IsGauntleted",1)
	select2:
		GAUN A 1 A_Raise
		GAUN AAA 0 A_Raise
		loop
	ready:
		TNT1 A 0 A_JumpIfInventory("IsUsingTome",1,"entome")
		GAUN A 1 A_WeaponReady
		loop
	fire:
		TNT1 A 0 A_JumpIfInventory("SyntheticGauntZaps",1,1)
		goto altfire
		TNT1 A 0 A_PlayWeaponSound("weapons/gauntletsuse")
		GAUN BC 2 bright
	hold:
		TNT1 A 0 A_JumpIfInventory("SyntheticGauntZaps",1,1)
		goto altholdfromhold
		GAUN DEF 1 bright A_FireCustomMissile("SyntheticGauntZap",0,0,0)
		TNT1 A 0 A_TakeInventory("SyntheticGauntZaps",1)
		TNT1 A 0 A_TakeInventory("SyntheticGauntTomed",1)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Refire
		GAUN CB 2 bright
		goto ready
	altholdtomed:
		GAUN LMN 1 bright A_FireCustomMissile("SyntheticGauntZap2",0,0,0)
		TNT1 A 0 A_TakeInventory("SyntheticGauntTomed",1)
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_Refire
		TNT1 A 0 A_FireCustomMissile("HDGunSmoker")
		GAUN JA 2 bright
		goto ready
	althold:
		TNT1 A 0 A_JumpIfInventory("SyntheticGauntTomed",16,"altholdtomed")
	altfire:
		TNT1 A 0 A_PlayWeaponSound("weapons/gauntletsuse")
		GAUN JK 2 bright
	altholdfromhold:
		TNT1 A 0 A_JumpIfInventory("SyntheticGauntTomed",16,"altholdtomed")
		TNT1 A 0 A_FireCustomMissile("HDGunSmoker")
		GAUN LMNLMNLMNLMNLMN 1 bright A_FireCustomMissile("SyntheticGauntZap2",0,0,0)
		TNT1 A 0 A_TakeInventory("SyntheticGauntTomed",15)
		TNT1 A 0 A_AlertMonsters
		TNT1 AAAAA 0 A_FireCustomMissile("HDGunSmoke")
		GAUN KJ 2 bright A_FireCustomMissile("HDGunSmoke")
		GAUN JJJ 1 A_WeaponReady(WRF_NOFIRE)
		GAUN JA 1 A_WeaponReady(WRF_NOFIRE)
		GAUN AAAAAAAAAAAA 1 A_WeaponReady(WRF_NOFIRE)
		GAUN A 0 A_Refire
		goto ready
	entome:
		GAUN A 2 offset(0,36)
		GAUN A 2 offset(0,42)
		TNT1 AA 20 A_PlayWeaponSound("himp/attack")
		TNT1 A 0 A_GiveInventory("SyntheticGauntTomed",50)
		GAUN A 2 offset(0,42)
		TNT1 A 0 A_TakeInventory("IsUsingTome",999)
		GAUN A 2 offset(0,36)
		goto ready
	spawn:
		WGNT A -1
		stop
	}
}
actor SyntheticGauntZap : FastProjectile
{
	height 3
	radius 3
	damagetype "burning"
	var int user_dmg;
	damage (user_dmg)
	-missile
	speed 32
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_JumpIfInTargetInventory("SyntheticGauntTomed",1,3)
		TNT1 A 0 A_SetUserVar("user_dmg",random(1,2))
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SetUserVar("user_dmg",5)
	spawn2:
		TNT1 A 0 A_ChangeFlag("missile",1)
		TNT1 A 0 A_PlayWeaponSound("weapons/gauntletson")
		TNT1 A 2
		stop
	death:
		TNT1 A 0 A_SpawnItemEx("GauntletPuff1",frandom(-2,2),frandom(-2,2),frandom(-2,2),frandom(-1,1),frandom(-1,1),frandom(0,2),0,SXF_NOCHECKPOSITION)
		goto spawn2
	}
}
actor SyntheticGauntZap2 : SyntheticGauntZap
{
	-missile
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_JumpIfInTargetInventory("SyntheticGauntTomed",1,2)
		TNT1 A 0 A_SetUserVar("user_dmg",random(-1,1))
		goto spawn2
		TNT1 A 0 A_SetUserVar("user_dmg",1)
		goto spawn2
	xdeath:
		TNT1 A 0 A_GiveToTarget("SyntheticGauntZaps",1)
		TNT1 A 0 A_JumpIf(random(1,10)==user_dmg,3)
		TNT1 A 0 A_Jump(8,2)
		TNT1 A 0 A_JumpIfInTargetInventory("SyntheticGauntTomed",1,1)
		goto death
		TNT1 A 0 A_GiveToTarget("RegenBlues",1)
	death:
		TNT1 A 0 A_SpawnItemEx("GauntletPuff2",frandom(-2,2),frandom(-2,2),frandom(-2,2),frandom(-1,1),frandom(-1,1),frandom(0,2),0,SXF_NOCHECKPOSITION)
		goto spawn2
	}
}
actor SyntheticGauntZaps : Ammo
{
	+inventory.undroppable
	inventory.maxamount 50
	ammo.backpackmaxamount 50
	ammo.backpackamount 0
}
actor SyntheticGauntTomed : InventoryFlag
{
	inventory.maxamount 50
}
actor IsGauntleted : InventoryFlag {}

