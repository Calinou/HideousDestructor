actor SyntheticWand:HDWeapon replaces GoldWand
{
	game Heretic
	weapon.ammotype1 "SyntheticWandLoaded"
	weapon.ammotype2 "SyntheticWandCrystal"
	weapon.slotnumber 2
	states
	{
	select:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 9
	select2:
		TNT1 A 0 A_JumpIfInventory("SyntheticWandLoaded",1,3)
		GWND E 1 A_Raise
		TNT1 A 0 A_Jump(256,2)
		GWND A 1 A_Raise
		TNT1 AAA 0 A_Raise
		loop
	deselect:
		TNT1 A 0 A_TakeInventory("IsWeaponShort")
		TNT1 A 0 A_JumpIfInventory("SyntheticWandLoaded",1,3)
		GWND E 1 A_Lower
		TNT1 A 0 A_Jump(256,2)
		GWND A 1 A_Lower
		TNT1 AAA 0 A_Lower
		loop
	ready:
		TNT1 A 0 A_JumpIfInventory("IsUsingTome",1,"entome")
		TNT1 A 0 A_GiveInventory("IsWeaponShort",1)
		TNT1 A 0 A_JumpIfInventory("SyntheticWandLoaded",1,3)
		GWND E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_Jump(256,2)
		GWND A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("SyntheticWandTomed",1,1)
		loop
		TNT1 A 0 A_GiveInventory("IsWeaponShort",1)
		TNT1 A 0 A_FireCustomMissile("SyntheticWandLightDummy")
		GWND F 1 bright A_WeaponReady(WRF_ALLOWRELOAD)
		loop
	entome:
		TNT1 A 0 A_JumpIfInventory("SyntheticWandLoaded",1,3)
		GWND E 0
		TNT1 E 0 A_Jump(256,2)
		GWND A 0
		GWND "#" 3 offset (0,38)
		GWND "#" 3 offset (0,48)
		GWND "#" 3 offset (0,60)
		TNT1 "#" 20 A_PlayWeaponSound("himp/attack")
		TNT1 "#" 40 A_PlayWeaponSound("weapons/staffhit")
		TNT1 "#" 0 A_GiveInventory("SyntheticWandTomed",1)
		GWND "#" 3 offset (0,60)
		GWND "#" 3 offset (0,48)
		GWND "#" 3 offset (0,38)
		TNT1 A 0 A_TakeInventory("IsUsingTome",1)
		goto ready
	reload:
		TNT1 A 0 A_JumpIfInventory("SyntheticWandLoaded",1,3)
		GWND E 0
		TNT1 E 0 A_Jump(256,2)
		GWND A 0
		GWND "#" 2 offset (0,38)
		TNT1 "#" 0 A_JumpIfInventory("SyntheticWandLoaded",0,"ready")
		TNT1 "#" 0 A_JumpIfInventory("SyntheticWandCrystal",1,1)
		goto ready
		TNT1 "#" 0 A_GiveInventory("Reloading",1)
		GWND "#" 2 offset (0,48)
		GWND "#" 3 offset (-2,60)
		GWND "#" 2 offset (3,61)
		GWND "#" 3 offset (-2,62)
		GWND "#" 2 offset (3,66)
		GWND "#" 1 offset (-3,74)
		TNT1 A 4 A_PlaySound("weapons/wandunload")
		TNT1 A 0 A_JumpIfInventory("SyntheticWandLoaded",3,3)
		TNT1 A 0 A_FireCustomMissile("SpentWandCrystal")
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_FireCustomMissile("PartialSpentWandCrystal")
		TNT1 A 10
		TNT1 A 0 A_PlaySound("weapons/wandload")
		TNT1 A 10
		TNT1 A 0 A_TakeInventory("SyntheticWandCrystal",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("SyntheticWandLoaded",999)
		GWND A 3 offset (0,60)
		GWND A 3 offset (0,48)
		GWND A 3 offset (0,38)
		TNT1 A 0 A_TakeInventory("Reloading",1)
		goto ready
	tomefire:
		TNT1 A 0 A_JumpIfInventory("SyntheticWandLoaded",1,1)
		goto noshot
		GWND A 2 bright light("WANDBALL_X3")
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -4, 0, Sin(Pitch) * 4, 1)
		TNT1 A 0 A_ZoomFactor(1.05)
		TNT1 A 0 A_PlayWeaponSound("weapons/wandhit")
		TNT1 AAAAAAAAAAAAAAA 0 A_FireCustomMissile("SyntheticWandTomeShot")
		GWND AFB 1 bright light("WANDBALL_X1") A_FireCustomMissile("SyntheticWandTomeShot")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(30,80),random(-20,40))
		TNT1 A 0 A_TakeInventory("SyntheticWandLoaded",random(5,20))
		TNT1 A 0 A_TakeInventory("SyntheticWandTomed",999)
		TNT1 A 0 A_ZoomFactor(1.00)
		GWND CD 1 bright light("WANDBALL_X3")
		GWND D 1 bright offset(0,35)
		GWND D 2 offset(0,38)
		GWND A 3 offset(0,37)
		GWND A 4 offset(0,34)
		GWND D 0 A_Refire
		goto ready
	noshot:
		GWND E 2 offset (0,35)
		GWND E 3 offset (0,38)
		GWND E 3 offset (0,37)
		GWND E 2 offset (0,33)
		goto nope
	fire:
		TNT1 A 0 A_JumpIfInventory("SyntheticWandTomed",1,"tomefire")
		TNT1 A 0 A_JumpIfInventory("SyntheticWandLoaded",1,1)
		goto noshot
		TNT1 A 0 A_ZoomFactor(1.02)
		TNT1 A 0 A_PlayWeaponSound("weapons/wandhit")
		GWND A 1 bright light("WANDBALL_X1") A_FireCustomMissile("SyntheticWandShot")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(10,40),random(-10,20))
		TNT1 A 0 A_TakeInventory("SyntheticWandLoaded",1)
		TNT1 A 0 A_ZoomFactor(1.00)
		GWND BCD 1 bright light("WANDBALL_X3")
		GWND D 1 bright offset(0,35)
		GWND A 3 offset(0,38)
		GWND A 3 offset(0,37)
		GWND A 0 A_Refire("fire")
		goto ready
	altfire:
		TNT1 A 0 A_JumpIfInventory("SyntheticWandTomed",1,"tomefire")
		TNT1 A 0 A_JumpIfInventory("SyntheticWandLoaded",3,1)
		goto fire
		GWND A 2 bright light("WANDBALL_X3")
		TNT1 A 0 A_ZoomFactor(1.05)
		TNT1 A 0 A_PlayWeaponSound("weapons/wandhit")
		GWND AF 1 bright light("WANDBALL_X1") A_FireCustomMissile("SyntheticWandWeaveShot")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(30,80),random(-20,40))
		TNT1 A 0 A_TakeInventory("SyntheticWandLoaded",random(2,3))
		TNT1 A 0 A_ZoomFactor(1.00)
		GWND BCD 1 light("WANDBALL_X3")
		GWND D 1 offset(0,35)
		GWND A 3 offset(0,38)
		GWND A 4 offset(0,37)
		GWND A 4 offset(0,34)
		GWND A 0 A_Refire("altfire")
		goto ready
	}
}
actor SyntheticWandTomed : InventoryFlag {}
actor SyntheticWandLoaded : Ammo
{
	+inventory.untossable
	inventory.maxamount 20
	ammo.backpackmaxamount 20
	ammo.backpackamount 0
}
actor SyntheticWandCrystal : Ammo replaces GoldWandAmmo
{
	inventory.amount 1
	inventory.maxamount 8
	ammo.backpackmaxamount 8
	ammo.backpackamount 0
	inventory.icon "INAMGLD"
	inventory.pickupmessage "power gem"
	spawnid 11
	states
	{
	spawn:
		AMG1 A 1 A_SetTics(random(10,100))
		AMG1 A 1 bright A_SetTics(random(1,4))
		loop
	}
}
actor SyntheticWandCrystals : SyntheticWandCrystal replaces GoldWandHefty
{
	inventory.pickupmessage "power gem"
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 AAAA 0 A_SpawnItemEx("SyntheticWandCrystal",0,0,0,1,0,0,random(0,360),SXF_NOCHECKPOSITION)
		goto super::spawn
	}
}

actor SyntheticWandLightDummy
{
	projectile
	speed 12
	+nointeraction
	height 1
	radius 1
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_JumpIfInTargetInventory("SyntheticWandLoaded",1,1)
		stop
		TNT1 A 2 light("WANDTOMED") A_SpawnItemEx("SyntheticWandPuff",0,0,0,frandom(0.4,1),frandom(-1,1),frandom(0.1,2),0,SXF_NOCHECKPOSITION,224)
		stop
	}
}

actor SyntheticWandShot : FastProjectile
{
	deathsound "weapons/bowhit"
	seesound "weapons/wandhit"
	obituary "$OB_MPPGOLDWAND"
	missiletype "SyntheticWandTrail"
	missileheight 8
	+windthrust
	radius 2
	height 2
	speed 1
	damage (30)
	states
	{
	spawn:
		TNT1 A 0 nodelay

		TNT1 A 0 A_JumpIfInTargetInventory("IsMoving",10,2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_ChangeVelocity(0,frandom(-0.03,0.03),frandom(-0.04,0.04),CVF_RELATIVE)

		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ScaleVelocity(160)
	spawn2:
		TNT1 A 0 A_ChangeVelocity(0,0,-1,CVF_RELATIVE)
		TNT1 A 0 A_ScaleVelocity(1.05)
		FX01 AB 1 bright light("WANDBALL")
		loop
	death:
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("SyntheticWandPuff",-2,0,1,frandom(-8,8),frandom(-8,8),frandom(-8,8),random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,0)
		FX01 EF 3 bright light("WANDBALL_X1")
		FX01 GH 3 bright light("WANDBALL_X3")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 1 A_RadiusThrust(120,5,RTF_NOIMPACTDAMAGE)
		stop
	}
}
actor SyntheticWandTrail : IdleDummy
{
	states
	{
	spawn:
		TNT1 AA 0 A_SpawnItemEx("SyntheticWandPuff",-1,0,0,random(0,-2),frandom(-1,1),frandom(-1,1),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,40)
		stop
	}
}
actor SyntheticWandWeaveShot
{
	projectile
	deathsound "weapons/bowhit"
	seesound "weapons/wandhit"
	obituary "$OB_MPPGOLDWAND"
	missiletype "SyntheticWandTrail"
	missileheight 8
	+windthrust
	radius 2
	height 2
	speed 1
	var int user_weavex;
	var int user_weavey;
	damage (30)
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_SetUserVar(user_weavex,random(0,1)*2-1)
		TNT1 A 0 A_SetUserVar(user_weavey,random(0,1)*2-1)

		TNT1 A 0 A_JumpIfInTargetInventory("IsMoving",10,2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_ChangeVelocity(0,frandom(-0.03,0.03),frandom(-0.04,0.04),CVF_RELATIVE)

		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_ChangeVelocity(frandom(-0.02,0.02),frandom(-0.02,0.02),frandom(-0.02,0.02),CVF_RELATIVE)
		TNT1 A 0 A_ScaleVelocity(20)
		TNT1 A 0 A_ChangeVelocity(0,0,-0.1,CVF_RELATIVE)
		TNT1 AA 0 A_SpawnItemEx("SyntheticWandPuff",0,0,0,random(-1,-3),frandom(-2,2),frandom(-3,4),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,0)
		FX01 AA 1 bright light("WANDBALL") A_Weave(user_weavex*6,user_weavey*6,0.5,0.5)
		TNT1 AA 0 A_SpawnItemEx("SyntheticWandPuff",0,0,0,random(-1,-3),frandom(-2,2),frandom(-3,4),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,0)
		FX01 BB 1 bright light("WANDBALL") A_Weave(user_weavex*5,user_weavey*5,0.5,0.5)
		TNT1 A 0 A_Jump(256,"spawn2")
	spawn2:
		TNT1 A 0 A_ChangeVelocity(0,0,-0.05,CVF_RELATIVE)
		TNT1 A 0 A_ScaleVelocity(1.05)
		TNT1 AA 0 A_SpawnItemEx("SyntheticWandPuff",0,0,0,random(0,-1),frandom(-1,1),frandom(-1,1),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,0)
		FX01 AA 1 bright light("WANDBALL") A_Weave(user_weavex*4,user_weavey*4,1.2,1)
		TNT1 AA 0 A_SpawnItemEx("SyntheticWandPuff",0,0,0,random(0,-1),frandom(-1,1),frandom(-1,1),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,0)
		FX01 BB 1 bright light("WANDBALL") A_Weave(user_weavex*4,user_weavey*4,1.2,1)
		loop
	death:
		TNT1 AAAAAAAAAAAA 0 A_SpawnItemEx("SyntheticWandPuff",-2,0,1,frandom(-8,8),frandom(-8,8),frandom(-8,8),random(0,360),SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,0)
		FX01 EF 3 bright light("WANDBALL_X1")
		FX01 GH 3 bright light("WANDBALL_X3")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 1 A_RadiusThrust(120,5,RTF_NOIMPACTDAMAGE)
		stop
	}
}
actor SyntheticWandTomeShot : SyntheticWandWeaveShot
{
	+doombounce
	bouncefactor 1
	speed 2
	states
	{
	spawn2:
		TNT1 A 0 A_ChangeVelocity(frandom(-1,1),frandom(-1,1),frandom(-1.1,1),CVF_RELATIVE)
		TNT1 AA 0 A_SpawnItemEx("SyntheticWandPuff",0,0,0,random(-1,-3),frandom(-2,2),frandom(-3,4),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,0)
		FX01 AA 1 bright light("WANDBALL") A_Weave(-4,-4,5.0,5.0)
		TNT1 AA 0 A_SpawnItemEx("SyntheticWandPuff",0,0,0,random(-1,-3),frandom(-2,2),frandom(-3,4),0,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS,0)
		FX01 BB 1 bright light("WANDBALL") A_Weave(-4,-4,5.0,5.0)
		TNT1 A 0 A_Jump(1,1)
		loop
		TNT1 A 0 A_ChangeFlag("doombounce",0)
		loop
	}
}


actor SyntheticWandPuff
{
	renderstyle add
	+nogravity
	height 4
	radius 4
	states
	{
	spawn:
		TNT1 A 0
		FX01 CDE 3 bright A_ChangeVelocity(frandom(-1,1),frandom(-1,1),frandom(-1,1),CVF_RELATIVE)
		FX01 FFFGGGHHH 1 bright A_FadeOut(0.1)
		stop
	}
}

actor SpentWandCrystal
{
	height 1
	radius 1
	projectile
	+doombounce +bounceonactors -nogravity
	+forcexybillboard
	speed 12
	states
	{
	spawn:
	death:
		TNT1 A 0 A_ChangeFlag("missile",0)
		AMG1 C -1
		stop
	}
}
actor PartialSpentWandCrystal : SpentWandCrystal
{
	states
	{
	spawn:
	death:
		AMG1 B 1 A_SetTics(random(4,20))
		AMG1 C 1 A_SetTics(random(6,66))
		loop
	}
}
