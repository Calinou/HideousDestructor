actor SyntheticStick : HDWeapon
{
	game Heretic
	weapon.selectionorder 3800
	weapon.slotnumber 1
	obituary "$OB_MPSTAFF"
	tag "$TAG_STAFF"
	states
	{
	ready:
		STFF A 0{
			A_GiveInventory("IsWeaponLong",1);
			if(
				(
					(countinv("IsWalking")>0)
					||(abs(velx)+abs(vely)<1)
				)
				&&(getzat>=z)
			){A_ChangeFlag("windthrust",floor(random(1,10)*0.1));}
			else{A_ChangeFlag("windthrust",1);}
		}
		STFF A 0 A_JumpIfInventory("SyntheticStickTomed",1,3)
		STFF A 0 A_JumpIfInventory("IsUsingTome",1,"entome")
		STFF A 1 A_WeaponReady
		loop
		TNT1 A 0 A_PlayWeaponSound("weapons/staffcrackle")
		STFF DEF 1 A_WeaponReady
		loop
	entome:
		STFF AB 6
		STFF B 6
		STFF B 10 offset(4,35) A_PlayWeaponSound("himp/attack")
		STFF B 20 offset(5,40)
		STFF B 10 offset(7,42) A_PlayWeaponSound("weapons/staffhit")
		STFF B 4 offset(4,37)
		STFF B 10 offset(5,39)
		TNT1 A 0 A_GiveInventory("SyntheticStickTomed",1)
		STFF BG 5 offset(4,36)
		STFF GGDEF 2
		TNT1 A 0 A_TakeInventory("IsUsingTome",1)
		goto Ready
	select:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 8
		STFF A 0{
			A_FireCustomMissile("SyntheticStickAttack");
			if(getzat>=z){A_Recoil(-1);}
		}
		goto select0small
	deselect:
		STFF A 0 A_ChangeFlag("windthrust",1)
		goto deselect0big
	fire:
		TNT1 A 0 A_ChangeFlag("windthrust",1)
		TNT1 A 0 A_JumpIfInventory("SyntheticStickTomed",1,"tomefire")
		TNT1 A 0 A_JumpIf(getzat<z,"shitty")
		TNT1 A 0 A_ScaleVelocity(0.4)
		TNT1 A 0 A_FireCustomMissile("SyntheticStickAttack")
		STFF A 1
		STFF B 3
		STFF B 2 {if(getzat>=z){A_Recoil(-2);}}
		goto end
	shitty:
		TNT1 A 0 A_FireCustomMissile("SyntheticStickAttackShitty")
		STFF A 1 A_WeaponReady(WRF_NOFIRE)
		STFF B 3
		goto end
	tomefire:
		TNT1 A 0 A_FireCustomMissile("SyntheticStickAttackTomed")
		STFF DEF 1
		STFF G 2
		STFF G 2 offset(0,34)
		TNT1 A 0 A_JumpIfInventory("SyntheticStickTomed",1,1)
		goto end
		STFF DEF 1
		STFF DEFE 1 offset(0,38)
		STFF A 0 A_Refire
		goto ready
	hold:
	althold:
	altfire:
		TNT1 A 0 A_ChangeFlag("windthrust",1)
		TNT1 A 0 A_JumpIfInventory("SyntheticStickTomed",1,"tomefire")
		TNT1 A 0 A_JumpIf(floorz<z,"shitty")
		TNT1 A 0 A_ScaleVelocity(0.4)
		TNT1 A 0 A_FireCustomMissile("SyntheticStickAttackShitty",2)
		TNT1 A 0 A_JumpIf(floorz<z,2)
		TNT1 A 0 A_Recoil(-2)
		STFF A 2 A_WeaponReady(WRF_NOFIRE)
		STFF B 4 offset(-6,38)
		TNT1 A 0 A_FireCustomMissile("SyntheticStickAttackShitty",-2)
		STFF A 3 A_WeaponReady(WRF_NOFIRE)
		STFF B 3 offset(4,38)
		TNT1 A 0 A_JumpIf(floorz<z,"end")
		STFF B 2 A_Recoil(-2)
		goto end
	end:
		STFF A 2
		STFF A 3 offset(0,38)
		STFF A 0 A_Refire
		goto ready
	}
}
actor SyntheticStickTomed : InventoryFlag {}
actor SyntheticStickAttack : FastProjectile
{
	speed 28
	var int user_dmg;
	damage (user_dmg)
	height 2
	radius 2
	+shootable +noblood
	+notargetswitch
	-noblockmap
	health 1
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_SetUserVar("user_dmg",25)
		TNT1 A 1
		TNT1 A 0 A_ScaleVelocity(0.8)
		TNT1 A 0 A_SetUserVar("user_dmg",80)
		TNT1 A 0 A_ChangeFlag("bloodsplatter",1)
		TNT1 A 1
		stop
	death:
		TNT1 A 0 A_RadiusThrust((160-user_dmg)*10,32,RTF_NOIMPACTDAMAGE|RTF_AFFECTSOURCE)
		TNT1 AA 0 A_SpawnItemEx("StaffPuff",0,0,0,0,frandom(-0.3,0.3),frandom(0.6,1.2),0,SXF_NOCHECKPOSITION)
	xdeath:
		TNT1 A 0 A_PlaySound("weapons/staffhit")
		TNT1 A 0 A_RadiusThrust((120-user_dmg)*10,32,RTF_NOIMPACTDAMAGE|RTF_AFFECTSOURCE)
		stop
	}
}
actor SyntheticStickAttackShitty : SyntheticStickAttack
{
	height 4
	radius 3
	speed 26
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_SetUserVar("user_dmg",20)
		TNT1 A 1
		TNT1 A 0 A_ScaleVelocity(0.8)
		TNT1 A 0 A_SetUserVar("user_dmg",30)
		TNT1 A 1
		stop
	death:
		TNT1 A 0 A_RadiusThrust((160-user_dmg)*10,32,RTF_NOIMPACTDAMAGE|RTF_AFFECTSOURCE)
		TNT1 A 0 A_SpawnItemEx("StaffPuff",0,0,0,0,frandom(-0.2,0.2),frandom(0.6,1),0,SXF_NOCHECKPOSITION)
	xdeath:
		TNT1 A 0 A_PlaySound("weapons/staffhit")
		TNT1 A 0 A_RadiusThrust((100-user_dmg)*10,32,RTF_NOIMPACTDAMAGE|RTF_AFFECTSOURCE)
		stop
	}
}
actor SyntheticStickAttackTomed : SyntheticStickAttack
{
	height 2
	radius 2
	speed 32
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_SetUserVar("user_dmg",100000)
		TNT1 A 1
		TNT1 A 0 A_ScaleVelocity(0.4)
		TNT1 A 1
		stop
	death:
	xdeath:
		TNT1 A 0 A_TakeFromTarget("SyntheticStickTomed",1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_PlaySound("weapons/phoenixhit")
		TNT1 AAAAAAAAAA 0 A_SpawnItemEx("SyntheticTomeStaffPuff",0,0,0,random(-6,6),random(-6,6),random(-6,6),0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_RadiusThrust(600,56,RTF_NOIMPACTDAMAGE|RTF_AFFECTSOURCE)
		TNT1 A 0 A_Explode(256,256,0,1)
		TNT1 A 20 A_Quake(3,20,0,256)
		stop
	}
}
actor SyntheticTomeStaffPuff
{
	renderstyle add
	+nogravity
	height 1
	radius 1
	states
	{
	spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySound("weapons/phoenixhit")
		PUF4 ABC 3 bright A_ChangeVelocity(frandom(-1,1),frandom(-1,1),frandom(-1,1),CVF_RELATIVE)
		PUF4 DDDEEEFFF 1 bright A_FadeOut(0.1)
		stop
	}
}
