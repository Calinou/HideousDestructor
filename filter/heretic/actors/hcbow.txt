actor SyntheticBow : HDWeapon replaces Crossbow
{
	game Heretic
	+weapon.noautofire
	weapon.ammotype1 "CrossBowAmmo"
	inventory.pickupmessage "ethereal crossbow"
	weapon.slotnumber 3
	weapon.yadjust 18
	states
	{
	select:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_JumpIfInventory("SyntheticBowLoaded",1,"selectloaded")
		CRBW E 1 A_Raise
		TNT1 A 0 A_Raise
		loop
	selectloaded:
		TNT1 A 10
	selectloaded2:
		CRBW A 1 A_Raise
		TNT1 A 0 A_Raise
		loop
	deselect:
//		TNT1 A 0 A_JumpIfInventory("SyntheticBowLoaded",1,"unload")
		TNT1 A 0 A_TakeInventory("IsWeaponWide")
		TNT1 A 0 A_JumpIfInventory("SyntheticBowLoaded",1,3)
		CRBW E 1 A_Lower
		TNT1 A 0 A_Jump(256,2)
		CRBW A 1 A_Lower
		TNT1 A 0 A_Lower
		loop
	ready:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_JumpIfInventory("IsUsingTome",1,"entome")
		TNT1 A 0 A_GiveInventory("IsWeaponWide",1)
		TNT1 A 0 A_JumpIfInventory("SyntheticBowLoaded",1,2)
		CRBW E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		loop
		TNT1 A 0 A_SetCrosshair(0)
		TNT1 A 0 A_JumpIfInventory("IsUsingTome",1,"entome")
		TNT1 A 0 A_GiveInventory("IsWeaponWide",1)
		CRBW A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("IsUsingTome",1,"entome")
		TNT1 A 0 A_GiveInventory("IsWeaponWide",1)
		CRBW B 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("IsUsingTome",1,"entome")
		TNT1 A 0 A_GiveInventory("IsWeaponWide",1)
		CRBW C 1 A_WeaponReady(WRF_ALLOWRELOAD)
		loop
	entome:
		TNT1 A 0 A_JumpIfInventory("SyntheticBowLoaded",1,4)
		CRBW E 3 offset (0,38)
		CRBW E 3 offset (0,48)
		CRBW E 3 offset (0,60)
		goto entome2
		CRBW A 3 offset (0,38)
		CRBW A 3 offset (0,48)
		CRBW A 3 offset (0,60)
	entome2:
		TNT1 A 20 A_PlayWeaponSound("himp/attack")
		TNT1 A 40 A_PlayWeaponSound("weapons/staffhit")
		TNT1 A 0 A_GiveInventory("SyntheticBowTomed",5)
		TNT1 A 0 A_JumpIfInventory("SyntheticBowLoaded",1,4)
		CRBW E 3 offset (0,60)
		CRBW E 3 offset (0,48)
		CRBW E 3 offset (0,38)
		goto entome3
		CRBW A 3 offset (0,60)
		CRBW A 3 offset (0,48)
		CRBW A 3 offset (0,38)
	entome3:
		TNT1 A 0 A_TakeInventory("IsUsingTome",1)
		goto ready
	reload:
		TNT1 A 0 A_JumpIfInventory("SyntheticBowLoaded",1,"unload")
		TNT1 A 0 A_JumpIfInventory("CrossBowAmmo",1,1)
		goto ready
		TNT1 A 0 A_GiveInventory("Reloading",1)
		CRBW E 6 offset (0,38)
		CRBW E 6 offset (-1,42)
		CRBW E 4 offset (2,42)
		CRBW E 4 offset (-2,48)
		CRBW E 3 offset (2,50)
		CRBW E 3 offset (2,52)
		CRBW F 3 offset (-3,51)
		CRBW F 2 offset (1,53)
		CRBW F 2 offset (-1,52)
		CRBW F 3 offset (3,54)
		CRBW G 2 offset (-3,51)
		CRBW G 2 offset (1,53)
		CRBW G 1 offset (-1,52)
		CRBW G 1 offset (3,54)
		CRBW H 2 offset (-3,53)
		CRBW H 2 offset (1,56)
		CRBW H 4 offset (-1,60)
		CRBW H 2 offset (1,58)
		CRBW H 3 offset (0,62)
		TNT1 A 0 A_TakeInventory("CrossBowAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("SyntheticBowLoaded",1)
		CRBW H 3 offset (0,60)
		CRBW C 3 offset (0,48)
		CRBW B 3 offset (0,38)
		TNT1 A 0 A_TakeInventory("Reloading",1)
		goto ready
	tomefire:
		TNT1 A 0 A_JumpIfInventory("SyntheticBowTomed",2,3)
		TNT1 A 0 A_TakeInventory("SyntheticBowLoaded",1)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_TakeInventory("CrossBowAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_PlaySound("mummy/attack1",CHAN_WEAPON,2.0)
		TNT1 A 0 A_JumpIfInventory("SyntheticBowTomed",2,"tomefiregreen")
	tomefirered:
		TNT1 A 0 A_ZoomFactor(1.07)
		CRBW D 3 bright A_FireCustomMissile("SyntheticAxeRed")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(30,60),random(-10,30))
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -4, 0, Sin(Pitch) * 4, 1)
		goto tomepost
	tomefiregreen:
		TNT1 A 0 A_ZoomFactor(1.03)
		CRBW D 2 bright A_FireCustomMissile("SyntheticAxe")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(10,20),random(-10,15))
	tomepost:
		TNT1 A 0 A_TakeInventory("SyntheticBowTomed",1)
		TNT1 A 0 A_ZoomFactor(1.00)
		TNT1 A 0 A_JumpIfInventory("SyntheticBowLoaded",1,1)
		goto postfireempty
		CRBW E 3 offset(0,35)
		CRBW F 2 offset(0,38)
		CRBW G 2 offset(0,37)
		CRBW H 2 offset(0,35)
		CRBW D 0 A_Refire
		goto ready
	noshot:
		CRBW E 3 offset (0,35)
		CRBW E 3 offset (0,38)
		CRBW E 3 offset (0,37)
		CRBW E 3 offset (0,33)
		goto nope
	fire:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_JumpIfInventory("SyntheticBowLoaded",1,1)
		goto noshot
		TNT1 A 0 A_JumpIfInventory("SyntheticBowTomed",1,"tomefire")
		TNT1 A 0 A_PlaySound("weapons/staffhit",CHAN_WEAPON,2.0)
		TNT1 A 0 A_ZoomFactor(1.05)
		CRBW D 2 bright A_FireCustomMissile("SyntheticBolt")
		TNT1 A 0 ACS_NamedExecuteAlways("MuzzleClimb",0,random(10,40),random(-10,20))
		TNT1 A 0 A_TakeInventory("SyntheticBowLoaded",999)
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -4, 0, Sin(Pitch) * 4, 1)
		TNT1 A 0 A_ZoomFactor(1.00)
	postfireempty:
		CRBW D 2 offset(0,35)
		CRBW E 2 offset(0,38)
		CRBW E 2 offset(0,37)
		CRBW E 0 A_Refire
		goto ready
	altfire:
	unload:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_JumpIfInventory("SyntheticBowLoaded",1,1)
		goto reload
		TNT1 A 0 A_GiveInventory("Reloading",1)
		CRBW A 3 offset (0,38)
		CRBW A 3 offset (0,48)
		CRBW A 3 offset (0,60)
		CRBW H 2 offset (-3,53)
		CRBW H 2 offset (1,56)
		CRBW H 4 offset (-1,60)
		CRBW H 2 offset (1,58)
		CRBW H 3 offset (0,62)
		TNT1 A 0 A_JumpIfInventory("CrossBowAmmo",0,3)
		TNT1 A 0 A_GiveInventory("CrossBowAmmo",1)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_SpawnItemEx("SyntheticBoltDropped",12,0,0,4,0,4,0,SXF_NOCHECKPOSITION)
		TNT1 A 0 A_TakeInventory("SyntheticBowLoaded",999)
		CRBW E 3 offset (0,60)
		CRBW E 3 offset (0,48)
		CRBW E 3 offset (0,38)
		CRBW A 0 A_TakeInventory("Reloading",1)
		goto ready
	spawn:
		WBOW A -1
		stop
	}
}
actor SyntheticBowTomed : InventoryFlag
{
	inventory.maxamount 5
}
actor SyntheticBowLoaded : Ammo
{

	+inventory.untossable
	inventory.maxamount 1
	ammo.backpackmaxamount 1
	ammo.backpackamount 0
}
actor SyntheticBoltAmmo : CrossBowAmmo replaces CrossbowAmmo
{
	inventory.amount 5
	inventory.pickupmessage "ethereal hoplon abstraction"
	scale 0.7
	states
	{
	spawn:
		AMC1 A 1 A_SetTics(random(10,100))
		AMC1 A 1 bright A_SetTics(random(1,4))
		loop
	}
}
actor SyntheticBoltDropped : CrossBowAmmo
{
	inventory.pickupmessage "$TXT_AMMOCROSSBOW1"
	inventory.amount 1
	states
	{
	spawn:
		FX03 A 1 A_SetTics(random(10,100))
		FX03 A 1 bright A_SetTics(random(1,4))
		loop
	}
}
actor SyntheticBolt : FastProjectile
{
	seesound "weapons/bowshoot"
	deathsound "weapons/bowhit"
	obituary "$OB_MPCROSSBOW"
	-noteleport
	height 1
	radius 2
	speed 1
	damage 120
	var int user_velx;
	var int user_vely;
	var int user_velz;
	missiletype "SyntheticBoltTailWeak"
	missileheight 8
	+windthrust
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_JumpIfInTargetInventory("IsMoving",10,2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_ChangeVelocity(0,frandom(-0.03,0.03),frandom(-0.04,0.04),CVF_RELATIVE)

		TNT1 A 0 A_ScaleVelocity(200)
	spawn2:
		TNT1 A 0 A_ChangeVelocity(0,0,-1,CVF_RELATIVE)
		TNT1 A 0 A_SetUserVar(user_velx,velx)
		TNT1 A 0 A_SetUserVar(user_vely,vely)
		TNT1 A 0 A_SetUserVar(user_velz,velz)
		FX03 B 1 bright light("SMALLBOWBOLT")
		loop
	death:
		TNT1 A 1 A_SpawnItemEx("SyntheticBolt2",-4,0,0,user_velx/100,user_vely/100,user_velz/100,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS)
		FX03 HHIIJJJ 2 bright light("BIGBOWBOLT_X3") A_FadeOut(0.1)
		stop
	}
}
actor SyntheticBolt2 : SyntheticBolt
{
	damage 70
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_ChangeVelocity(0,frandom(-0.03,0.03),frandom(-0.03,0.03),CVF_RELATIVE)
		TNT1 A 0 A_ScaleVelocity(60)
		goto spawn2
	death:
		TNT1 A 0 A_Jump(220,1)
		goto super::death
		TNT1 A 0 A_SetTranslucent(1,1)
		TNT1 A 0 A_AlertMonsters
		TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("SyntheticBoltTail",0,0,random(-50,90),0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 AAAAAAAAAAAAAAAAAAAAAA 0 A_SpawnItemEx("SyntheticBoltTail",0,random(-50,50),0,0,0,0,0,SXF_NOCHECKPOSITION)
		FX03 HHIIJJ 4 bright light("BIGBOWBOLT_X1") A_FadeOut(0.1)
		stop
	}
}
actor SyntheticBoltTail
{
	+nointeraction
	+fixmapthingpos
	renderstyle add
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_ChangeVelocity(frandom(0,1),frandom(-1,1),frandom(-1,1))
		FX03 A 3 bright A_FadeOut(0.1)
		wait
	}
}
actor SyntheticBoltTailWeak : SyntheticBoltTail
{
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_Jump(208,1)
		goto super::spawn
		TNT1 A 0
		stop
	}
}
actor SyntheticAxe
{
	projectile
	height 8
	radius 6
	damage 70
	speed 50
	-nogravity
	-noteleport
	+doombounce
	+bounceonactors
	+windthrust
	+canbouncewater
	bouncefactor 0.8
	deathsound "hknight/hit"
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_JumpIfInTargetInventory("IsMoving",10,2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_ChangeVelocity(0,frandom(-0.03,0.03),frandom(-0.04,0.04),CVF_RELATIVE)
		TNT1 A 0 A_Jump(256,"spawn2")
	spawn2:
		TNT1 A 0 A_JumpIf(abs(velx)+abs(vely)>4,2)
		TNT1 A 0 A_ChangeFlag("doombounce",0)
		TNT1 A 0 A_PlaySound("hknight/axewhoosh")
		SPAX ABC 3 bright
		loop
	death:
		TNT1 A 0 A_SetTranslucent(1,1)
		TNT1 A 0 A_NoGravity
		TNT1 A 0 A_ChangeVelocity(0,0,3)
		TNT1 A 0 A_Jump(256,"death2")
	death2:
		TNT1 A 0 A_Explode(16,16)
		SPAX DDEEFF 2 bright A_FadeOut(0.1)
		stop
	}
}
actor SyntheticAxe2 : SyntheticAxe
{
	states
	{
	spawn:
		TNT1 A 0 nodelay
		goto spawn2
	}
}
actor SyntheticAxeRed : SyntheticAxe
{
	damage 200
	var int user_velx;
	var int user_vely;
	var int user_velz;
	-bounceonactors
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_JumpIfInTargetInventory("IsMoving",10,2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_ChangeVelocity(0,frandom(-0.03,0.03),frandom(-0.04,0.04),CVF_RELATIVE)
		TNT1 A 0 A_Jump(256,"spawn2")
	spawn2:
		TNT1 A 0 A_SetUserVar(user_velx,velx)
		TNT1 A 0 A_SetUserVar(user_vely,vely)
		TNT1 A 0 A_SetUserVar(user_velz,velz)
		TNT1 A 0 A_JumpIf(abs(velx)+abs(vely)>4,2)
		TNT1 A 0 A_ChangeFlag("doombounce",0)
		TNT1 A 0 A_PlaySound("hknight/axewhoosh")
		RAXE AB 1 bright
		loop
	death2:
		TNT1 A 0 A_SpawnItemEx("SyntheticAxe2",-2,0,0,user_velx,user_vely,user_velz,0,SXF_NOCHECKPOSITION|SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_Explode(32,32)
		RAXE CCDDEE 2 bright A_FadeOut(0.1)
		stop
	}
}
