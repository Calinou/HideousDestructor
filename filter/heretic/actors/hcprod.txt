actor SyntheticPhoenixRod : HDWeapon replaces PhoenixRod
{
	game Heretic
	weapon.slotnumber 6
	weapon.yadjust 16
	weapon.ammotype1 SyntheticProdLoaded
	weapon.ammotype2 SyntheticProdBall
	weapon.bobstyle normal
	weapon.bobspeed 1.6
	inventory.pickupmessage "phoenix rod"
	obituary "%o tasted %k's clean, orangey zest for vengeance."
	states
	{
	select:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_TakeInventory("IsWeaponLong")
		TNT1 A 0 A_TakeInventory("IsWeaponShort")
		TNT1 A 14
	select2:
		TNT1 A 0 A_JumpIfInventory("SyntheticProdLoaded",1,3)
		PHNX E 1 A_Raise
		TNT1 A 0 A_Jump(256,2)
		PHNX A 1 A_Raise
		TNT1 A 0
		loop
	deselect:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_TakeInventory("IsWeaponWide",999)
		TNT1 A 0 A_JumpIfInventory("SyntheticProdLoaded",1,3)
		PHNX E 1 A_Lower
		TNT1 A 0 A_Jump(256,2)
		PHNX A 1 A_Lower
		TNT1 A 0
		loop
	ready:
		TNT1 A 0 A_JumpIfInventory("IsUsingTome",1,"entome")
		TNT1 A 0 A_GiveInventory("IsWeaponWide",1)
		TNT1 A 0 A_JumpIfInventory("SyntheticProdLoaded",1,2)
		PHNX E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		loop
		PHNX A 0{
			if(countinv("IsMoving")<7){A_SetCrosshair(0);}
			else{A_SetCrosshair(21);}
		}
		PHNX A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		TNT1 A 0 A_JumpIfInventory("SyntheticProdTomed",1,1)
		loop
		TNT1 A 0 A_GiveInventory("IsWeaponWide",1)
		TNT1 A 0 A_FireCustomMissile("SyntheticWandLightDummy")
		PHNX E 1 A_WeaponReady(WRF_ALLOWRELOAD)
		loop
	entome:
		TNT1 E 0 A_JumpIfInventory("SyntheticProdLoaded",1,3)
		PHNX E 0
		TNT1 E 0 A_Jump(256,2)
		PHNX A 0
		PHNX "#" 3 offset (0,38)
		PHNX "#" 3 offset (0,48)
		PHNX "#" 3 offset (0,60)
		TNT1 "#" 20 A_PlayWeaponSound("himp/attack")
		TNT1 "#" 40 A_PlayWeaponSound("weapons/staffhit")
		TNT1 "#" 0 A_GiveInventory("SyntheticProdTomed",1)
		PHNX "#" 3 offset (0,60)
		PHNX "#" 3 offset (0,48)
		PHNX "#" 3 offset (0,38)
		TNT1 A 0 A_TakeInventory("IsUsingTome",1)
		goto ready
	reload:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_JumpIfInventory("SyntheticProdLoaded",0,"noshot")
		TNT1 A 0 A_JumpIfInventory("SyntheticProdBall",1,1)
		goto noshot
		TNT1 A 0 A_GiveInventory("Reloading",1)
		PHNX E 3 offset(-1,34) ACS_NamedExecuteAlways("MuzzleClimb",0,random(-12,-24),random(12,24))
		PHNX E 3 offset(-4,37) ACS_NamedExecuteAlways("MuzzleClimb",0,random(-12,-24),random(12,24))
		PHNX E 3 offset(-6,42) ACS_NamedExecuteAlways("MuzzleClimb",0,random(-12,-24),random(12,24))

		PHNX E 2 offset(-8,48)
		PHNX E 5 offset(-7,49)
		PHNX E 4 offset(-8,46)

		TNT1 A 0 A_TakeInventory("SyntheticProdBall",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_GiveInventory("SyntheticProdLoaded",999)

		PHNX E 3 offset(-6,42)
		TNT1 A 0 A_TakeInventory("Reloading",999)
		PHNX E 3 offset(-4,37)
		PHNX E 3 offset(-2,34)
		PHNX EEEEEEAAEEEEAEEEEAEEAEAEA 1 A_WeaponReady(WRF_NOFIRE)
		goto ready
	hold:
	noshot:
		PHNX AE 1 A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_Refire
		goto ready
	fire:
		TNT1 A 0 A_JumpIfInventory("SyntheticProdLoaded",20,1)
		goto noshot
		TNT1 A 0 A_PlaySound("weapons/phoenixshoot")
		PHNX A 1 offset(1,33)
		PHNX A 1 offset(-1,33)
		PHNX A 1 offset(1,33)
		PHNX A 1 offset(-1,33)
		PHNX A 1 offset(1,34)
		PHNX A 1 offset(-1,34)
		PHNX A 1 offset(1,34)
		PHNX A 1 offset(-1,34)
		TNT1 A 0 A_ZoomFactor(0.9)
		PHNX C 1 bright A_FireCustomMissile("ProdBird",0,0,0,-6)
		TNT1 A 0 A_ChangeVelocity (Cos(Pitch) * -4, 0, Sin(Pitch) * 4, 1)
		TNT1 A 0 A_AlertMonsters
		PHNX D 1 bright
		TNT1 A 0 A_ZoomFactor(1.0)
		PHNX DB 2 bright ACS_NamedExecuteAlways("MuzzleClimb",0,random(-24,-48),random(24,48))
		PHNX E 2
		TNT1 A 0 A_Refire
		goto ready
	altfire:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_JumpIfInventory("SyntheticProdLoaded",1,1)
		TNT1 A 0 A_JumpIfInventory("SyntheticProdTomed",1,"fire")
		goto noshot
		PHNX B 1
		PHNX D 1 bright
	altblast:
	althold:
		TNT1 A 0 A_SetCrosshair(21)
		TNT1 A 0 A_JumpIfInventory("SyntheticProdLoaded",1,1)
		goto noshot
		TNT1 A 0 A_JumpIfInventory("SyntheticProdTomed",1,"fire")
		TNT1 A 0 A_FireCustomMissile("ProdFlame",0,0,0,-6)
		PHNX C 1 bright A_WeaponReady(WRF_NOFIRE)
		TNT1 A 0 A_TakeInventory("SyntheticProdLoaded",1)
		TNT1 A 0 A_JumpIfInventory("SyntheticProdLoaded",1,2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_Refire
		TNT1 A 0 A_ClearRefire
		PHNX DB 2
		goto ready
	}
}
actor SyntheticProdTomed : InventoryFlag {}
actor SyntheticProdLoaded : Ammo
{
	+inventory.untossable
	inventory.maxamount 70
	ammo.backpackmaxamount 70
	ammo.backpackamount 0
}
actor SyntheticProdBall : Ammo replaces PhoenixRodHefty
{
	inventory.amount 1
	inventory.maxamount 12
	ammo.backpackmaxamount 12
	ammo.backpackamount 0
	inventory.icon "INAMPNX"
	inventory.pickupmessage "phoenix ball"
	spawnid 11
	states
	{
	spawn:
		AMP1 ABC 1 bright A_SetTics(random(1,4))
		loop
	}
}
actor SyntheticProdGem : RandomSpawner replaces PhoenixRodAmmo
{
	dropitem "SyntheticWandCrystal",256,3
	dropitem "SyntheticProdBall",250,1
}

actor ProdBird : FastProjectile
{
	damagetype "piercing"
	-noteleport
	height 8
	radius 8
	+forcexybillboard
	+skyexplode
	renderstyle add
	seesound "weapons/phoenixshoot"
	deathsound "weapons/phoenixhit"
	missileheight 8
	missiletype "ProdBirdFlame"
	speed 1
	var int user_counter0;
	var int user_counter1;
	damage(user_counter0<<2)
	+nointeraction +noclip
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_AlertMonsters

		TNT1 A 0 A_JumpIfInTargetInventory("IsMoving",10,2)
		TNT1 A 0 A_Jump(256,2)
		TNT1 A 0 A_ChangeVelocity(0,frandom(-0.03,0.03),frandom(-0.04,0.04),CVF_RELATIVE)

		TNT1 A 0 A_SetUserVar(user_counter0,0)
		TNT1 A 0 A_ScaleVelocity(30)
		TNT1 A 0 A_JumpIfInTargetInventory("SyntheticProdTomed",1,1)
		goto spawn2
		TNT1 A 0 A_GiveInventory("SyntheticProdTomed",1)
		TNT1 A 0 A_TakeFromTarget("SyntheticProdTomed",999)
		TNT1 A 0 A_BFGSpray("ProdBFG",66,20)
		TNT1 A 0 A_SetUserVar(user_counter0,100)
	spawn2:
		TNT1 A 0 A_TakeFromTarget("SyntheticProdLoaded",1)
		TNT1 A 0 A_SetUserVar(user_counter0,user_counter0+1)
		TNT1 A 0 A_JumpIfInTargetInventory("SyntheticProdLoaded",1,"spawn2")
		TNT1 A 0 A_ChangeFlag("nointeraction",0)
		TNT1 A 0 A_ChangeFlag("noclip",0)
	spawn3:
		FX04 A 2 bright light("PHOENIXSHOT") A_ScaleVelocity(1.1)
		loop
	death:
		TNT1 A 0 A_SetDamageType("blunt")
		TNT1 A 0 A_AlertMonsters
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,45,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,135,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,225,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,315,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)

		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,43,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,133,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,223,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,313,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)

		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,47,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,137,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,227,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,1,30,0,1,317,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_Explode
		TNT1 A 0 A_Explode(user_counter0*5,user_counter0*5,0)
		TNT1 A 0 A_Quake(5,10,0,user_counter0<<3)
		TNT1 A 0 A_RadiusGive("HDFireStarter",128,RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS|RGF_CORPSES|RGF_CUBE,1)
		TNT1 A 0 A_SetScale(0.6+user_counter0*0.014)
		TNT1 A 0 A_ChangeFlag("nointeraction",1)
		TNT1 A 0 A_ChangeFlag("noclip",1)
		TNT1 A 0 A_ChangeFlag("missile",0)
		TNT1 A 0 A_Recoil(1)
	death2:
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,2,random(1,20+user_counter0),0,random(1,14),user_counter1*6,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SetUserVar(user_counter1,user_counter1+1)
		TNT1 A 0 A_JumpIf(user_counter1>10,2)
		FX08 A 1 bright light("PHOENIX_X1")
		loop
		TNT1 A 0 A_Quake(2,30,0,user_counter0<<3)
		TNT1 A 0 A_Stop
	death3:
		TNT1 A 0 A_SpawnItemEx("ProdTail",0,0,2,random(1,20),0,random(1,14),user_counter1*6,SXF_NOCHECKPOSITION|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SetUserVar(user_counter1,user_counter1+1)
		TNT1 A 0 A_JumpIf(user_counter1>user_counter0,2)
		FX08 A 1 bright light("PHOENIX_X1")
		loop

		FX08 A 3 bright light("PHOENIX_X3")
		FX08 BB 2 bright light("PHOENIX_X5") A_FadeOut(0.3)
		stop
	}
}
actor ProdTail
{
	projectile
	+ripper +noblood
	+doombounce
	+skyexplode
	-noteleport
	damage (2)
	damagetype "burning"
	height 8
	radius 12
	renderstyle add
	seesound "weapons/hornrodshoot"
	states
	{
	spawn:
		TNT1 A 0 nodelay
		TNT1 A 0 A_ChangeVelocity(frandom(-1,0),frandom(-1,1),frandom(-1,1),CVF_RELATIVE)
		FX08 CDE 3
		TNT1 A 0 A_ChangeFlag("doombounce",0)
		FX08 FGH 3 A_FadeOut(0.2)
		stop
	}
}
actor ProdBFG
{
	+nointeraction
	damagetype "burning"
	renderstyle add
	states
	{
	spawn:
		FX08 ABC 2 bright light("PHOENIXFLAME2")
		TNT1 A 0 A_ChangeVelocity(0,0,frandom(0,1),CVF_RELATIVE)
		FX08 CCDDEFGH 3 bright light("PHOENIXFLAME3") A_FadeOut(0.1)
		stop
	}
}

actor ProdFlame
{
	projectile
	height 3
	radius 3
	speed 24
	damage(8)
	damagetype "burning"
	projectilekickback 10
	+forcexybillboard
	-noteleport
	renderstyle add
	seesound "weapons/phoenixpowshoot"
	states
	{
	spawn:
		FX09 ABABABABABABABAB 1 bright light("PHOENIXFLAME") A_ChangeVelocity(frandom(0,-0.6),frandom(-0.6,0.6),frandom(0.2,0.4),CVF_RELATIVE)
		goto spawn2
	death:
		TNT1 A 0 A_ChangeVelocity(0,frandom(-2,2),frandom(2,4),CVF_RELATIVE)
		TNT1 A 0 A_SetScale(1.4)
	spawn2:
		TNT1 A 0 A_RadiusGive("HDFireStarter",24,RGF_PLAYERS|RGF_MONSTERS|RGF_OBJECTS|RGF_CORPSES|RGF_CUBE,1)
		FX09 CCDDEEFF 1 bright light("PHOENIXFLAME3") A_FadeOut(0.1)
		stop
	}
}
actor ProdBirdFlame
{
	+nogravity
	renderstyle add
	states
	{
	spawn:
		FX09 ABABAB 1 bright A_ChangeVelocity(frandom(1,-0.6),frandom(-1,1),frandom(0,1),CVF_RELATIVE)
		goto spawn2
	death:
		TNT1 A 0 A_ChangeVelocity(0,frandom(-2,2),frandom(2,4),CVF_RELATIVE)
		TNT1 A 0 A_SetScale(1.4)
	spawn2:
		FX09 CCDDEEFF 1 bright A_FadeOut(0.1)
		stop
	}
}



