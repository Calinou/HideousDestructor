
// 		ENCUMBRANCE CHECK
script "CarryLimit" (void)
{
	SetResultValue(true);
	overloaded[PlayerNumber()]=0;
	GiveInventory("HDRealDoomguy",1);

	if((GetCvar("hd_carrylimit"))||(GameSkill()==punisherskill))
	{
		TakeInventory("HDRealDoomguy",1);
		setfont("smallfont");sethudsize(800,600,1);
		int blox=

		//weapons
		(
		(CheckInventory("Slayer")-CheckWeapon("Slayer"))*4
		+(CheckInventory("Blooper")-CheckWeapon("Blooper"))*3
		+(CheckInventory("Brontornis")-CheckWeapon("Brontornis"))*3
		+(CheckInventory("HDSMG")-CheckWeapon("HDSMG"))*4
			-(CheckInventory("HDSMGNoMag")*CheckInventory("HDSMG"))
		+(CheckInventory("ZM66SemiRifle")-CheckWeapon("ZM66SemiRifle"))*4
		+(CheckInventory("ZM66Rifle")-CheckWeapon("ZM66Rifle"))*4
		+(CheckInventory("LiberatorRifle")-CheckWeapon("LiberatorRifle"))*5
		+(CheckInventory("ZM66GrenadeRifle")-CheckWeapon("ZM66GrenadeRifle"))*5
		+(CheckInventory("LiberatorGrenadeRifle")-CheckWeapon("LiberatorGrenadeRifle"))*6
		+(CheckInventory("Lumberjack")-CheckWeapon("Lumberjack"))*6
		+(CheckInventory("Hunter")-CheckWeapon("Hunter"))*5
		+(CheckInventory("BossRifle")-CheckWeapon("BossRifle"))*7
		+(CheckInventory("RocquetteLauncher")-CheckWeapon("RocquetteLauncher"))*6
		+(CheckInventory("ThunderBuster")-CheckWeapon("ThunderBuster"))*5
		+(CheckInventory("Vulcanette")-CheckWeapon("Vulcanette"))*7
		+(CheckInventory("BFG9k")-CheckWeapon("BFG9k"))*8
		+(CheckInventory("HERPUsable")-CheckWeapon("HERPUsable"))*5
		+(CheckInventory("HERPDEAD")-CheckWeapon("HERPDEAD"))*5
		)*25

		//armour
		+CheckInventory("GreenArmourStored")*21
		+CheckInventory("BlueArmourStored")*27
		+CheckInventory("BlackArmourStored")*35

		//usables
		+CheckInventory("HDIEDKit")*3
		+CheckInventory("SquadSummoner")*5
		+CheckInventory("PortableStimpack")*5
		+CheckInventory("PortableBerserkPack")*8
		+CheckInventory("PortableMedikit")*10
		+CheckInventory("PortableLiteAmp")*12
		+CheckInventory("PortableRadsuit")*25
		+CheckInventory("BluePotion")*8
		+CheckInventory("HDSecondPistol")*30
		+CheckInventory("DERPUsable")*35
		+CheckInventory("DERPDEAD")*35
		+CheckInventory("MedDeployer")*50

		//loose/magged ambiguous ammo
		+CheckInventory("HDPistolAmmo")*1
		+CheckInventory("SevenMilAmmo")*3/2
		+CheckInventory("FourMilAmmo")/3
		+CheckInventory("HDCellAmmo")/2

		//single loose ammo
		+CheckInventory("HDShellAmmo")*3
		+CheckInventory("BrontornisRound")*6
		+CheckInventory("HDFragGrenadeAmmo")*13
		+CheckInventory("HDRocketAmmo")*13
		+CheckInventory("DudRocquetteAmmo")*13
		+CheckInventory("HEATAmmo")*15

		//full mags
		+CheckInventory("HDPistolMag")*10
		+CheckInventory("BossClip")*12
		+CheckInventory("ZM66RifleMags")*17
		+CheckInventory("HDCellPacks")*18
		+CheckInventory("HDCellPackEmpty")*18
		+CheckInventory("HDSMGMag")*18
		+CheckInventory("LiberatorMag")*21

		//loaded into gun
		+(
			CheckInventory("LumberEmptyCell")
			+sign(CheckInventory("LumberCell"))
			+CheckInventory("VulcEmptyCell")
			+sign(CheckInventory("VulcJuice"))
			+CheckInventory("PlasmaEmptyCell")
			+sign(CheckInventory("PlasmaBattery"))
			+CheckInventory("BFGLoaded")
			+sign(CheckInventory("HERPCell"))
		)*8
		+(
			CheckInventory("HDPistolChamber")
			+CheckInventory("HDPistolLoaded")
			+CheckInventory("HDSecondPistolChamber")
			+CheckInventory("HDSecondPistolLoaded")
			+CheckInventory("DERPAmmo")
			+CheckInventory("HDSMGLoaded")
		)/3
		+(
			CheckInventory("ZM66Loaded")
			+CheckInventory("HERPAmmo")
			+CheckInventory("VulcLoaded1")
			+CheckInventory("VulcLoaded2")
			+CheckInventory("VulcLoaded3")
			+CheckInventory("VulcLoaded4")
			+CheckInventory("VulcLoaded5")
		)/5
		+(
			CheckInventory("RocketMag")
			+CheckInventory("BloopChamber")
			+CheckInventory("ZM66GLoaded")
			+CheckInventory("LiberatorGrenadeChamber")
		)*3
		+(
			CheckInventory("BossMag")
			+CheckInventory("LiberatorLoaded")
		)/3
		+CheckInventory("ShotgunTube")
		+CheckInventory("ShotgunChamber")
		+CheckInventory("ShellChamber1")
		+CheckInventory("ShellChamber2")
		+CheckInventory("ShotgunQuickAmmo")
		+CheckInventory("ShotgunQuickAmmo2")

		+CheckInventory("HEATLoaded")*4
		+CheckInventory("BrontornisMag")*2
		;

		if (CheckInventory("HDBackpack")) GiveInventory("Backpack",1);
		int carrymax=550+CheckInventory("PowerStrength")*500+CheckInventory("RegenBlues")*2+CheckInventory("RegenStims")*3;
		if (blox>carrymax) overloaded[PlayerNumber()]=(blox-carrymax)*0.001;
		if (blox>carrymax+220+CheckInventory("Backpack")*300)
		{
			SetResultValue(false);
			if (CheckInventory("HDBackpack"))
			{
				if (overloaded[PlayerNumber()]<0.05) overloaded[PlayerNumber()]=0.05;
			}
		}
//		if(CheckInventory("GimmeHUD")) HudMessage(s:"carrying ",i:blox,s:" blocks";HUDMSG_PLAIN,8,CR_DARKGRAY,32.0,570.0, 0.3,1.0);
		if(blox!=CheckInventory("Encumbrance")){
			TakeInventory("Encumbrance",99999);
			GiveInventory("Encumbrance",blox);
		}
	}
	else{
		TakeInventory("Backpack",999);
		TakeInventory("Encumbrance",99999);
	}
	terminate;
}

//ditches all primary weapons except the one being held
//ditches all ammo not used for the remaining weapon(s)
//(if fist/pistol/frags/injector out, all primaries dropped)
//(also ditches half your frags and meds unless frags/inj out)

script "PurgeWeapons"(void){
	GiveInventory("DropExtra",1);
//	ACS_NamedExecuteAlways("CarryLimit",0,0,0);
}


//drop one unit of ammo for whatever weapon you're carrying
script "DropOneAmmo"(void)
{
	if(CheckWeapon("HDPistol")) GiveInventory("DropOnePistolMag",1);
	else if(CheckWeapon("HDSMG")) GiveInventory("DropOneSMGMag",1);
	else if((CheckWeapon("Hunter"))
		||(CheckWeapon("Slayer"))
		) GiveInventory("DropFourSG",1);
	else if((CheckWeapon("ZM66Rifle"))
		||(CheckWeapon("ZM66SemiRifle"))
		) GiveInventory("DropOneZMag",1);
	else if(CheckWeapon("ZM66GrenadeRifle")) GiveInventory("DropOneZMagFrag",1);
	else if(CheckWeapon("Vulcanette")) GiveInventory("DropOneZMagCell",1);
	else if(CheckWeapon("RocquetteLauncher")) GiveInventory("DropOneRocketHEAT",1);
	else if(CheckWeapon("Blooper")) GiveInventory("DropOneRocket",1);
	else if(CheckWeapon("HDFragGrenades")) GiveInventory("DropOneFrag",1);
	else if(CheckWeapon("LiberatorRifle")) GiveInventory("DropOneLib",1);
	else if(CheckWeapon("LiberatorGrenadeRifle")) GiveInventory("DropOneLibFrag",1);
	else if(CheckWeapon("Brontornis")) GiveInventory("DropOneBronto",1);
	else if(CheckWeapon("BossRifle")) GiveInventory("DropOneBoss",1);
	else if((CheckWeapon("ThunderBuster"))
		||(CheckWeapon("BFG9k"))
		||(CheckWeapon("Lumberjack"))
		) GiveInventory("DropOneCell",1);
	else if(CheckWeapon("Injector")) GiveInventory("DropOneMedStimZerk",1);
	else GiveInventory("DropExtra",1);
//	ACS_NamedExecuteAlways("CarryLimit",0,0,0);
}


//		DOOMGUY MODE
script "HDDoomguy" (void)
{
	bool rk = CheckInventory("RedCard");
	bool bk = CheckInventory("BlueCard");
	bool yk = CheckInventory("YellowCard");
	bool rs = CheckInventory("RedSkull");
	bool bs = CheckInventory("BlueSkull");
	bool ys = CheckInventory("YellowSkull");
	ClearInventory();
	GiveInventory("DoomguyLoadout",1);
	if (rk) GiveInventory("RedCard",1);
	if (bk) GiveInventory("BlueCard",1);
	if (yk) GiveInventory("YellowCard",1);
	if (rs) GiveInventory("RedSkull",1);
	if (bs) GiveInventory("BlueSkull",1);
	if (ys) GiveInventory("YellowSkull",1);
}
